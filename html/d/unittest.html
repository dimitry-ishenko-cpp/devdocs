<h1>Unit Tests</h1>       <b>Contents</b>  <ol> <li><a href="#attributes_unittest">Attributed Unittests</a></li> <li><a href="#documented-unittests">Documented Unittests</a></li> </ol>   <pre>UnitTest:
    unittest BlockStatement
</pre> <p>Unit tests are a builtin framework of test cases applied to a module to determine if it is working properly. A D program can be run with unit tests enabled or disabled. </p>  <p>Unit tests are a special function defined like:</p>  <pre data-language="d">unittest
{
    ...test code...
}
</pre>  <p>Individual tests are specified in the unit test using <a href="expression.html#AssertExpression">AssertExpressions</a>. Unlike <i>AssertExpression</i>s used elsewhere, the assert is not assumed to hold, and upon assert failure the program is still in a defined state. </p>  <p>There can be any number of unit test functions in a module, including within struct, union and class declarations. They are executed in lexical order. </p>  <p>Unit tests, when enabled, are run after all static initialization is complete and before the <code>main()</code> function is called. </p>  <p>For example, given a class <code>Sum</code> that is used to add two values, a unit test can be given:</p>  <pre data-language="d">class Sum
{
    int add(int x, int y) { return x + y; }

    unittest
    {
        Sum sum = new Sum;
        assert(sum.add(3,4) == 7);
        assert(sum.add(-2,0) == -2);
    }
}
</pre>  <p>When unit tests are enabled, the <a href="version.html#PredefinedVersions">version identifier</a> <code>unittest</code> is predefined. </p>   <h2 id="attributes_unittest">Attributed Unittests</h2>  <p>A unittest may be attributed with any of the global function attributes. Such unittests are useful in verifying the given attribute(s) on a template function:</p>  <pre data-language="d">void myFunc(T)(T[] data)
{
    if (data.length &gt; 2)
        data[0] = data[1];
}

@safe nothrow unittest
{
    auto arr = [1,2,3];
    myFunc(arr);
    assert(arr == [2,2,3]);
}
</pre>  <p>This unittest verifies that <code>myFunc</code> contains only <code>@safe</code>, <code>nothrow</code> code. Although this can also be accomplished by attaching these attributes to <code>myFunc</code> itself, that would prevent <code>myFunc</code> from being instantiated with types <code>T</code> that have <code>@system</code> or throwing code in their <code>opAssign</code> method, or other methods that <code>myFunc</code> may call. The above idiom allows <code>myFunc</code> to be instantiated with such types, yet at the same time verify that the <code>@system</code> and throwing behavior is not introduced by the code within <code>myFunc</code> itself.</p>  <b>Implementation Defined:</b> <ol> <li>If unit tests are not enabled, the implementation is not required to check the <a href="#UnitTest"><i>UnitTest</i></a> for syntactic or semantic correctness. This is to reduce the compile time impact of larger unit test sections. The tokens must still be valid, and the implementation can merely count <code>{</code> and <code>}</code> tokens to find the end of the <a href="#UnitTest"><i>UnitTest</i></a>'s <a href="statement.html#BlockStatement"><i>BlockStatement</i></a>. </li> <li>The presentation of unit test results to the user.</li> <li>The method used to enable or disable the unit tests. Use of a compiler switch such as <a href="https://dlang.org/dmd.html#switch-unittest"><b>-unittest</b></a> to enable them is suggested.</li> <li>The order in which modules are called to run their unit tests.</li> <li>Whether the program stops on the first unit test failure, or continues running the unit tests.</li> </ol>   <b>Best Practices:</b> <ol> <li>Using unit tests in conjunction with coverage testing (such as <a href="https://dlang.org/dmd.html#switch-cov"><b>-cov</b></a>) is effective.</li> <li>A unit test for a function should appear immediately following it.</li> </ol>    <h2 id="documented-unittests">Documented Unittests</h2>  <p>Documented unittests allow the developer to deliver code examples to the user, while at the same time automatically verifying that the examples are valid. This avoids the frequent problem of having outdated documentation for some piece of code.</p>  <p>If a declaration is followed by a documented unittest, the code in the unittest will be inserted in the <b>example</b> section of the declaration:</p>  <pre data-language="d">/// Math class
class Math
{
    /// add function
    static int add(int x, int y) { return x + y; }

    ///
    unittest
    {
        assert(add(2, 2) == 4);
    }
}

///
unittest
{
    auto math = new Math();
    auto result = math.add(2, 2);
}
</pre>  <p>The above will generate the following documentation:</p>  <dl>
<dt><big>class <u>Math</u>; </big></dt> <dd>
<u>Math</u> class<br><br> <b>Example:</b><pre data-language="d">auto math = new Math;
auto result = math.add(2, 2);
</pre>
<br> <dl>
<dt><big>int <u>add</u>(int <i>x</i>, int <i>y</i>); </big></dt> <dd>
<u>add</u> function<br><br> <b>Example:</b><pre data-language="d">assert(add(2, 2) == 4);
</pre> </dd> </dl> </dd> </dl>  <p>A unittest which is not documented, or is marked as private will not be used to generate code samples.</p>  <p>There can be multiple documented unittests and they can appear in any order. They will be attached to the last non-unittest declaration:</p>  <pre data-language="d">/// add function
int add(int x, int y) { return x + y; }

/// code sample generated
unittest
{
    assert(add(1, 1) == 2);
}

/// code sample not generated because the unittest is private
private unittest
{
    assert(add(2, 2) == 4);
}

unittest
{
    /// code sample not generated because the unittest isn't documented
    assert(add(3, 3) == 6);
}

/// code sample generated, even if it only includes comments (or is empty)
unittest
{
    /** assert(add(4, 4) == 8); */
}
</pre>  <p>The above will generate the following documentation:</p>  <dl>
<dt><big>int <u>add</u>(int <i>x</i>, int <i>y</i>); </big></dt> <dd>
<u>add</u> function<br><br> <b>Examples:</b><br> code sample generated <pre data-language="d">
assert(add(1, 1) == 2);
</pre> <br><br><b>Examples:</b><br> code sample generated, even if it is empty or only includes comments <pre data-language="d">
/** assert(add(4, 4) == 8); */
</pre> <br><br> </dd> </dl>      <div class="_attribution">
  <p class="_attribution-p">
    &copy; 1999&ndash;2021 The D Language Foundation<br>Licensed under the Boost License 1.0.<br>
    <a href="https://dlang.org/spec/unittest.html" class="_attribution-link">https://dlang.org/spec/unittest.html</a>
  </p>
</div>
