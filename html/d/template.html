<h1>Templates</h1>       <b>Contents</b>  <ol> <li>
<a href="#explicit_tmp_instantiation">Explicit Template Instantiation</a><ol> <li><a href="#copy_example">Practical Example</a></li> </ol>
</li> <li><a href="#instantiation_scope">Instantiation Scope</a></li> <li><a href="#argument_deduction">Argument Deduction</a></li> <li>
<a href="#template_type_parameters">Template Type Parameters</a><ol> <li><a href="#parameters_specialization">Specialization</a></li> </ol>
</li> <li><a href="#template_this_parameter">Template This Parameters</a></li> <li><a href="#template_value_parameter">Template Value Parameters</a></li> <li>
<a href="#aliasparameters">Template Alias Parameters</a><ol> <li><a href="#typed_alias_op">Typed alias parameters</a></li> <li><a href="#alias_parameter_specialization">Specialization</a></li> </ol>
</li> <li>
<a href="#variadic-templates">Template Sequence Parameters</a><ol> <li><a href="#variadic_template_specialization">Specialization</a></li> </ol>
</li> <li><a href="#template_parameter_def_values">Template Parameter Default Values</a></li> <li><a href="#implicit_template_properties">Eponymous Templates</a></li> <li><a href="#template_ctors">Template Constructors</a></li> <li><a href="#aggregate_templates">Aggregate Templates</a></li> <li><a href="#function-templates">Function Templates</a></li> <li><a href="#variable-template">Enum &amp; Variable Templates</a></li> <li>
<a href="#alias-template">Alias Templates</a><ol> <li><a href="#auto-ref-parameters">Function Templates with Auto Ref Parameters</a></li> </ol>
</li> <li>
<a href="#nested-templates">Nested Templates</a><ol> <li><a href="#implicit-nesting">Implicit Nesting</a></li> <li><a href="#nested_template_limitation">Context Limitation</a></li> </ol>
</li> <li><a href="#recursive_templates">Recursive Templates</a></li> <li><a href="#template_constraints">Template Constraints</a></li> <li><a href="#limitations">Limitations</a></li> </ol>   <p>Templates are D's approach to generic programming. Templates are defined with a <i>TemplateDeclaration</i>: </p>  <pre>TemplateDeclaration:
    template Identifier TemplateParameters Constraintopt { DeclDefsopt }

TemplateParameters:
    ( TemplateParameterListopt )

TemplateParameterList:
    TemplateParameter
    TemplateParameter ,
    TemplateParameter , TemplateParameterList

TemplateParameter:
    TemplateTypeParameter
    TemplateValueParameter
    TemplateAliasParameter
    TemplateSequenceParameter
    TemplateThisParameter
</pre>  <p>The body of the <i>TemplateDeclaration</i> must be syntactically correct even if never instantiated. Semantic analysis is not done until instantiated. A template forms its own scope, and the template body can contain classes, structs, types, enums, variables, functions, and other templates. </p>  <p>Template parameters can be types, values, symbols, or sequences. Types can be any type. Value parameters must be of an integral type, floating point type, or string type and specializations for them must resolve to an integral constant, floating point constant, null, or a string literal. Symbols can be any non-local symbol. Sequences can contain zero or more types, values or symbols. </p>  <p>Template parameter specializations constrain the values or types the <i>TemplateParameter</i> can accept. </p> <p>Template parameter defaults are the value or type to use for the <i>TemplateParameter</i> in case one is not supplied. </p>  <h2 id="explicit_tmp_instantiation">Explicit Template Instantiation</h2>  <p>Templates are explicitly instantiated with: </p>  <pre>TemplateInstance:
    Identifier TemplateArguments

TemplateArguments:
    ! ( TemplateArgumentListopt )
    ! TemplateSingleArgument

TemplateArgumentList:
    TemplateArgument
    TemplateArgument ,
    TemplateArgument , TemplateArgumentList

TemplateArgument:
    Type
    AssignExpression
    Symbol

Symbol:
    SymbolTail
    . SymbolTail

SymbolTail:
    Identifier
    Identifier . SymbolTail
    TemplateInstance
    TemplateInstance . SymbolTail

TemplateSingleArgument:
    Identifier
    FundamentalType
    CharacterLiteral
    StringLiteral
    IntegerLiteral
    FloatLiteral
    true
    false
    null
    this
    SpecialKeyword
</pre>  <p>Once instantiated, the declarations inside the template, called the template members, are in the scope of the <i>TemplateInstance</i>:</p>  <pre data-language="d">template TFoo(T) { alias Ptr = T*; }
...
TFoo!(int).Ptr x; // declare x to be of type int*
</pre>  <p>If the <a href="#TemplateArgument"><i>TemplateArgument</i></a> is one token long, the parentheses can be omitted:</p>  <pre data-language="d">TFoo!int.Ptr x;   // same as TFoo!(int).Ptr x;
</pre>  <p>A template instantiation can be aliased:</p>  <pre data-language="d">template TFoo(T) { alias Ptr = T*; }
alias foo = TFoo!(int);
foo.Ptr x;        // declare x to be of type int*
</pre>  <p>Multiple instantiations of a <i>TemplateDeclaration</i> with the same <i>TemplateArgumentList</i> will all refer to the same instantiation. For example:</p>  <pre data-language="d">template TFoo(T) { T f; }
alias a = TFoo!(int);
alias b = TFoo!(int);
...
a.f = 3;
assert(b.f == 3);  // a and b refer to the same instance of TFoo
</pre>  <p>This is true even if the <i>TemplateInstance</i>s are done in different modules. </p>  <p>Even if template arguments are implicitly converted to the same template parameter type, they still refer to the same instance. This example uses a <a href="#aggregate_templates"><code>struct</code> template</a>:</p>  <pre data-language="d">struct TFoo(int x) { }

// Different template parameters create different struct types
static assert(!is(TFoo!(3) == TFoo!(2)));
// 3 and 2+1 are both 3 of type int - same TFoo instance
static assert(is(TFoo!(3) == TFoo!(2 + 1)));

// 3u is implicitly converted to 3 to match int parameter,
// and refers to exactly the same instance as TFoo!(3)
static assert(is(TFoo!(3) == TFoo!(3u)));
</pre>  <p>If multiple templates with the same <i>Identifier</i> are declared, they are distinct if they have a different number of arguments or are differently specialized. </p>  <h3 id="copy_example">Practical Example</h3>  <p>A simple generic copy template would be:</p>  <pre data-language="d">template TCopy(T)
{
    void copy(out T to, T from)
    {
        to = from;
    }
}
</pre>  <p>To use this template, it must first be instantiated with a specific type:</p>  <pre data-language="d">int i;
TCopy!(int).copy(i, 3);
</pre> <p>See also <a href="#function-templates">function templates</a>.</p>  <h2 id="instantiation_scope">Instantiation Scope</h2>  <p><i>TemplateInstance</i>s are always instantiated in the scope of where the <i>TemplateDeclaration</i> is declared, with the addition of the template parameters being declared as aliases for their deduced types. </p> <p>For example:</p>  <br><br> <em class="u">module a</em> <pre data-language="d">template TFoo(T) { void bar() { func(); } }
</pre>  <em class="u">module b</em> <pre data-language="d">import a;

void func() { }
alias f = TFoo!(int); // error: func not defined in module a
</pre>  and:  <br><br> <em class="u">module a</em> <pre data-language="d">template TFoo(T) { void bar() { func(1); } }
void func(double d) { }
</pre>  <em class="u">module b</em> <pre data-language="d">import a;

void func(int i) { }
alias f = TFoo!(int);
...
f.bar();  // will call a.func(double)
</pre>  <p><i>TemplateParameter</i> specializations and default values are evaluated in the scope of the <i>TemplateDeclaration</i>. </p>  <h2 id="argument_deduction">Argument Deduction</h2>  <p>The types of template parameters are deduced for a particular template instantiation by comparing the template argument with the corresponding template parameter. </p>  <p>For each template parameter, the following rules are applied in order until a type is deduced for each parameter: </p>  <ol> <li>If there is no type specialization for the parameter, the type of the parameter is set to the template argument.</li>  <li>If the type specialization is dependent on a type parameter, the type of that parameter is set to be the corresponding part of the type argument.</li>  <li>If after all the type arguments are examined, there are any type parameters left with no type assigned, they are assigned types corresponding to the template argument in the same position in the <i>TemplateArgumentList</i>.</li>  <li>If applying the above rules does not result in exactly one type for each template parameter, then it is an error.</li> </ol>  <p>For example:</p>  <pre data-language="d">template TFoo(T) { }
alias foo1 = TFoo!(int);     // (1) T is deduced to be int
alias foo2 = TFoo!(char*);   // (1) T is deduced to be char*

template TBar(T : T*) { }
alias bar = TBar!(char*);    // (2) T is deduced to be char

template TAbc(D, U : D[]) { }
alias abc1 = TAbc!(int, int[]);  // (2) D is deduced to be int, U is int[]
alias abc2 = TAbc!(char, int[]); // (4) error, D is both char and int

template TDef(D : E*, E) { }
alias def = TDef!(int*, int);  // (1) E is int
                               // (3) D is int*
</pre>  <p>Deduction from a specialization can provide values for more than one parameter:</p>  <pre data-language="d">template Foo(T: T[U], U)
{
    ...
}

Foo!(int[long])  // instantiates Foo with T set to int, U set to long
</pre>  <p>When considering matches, a class is considered to be a match for any super classes or interfaces:</p>  <pre data-language="d">class A { }
class B : A { }

template TFoo(T : A) { }
alias foo = TFoo!(B);      // (3) T is B

template TBar(T : U*, U : A) { }
alias bar = TBar!(B*, B);  // (2) T is B*
                           // (3) U is B
</pre>  <h2 id="template_type_parameters">Template Type Parameters</h2>  <pre>TemplateTypeParameter:
    Identifier
    Identifier TemplateTypeParameterSpecialization
    Identifier TemplateTypeParameterDefault
    Identifier TemplateTypeParameterSpecialization TemplateTypeParameterDefault

TemplateTypeParameterSpecialization:
    : Type

TemplateTypeParameterDefault:
    = Type
</pre>  <h3 id="parameters_specialization">Specialization</h3>  <p>Templates may be specialized for particular types of arguments by following the template parameter identifier with a : and the specialized type. For example:</p>  <pre data-language="d">template TFoo(T)        { ... } // #1
template TFoo(T : T[])  { ... } // #2
template TFoo(T : char) { ... } // #3
template TFoo(T, U, V)  { ... } // #4

alias foo1 = TFoo!(int);            // instantiates #1
alias foo2 = TFoo!(double[]);       // instantiates #2 with T being double
alias foo3 = TFoo!(char);           // instantiates #3
alias fooe = TFoo!(char, int);      // error, number of arguments mismatch
alias foo4 = TFoo!(char, int, int); // instantiates #4
</pre>  <p>The template picked to instantiate is the one that is most specialized that fits the types of the <i>TemplateArgumentList</i>. If the result is ambiguous, it is an error. </p>   <h2 id="template_this_parameter">Template This Parameters</h2>  <pre>TemplateThisParameter:
    this TemplateTypeParameter
</pre>  <p><i>TemplateThisParameter</i>s are used in member function templates to pick up the type of the <i>this</i> reference. It also will infer the mutability of the <code>this</code> reference. For example, if <code>this</code> is <code>const</code>, then the function is marked <code>const</code>.</p>  <pre data-language="d">import std.stdio;

struct S
{
    const void foo(this T)(int i)
    {
        writeln(typeid(T));
    }
}

void main()
{
    const(S) s;
    (&amp;s).foo(1);
    S s2;
    s2.foo(2);
    immutable(S) s3;
    s3.foo(3);
}
</pre>  Prints:  <pre>const(S)
S
immutable(S)
</pre>  <p>This is especially useful when used with inheritance. For example, consider the implementation of a final base method which returns a derived class type. Typically this would return a base type, but that would prohibit calling or accessing derived properties of the type:</p>  <pre data-language="d">interface Addable(T)
{
    final auto add(T t)
    {
        return this;
    }
}

class List(T) : Addable!T
{
    List remove(T t)
    {
        return this;
    }
}

void main()
{
    auto list = new List!int;
    list.add(1).remove(1);  // error: no 'remove' method for Addable!int
}
</pre>  <p>Here the method <code>add</code> returns the base type, which doesn't implement the <code>remove</code> method. The <code>template this</code> parameter can be used for this purpose:</p>  <pre data-language="d">interface Addable(T)
{
    final R add(this R)(T t)
    {
        return cast(R)this;  // cast is necessary, but safe
    }
}

class List(T) : Addable!T
{
    List remove(T t)
    {
        return this;
    }
}

void main()
{
    auto list = new List!int;
    list.add(1).remove(1);  // ok
}
</pre>  <h2 id="template_value_parameter">Template Value Parameters</h2>  <pre>TemplateValueParameter:
    BasicType Declarator
    BasicType Declarator TemplateValueParameterSpecialization
    BasicType Declarator TemplateValueParameterDefault
    BasicType Declarator TemplateValueParameterSpecialization TemplateValueParameterDefault

TemplateValueParameterSpecialization:
    : ConditionalExpression

TemplateValueParameterDefault:
    = AssignExpression
    = SpecialKeyword
</pre>  <p>Template value parameter types can be any type which can be statically initialized at compile time. Template value arguments can be integer values, floating point values, nulls, string values, array literals of template value arguments, associative array literals of template value arguments, or struct literals of template value arguments.</p>  <pre data-language="d">template foo(string s)
{
    string bar() { return s ~ " betty"; }
}

void main()
{
    writefln("%s", foo!("hello").bar()); // prints: hello betty
}
</pre>  <p>This example of template foo has a value parameter that is specialized for 10:</p>  <pre data-language="d">template foo(U : int, int v : 10)
{
    U x = v;
}

void main()
{
    assert(foo!(int, 10).x == 10);
}
</pre>  <h2 id="aliasparameters">Template Alias Parameters</h2>  <pre>TemplateAliasParameter:
    alias Identifier TemplateAliasParameterSpecializationopt TemplateAliasParameterDefaultopt
    alias BasicType Declarator TemplateAliasParameterSpecializationopt TemplateAliasParameterDefaultopt

TemplateAliasParameterSpecialization:
    : Type
    : ConditionalExpression

TemplateAliasParameterDefault:
    = Type
    = ConditionalExpression
</pre>  <p>Alias parameters enable templates to be parameterized with symbol names or values computed at compile-time. Almost any kind of D symbol can be used, including user-defined type names, global names, local names, module names, template names, and template instance names. </p>  <p><b>Symbol examples:</b></p>  <ul> <li>User-defined type names  <pre data-language="d">class Foo
{
    static int x;
}

template Bar(alias a)
{
    alias sym = a.x;
}

void main()
{
    alias bar = Bar!(Foo);
    bar.sym = 3;  // sets Foo.x to 3
}
</pre> </li>  <li>Global names  <pre data-language="d">shared int x;

template Foo(alias var)
{
    auto ptr = &amp;var;
}

void main()
{
    alias bar = Foo!(x);
    *bar.ptr = 3;       // set x to 3
    static shared int y;
    alias abc = Foo!(y);
    *abc.ptr = 3;       // set y to 3
}
</pre> </li>  <li>Local names <pre data-language="d">template Foo(alias var)
{
    void inc() { var++; }
}

void main()
{
    int v = 4;
    alias foo = Foo!v;
    foo.inc();
    assert(v == 5);
}
</pre> See also <a href="#implicit-nesting">Implicit Template Nesting</a>. </li>  <li>Module names  <pre data-language="d">import std.conv;

template Foo(alias a)
{
    alias sym = a.text;
}

void main()
{
    alias bar = Foo!(std.conv);
    bar.sym(3);   // calls std.conv.text(3)
}
</pre> </li>  <li>Template names  <pre data-language="d">shared int x;

template Foo(alias var)
{
    auto ptr = &amp;var;
}

template Bar(alias Tem)
{
    alias instance = Tem!(x);
}

void main()
{
    alias bar = Bar!(Foo);
    *bar.instance.ptr = 3;  // sets x to 3
}
</pre> </li>  <li>Template instance names  <pre data-language="d">shared int x;

template Foo(alias var)
{
    auto ptr = &amp;var;
}

template Bar(alias sym)
{
    alias p = sym.ptr;
}

void main()
{
    alias foo = Foo!(x);
    alias bar = Bar!(foo);
    *bar.p = 3;  // sets x to 3
}
</pre> </li> </ul>  <p><b>Value examples:</b></p>  <ul> <li>Literals  <pre data-language="d">template Foo(alias x, alias y)
{
    static int i = x;
    static string s = y;
}

void main()
{
    alias foo = Foo!(3, "bar");
    writeln(foo.i, foo.s);  // prints 3bar
}
</pre> </li>  <li>Compile-time values  <pre data-language="d">template Foo(alias x)
{
    static int i = x;
}

void main()
{
    // compile-time argument evaluation
    enum two = 1 + 1;
    alias foo = Foo!(5 * two);
    assert(foo.i == 10);
    static assert(foo.stringof == "Foo!10");

    // compile-time function evaluation
    int get10() { return 10; }
    alias bar = Foo!(get10());
    // bar is the same template instance as foo
    assert(&amp;bar.i is &amp;foo.i);
}
</pre> </li>  <li>Lambdas  <pre data-language="d">template Foo(alias fun)
{
    enum val = fun(2);
}

alias foo = Foo!((int x) =&gt; x * x);
static assert(foo.val == 4);
</pre> </li> </ul>  <h3 id="typed_alias_op">Typed alias parameters</h3>  <p>Alias parameters can also be typed. These parameters will accept symbols of that type:</p>  <pre data-language="d">template Foo(alias int x) { }
int x;
float f;

Foo!x;  // ok
Foo!f;  // fails to instantiate
</pre>  <h3 id="alias_parameter_specialization">Specialization</h3>  <p>Alias parameters can accept both literals and user-defined type symbols, but they are less specialized than the matches to type parameters and value parameters:</p>  <pre data-language="d">template Foo(T)         { ... }  // #1
template Foo(int n)     { ... }  // #2
template Foo(alias sym) { ... }  // #3

struct S {}
int var;

alias foo1  = Foo!(S);      // instantiates #1
alias foo2  = Foo!(1);      // instantiates #2
alias foo3a = Foo!([1,2]);  // instantiates #3
alias foo3b = Foo!(var);    // instantiates #3
</pre>  <pre data-language="d">template Bar(alias A) { ... }                 // #4
template Bar(T : U!V, alias U, V...) { ... }  // #5

class C(T) {}
alias bar = Bar!(C!int);    // instantiates #5
</pre>  <h2 id="variadic-templates">Template Sequence Parameters</h2>  <pre>TemplateSequenceParameter:
    Identifier ...
</pre>  <p>If the last template parameter in the <i>TemplateParameterList</i> is declared as a <i>TemplateSequenceParameter</i>, it is a match with any trailing template arguments. Such a sequence of arguments can be defined using the template <a href="https://dlang.org/phobos/std_meta.html#AliasSeq"><code>std.meta.AliasSeq</code></a> and will thus henceforth be referred to by that name for clarity. An <i>AliasSeq</i> is not itself a type, value, or symbol. It is a compile-time sequence of any mix of types, values or symbols. </p>  <p>An <i>AliasSeq</i> whose elements consist entirely of types is called a type sequence or <i>TypeSeq</i>. An <i>AliasSeq</i> whose elements consist entirely of values is called a value sequence or <i>ValueSeq</i>. </p>  <p>An <i>AliasSeq</i> can be used as an argument list to instantiate another template, or as the list of parameters for a function.</p>  <pre data-language="d">template print(args...)
{
    void print()
    {
        writeln("args are ", args); // args is a ValueSeq
    }
}

template write(Args...)
{
    void write(Args args) // Args is a TypeSeq
                          // args is a ValueSeq
    {
        writeln("args are ", args);
    }
}

void main()
{
    print!(1,'a',6.8).print();                    // prints: args are 1a6.8
    write!(int, char, double).write(1, 'a', 6.8); // prints: args are 1a6.8
}
</pre>  <p>The number of elements in an <i>AliasSeq</i> can be retrieved with the <code>.length</code> property. The <i>n</i>th element can be retrieved by indexing the <i>AliasSeq</i> with [<i>n</i>], and sub-sequences are denoted by the slicing syntax. </p>  <p><i>AliasSeq</i>-s are static compile-time entities, there is no way to dynamically change, add, or remove elements either at compile-time or run-time. </p>  <p>Type sequences can be deduced from the trailing parameters of an implicitly instantiated function template:</p>  <pre data-language="d">template print(T, Args...)
{
    void print(T first, Args args)
    {
        writeln(first);
        static if (args.length) // if more arguments
            print(args);        // recurse for remaining arguments
    }
}

void main()
{
    print(1, 'a', 6.8);
}
</pre>  prints:  <pre>1
a
6.8
</pre>  <p>Type sequences can also be deduced from the type of a delegate or function parameter list passed as a function argument:</p>  <pre data-language="d">import std.stdio;

/* Partially applies a delegate by tying its first argument to a particular value.
 * R = return type
 * T = first argument type
 * Args = TypeSeq of remaining argument types
 */
R delegate(Args) partial(R, T, Args...)(R delegate(T, Args) dg, T first)
{
    // return a closure
    return (Args args) =&gt; dg(first, args);
}

void main()
{
    int plus(int x, int y, int z)
    {
        return x + y + z;
    }

    auto plus_two = partial(&amp;plus, 2);
    writefln("%d", plus_two(6, 8)); // prints 16
}
</pre> See also: <a href="https://dlang.org/phobos/std_functional.html#partial"><code>std.functional.partial</code></a>  <h3 id="variadic_template_specialization">Specialization</h3>  <p>If both a template with a sequence parameter and a template without a sequence parameter exactly match a template instantiation, the template without a <i>TemplateSequenceParameter</i> is selected.</p>  <pre data-language="d">template Foo(T)         { pragma(msg, "1"); }   // #1
template Foo(int n)     { pragma(msg, "2"); }   // #2
template Foo(alias sym) { pragma(msg, "3"); }   // #3
template Foo(Args...)   { pragma(msg, "4"); }   // #4

import std.stdio;

// Any sole template argument will never match to #4
alias foo1 = Foo!(int);          // instantiates #1
alias foo2 = Foo!(3);            // instantiates #2
alias foo3 = Foo!(std);          // instantiates #3

alias foo4 = Foo!(int, 3, std);  // instantiates #4
</pre>  <h2 id="template_parameter_def_values">Template Parameter Default Values</h2>  <p>Trailing template parameters can be given default values:</p>  <pre data-language="d">template Foo(T, U = int) { ... }
Foo!(uint,long); // instantiate Foo with T as uint, and U as long
Foo!(uint);      // instantiate Foo with T as uint, and U as int

template Foo(T, U = T*) { ... }
Foo!(uint);      // instantiate Foo with T as uint, and U as uint*
</pre>  <h2 id="implicit_template_properties">Eponymous Templates</h2>  <p>If a template contains members whose name is the same as the template identifier then these members are assumed to be referred to in a template instantiation:</p>  <pre data-language="d">template foo(T)
{
    T foo; // declare variable foo of type T
}

void main()
{
    foo!(int) = 6; // instead of foo!(int).foo
}
</pre>  <p>Using functions and more types than the template:</p>  <pre data-language="d">template foo(S, T)
{
    // each member contains all the template parameters
    void foo(S s, T t) {}
    void foo(S s, T t, string) {}
}

void main()
{
    foo(1, 2, "test"); // foo!(int, int).foo(1, 2, "test")
    foo(1, 2); // foo!(int, int).foo(1, 2)
}
</pre>  <p>When the template parameters must be deduced, the eponymous members can't rely on a <a href="version.html#StaticIfCondition"><code>static if</code></a> condition since the deduction relies on how the in members are used:</p>  <pre data-language="d">template foo(T)
{
    static if (is(T)) // T is not yet known...
        void foo(T t) {} // T is deduced from the member usage
}

void main()
{
    foo(0); // Error: cannot deduce function from argument types
    foo!int(0); // Ok since no deduction necessary
}
</pre>  <h2 id="template_ctors">Template Constructors</h2>  <pre>ConstructorTemplate:
    this TemplateParameters Parameters MemberFunctionAttributesopt Constraintopt :
    this TemplateParameters Parameters MemberFunctionAttributesopt Constraintopt FunctionBody
</pre>  <p>Templates can be used to form constructors for classes and structs. </p>  <h2 id="aggregate_templates">Aggregate Templates</h2>  <pre>ClassTemplateDeclaration:
    class Identifier TemplateParameters ;
    class Identifier TemplateParameters Constraintopt BaseClassListopt AggregateBody
    class Identifier TemplateParameters BaseClassListopt Constraintopt AggregateBody

InterfaceTemplateDeclaration:
    interface Identifier TemplateParameters ;
    interface Identifier TemplateParameters Constraintopt BaseInterfaceListopt AggregateBody
    interface Identifier TemplateParameters BaseInterfaceList Constraint AggregateBody

StructTemplateDeclaration:
    struct Identifier TemplateParameters ;
    struct Identifier TemplateParameters Constraintopt AggregateBody

UnionTemplateDeclaration:
    union Identifier TemplateParameters ;
    union Identifier TemplateParameters Constraintopt AggregateBody
</pre>  <p>If a template declares exactly one member, and that member is a class with the same name as the template (see <a href="#implicit_template_properties">Eponymous Templates</a>:)</p>  <pre data-language="d">template Bar(T)
{
    class Bar
    {
        T member;
    }
}
</pre>  then the semantic equivalent, called a <i>ClassTemplateDeclaration</i> can be written as:  <pre data-language="d">class Bar(T)
{
    T member;
}
</pre>  <p>See also: <a href="#template_this_parameter">Template This Parameters</a>. </p>  <p>Analogously to class templates, struct, union and interfaces can be transformed into templates by supplying a template parameter list. </p>  <h2 id="function-templates">Function Templates</h2>  <p>If a template declares exactly one member, and that member is a function with the same name as the template, it is a function template declaration. Alternatively, a function template declaration is a function declaration with a <a href="#TemplateParameterList"><i>TemplateParameterList</i></a> immediately preceding the <a href="function.html#Parameters"><i>Parameters</i></a>. </p>  <p>A function template to compute the square of type <i>T</i> is:</p>  <pre data-language="d">T Square(T)(T t)
{
    return t * t;
}
</pre>  <p>It is lowered to:</p>  <pre data-language="d">template Square(T)
{
    T Square(T t)
    {
        return t * t;
    }
}
</pre>  <p>Function templates can be explicitly instantiated with a !(<i>TemplateArgumentList</i>):</p>  <pre data-language="d">writefln("The square of %s is %s", 3, Square!(int)(3));
</pre>  or implicitly, where the <i>TemplateArgumentList</i> is deduced from the types of the function arguments:  <pre data-language="d">writefln("The square of %s is %s", 3, Square(3));  // T is deduced to be int
</pre>  <p>If there are fewer arguments supplied in the <i>TemplateArgumentList</i> than parameters in the <i>TemplateParameterList</i>, the arguments fulfill parameters from left to right, and the rest of the parameters are then deduced from the function arguments. </p>  <p>The process of deducing template type parameters from function arguments is called Implicit Function Template Instantiation (IFTI). </p>  <p>Function template type parameters that are to be implicitly deduced may not have specializations:</p>  <pre data-language="d">void Foo(T : T*)(T t) { ... }

int x,y;
Foo!(int*)(x);   // ok, T is not deduced from function argument
Foo(&amp;y);         // error, T has specialization
</pre>  <p>Template arguments not implicitly deduced can have default values:</p>  <pre data-language="d">void Foo(T, U=T*)(T t) { U p; ... }

int x;
Foo(x);    // T is int, U is int*
</pre>  <p>If template type parameters match the literal expressions on function arguments, the deduced types may consider narrowing conversions of them. </p>  <pre data-language="d">void foo(T)(T v)        { pragma(msg, "in foo, T = ", T); }
void bar(T)(T v, T[] a) { pragma(msg, "in bar, T = ", T); }

foo(1);
// an integer literal type is analyzed as int by default
// then T is deduced to int

short[] arr;
bar(1, arr);
// arr is short[], and the integer literal 1 is
// implicitly convertible to short.
// then T will be deduced to short.

bar(1, [2.0, 3.0]);
// the array literal is analyzed as double[],
// and the integer literal 1 is implicitly convertible to double.
// then T will be deduced to double.
</pre>   <p>The deduced type parameter for dynamic array and pointer arguments has an unqualified head:</p>  <pre data-language="d">void foo(T)(T arg) { pragma(msg, T); }

int[] marr;
const(int[]) carr;
immutable(int[]) iarr;
foo(marr);  // T == int[]
foo(carr);  // T == const(int)[]
foo(iarr);  // T == immutable(int)[]

int* mptr;
const(int*) cptr;
immutable(int*) iptr;
foo(mptr);  // T == int*
foo(cptr);  // T == const(int)*
foo(iptr);  // T == immutable(int)*
</pre>  <p>Type parameter deduction is not influenced by the order of function arguments. </p>  <p>Function templates can have their return types deduced based on the <a href="statement.html#ReturnStatement"><i>ReturnStatement</i></a>s in the function, just as with normal functions. See <a href="https://dlang.org/function.html#auto-functions">Auto Functions</a>. </p>  <pre data-language="d">auto Square(T)(T t)
{
    return t * t;
}
</pre>  <p>Variadic Function Templates can have parameters with default values. These parameters are always set to their default value in case of IFTI. </p>   <pre data-language="d">size_t fun(T...)(T t, string file = __FILE__)
{
    import std.stdio;
    writeln(file, " ", t);
    return T.length;
}
assert(fun(1, "foo") == 2);  // uses IFTI
assert(fun!int(1, "foo") == 1);  // no IFTI
</pre>   <h2 id="variable-template">Enum &amp; Variable Templates</h2>  <p>Like aggregates and functions, manifest constant and variable declarations can have template parameters, providing there is an <a href="declaration.html#Initializer"><i>Initializer</i></a>:</p>  <pre data-language="d">enum string constant(TL...) = TL.stringof;
ubyte[T.sizeof] storage(T) = 0;
auto array(alias a) = a;
</pre>  <p>These declarations are transformed into templates:</p>  <pre data-language="d">template constant(TL...)
{
    enum string constant = TL.stringof;
}
template storage(T)
{
    ubyte[T.sizeof] storage = 0;
}
template array(alias a)
{
    auto array = a;
}
</pre>  <h2 id="alias-template">Alias Templates</h2>  <p><a href="declaration.html#AliasDeclaration"><i>AliasDeclaration</i></a> can also have optional template parameters:</p>  <pre data-language="d">alias Sequence(TL...) = TL;
</pre>  It is lowered to:  <pre data-language="d">template Sequence(TL...)
{
    alias Sequence = TL;
}
</pre>  <h3 id="auto-ref-parameters">Function Templates with Auto Ref Parameters</h3>  <p>An auto ref function template parameter becomes a ref parameter if its corresponding argument is an lvalue, otherwise it becomes a value parameter:</p>  <pre data-language="d">int foo(Args...)(auto ref Args args)
{
    int result;

    foreach (i, v; args)
    {
        if (v == 10)
            assert(__traits(isRef, args[i]));
        else
            assert(!__traits(isRef, args[i]));
        result += v;
    }
    return result;
}

void main()
{
    int y = 10;
    int r;
    r = foo(8);       // returns 8
    r = foo(y);       // returns 10
    r = foo(3, 4, y); // returns 17
    r = foo(4, 5, y); // returns 19
    r = foo(y, 6, y); // returns 26
}
</pre>  <p>Auto ref parameters can be combined with auto ref return attributes:</p>  <pre data-language="d">auto ref min(T, U)(auto ref T lhs, auto ref U rhs)
{
    return lhs &gt; rhs ? rhs : lhs;
}

void main()
{
    int x = 7, y = 8;
    int i;

    i = min(4, 3);     // returns 3
    i = min(x, y);     // returns 7
    min(x, y) = 10;    // sets x to 10
    static assert(!__traits(compiles, min(3, y) = 10));
    static assert(!__traits(compiles, min(y, 3) = 10));
}
</pre>  <h2 id="nested-templates">Nested Templates</h2>  <p>If a template is declared in aggregate or function local scope, the instantiated functions will implicitly capture the context of the enclosing scope.</p>  <pre data-language="d">class C
{
    int num;

    this(int n) { num = n; }

    template Foo()
    {
        // 'foo' can access 'this' reference of class C object.
        void foo(int n) { this.num = n; }
    }
}

void main()
{
    auto c = new C(1);
    assert(c.num == 1);

    c.Foo!().foo(5);
    assert(c.num == 5);

    template Bar()
    {
        // 'bar' can access local variable of 'main' function.
        void bar(int n) { c.num = n; }
    }
    Bar!().bar(10);
    assert(c.num == 10);
}
</pre>  <p>Above, <code>Foo!().foo</code> will work just the same as a member function of class <code>C</code>, and <code>Bar!().bar</code> will work just the same as a nested function within function <code>main()</code>.</p>  <h3 id="implicit-nesting">Implicit Nesting</h3>  <p>If a template has a <a href="#aliasparameters">template alias parameter</a>, and is instantiated with a local symbol, the instantiated function will implicitly become nested in order to access runtime data of the given local symbol.</p>  <pre data-language="d">template Foo(alias sym)
{
    void foo() { sym = 10; }
}

class C
{
    int num;

    this(int n) { num = n; }

    void main()
    {
        assert(this.num == 1);

        alias fooX = Foo!(C.num).foo;

        // fooX will become member function implicitly, so &amp;fooX
        //     returns a delegate object.
        static assert(is(typeof(&amp;fooX) == delegate));

        fooX(); // called by using valid 'this' reference.
        assert(this.num == 10);  // OK
    }
}

void main()
{
    new C(1).main();

    int num;
    alias fooX = Foo!num.foo;

    // fooX will become nested function implicitly, so &amp;fooX
    //     returns a delegate object.
    static assert(is(typeof(&amp;fooX) == delegate));

    fooX();
    assert(num == 10);  // OK
}
</pre>  <p>Not only functions, but also instantiated class and struct types can become nested via implicitly captured context.</p>  <pre data-language="d">class C
{
    int num;
    this(int n) { num = n; }

    class N(T)
    {
        // instantiated class N!T can become nested in C
        T foo() { return num * 2; }
    }
}

void main()
{
    auto c = new C(10);
    auto n = c.new N!int();
    assert(n.foo() == 20);
}
</pre>  <p>See also: <a href="class.html#nested-explicit">Nested Class Instantiation</a>.</p>  <pre data-language="d">void main()
{
    int num = 10;
    struct S(T)
    {
        // instantiated struct S!T can become nested in main()
        T foo() { return num * 2; }
    }
    S!int s;
    assert(s.foo() == 20);
}
</pre>  <p>A templated <code>struct</code> can become a nested <code>struct</code> if it is instantiated with a local symbol passed as an aliased argument:</p>  <pre data-language="d">struct A(alias F)
{
    int fun(int i) { return F(i); }
}

A!F makeA(alias F)() { return A!F(); }

void main()
{
    int x = 40;
    int fun(int i) { return x + i; }
    A!fun a = makeA!fun();
    assert(a.fun(2) == 42);
}
</pre>  <h3 id="nested_template_limitation">Context Limitation</h3>  <p>Currently nested templates can capture at most one context. As a typical example, non-static template member functions cannot take local symbol by using template alias parameter.</p>  <pre data-language="d">class C
{
    int num;
    void foo(alias sym)() { num = sym * 2; }
}

void main()
{
    auto c = new C();
    int var = 10;
    c.foo!var();    // NG, foo!var requires two contexts, 'this' and 'main()'
}
</pre>  <p>But, if one context is indirectly accessible from other context, it is allowed.</p>  <pre data-language="d">int sum(alias x, alias y)() { return x + y; }

void main()
{
    int a = 10;
    void nested()
    {
        int b = 20;
        assert(sum!(a, b)() == 30);
    }
    nested();
}
</pre>  <p>Two local variables <code>a</code> and <code>b</code> are in different contexts, but outer context is indirectly accessible from innter context, so nested template instance <code>sum!(a, b)</code> will capture only inner context.</p>  <h2 id="recursive_templates">Recursive Templates</h2>  <p>Template features can be combined to produce some interesting effects, such as compile time evaluation of non-trivial functions. For example, a factorial template can be written:</p>   <pre data-language="d">template factorial(int n)
{
    static if (n == 1)
        enum factorial = 1;
    else
        enum factorial = n * factorial!(n - 1);
}

static assert(factorial!(4) == 24);
</pre>  <p>For more information and a <acronym title="Compile-time Function Execution">CTFE</acronym> (Compile-time Function Execution) factorial alternative, see: <a href="https://dlang.org/articles/templates-revisited.html#template-recursion">Template Recursion</a>. </p>  <h2 id="template_constraints">Template Constraints</h2>  <pre>Constraint:
    if ( Expression )
</pre>  <p><i>Constraint</i>s are used to impose additional constraints on matching arguments to a template beyond what is possible in the <a href="#TemplateParameterList"><i>TemplateParameterList</i></a>. The <i>Expression</i> is computed at compile time and returns a result that is converted to a boolean value. If that value is true, then the template is matched, otherwise the template is not matched. </p>  <p>For example, the following function template only matches with odd values of <code>N</code>:</p>  <pre data-language="d">void foo(int N)()
    if (N &amp; 1)
{
    ...
}
...
foo!(3)();  // OK, matches
foo!(4)();  // Error, no match
</pre>  <p>Template constraints can be used with aggregate types (structs, classes, unions). Constraints are effectively used with library module <a href="https://dlang.org/phobos/std_traits.html"><code>std.traits</code></a>:</p>  <pre data-language="d">import std.traits;

struct Bar(T)
    if (isIntegral!T)
{
    ...
}
...
auto x = Bar!int;       // OK, int is an integral type
auto y = Bar!double;    // Error, double does not satisfy constraint
</pre>  <h2 id="limitations">Limitations</h2>  <p>Templates cannot be used to add non-static fields or virtual functions to classes or interfaces. For example:</p>  <pre data-language="d">class Foo
{
    template TBar(T)
    {
        T xx;               // becomes a static field of Foo
        int func(T) { ... } // non-virtual

        static T yy;                        // Ok
        static int func(T t, int y) { ... } // Ok
    }
}
</pre>     <div class="_attribution">
  <p class="_attribution-p">
    &copy; 1999&ndash;2021 The D Language Foundation<br>Licensed under the Boost License 1.0.<br>
    <a href="https://dlang.org/spec/template.html" class="_attribution-link">https://dlang.org/spec/template.html</a>
  </p>
</div>
