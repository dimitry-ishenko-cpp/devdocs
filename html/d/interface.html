<h1>Interfaces</h1>       <b>Contents</b>  <ol> <li><a href="#interface-contracts">Interfaces with Contracts</a></li> <li><a href="#const-interface">Const and Immutable Interfaces</a></li> <li><a href="#com-interfaces">COM Interfaces</a></li> <li><a href="#cpp-interfaces">C++ Interfaces</a></li> </ol>   <p>An <i>Interface</i> describes a list of functions that a class which inherits from the interface must implement.</p>  <pre>InterfaceDeclaration:
    interface Identifier ;
    interface Identifier BaseInterfaceListopt AggregateBody
    InterfaceTemplateDeclaration

BaseInterfaceList:
    : Interfaces
</pre>  <b>Implementation Defined:</b> <p>Specialized interfaces may be supported:</p>  <ol> <li>
<a href="#com-interfaces"><i>COM Interfaces</i></a> are binary compatible with COM/OLE/ActiveX objects for Windows. </li>  <li>
<a href="#cpp-interfaces"><i>C++ Interfaces</i></a> are binary compatible with C++ abstract classes. </li>  <li>
<a href="objc_interface.html#protocols">Objective-C Interfaces</a> are binary compatible with Objective-C protocols. </li> </ol>    <p>A class that implements an interface can be implicitly converted to a reference to that interface.</p>  <p>Interfaces cannot derive from classes; only from other interfaces. Classes cannot derive from an interface multiple times. </p>  <pre data-language="d">interface D
{
    void foo();
}

class A : D, D  // error, duplicate interface
{
}
</pre>  <p>An instance of an interface cannot be created.</p>  <pre data-language="d">interface D
{
    void foo();
}

...

D d = new D();  // error, cannot create instance of interface
</pre>   <p>Virtual interface member functions do not have implementations. Interfaces are expected to implement static or final functions. </p>  <pre data-language="d">interface D
{
    void bar() { }  // error, implementation not allowed
    static void foo() { } // ok
    final void abc() { } // ok
}
</pre>  <p>Interfaces can have function templates in the members. All instantiated functions are implicitly <code>final</code>. </p>  <pre data-language="d">interface D
{
    void foo(T)() { }  // ok, it's implicitly final
}
</pre>  <p>Classes that inherit from an interface may not override final or static interface member functions.</p>  <pre data-language="d">interface D
{
    void bar();
    static void foo() { }
    final void abc() { }
}

class C : D
{
    void bar() { } // ok
    void foo() { } // error, cannot override static D.foo()
    void abc() { } // error, cannot override final D.abc()
}
</pre>   <p>All interface functions must be defined in a class that inherits from that interface: </p> <pre data-language="d">interface D
{
    void foo();
}

class A : D
{
    void foo() { }  // ok, provides implementation
}

class B : D
{
    int foo() { }   // error, no void foo() implementation
}
</pre>  <p>Interfaces can be inherited and functions overridden:</p>  <pre data-language="d">interface D
{
    int foo();
}

class A : D
{
    int foo() { return 1; }
}

class B : A
{
    int foo() { return 2; }
}

...

B b = new B();
b.foo();            // returns 2
D d = cast(D) b;    // ok since B inherits A's D implementation
d.foo();            // returns 2;
</pre>  <p>Interfaces can be reimplemented in derived classes:</p>  <pre data-language="d">interface D
{
    int foo();
}

class A : D
{
    int foo() { return 1; }
}

class B : A, D
{
    int foo() { return 2; }
}

...

B b = new B();
b.foo();            // returns 2
D d = cast(D) b;
d.foo();            // returns 2
A a = cast(A) b;
D d2 = cast(D) a;
d2.foo();           // returns 2, even though it is A's D, not B's D
</pre>  <p>A reimplemented interface must implement all the interface functions, it does not inherit them from a super class: </p>  <pre data-language="d">interface D
{
    int foo();
}

class A : D
{
    int foo() { return 1; }
}

class B : A, D
{
}       // error, no foo() for interface D
</pre>  <h2><span id="InterfaceContracts">Interfaces with Contracts</span></h2> <p>Interface member functions can have contracts even though there is no body for the function. The contracts are inherited by any class member function that implements that interface member function. </p>  <pre data-language="d">interface I
{
    int foo(int i)
    in { assert(i &gt; 7); }
    out (result) { assert(result &amp; 1); }

    void bar();
}
</pre>  <h2><span id="ConstInterface">Const and Immutable Interfaces</span></h2> <p>If an interface has <code>const</code> or <code>immutable</code> storage class, then all members of the interface are <code>const</code> or <code>immutable</code>. This storage class is not inherited. </p>   <h2><span id="COM-Interfaces">COM Interfaces</span></h2> <p>A variant on interfaces is the COM interface. A COM interface is designed to map directly onto a Windows COM object. Any COM object can be represented by a COM interface, and any D object with a COM interface can be used by external COM clients. </p>  <p>A COM interface is defined as one that derives from the interface <code>core.stdc.win</code>Â­<code>dows.com.IUnknown</code>. A COM interface differs from a regular D interface in that: </p>  <ul> <li>It derives from the interface <code>core.stdc.windows.com.IUnknown</code>.</li> <li>It cannot be the argument to <a href="https://dlang.org/phobos/object.html#destroy"><code>destroy</code></a>.</li> <li>References cannot be upcast to the enclosing class object, nor can they be downcast to a derived interface. Implement <code>QueryInterface()</code> for that interface in standard COM fashion to convert to another COM interface.</li> <li>Classes derived from COM interfaces are COM classes.</li> <li>The default linkage for member functions of COM classes is <code>extern(System)</code>.  <b>Note:</b> To implement or override any base-class methods of D interfaces or classes (ones which do not inherit from <code>IUnknown</code>), explicitly mark them as having the <code>extern(D)</code> linkage.  <pre data-language="d">import core.sys.windows.windows;
import core.stdc.windows.com;

interface IText
{
    void write();
}

abstract class Printer : IText
{
    void print() { }
}

class C : Printer, IUnknown
{
    // Implements the IText `write` class method.
    extern(D) void write() { }

    // Overrides the Printer `print` class method.
    extern(D) override void print() { }

    // Overrides the Object base class `toString` method.
    extern(D) override string toString() { return "Class C"; }

    // Methods of class implementing the IUnknown interface have
    // the extern(System) calling convention by default.
    HRESULT QueryInterface(const(IID)*, void**);
    uint AddRef();
    uint Release();
}
</pre>  <p>The same applies to other <code>Object</code> methods such as <code>opCmp</code>, <code>toHash</code>, etc.</p>  </li> <li>The first member of the COM <code>vtbl[]</code> is not the pointer to the InterfaceInfo, but the first virtual function pointer.</li> </ul>  <p>See also <a href="http://www.lunesu.com/uploads/ModernCOMProgramminginD.pdf">Modern COM Programming in D</a> </p>   <h2><span id="CPP-Interfaces">C++ Interfaces</span></h2> <p>C++ interfaces are interfaces declared with C++ linkage: </p>  <pre data-language="d">extern (C++) interface Ifoo
{
    void foo();
    void bar();
}
</pre>  which is meant to correspond with the following C++ declaration:  <pre>class Ifoo
{
    virtual void foo();
    virtual void bar();
};
</pre>  <p>Any interface that derives from a C++ interface is also a C++ interface. A C++ interface differs from a D interface in that: </p>  <ul> <li>It cannot be the argument to <a href="https://dlang.org/phobos/object.html#destroy"><code>destroy</code></a>.</li> <li>References cannot be upcast to the enclosing class object, nor can they be downcast to a derived interface.</li> <li>The C++ calling convention is the default convention for its member functions, rather than the D calling convention.</li> <li>The first member of the <code>vtbl[]</code> is not the pointer to the <code>Interface</code>, but the first virtual function pointer.</li> </ul>      <div class="_attribution">
  <p class="_attribution-p">
    &copy; 1999&ndash;2021 The D Language Foundation<br>Licensed under the Boost License 1.0.<br>
    <a href="https://dlang.org/spec/interface.html" class="_attribution-link">https://dlang.org/spec/interface.html</a>
  </p>
</div>
