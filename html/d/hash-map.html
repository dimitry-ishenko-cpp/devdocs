<h1>Associative Arrays</h1>       <b>Contents</b>  <ol> <li><a href="#removing_keys">Removing Keys</a></li> <li><a href="#testing_membership">Testing Membership</a></li> <li><a href="#using_classes_as_key">Using Classes as the KeyType</a></li> <li><a href="#using_struct_as_key">Using Structs or Unions as the KeyType</a></li> <li><a href="#construction_assignment_entries">Construction or Assignment on Setting AA Entries</a></li> <li><a href="#inserting_if_not_present">Inserting if not present</a></li> <li><a href="#advanced_updating">Advanced updating</a></li> <li><a href="#static_initialization">Static Initialization of AAs</a></li> <li><a href="#runtime_initialization">Runtime Initialization of Immutable AAs</a></li> <li><a href="#construction_and_ref_semantic">Construction and Reference Semantics</a></li> <li>
<a href="#properties">Properties</a><ol> <li><a href="#aa_example">Associative Array Example: word count</a></li> <li><a href="#aa_example_iteration">Associative Array Example: counting Tuples</a></li> </ol>
</li> </ol>   <p>Associative arrays have an index that is not necessarily an integer, and can be sparsely populated. The index for an associative array is called the <i>key</i>, and its type is called the <i>KeyType</i>.</p>  <p>Associative arrays are declared by placing the <i>KeyType</i> within the <code>[ ]</code> of an array declaration: </p>  <pre data-language="d">int[string] aa;   // Associative array of ints that are
                  // indexed by string keys.
                  // The KeyType is string.
aa["hello"] = 3;  // set value associated with key "hello" to 3
int value = aa["hello"];  // lookup value from a key
assert(value == 3);
</pre>  <p>Neither the <i>KeyType</i>s nor the element types of an associative array can be function types or <code>void</code>. </p>  <b>Implementation Defined:</b> The built-in associative arrays do not preserve the order of the keys inserted into the array. In particular, in a <code>foreach</code> loop the order in which the elements are iterated is typically unspecified.  <h2 id="removing_keys">Removing Keys</h2>  <p>Particular keys in an associative array can be removed with the <code>remove</code> function: </p>  <pre data-language="d">aa.remove("hello");
</pre>  <p><code>remove(key)</code> does nothing if the given <i>key</i> does not exist and returns <code>false</code>. If the given <i>key</i> does exist, it removes it from the AA and returns <code>true</code>. </p>  <p>All keys can be removed by using the method <code>clear</code>.</p>  <h2 id="testing_membership">Testing Membership</h2>  <p>The <a href="expression.html#InExpression"><i>InExpression</i></a> yields a pointer to the value if the key is in the associative array, or <code>null</code> if not: </p>  <pre data-language="d">int* p;

p = ("hello" in aa);
if (p !is null)
{
    *p = 4;  // update value associated with key
    assert(aa["hello"] == 4);
}
</pre>  <b>Undefined Behavior:</b> Adjusting the pointer to point before or after the element whose address is returned, and then dereferencing it.  <h2 id="using_classes_as_key">Using Classes as the KeyType</h2>  <p>Classes can be used as the <i>KeyType</i>. For this to work, the class definition must override the following member functions of class <code>Object</code>:</p>  <ul> <li><code>size_t toHash() @trusted nothrow</code></li> <li><code>bool opEquals(Object)</code></li> </ul>  <p>Note that the parameter to <code>opEquals</code> is of type <code>Object</code>, not the type of the class in which it is defined.</p>  <p>For example:</p>  <pre data-language="d">class Foo
{
    int a, b;

    override size_t toHash() { return a + b; }

    override bool opEquals(Object o)
    {
        Foo foo = cast(Foo) o;
        return foo &amp;&amp; a == foo.a &amp;&amp; b == foo.b;
    }
}
</pre>  <b>Implementation Defined:</b> <code>opCmp</code> is not used to check for equality by the associative array. However, since the actual <code>opEquals</code> or <code>opCmp</code> called is not decided until runtime, the compiler cannot always detect mismatched functions. Because of legacy issues, the compiler may reject an associative array key type that overrides <code>opCmp</code> but not <code>opEquals</code>. This restriction may be removed in future versions.  <b>Undefined Behavior:</b> <ol> <li>If <code>toHash</code> must consistently be the same value when <code>opEquals</code> returns true. In other words, two objects that are considered equal should always have the same hash value. Otherwise, undefined behavior will result.</li> </ol>  <b>Best Practices:</b> <ol> <li>Use the attributes <code>@safe</code>, <code>@nogc</code>, <code>pure</code>, <code>const</code>, and <code>scope</code> as much as possible on the <code>toHash</code> and <code>opEquals</code> overrides.</li> </ol>  <h2 id="using_struct_as_key">Using Structs or Unions as the KeyType</h2>  <p>If the <i>KeyType</i> is a struct or union type, a default mechanism is used to compute the hash and comparisons of it based on the fields of the struct value. A custom mechanism can be used by providing the following functions as struct members: </p>  <pre data-language="d">size_t toHash() const @safe pure nothrow;
bool opEquals(ref const typeof(this) s) const @safe pure nothrow;
</pre>  <p>For example:</p>  <pre data-language="d">import std.string;

struct MyString
{
    string str;

    size_t toHash() const @safe pure nothrow
    {
        size_t hash;
        foreach (char c; str)
            hash = (hash * 9) + c;
        return hash;
    }

    bool opEquals(ref const MyString s) const @safe pure nothrow
    {
        return std.string.cmp(this.str, s.str) == 0;
    }
}
</pre>  <p>The functions can use <code>@trusted</code> instead of <code>@safe</code>.</p>  <b>Implementation Defined:</b> <code>opCmp</code> is not used to check for equality by the associative array. For this reason, and for legacy reasons, an associative array key is not allowed to define a specialized <code>opCmp</code>, but omit a specialized <code>opEquals</code>. This restriction may be removed in future versions of D.  <b>Undefined Behavior:</b> <ol> <li>If <code>toHash</code> must consistently be the same value when <code>opEquals</code> returns true. In other words, two structs that are considered equal should always have the same hash value. Otherwise, undefined behavior will result.</li> </ol>  <b>Best Practices:</b> <ol> <li>Use the attributes <code>@nogc</code> as much as possible on the <code>toHash</code> and <code>opEquals</code> overrides.</li> </ol>  <h2 id="construction_assignment_entries">Construction or Assignment on Setting AA Entries</h2>  <p>When an AA indexing access appears on the left side of an assignment operator, it is specially handled for setting an AA entry associated with the key.</p>  <pre data-language="d">string[int] aa;
string s;
s = aa[1];          // throws RangeError in runtime
aa[1] = "hello";    // handled for setting AA entry
s = aa[1];          // succeeds to lookup
assert(s == "hello");
</pre>  <p>If the assigned value type is equivalent with the AA element type:</p>  <ol> <li>If the indexing key does not yet exist in AA, a new AA entry will be allocated, and it will be initialized with the assigned value.</li> <li>If the indexing key already exists in the AA, the setting runs normal assignment.</li> </ol>  <pre data-language="d">struct S
{
    int val;
    void opAssign(S rhs) { this.val = rhs.val * 2; }
}
S[int] aa;
aa[1] = S(10);  // first setting initializes the entry aa[1]
assert(aa[1].val == 10);
aa[1] = S(10);  // second setting invokes normal assignment, and
                // operator-overloading rewrites it to member opAssign function.
assert(aa[1].val == 20);
</pre>  <p>If the assigned value type is <b>not</b> equivalent with the AA element type, the expression could invoke operator overloading with normal indexing access:</p>  <pre data-language="d">struct S
{
    int val;
    void opAssign(int v) { this.val = v * 2; }
}
S[int] aa;
aa[1] = 10;     // is rewritten to: aa[1].opAssign(10), and
                // throws RangeError before opAssign is called
</pre>  <p>However, if the AA element type is a struct which supports an implicit constructor call from the assigned value, implicit construction is used for setting the AA entry:</p>  <pre data-language="d">struct S
{
    int val;
    this(int v) { this.val = v; }
    void opAssign(int v) { this.val = v * 2; }
}
S s = 1;    // OK, rewritten to: S s = S(1);
  s = 1;    // OK, rewritten to: s.opAssign(1);

S[int] aa;
aa[1] = 10; // first setting is rewritten to: aa[1] = S(10);
assert(aa[1].val == 10);
aa[1] = 10; // second setting is rewritten to: aa[1].opAssign(10);
assert(aa[1].val == 20);
</pre>  This is designed for efficient memory reuse with some value-semantics structs, eg. <a href="https://dlang.org/phobos/std_bigint.html#BigInt"><code>std.bigint.BigInt</code></a>.  <pre data-language="d">import std.bigint;
BigInt[string] aa;
aa["a"] = 10;   // construct BigInt(10) and move it in AA
aa["a"] = 20;   // call aa["a"].opAssign(20)
</pre>  <h2 id="inserting_if_not_present">Inserting if not present</h2>  <p>When AA access requires that there must be a value corresponding to the key, a value must be constructed and inserted if not present. The <code>require</code> function provides a means to construct a new value via a lazy argument. The lazy argument is evaluated when the key is not present. The <code>require</code> operation avoids the need to perform multiple key lookups.</p>  <pre data-language="d">class C{}
C[string] aa;

auto a = aa.require("a", new C);   // lookup "a", construct if not present
</pre>  <p>Sometimes it is necessary to know whether the value was constructed or already exists. The <code>require</code> function doesn't provide a boolean parameter to indicate whether the value was constructed but instead allows the construction via a function or delegate. This allows the use of any mechanism as demonstrated below.</p>  <pre data-language="d">class C{}
C[string] aa;

bool constructed;
auto a = aa.require("a", { constructed=true; return new C;}());
assert(constructed == true);

C newc;
auto b = aa.require("b", { newc = new C; return newc;}());
assert(b is newc);
</pre>  <h2 id="advanced_updating">Advanced updating</h2>  <p>Typically updating a value in an associative array is simply done with an assign statement.</p>  <pre data-language="d">int[string] aa;

aa["a"] = 3;  // set value associated with key "a" to 3
</pre>  <p>Sometimes it is necessary to perform different operations depending on whether a value already exists or needs to be constructed. The <code>update</code> function provides a means to construct a new value via the <code>create</code> delegate or update an existing value via the <code>update</code> delegate. The <code>update</code> operation avoids the need to perform multiple key lookups.</p>  <pre data-language="d">class C{}
C[string] aa;

C older;
C newer;
aa.update("a",
{
    newer = new C;
    return newer;
},
(ref C c)
{
    older = c;
    newer = new C;
    return newer;
});
</pre>  <h2 id="static_initialization">Static Initialization of AAs</h2>  NOTE: Not yet implemented.  <pre data-language="d">immutable long[string] aa = [
  "foo": 5,
  "bar": 10,
  "baz": 2000
];

unittest
{
    assert(aa["foo"] == 5);
    assert(aa["bar"] == 10);
    assert(aa["baz"] == 2000);
}
</pre>  <h2 id="runtime_initialization">Runtime Initialization of Immutable AAs</h2>  <p>Immutable associative arrays are often desirable, but sometimes initialization must be done at runtime. This can be achieved with a constructor (static constructor depending on scope), a buffer associative array and <code>assumeUnique</code>:</p>  <pre data-language="d">immutable long[string] aa;

shared static this()
{
    import std.exception : assumeUnique;
    import std.conv : to;

    long[string] temp; // mutable buffer
    foreach(i; 0 .. 10)
    {
        temp[to!string(i)] = i;
    }
    temp.rehash; // for faster lookups

    aa = assumeUnique(temp);
}

unittest
{
    assert(aa["1"] == 1);
    assert(aa["5"] == 5);
    assert(aa["9"] == 9);
}
</pre>  <h2 id="construction_and_ref_semantic">Construction and Reference Semantics</h2>  <p>An Associative Array defaults to <code>null</code>, and is constructed upon assigning the first key/value pair. However, once constructed, an associative array has <i>reference semantics</i>, meaning that assigning one array to another does not copy the data. This is especially important when attempting to create multiple references to the same array.</p>  <pre data-language="d">int[int] aa;             // defaults to null
int[int] aa2 = aa;       // copies the null reference
aa[1] = 1;
assert(aa2.length == 0); // aa2 still is null
aa2 = aa;
aa2[2] = 2;
assert(aa[2] == 2);      // now both refer to the same instance
</pre>  <h2 id="properties">Properties</h2>  <p>Properties for associative arrays are:</p>  <table>
<caption>Associative Array Properties</caption> <tr>
<th class="donthyphenate"><b>Property</b></th>
<th class="donthyphenate"><b>Description</b></th>
</tr> <tr>
<td><code>.sizeof</code></td>
<td>Returns the size of the reference to the associative array; it is 4 in 32-bit builds and 8 on 64-bit builds.</td>
</tr> <tr>
<td><code>.length</code></td>
<td>Returns number of values in the associative array. Unlike for dynamic arrays, it is read-only.</td>
</tr> <tr>
<td><code>.dup</code></td>
<td>Create a new associative array of the same size and copy the contents of the associative array into it.</td>
</tr> <tr>
<td><code>.keys</code></td>
<td>Returns dynamic array, the elements of which are the keys in the associative array.</td>
</tr> <tr>
<td><code>.values</code></td>
<td>Returns dynamic array, the elements of which are the values in the associative array.</td>
</tr> <tr>
<td><code>.rehash</code></td>
<td>Reorganizes the associative array in place so that lookups are more efficient. <code>rehash</code> is effective when, for example, the program is done loading up a symbol table and now needs fast lookups in it. Returns a reference to the reorganized array.</td>
</tr> <tr>
<td><code>.clear</code></td>
<td>Removes all remaining keys and values from an associative array. The array is not rehashed after removal, to allow for the existing storage to be reused. This will affect all references to the same instance and is not equivalent to <code>destroy(aa)</code> which only sets the current reference to <code>null</code>
</td>
</tr> <tr>
<td><code>.byKey()</code></td>
<td>Returns a forward range suitable for use as a <i>ForeachAggregate</i> to a <a href="statement.html#ForeachStatement"><i>ForeachStatement</i></a> which will iterate over the keys of the associative array.</td>
</tr> <tr>
<td><code>.byValue()</code></td>
<td>Returns a forward range suitable for use as a <i>ForeachAggregate</i> to a <a href="statement.html#ForeachStatement"><i>ForeachStatement</i></a> which will iterate over the values of the associative array.</td>
</tr> <tr>
<td><code>.byKeyValue()</code></td>
<td>Returns a forward range suitable for use as a <i>ForeachAggregate</i> to a <a href="statement.html#%20%20%20%20%20%20%20%20ForeachStatement"><i> ForeachStatement</i></a> which will iterate over key-value pairs of the associative array. The returned pairs are represented by an opaque type with <code>.key</code> and <code>.value</code> properties for accessing the key and value of the pair, respectively. Note that this is a low-level interface to iterating over the associative array and is not compatible with the <a href="https://dlang.org/phobos/std_typecons.html#Tuple"><code>Tuple</code></a> type in Phobos. For compatibility with <code>Tuple</code>, use <a href="https://dlang.org/phobos/std_array.html#byPair">std.array.byPair</a> instead.</td>
</tr> <tr>
<td><code>.get(Key key, lazy Value defVal)</code></td>
<td> Looks up <code>key</code>; if it exists returns corresponding value else evaluates and returns <code>defVal</code>.</td>
</tr> <tr>
<td><code>.require(Key key, lazy Value value)</code></td>
<td> Looks up <code>key</code>; if it exists returns corresponding value else evaluates <code>value</code>, adds it to the associative array and returns it.</td>
</tr> <tr>
<td><code>.update(Key key, Value delegate() create, Value delegate(Value) update)</code></td>
<td> Looks up <code>key</code>; if it exists applies the <code>update</code> delegate else evaluates the <code>create</code> delegate and adds it to the associative array</td>
</tr> </table>  <hr> <h3 id="aa_example">Associative Array Example: word count</h3>  <p>Let's consider the file is ASCII encoded with LF EOL. In general case we should use <i>dchar c</i> for iteration over code points and functions from <a href="https://dlang.org/phobos/std_uni.html">std.uni</a>. </p>  <pre data-language="d">import std.file;         // D file I/O
import std.stdio;
import std.ascii;

void main (string[] args)
{
    ulong totalWords, totalLines, totalChars;
    ulong[string] dictionary;

    writeln("   lines   words   bytes file");
    foreach (arg; args[1 .. $]) // for each argument except the first one
    {
        ulong wordCount, lineCount, charCount;

        foreach(line; File(arg).byLine())
        {
            bool inWord;
            size_t wordStart;

            void tryFinishWord(size_t wordEnd)
            {
                if (inWord)
                {
                    auto word = line[wordStart .. wordEnd];
                    ++dictionary[word.idup];   // increment count for word
                    inWord = false;
                }
            }

            foreach (i, char c; line)
            {
                if (std.ascii.isDigit(c))
                {
                    // c is a digit (0..9)
                }
                else if (std.ascii.isAlpha(c))
                {
                    // c is an ASCII letter (A..Z, a..z)
                    if (!inWord)
                    {
                        wordStart = i;
                        inWord = true;
                        ++wordCount;
                    }
                }
                else
                    tryFinishWord(i);
                ++charCount;
            }
            tryFinishWord(line.length);
            ++lineCount;
        }

        writefln("%8s%8s%8s %s", lineCount, wordCount, charCount, arg);
        totalWords += wordCount;
        totalLines += lineCount;
        totalChars += charCount;
    }

    if (args.length &gt; 2)
    {
        writefln("-------------------------------------\n%8s%8s%8s total",
                 totalLines, totalWords, totalChars);
    }

    writeln("-------------------------------------");
    foreach (word; dictionary.keys.sort)
    {
        writefln("%3s %s", dictionary[word], word);
    }
}
</pre>  <h3 id="aa_example_iteration">Associative Array Example: counting Tuples</h3>  <p>An Associative Array can be iterated in key/value fashion using a <a href="statement.html#ForeachStatement">foreach statement</a>. As an example, the number of occurrences of all possible substrings of length 2 (aka 2-mers) in a string will be counted:</p>  <pre data-language="d">import std.range : dropOne, only, save, zip;
import std.stdio : writefln;
import std.typecons : Tuple;
import std.utf : byCodeUnit; // avoids UTF-8 auto-decoding

int[Tuple!(immutable char, immutable char)] aa;

// The string `arr` has a limited alphabet: {A, C, G, T}
// Thus, for better performance, iteration can be done _without_ decoding
auto arr = "AGATAGA".byCodeUnit;

// iterate over all pairs in the string and observe each pair
// ('A', 'G'), ('G', 'A'), ('A', 'T'), ...
foreach (window; arr.zip(arr.save.dropOne))
    aa[window]++;

// iterate over all key/value pairs of the Associative Array
foreach (key, value; aa)
{
    // the second parameter uses tuple-expansion
    writefln("key: %s [%s], value: %d", key, key.expand.only, value);
}
</pre> <pre>% rdmd count.d
key: Tuple!(immutable(char), immutable(char))('A', 'G') [AG], value: 2
key: Tuple!(immutable(char), immutable(char))('G', 'A') [GA], value: 2
key: Tuple!(immutable(char), immutable(char))('A', 'T') [AT], value: 1
key: Tuple!(immutable(char), immutable(char))('T', 'A') [TA], value: 1
</pre>     <div class="_attribution">
  <p class="_attribution-p">
    &copy; 1999&ndash;2021 The D Language Foundation<br>Licensed under the Boost License 1.0.<br>
    <a href="https://dlang.org/spec/hash-map.html" class="_attribution-link">https://dlang.org/spec/hash-map.html</a>
  </p>
</div>
