<h1>std.csv</h1>  <p>Implements functionality to read Comma Separated Values and its variants from an <a href="std_range_primitives.html#isInputRange">input range</a> of <code>dchar</code>. </p>
<p>Comma Separated Values provide a simple means to transfer and store tabular data. It has been common for programs to use their own variant of the CSV format. This parser will loosely follow the <a href="http://tools.ietf.org/html/rfc4180">RFC-4180</a>. CSV input should adhere to the following criteria (differences from RFC-4180 in parentheses): <br><br> <ul> <li>A record is separated by a new line (CRLF,LF,CR)</li> <li>A final record may end with a new line</li> <li>A header may be provided as the first record in input</li> <li>A record has fields separated by a comma (customizable)</li> <li>A field containing new lines, commas, or double quotes should be enclosed in double quotes (customizable)</li> <li>Double quotes in a field are escaped with a double quote</li> <li>Each record should contain the same number of fields</li> </ul> </p> <dl>
<dt>Example</dt>

</dl>
<pre data-language="d">import std.algorithm;
import std.array;
import std.csv;
import std.stdio;
import std.typecons;

void main()
{
    auto text = "Joe,Carpenter,300000\nFred,Blacksmith,400000\r\n";

    foreach (record; csvReader!(Tuple!(string, string, int))(text))
    {
        writefln("%s works as a %s and earns &amp;dollar;%d per year",
                 record[0], record[1], record[2]);
    }

    // To read the same string from the file "filename.csv":

    auto file = File("filename.csv", "r");
    foreach (record;
        file.byLine.joiner("\n").csvReader!(Tuple!(string, string, int)))
    {
        writefln("%s works as a %s and earns &amp;dollar;%d per year",
                 record[0], record[1], record[2]);
    }
}
}
</pre>  When an input contains a header the <code>Contents</code> can be specified as an associative array. Passing null to signify that a header is present.  <pre data-language="d">auto text = "Name,Occupation,Salary\r"
    "Joe,Carpenter,300000\nFred,Blacksmith,400000\r\n";

foreach (record; csvReader!(string[string])
        (text, null))
{
    writefln("%s works as a %s and earns &amp;dollar;%s per year.",
             record["Name"], record["Occupation"],
             record["Salary"]);
}

// To read the same string from the file "filename.csv":

auto file = File("filename.csv", "r");

foreach (record; csvReader!(string[string])
        (file.byLine.joiner("\n"), null))
{
    writefln("%s works as a %s and earns &amp;dollar;%s per year.",
             record["Name"], record["Occupation"],
             record["Salary"]);
}
</pre>  This module allows content to be iterated by record stored in a struct, class, associative array, or as a range of fields. Upon detection of an error an CSVException is thrown (can be disabled). csvNextToken has been made public to allow for attempted recovery.  Disabling exceptions will lift many restrictions specified above. A quote can appear in a field if the field was not quoted. If in a quoted field any quote by itself, not at the end of a field, will end processing for that field. The field is ended when there is no input, even if the quote was not closed.  <dl>
<dt>See Also:</dt>
<dd>
<a href="http://en.wikipedia.org/wiki/Comma-separated_values">Wikipedia Comma-separated values</a> </dd>
</dl> <dl>
<dt>License:</dt>
<dd>
<a href="http://www.boost.org/LICENSE_1_0.txt">Boost License 1.0</a>. </dd>
</dl> <dl>
<dt>Authors:</dt>
<dd>Jesse Phillips </dd>
</dl> <dl>
<dt>Source</dt>
<dd> <span class="phobos_src"><a class="https" href="https://github.com/dlang/phobos/blob/master/std/csv.d">std/csv.d</a></span>
</dd>
</dl> <dl>
<dt class="d_decl" id="CSVException">class <strong id="CSVException">CSVException</strong>: object.Exception; </dt> <dd>
<p>Exception containing the row and column for when an exception was thrown. </p>
<p>Numbering of both row and col start at one and corresponds to the location in the file rather than any specified header. Special consideration should be made when there is failure to match the header see <a href="#%20HeaderMismatchException"><code> HeaderMismatchException</code></a> for details. <br><br> When performing type conversions, <a href="std_conv.html#ConvException"><code>std.conv.ConvException</code></a> is stored in the <code>next</code> field.</p> <dl>
<dt>Examples:</dt>
<dd>
<pre data-language="d">import std.exception : collectException;
import std.algorithm.searching : count;
string text = "a,b,c\nHello,65";
auto ex = collectException!CSVException(csvReader(text).count);
// "(Row: 0, Col: 0) Row 2's length 2 does not match previous length of 3."
writeln(ex.toString);
</pre> </dd>
</dl> <dl>
<dt>Examples:</dt>
<dd>
<pre data-language="d">import std.exception : collectException;
import std.algorithm.searching : count;
import std.typecons : Tuple;
string text = "a,b\nHello,65";
auto ex = collectException!CSVException(csvReader!(Tuple!(string,int))(text).count);
// "(Row: 1, Col: 2) Unexpected 'b' when converting from type string to type int"
writeln(ex.toString);
</pre> </dd>
</dl> <dl>
<dt class="d_decl" id="CSVException.row">size_t <strong id="row">row</strong>; </dt>  <dt class="d_decl" id="CSVException.col">size_t <strong id="col">col</strong>; </dt>  </dl> </dd> <dt class="d_decl" id="IncompleteCellException">class <strong id="IncompleteCellException">IncompleteCellException</strong>: std.csv.CSVException; </dt> <dd>
<p>Exception thrown when a Token is identified to not be completed: a quote is found in an unquoted field, data continues after a closing quote, or the quoted field was not closed before data was empty.</p>
<dl>
<dt>Examples:</dt>
<dd>
<pre data-language="d">import std.exception : assertThrown;
string text = "a,\"b,c\nHello,65,2.5";
assertThrown!IncompleteCellException(text.csvReader(["a","b","c"]));
</pre> </dd>
</dl> <dl>
<dt class="d_decl" id="IncompleteCellException.partialData">dstring <strong id="partialData">partialData</strong>; </dt> <dd>
<p>Data pulled from input before finding a problem </p>
<p>This field is populated when using <a href="#csvReader"><code>csvReader</code></a> but not by <a href="#csvNextToken"><code>csvNextToken</code></a> as this data will have already been fed to the output range.</p> </dd> </dl> </dd> <dt class="d_decl" id="HeaderMismatchException">class <strong id="HeaderMismatchException">HeaderMismatchException</strong>: std.csv.CSVException; </dt> <dd>
<p>Exception thrown under different conditions based on the type of <code> Contents</code>. </p>
<p>Structure, Class, and Associative Array <ul> <li>When a header is provided but a matching column is not found</li> </ul> <br><br> Other <ul> <li>When a header is provided but a matching column is not found</li> <li>Order did not match that found in the input</li> </ul> <br><br> Since a row and column is not meaningful when a column specified by the header is not found in the data, both row and col will be zero. Otherwise row is always one and col is the first instance found in header that occurred before the previous starting at one.</p> <dl>
<dt>Examples:</dt>
<dd>
<pre data-language="d">import std.exception : assertThrown;
string text = "a,b,c\nHello,65,2.5";
assertThrown!HeaderMismatchException(text.csvReader(["b","c","invalid"]));
</pre> </dd>
</dl> </dd> <dt class="d_decl" id="Malformed">enum <strong id="Malformed">Malformed</strong>: int; </dt> <dd>
<p>Determines the behavior for when an error is detected. </p>
<p>Disabling exception will follow these rules: <ul> <li>A quote can appear in a field if the field was not quoted.</li> <li>If in a quoted field any quote by itself, not at the end of a field, will end processing for that field.</li> <li>The field is ended when there is no input, even if the quote was not closed.</li> <li>If the given header does not match the order in the input, the content will return as it is found in the input.</li> <li>If the given header contains columns not found in the input they will be ignored.</li> </ul></p> <dl>
<dt>Examples:</dt>
<dd>
<pre data-language="d">import std.algorithm.comparison : equal;
import std.algorithm.searching : count;
import std.exception : assertThrown;

string text = "a,b,c\nHello,65,\"2.5";
assertThrown!IncompleteCellException(text.csvReader.count);

// ignore the exceptions and try to handle invalid CSV
auto firstLine = text.csvReader!(string, Malformed.ignore)(null).front;
assert(firstLine.equal(["Hello", "65", "2.5"]));
</pre> </dd>
</dl> <dl>
<dt class="d_decl" id="Malformed.ignore"><strong id="ignore">ignore</strong></dt> <dd>
<p>No exceptions are thrown due to incorrect CSV.</p> </dd> <dt class="d_decl" id="Malformed.throwException"><strong id="throwException">throwException</strong></dt> <dd>
<p>Use exceptions when input has incorrect CSV.</p> </dd> </dl> </dd> <dt class="d_decl" id="csvReader">auto <strong id="csvReader">csvReader</strong>(Contents = string, Malformed ErrorLevel = Malformed.throwException, Range, Separator = char)(Range input, Separator delimiter = ',', Separator quote = '"')<br><small>  Constraints: if (isInputRange!Range &amp;&amp; is(immutable(ElementType!Range) == immutable(dchar)) &amp;&amp; isSomeChar!Separator &amp;&amp; !is(Contents T : T[U], U : string)); </small><br><br>auto <strong id="csvReader">csvReader</strong>(Contents = string, Malformed ErrorLevel = Malformed.throwException, Range, Header, Separator = char)(Range input, Header header, Separator delimiter = ',', Separator quote = '"')<br><small>  Constraints: if (isInputRange!Range &amp;&amp; is(immutable(ElementType!Range) == immutable(dchar)) &amp;&amp; isSomeChar!Separator &amp;&amp; isForwardRange!Header &amp;&amp; isSomeString!(ElementType!Header)); </small><br><br>auto <strong id="csvReader">csvReader</strong>(Contents = string, Malformed ErrorLevel = Malformed.throwException, Range, Header, Separator = char)(Range input, Header header, Separator delimiter = ',', Separator quote = '"')<br><small>  Constraints: if (isInputRange!Range &amp;&amp; is(immutable(ElementType!Range) == immutable(dchar)) &amp;&amp; isSomeChar!Separator &amp;&amp; is(Header : typeof(null))); </small>
</dt> <dd>
<p>Returns an <a href="std_range_primitives.html#isInputRange">input range</a> for iterating over records found in <code>input</code>. </p>
<p>An optional <code>header</code> can be provided. The first record will be read in as the header. If <code>Contents</code> is a struct then the header provided is expected to correspond to the fields in the struct. When <code>Contents</code> is not a type which can contain the entire record, the <code>header</code> must be provided in the same order as the input or an exception is thrown. </p> <dl>
<dt>Returns:</dt>
<dd>An input range R as defined by <a href="std_range_primitives.html#isInputRange"><code>std.range.primitives.isInputRange</code></a>. When <code>Contents</code> is a struct, class, or an associative array, the element type of R is <code>Contents</code>, otherwise the element type of R is itself a range with element type <code>Contents</code>.  If a <code>header</code> argument is provided, the returned range provides a <code>header</code> field for accessing the header from the input in array form. </dd>
</dl> <dl>
<dt>Throws:</dt>
<dd>
<a href="#CSVException"><code>CSVException</code></a> When a quote is found in an unquoted field, data continues after a closing quote, the quoted field was not closed before data was empty, a conversion failed, or when the row's length does not match the previous length.  <a href="#HeaderMismatchException"><code>HeaderMismatchException</code></a> when a header is provided but a matching column is not found or the order did not match that found in the input. Read the exception documentation for specific details of when the exception is thrown for different types of <code>Contents</code>.</dd>
</dl> <dl>
<dt>Examples:</dt>
<dd>The <code>Contents</code> of the input can be provided if all the records are the same type such as all integer data: <pre data-language="d">import std.algorithm.comparison : equal;
string text = "76,26,22";
auto records = text.csvReader!int;
assert(records.equal!equal([
    [76, 26, 22],
]));
</pre> </dd>
</dl> <dl>
<dt>Examples:</dt>
<dd>Using a struct with modified delimiter: <pre data-language="d">import std.algorithm.comparison : equal;
string text = "Hello;65;2.5\nWorld;123;7.5";
struct Layout
{
    string name;
    int value;
    double other;
}

auto records = text.csvReader!Layout(';');
assert(records.equal([
    Layout("Hello", 65, 2.5),
    Layout("World", 123, 7.5),
]));
</pre> </dd>
</dl> <dl>
<dt>Examples:</dt>
<dd>Specifying <code>ErrorLevel</code> as <a href="#Malformed.ignore"><code>Malformed.ignore</code></a> will lift restrictions on the format. This example shows that an exception is not thrown when finding a quote in a field not quoted. <pre data-language="d">string text = "A \" is now part of the data";
auto records = text.csvReader!(string, Malformed.ignore);
auto record = records.front;

writeln(record.front); // text
</pre> </dd>
</dl> <dl>
<dt>Examples:</dt>
<dd>Read only column "b" <pre data-language="d">import std.algorithm.comparison : equal;
string text = "a,b,c\nHello,65,63.63\nWorld,123,3673.562";
auto records = text.csvReader!int(["b"]);

assert(records.equal!equal([
    [65],
    [123],
]));
</pre> </dd>
</dl> <dl>
<dt>Examples:</dt>
<dd>Read while rearranging the columns by specifying a header with a different order" <pre data-language="d">import std.algorithm.comparison : equal;
string text = "a,b,c\nHello,65,2.5\nWorld,123,7.5";
struct Layout
{
    int value;
    double other;
    string name;
}

auto records = text.csvReader!Layout(["b","c","a"]);
assert(records.equal([
    Layout(65, 2.5, "Hello"),
    Layout(123, 7.5, "World")
]));
</pre> </dd>
</dl> <dl>
<dt>Examples:</dt>
<dd>The header can also be left empty if the input contains a header row and all columns should be iterated. The header from the input can always be accessed from the <code>header</code> field. <pre data-language="d">string text = "a,b,c\nHello,65,63.63";
auto records = text.csvReader(null);

writeln(records.header); // ["a", "b", "c"]
</pre> </dd>
</dl> </dd> <dt class="d_decl" id="csvNextToken">void <strong id="csvNextToken">csvNextToken</strong>(Range, Malformed ErrorLevel = Malformed.throwException, Separator, Output)(ref Range input, ref Output ans, Separator sep, Separator quote, bool startQuoted = false)<br><small>  Constraints: if (isSomeChar!Separator &amp;&amp; isInputRange!Range &amp;&amp; is(immutable(ElementType!Range) == immutable(dchar)) &amp;&amp; isOutputRange!(Output, dchar)); </small>
</dt> <dd>
<p>Lower level control over parsing CSV </p>
<p>This function consumes the input. After each call the input will start with either a delimiter or record break (\n, \r\n, \r) which must be removed for subsequent calls. </p> <dl>
<dt>Parameters:</dt>
<dd><table>
<tr>
<td>Range <code>input</code>
</td> <td>Any CSV input</td>
</tr> <tr>
<td>Output <code>ans</code>
</td> <td>The first field in the input</td>
</tr> <tr>
<td>Separator <code>sep</code>
</td> <td>The character to represent a comma in the specification</td>
</tr> <tr>
<td>Separator <code>quote</code>
</td> <td>The character to represent a quote in the specification</td>
</tr> <tr>
<td>bool <code>startQuoted</code>
</td> <td>Whether the input should be considered to already be in quotes</td>
</tr> </table></dd>
</dl> <dl>
<dt>Throws:</dt>
<dd>
<a href="#IncompleteCellException"><code>IncompleteCellException</code></a> When a quote is found in an unquoted field, data continues after a closing quote, or the quoted field was not closed before data was empty.</dd>
</dl> <dl>
<dt>Examples:</dt>
<dd>
<pre data-language="d">import std.array : appender;
import std.range.primitives : popFront;

string str = "65,63\n123,3673";

auto a = appender!(char[])();

csvNextToken(str,a,',','"');
writeln(a.data); // "65"
writeln(str); // ",63\n123,3673"

str.popFront();
a.shrinkTo(0);
csvNextToken(str,a,',','"');
writeln(a.data); // "63"
writeln(str); // "\n123,3673"

str.popFront();
a.shrinkTo(0);
csvNextToken(str,a,',','"');
writeln(a.data); // "123"
writeln(str); // ",3673"
</pre> </dd>
</dl> </dd> </dl><div class="_attribution">
  <p class="_attribution-p">
    &copy; 1999&ndash;2021 The D Language Foundation<br>Licensed under the Boost License 1.0.<br>
    <a href="https://dlang.org/phobos/std_csv.html" class="_attribution-link">https://dlang.org/phobos/std_csv.html</a>
  </p>
</div>
