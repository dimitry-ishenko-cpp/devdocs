<h1>std.conv</h1>  <p>A one-stop shop for converting values from one type to another. </p>
<p> <table>

<tr>
<th scope="col">Category</th> <th scope="col">Functions</th>
</tr> <tr>
<td>Generic</td> <td> <a href="#asOriginalType"><code>asOriginalType</code></a> <a href="#castFrom"><code>castFrom</code></a> <a href="#emplace"><code>emplace</code></a> <a href="#parse"><code>parse</code></a> <a href="#to"><code>to</code></a> <a href="#toChars"><code>toChars</code></a> </td>
</tr> <tr>
<td>Strings</td> <td> <a href="#text"><code>text</code></a> <a href="#wtext"><code>wtext</code></a> <a href="#dtext"><code>dtext</code></a> <a href="#hexString"><code>hexString</code></a> </td>
</tr> <tr>
<td>Numeric</td> <td> <a href="#octal"><code>octal</code></a> <a href="#roundTo"><code>roundTo</code></a> <a href="#signed"><code>signed</code></a> <a href="#unsigned"><code>unsigned</code></a> </td>
</tr> <tr>
<td>Exceptions</td> <td> <a href="#ConvException"><code>ConvException</code></a> <a href="#ConvOverflowException"><code>ConvOverflowException</code></a> </td>
</tr> </table> </p> <dl>
<dt>License:</dt>
<dd>
<a href="http://boost.org/LICENSE_1_0.txt">Boost License 1.0</a>. </dd>
</dl> <dl>
<dt>Authors:</dt>
<dd>
<a href="http://digitalmars.com">Walter Bright</a>, <a href="http://erdani.org">Andrei Alexandrescu</a>, Shin Fujishiro, Adam D. Ruppe, Kenji Hara </dd>
</dl> <dl>
<dt>Source</dt>
<dd> <span class="phobos_src"><a class="https" href="https://github.com/dlang/phobos/blob/master/std/conv.d">std/conv.d</a></span>
</dd>
</dl> <dl>
<dt class="d_decl" id="ConvException">class <strong id="ConvException">ConvException</strong>: object.Exception; </dt> <dd>
<p>Thrown on conversion errors.</p>
<dl>
<dt>Examples:</dt>
<dd>
<pre data-language="d">import std.exception : assertThrown;
assertThrown!ConvException(to!int("abc"));
</pre> </dd>
</dl> </dd> <dt class="d_decl" id="ConvOverflowException">class <strong id="ConvOverflowException">ConvOverflowException</strong>: std.conv.ConvException; </dt> <dd>
<p>Thrown on conversion overflow errors.</p>
<dl>
<dt>Examples:</dt>
<dd>
<pre data-language="d">import std.exception : assertThrown;
assertThrown!ConvOverflowException(to!ubyte(1_000_000));
</pre> </dd>
</dl> </dd> <dt class="d_decl" id="to">template <strong id="to">to</strong>(T)</dt> <dd>
<p>The <code>to</code> template converts a value from one type to another. The source type is deduced and the target type must be specified, for example the expression <code>to!int(42.0)</code> converts the number 42 from <code>double</code> to <code>int</code>. The conversion is "safe", i.e., it checks for overflow; <code>to!int(4.2e10)</code> would throw the <code>ConvOverflowException</code> exception. Overflow checks are only inserted when necessary, e.g., <code>to!double(42)</code> does not do any checking because any <code>int</code> fits in a <code>double</code>. </p>
<p>Conversions from string to numeric types differ from the C equivalents <code>atoi()</code> and <code>atol()</code> by checking for overflow and not allowing whitespace. <br><br> For conversion of strings to signed types, the grammar recognized is: <pre>Integer: Sign UnsignedInteger
UnsignedInteger
Sign:
    +
    -</pre> <br><br> For conversion to unsigned types, the grammar recognized is: <pre>UnsignedInteger:
    DecimalDigit
    DecimalDigit UnsignedInteger</pre></p> <dl>
<dt>Examples:</dt>
<dd>Converting a value to its own type (useful mostly for generic code) simply returns its argument. <pre data-language="d">int a = 42;
int b = to!int(a);
double c = to!double(3.14); // c is double with value 3.14
</pre> </dd>
</dl> <dl>
<dt>Examples:</dt>
<dd>Converting among numeric types is a safe way to cast them around.  Conversions from floating-point types to integral types allow loss of precision (the fractional part of a floating-point number). The conversion is truncating towards zero, the same way a cast would truncate. (To round a floating point value when casting to an integral, use <code>roundTo</code>.) <pre data-language="d">import std.exception : assertThrown;

int a = 420;
writeln(to!long(a)); // a
assertThrown!ConvOverflowException(to!byte(a));

writeln(to!int(4.2e6)); // 4200000
assertThrown!ConvOverflowException(to!uint(-3.14));
writeln(to!uint(3.14)); // 3
writeln(to!uint(3.99)); // 3
writeln(to!int(-3.99)); // -3
</pre> </dd>
</dl> <dl>
<dt>Examples:</dt>
<dd>When converting strings to numeric types, note that the D hexadecimal and binary literals are not handled. Neither the prefixes that indicate the base, nor the horizontal bar used to separate groups of digits are recognized. This also applies to the suffixes that indicate the type.  To work around this, you can specify a radix for conversions involving numbers. <pre data-language="d">auto str = to!string(42, 16);
writeln(str); // "2A"
auto i = to!int(str, 16);
writeln(i); // 42
</pre> </dd>
</dl> <dl>
<dt>Examples:</dt>
<dd>Conversions from integral types to floating-point types always succeed, but might lose accuracy. The largest integers with a predecessor representable in floating-point format are <code>2^24-1</code> for <code>float</code>, <code>2^53-1</code> for <code>double</code>, and <code>2^64-1</code> for <code>real</code> (when <code>real</code> is 80-bit, e.g. on Intel machines). <pre data-language="d">// 2^24 - 1, largest proper integer representable as float
int a = 16_777_215;
writeln(to!int(to!float(a))); // a
writeln(to!int(to!float(-a))); // -a
</pre> </dd>
</dl> <dl>
<dt>Examples:</dt>
<dd>Conversion from string types to char types enforces the input to consist of a single code point, and said code point must fit in the target type. Otherwise, <a href="#ConvException"><code>ConvException</code></a> is thrown. <pre data-language="d">import std.exception : assertThrown;

writeln(to!char("a")); // 'a'
assertThrown(to!char("침")); // '침' does not fit into a char
writeln(to!wchar("침")); // '침'
assertThrown(to!wchar("游땎")); // '游땎' does not fit into a wchar
writeln(to!dchar("游땎")); // '游땎'

// Using wstring or dstring as source type does not affect the result
writeln(to!char("a"w)); // 'a'
writeln(to!char("a"d)); // 'a'

// Two code points cannot be converted to a single one
assertThrown(to!char("ab"));
</pre> </dd>
</dl> <dl>
<dt>Examples:</dt>
<dd>Converting an array to another array type works by converting each element in turn. Associative arrays can be converted to associative arrays as long as keys and values can in turn be converted. <pre data-language="d">import std.string : split;

int[] a = [1, 2, 3];
auto b = to!(float[])(a);
writeln(b); // [1.0f, 2, 3]
string str = "1 2 3 4 5 6";
auto numbers = to!(double[])(split(str));
writeln(numbers); // [1.0, 2, 3, 4, 5, 6]
int[string] c;
c["a"] = 1;
c["b"] = 2;
auto d = to!(double[wstring])(c);
assert(d["a"w] == 1 &amp;&amp; d["b"w] == 2);
</pre> </dd>
</dl> <dl>
<dt>Examples:</dt>
<dd>Conversions operate transitively, meaning that they work on arrays and associative arrays of any complexity.  This conversion works because <code>to!short</code> applies to an <code>int</code>, <code>to!wstring</code> applies to a <code>string</code>, <code>to!string</code> applies to a <code>double</code>, and <code>to!(double[])</code> applies to an <code>int[]</code>. The conversion might throw an exception because <code>to!short</code> might fail the range check. <pre data-language="d">int[string][double[int[]]] a;
auto b = to!(short[wstring][string[double[]]])(a);
</pre> </dd>
</dl> <dl>
<dt>Examples:</dt>
<dd>Object-to-object conversions by dynamic casting throw exception when the source is non-null and the target is null. <pre data-language="d">import std.exception : assertThrown;
// Testing object conversions
class A {}
class B : A {}
class C : A {}
A a1 = new A, a2 = new B, a3 = new C;
assert(to!B(a2) is a2);
assert(to!C(a3) is a3);
assertThrown!ConvException(to!B(a3));
</pre> </dd>
</dl> <dl>
<dt>Examples:</dt>
<dd>Stringize conversion from all types is supported. <ul> <li>String to string conversion works for any two string types having (<code>char</code>, <code>wchar</code>, <code>dchar</code>) character widths and any combination of qualifiers (mutable, <code>const</code>, or <code>immutable</code>).</li> <li>Converts array (other than strings) to string. Each element is converted by calling <code>to!T</code>.</li> <li>Associative array to string conversion. Each element is converted by calling <code>to!T</code>.</li> <li>Object to string conversion calls <code>toString</code> against the object or returns <code>"null"</code> if the object is null.</li> <li>Struct to string conversion calls <code>toString</code> against the struct if it is defined.</li> <li>For structs that do not define <code>toString</code>, the conversion to string produces the list of fields.</li> <li>Enumerated types are converted to strings as their symbolic names.</li> <li>Boolean values are converted to <code>"true"</code> or <code>"false"</code>.</li> <li>
<code>char</code>, <code>wchar</code>, <code>dchar</code> to a string type.</li> <li>Unsigned or signed integers to strings. <dl>
<dt>[special case]</dt> <dd>Convert integral value to string in <span class="d_param">radix</span> radix. radix must be a value from 2 to 36. value is treated as a signed value only if radix is 10. The characters A through Z are used to represent values 10 through 36 and their case is determined by the <span class="d_param">letterCase</span> parameter.</dd>
</dl>
</li> <li>All floating point types to all string types.</li> <li>Pointer to string conversions convert the pointer to a <code>size_t</code> value. If pointer is <code>char*</code>, treat it as C-style strings. In that case, this function is <code>@system</code>.</li>
</ul> See <a href="std_format.html#formatValue"><code>std.format.formatValue</code></a> on how toString should be defined. <pre data-language="d">// Conversion representing dynamic/static array with string
long[] a = [ 1, 3, 5 ];
writeln(to!string(a)); // "[1, 3, 5]"

// Conversion representing associative array with string
int[string] associativeArray = ["0":1, "1":2];
assert(to!string(associativeArray) == `["0":1, "1":2]` ||
       to!string(associativeArray) == `["1":2, "0":1]`);

// char* to string conversion
writeln(to!string(cast(char*)null)); // ""
writeln(to!string("foo\0".ptr)); // "foo"

// Conversion reinterpreting void array to string
auto w = "abcx"w;
const(void)[] b = w;
writeln(b.length); // 8

auto c = to!(wchar[])(b);
writeln(c); // "abcx"
</pre> </dd>
</dl> </dd> <dt class="d_decl" id="roundTo">template <strong id="roundTo">roundTo</strong>(Target)</dt> <dd>
<p>Rounded conversion from floating point to integral. </p>
<p>Rounded conversions do not work with non-integral target types.</p> <dl>
<dt>Examples:</dt>
<dd>
<pre data-language="d">writeln(roundTo!int(3.14)); // 3
writeln(roundTo!int(3.49)); // 3
writeln(roundTo!int(3.5)); // 4
writeln(roundTo!int(3.999)); // 4
writeln(roundTo!int(-3.14)); // -3
writeln(roundTo!int(-3.49)); // -3
writeln(roundTo!int(-3.5)); // -4
writeln(roundTo!int(-3.999)); // -4
writeln(roundTo!(const int)(to!(const double)(-3.999))); // -4
</pre> </dd>
</dl> </dd> <dt class="d_decl" id="parse">auto <strong id="parse">parse</strong>(Target, Source, Flag!"doCount" doCount = No.doCount)(ref Source source)<br><small>  Constraints: if (isInputRange!Source &amp;&amp; isSomeChar!(ElementType!Source) &amp;&amp; is(immutable(Target) == immutable(bool))); </small>
</dt> <dd>
<p>The <code>parse</code> family of functions works quite like the <code>to</code> family, except that: <ol> <li>It only works with character ranges as input.</li> <li>It takes the input by reference. (This means that rvalues - such as string literals - are not accepted: use <code>to</code> instead.)</li> <li>It advances the input to the position following the conversion.</li> <li>It does not throw if it could not convert the entire input.</li>
</ol> </p>
<p>This overload converts a character input range to a <code>bool</code>. </p> <dl>
<dt>Parameters:</dt>
<dd><table>
<tr>
<td>Target</td> <td>the type to convert to</td>
</tr> <tr>
<td>Source <code>source</code>
</td> <td>the lvalue of an <a href="std_range_primitives.html#isInputRange">input range</a>
</td>
</tr> <tr>
<td>doCount</td> <td>the flag for deciding to report the number of consumed characters</td>
</tr> </table></dd>
</dl> <dl>
<dt>Returns:</dt>
<dd>
<ul> <li>A <code>bool</code> if <code>doCount</code> is set to <code>No.doCount</code>
</li> <li>A <code>tuple</code> containing a <code>bool</code> and a <code>size_t</code> if <code>doCount</code> is set to <code>Yes.doCount</code>
</li>
</ul> </dd>
</dl> <dl>
<dt>Throws:</dt>
<dd>A <a href="#ConvException"><code>ConvException</code></a> if the range does not represent a <code>bool</code>. </dd>
</dl> <dl>
<dt>Note</dt>
<dd> All character input range conversions using <a href="#to"><code>to</code></a> are forwarded to <code>parse</code> and do not require lvalues.</dd>
</dl> <dl>
<dt>Examples:</dt>
<dd>
<pre data-language="d">import std.typecons : Flag, Yes, No;
auto s = "true";
bool b = parse!bool(s);
assert(b);
auto s2 = "true";
bool b2 = parse!(bool, string, No.doCount)(s2);
assert(b2);
auto s3 = "true";
auto b3 = parse!(bool, string, Yes.doCount)(s3);
assert(b3.data &amp;&amp; b3.count == 4);
auto s4 = "falSE";
auto b4 = parse!(bool, string, Yes.doCount)(s4);
assert(!b4.data &amp;&amp; b4.count == 5);
</pre> </dd>
</dl> </dd> <dt class="d_decl" id="parse.2">auto <strong id="parse">parse</strong>(Target, Source, Flag!"doCount" doCount = No.doCount)(ref Source s)<br><small>  Constraints: if (isSomeChar!(ElementType!Source) &amp;&amp; isIntegral!Target &amp;&amp; !is(Target == enum)); </small><br><br>auto <strong id="parse">parse</strong>(Target, Source, Flag!"doCount" doCount = No.doCount)(ref Source source, uint radix)<br><small>  Constraints: if (isSomeChar!(ElementType!Source) &amp;&amp; isIntegral!Target &amp;&amp; !is(Target == enum)); </small>
</dt> <dd>
<p>Parses a character <a href="std_range_primitives.html#isInputRange">input range</a> to an integral value. </p>
<dl>
<dt>Parameters:</dt>
<dd><table>
<tr>
<td>Target</td> <td>the integral type to convert to</td>
</tr> <tr>
<td>Source <code>s</code>
</td> <td>the lvalue of an input range</td>
</tr> <tr>
<td>doCount</td> <td>the flag for deciding to report the number of consumed characters</td>
</tr> </table></dd>
</dl> <dl>
<dt>Returns:</dt>
<dd>
<ul> <li>A number of type <code>Target</code> if <code>doCount</code> is set to <code>No.doCount</code>
</li> <li>A <code>tuple</code> containing a number of type <code>Target</code> and a <code>size_t</code> if <code>doCount</code> is set to <code>Yes.doCount</code>
</li>
</ul> </dd>
</dl> <dl>
<dt>Throws:</dt>
<dd>A <a href="#ConvException"><code>ConvException</code></a> If an overflow occurred during conversion or if no character of the input was meaningfully converted.</dd>
</dl> <dl>
<dt>Examples:</dt>
<dd>
<pre data-language="d">import std.typecons : Flag, Yes, No;
string s = "123";
auto a = parse!int(s);
writeln(a); // 123

string s1 = "123";
auto a1 = parse!(int, string, Yes.doCount)(s1);
assert(a1.data == 123 &amp;&amp; a1.count == 3);

// parse only accepts lvalues
static assert(!__traits(compiles, parse!int("123")));
</pre> </dd>
</dl> <dl>
<dt>Examples:</dt>
<dd>
<pre data-language="d">import std.string : tr;
import std.typecons : Flag, Yes, No;
string test = "123 \t  76.14";
auto a = parse!uint(test);
writeln(a); // 123
assert(test == " \t  76.14"); // parse bumps string
test = tr(test, " \t\n\r", "", "d"); // skip ws
writeln(test); // "76.14"
auto b = parse!double(test);
writeln(b); // 76.14
writeln(test); // ""

string test2 = "123 \t  76.14";
auto a2 = parse!(uint, string, Yes.doCount)(test2);
assert(a2.data == 123 &amp;&amp; a2.count == 3);
assert(test2 == " \t  76.14");// parse bumps string
test2 = tr(test2, " \t\n\r", "", "d"); // skip ws
writeln(test2); // "76.14"
auto b2 = parse!(double, string, Yes.doCount)(test2);
assert(b2.data == 76.14 &amp;&amp; b2.count == 5);
writeln(test2); // ""
</pre> </dd>
</dl> </dd> <dt class="d_decl" id="parse.3">auto <strong id="parse">parse</strong>(Target, Source, Flag!"doCount" doCount = No.doCount)(ref Source s)<br><small>  Constraints: if (isSomeString!Source &amp;&amp; !is(Source == enum) &amp;&amp; is(Target == enum)); </small>
</dt> <dd>
<p>Takes a string representing an <code>enum</code> type and returns that type. </p>
<dl>
<dt>Parameters:</dt>
<dd><table>
<tr>
<td>Target</td> <td>the <code>enum</code> type to convert to</td>
</tr> <tr>
<td>Source <code>s</code>
</td> <td>the lvalue of the range to parse</td>
</tr> <tr>
<td>doCount</td> <td>the flag for deciding to report the number of consumed characters</td>
</tr> </table></dd>
</dl> <dl>
<dt>Returns:</dt>
<dd>
<ul> <li>An <code>enum</code> of type <code>Target</code> if <code>doCount</code> is set to <code>No.doCount</code>
</li> <li>A <code>tuple</code> containing an <code>enum</code> of type <code>Target</code> and a <code>size_t</code> if <code>doCount</code> is set to <code>Yes.doCount</code>
</li>
</ul> </dd>
</dl> <dl>
<dt>Throws:</dt>
<dd>A <a href="#ConvException"><code>ConvException</code></a> if type <code>Target</code> does not have a member represented by <code>s</code>.</dd>
</dl> <dl>
<dt>Examples:</dt>
<dd>
<pre data-language="d">import std.typecons : Flag, Yes, No, tuple;
enum EnumType : bool { a = true, b = false, c = a }

auto str = "a";
writeln(parse!EnumType(str)); // EnumType.a
auto str2 = "a";
writeln(parse!(EnumType, string, No.doCount)(str2)); // EnumType.a
auto str3 = "a";
writeln(parse!(EnumType, string, Yes.doCount)(str3)); // tuple(EnumType.a, 1)
</pre> </dd>
</dl> </dd> <dt class="d_decl" id="parse.4">auto <strong id="parse">parse</strong>(Target, Source, Flag!"doCount" doCount = No.doCount)(ref Source source)<br><small>  Constraints: if (isInputRange!Source &amp;&amp; isSomeChar!(ElementType!Source) &amp;&amp; !is(Source == enum) &amp;&amp; isFloatingPoint!Target &amp;&amp; !is(Target == enum)); </small>
</dt> <dd>
<p>Parses a character range to a floating point number. </p>
<dl>
<dt>Parameters:</dt>
<dd><table>
<tr>
<td>Target</td> <td>a floating point type</td>
</tr> <tr>
<td>Source <code>source</code>
</td> <td>the lvalue of the range to parse</td>
</tr> <tr>
<td>doCount</td> <td>the flag for deciding to report the number of consumed characters</td>
</tr> </table></dd>
</dl> <dl>
<dt>Returns:</dt>
<dd>
<ul> <li>A floating point number of type <code>Target</code> if <code>doCount</code> is set to <code>No.doCount</code>
</li> <li>A <code>tuple</code> containing a floating point number of췅type <code>Target</code> and a <code>size_t</code> if <code>doCount</code> is set to <code>Yes.doCount</code>
</li>
</ul> </dd>
</dl> <dl>
<dt>Throws:</dt>
<dd>A <a href="#ConvException"><code>ConvException</code></a> if <code>source</code> is empty, if no number could be parsed, or if an overflow occurred.</dd>
</dl> <dl>
<dt>Examples:</dt>
<dd>
<pre data-language="d">import std.math : approxEqual;
import std.typecons : Flag, Yes, No;
import std.math : isNaN, isInfinity;
auto str = "123.456";
assert(parse!double(str).approxEqual(123.456));
auto str2 = "123.456";
assert(parse!(double, string, No.doCount)(str2).approxEqual(123.456));
auto str3 = "123.456";
auto r = parse!(double, string, Yes.doCount)(str3);
assert(r.data.approxEqual(123.456));
writeln(r.count); // 7
auto str4 = "-123.456";
r = parse!(double, string, Yes.doCount)(str4);
assert(r.data.approxEqual(-123.456));
writeln(r.count); // 8
auto str5 = "+123.456";
r = parse!(double, string, Yes.doCount)(str5);
assert(r.data.approxEqual(123.456));
writeln(r.count); // 8
auto str6 = "inf0";
r = parse!(double, string, Yes.doCount)(str6);
assert(isInfinity(r.data) &amp;&amp; r.count == 3 &amp;&amp; str6 == "0");
auto str7 = "-0";
auto r2 = parse!(float, string, Yes.doCount)(str7);
assert(r2.data.approxEqual(0.0) &amp;&amp; r2.count == 2);
auto str8 = "nan";
auto r3 = parse!(real, string, Yes.doCount)(str8);
assert(isNaN(r3.data) &amp;&amp; r3.count == 3);
</pre> </dd>
</dl> </dd> <dt class="d_decl" id="parse.5">auto <strong id="parse">parse</strong>(Target, Source, Flag!"doCount" doCount = No.doCount)(ref Source s)<br><small>  Constraints: if (isSomeString!Source &amp;&amp; !is(Source == enum) &amp;&amp; (staticIndexOf!(immutable(Target), immutable(dchar), immutable(ElementEncodingType!Source)) &gt;= 0)); </small><br><br>auto <strong id="parse">parse</strong>(Target, Source, Flag!"doCount" doCount = No.doCount)(ref Source s)<br><small>  Constraints: if (!isSomeString!Source &amp;&amp; isInputRange!Source &amp;&amp; isSomeChar!(ElementType!Source) &amp;&amp; isSomeChar!Target &amp;&amp; (Target.sizeof &gt;= ElementType!Source.sizeof) &amp;&amp; !is(Target == enum)); </small>
</dt> <dd>
<p>Parsing one character off a range returns the first element and calls <code>popFront</code>. </p>
<dl>
<dt>Parameters:</dt>
<dd><table>
<tr>
<td>Target</td> <td>the type to convert to</td>
</tr> <tr>
<td>Source <code>s</code>
</td> <td>the lvalue of an <a href="std_range_primitives.html#isInputRange">input range</a>
</td>
</tr> <tr>
<td>doCount</td> <td>the flag for deciding to report the number of consumed characters</td>
</tr> </table></dd>
</dl> <dl>
<dt>Returns:</dt>
<dd>
<ul> <li>A character of type <code>Target</code> if <code>doCount</code> is set to <code>No.doCount</code>
</li> <li>A <code>tuple</code> containing a character of type <code>Target</code> and a <code>size_t</code> if <code>doCount</code> is set to <code>Yes.doCount</code>
</li>
</ul> </dd>
</dl> <dl>
<dt>Throws:</dt>
<dd>A <a href="#ConvException"><code>ConvException</code></a> if the range is empty.</dd>
</dl> <dl>
<dt>Examples:</dt>
<dd>
<pre data-language="d">import std.typecons : Flag, Yes, No;
auto s = "Hello, World!";
char first = parse!char(s);
writeln(first); // 'H'
writeln(s); // "ello, World!"
char second = parse!(char, string, No.doCount)(s);
writeln(second); // 'e'
writeln(s); // "llo, World!"
auto third = parse!(char, string, Yes.doCount)(s);
assert(third.data == 'l' &amp;&amp; third.count == 1);
writeln(s); // "lo, World!"
</pre> </dd>
</dl> </dd> <dt class="d_decl" id="parse.6">auto <strong id="parse">parse</strong>(Target, Source, Flag!"doCount" doCount = No.doCount)(ref Source s)<br><small>  Constraints: if (isInputRange!Source &amp;&amp; isSomeChar!(ElementType!Source) &amp;&amp; is(immutable(Target) == immutable(typeof(null)))); </small>
</dt> <dd>
<p>Parsing a character range to <code>typeof(null)</code> returns <code>null</code> if the range spells <code>"null"</code>. This function is case insensitive. </p>
<dl>
<dt>Parameters:</dt>
<dd><table>
<tr>
<td>Target</td> <td>the type to convert to</td>
</tr> <tr>
<td>Source <code>s</code>
</td> <td>the lvalue of an <a href="std_range_primitives.html#isInputRange">input range</a>
</td>
</tr> <tr>
<td>doCount</td> <td>the flag for deciding to report the number of consumed characters</td>
</tr> </table></dd>
</dl> <dl>
<dt>Returns:</dt>
<dd>
<ul> <li>
<code>null</code> if <code>doCount</code> is set to <code>No.doCount</code>
</li> <li>A <code>tuple</code> containing <code>null</code> and a <code>size_t</code> if <code>doCount</code> is set to <code>Yes.doCount</code>
</li>
</ul> </dd>
</dl> <dl>
<dt>Throws:</dt>
<dd>A <a href="#ConvException"><code>ConvException</code></a> if the range doesn't represent <code>null</code>.</dd>
</dl> <dl>
<dt>Examples:</dt>
<dd>
<pre data-language="d">import std.exception : assertThrown;
import std.typecons : Flag, Yes, No;

alias NullType = typeof(null);
auto s1 = "null";
assert(parse!NullType(s1) is null);
writeln(s1); // ""

auto s2 = "NUll"d;
assert(parse!NullType(s2) is null);
writeln(s2); // ""

auto s3 = "nuLlNULl";
assert(parse!(NullType, string, No.doCount)(s3) is null);
auto r = parse!(NullType, string, Yes.doCount)(s3);
assert(r.data is null &amp;&amp; r.count == 4);

auto m = "maybe";
assertThrown!ConvException(parse!NullType(m));
assertThrown!ConvException(parse!(NullType, string, Yes.doCount)(m));
assert(m == "maybe");  // m shouldn't change on failure

auto s = "NULL";
assert(parse!(const NullType)(s) is null);
</pre> </dd>
</dl> </dd> <dt class="d_decl" id="parse.7">auto <strong id="parse">parse</strong>(Target, Source, Flag!"doCount" doCount = No.doCount)(ref Source s, dchar lbracket = '[', dchar rbracket = ']', dchar comma = ',')<br><small>  Constraints: if (isSomeString!Source &amp;&amp; !is(Source == enum) &amp;&amp; isDynamicArray!Target &amp;&amp; !is(Target == enum)); </small><br><br>auto <strong id="parse">parse</strong>(Target, Source, Flag!"doCount" doCount = No.doCount)(ref Source s, dchar lbracket = '[', dchar rbracket = ']', dchar comma = ',')<br><small>  Constraints: if (isExactSomeString!Source &amp;&amp; isStaticArray!Target &amp;&amp; !is(Target == enum)); </small>
</dt> <dd>
<p>Parses an array from a string given the left bracket (default <code> '['</code>), right bracket (default <code>']'</code>), and element separator (by default <code>','</code>). A trailing separator is allowed. </p>
<dl>
<dt>Parameters:</dt>
<dd><table>
<tr>
<td>Source <code>s</code>
</td> <td>The string to parse</td>
</tr> <tr>
<td>dchar <code>lbracket</code>
</td> <td>the character that starts the array</td>
</tr> <tr>
<td>dchar <code>rbracket</code>
</td> <td>the character that ends the array</td>
</tr> <tr>
<td>dchar <code>comma</code>
</td> <td>the character that separates the elements of the array</td>
</tr> <tr>
<td>doCount</td> <td>the flag for deciding to report the number of consumed characters</td>
</tr> </table></dd>
</dl> <dl>
<dt>Returns:</dt>
<dd><ul> <li>An array of type <code>Target</code> if <code>doCount</code> is set to <code>No.doCount</code>
</li> <li>A <code>tuple</code> containing an array of type <code>Target</code> and a <code>size_t</code> if <code>doCount</code> is set to <code>Yes.doCount</code>
</li>
</ul></dd>
</dl> <dl>
<dt>Examples:</dt>
<dd>
<pre data-language="d">import std.typecons : Flag, Yes, No;
auto s1 = `[['h', 'e', 'l', 'l', 'o'], "world"]`;
auto a1 = parse!(string[])(s1);
writeln(a1); // ["hello", "world"]

auto s2 = `["aaa", "bbb", "ccc"]`;
auto a2 = parse!(string[])(s2);
writeln(a2); // ["aaa", "bbb", "ccc"]

auto s3 = `[['h', 'e', 'l', 'l', 'o'], "world"]`;
auto len3 = s3.length;
auto a3 = parse!(string[], string, Yes.doCount)(s3);
writeln(a3.data); // ["hello", "world"]
writeln(a3.count); // len3
</pre> </dd>
</dl> </dd> <dt class="d_decl" id="parse.8">auto <strong id="parse">parse</strong>(Target, Source, Flag!"doCount" doCount = No.doCount)(ref Source s, dchar lbracket = '[', dchar rbracket = ']', dchar keyval = ':', dchar comma = ',')<br><small>  Constraints: if (isSomeString!Source &amp;&amp; !is(Source == enum) &amp;&amp; isAssociativeArray!Target &amp;&amp; !is(Target == enum)); </small>
</dt> <dd>
<p>Parses an associative array from a string given the left bracket (default <code> '['</code>), right bracket (default <code>']'</code>), key-value separator (default <code> ':'</code>), and element seprator (by default <code>','</code>). </p>
<dl>
<dt>Parameters:</dt>
<dd><table>
<tr>
<td>Source <code>s</code>
</td> <td>the string to parse</td>
</tr> <tr>
<td>dchar <code>lbracket</code>
</td> <td>the character that starts the associative array</td>
</tr> <tr>
<td>dchar <code>rbracket</code>
</td> <td>the character that ends the associative array</td>
</tr> <tr>
<td>dchar <code>keyval</code>
</td> <td>the character that associates the key with the value</td>
</tr> <tr>
<td>dchar <code>comma</code>
</td> <td>the character that separates the elements of the associative array</td>
</tr> <tr>
<td>doCount</td> <td>the flag for deciding to report the number of consumed characters</td>
</tr> </table></dd>
</dl> <dl>
<dt>Returns:</dt>
<dd><ul> <li>An associative array of type <code>Target</code> if <code>doCount</code> is set to <code>No.doCount</code>
</li> <li>A <code>tuple</code> containing an associative array of type <code>Target</code> and a <code>size_t</code> if <code>doCount</code> is set to <code>Yes.doCount</code>
</li>
</ul></dd>
</dl> <dl>
<dt>Examples:</dt>
<dd>
<pre data-language="d">import std.typecons : Flag, Yes, No, tuple;
import std.range.primitives : save;
import std.array : assocArray;
auto s1 = "[1:10, 2:20, 3:30]";
auto copyS1 = s1.save;
auto aa1 = parse!(int[int])(s1);
writeln(aa1); // [1:10, 2:20, 3:30]
// parse!(int[int], string, Yes.doCount)(copyS1)
writeln(tuple([1:10, 2:20, 3:30], copyS1.length));

auto s2 = `["aaa":10, "bbb":20, "ccc":30]`;
auto copyS2 = s2.save;
auto aa2 = parse!(int[string])(s2);
writeln(aa2); // ["aaa":10, "bbb":20, "ccc":30]
assert(tuple(["aaa":10, "bbb":20, "ccc":30], copyS2.length) ==
    parse!(int[string], string, Yes.doCount)(copyS2));

auto s3 = `["aaa":[1], "bbb":[2,3], "ccc":[4,5,6]]`;
auto copyS3 = s3.save;
auto aa3 = parse!(int[][string])(s3);
writeln(aa3); // ["aaa":[1], "bbb":[2, 3], "ccc":[4, 5, 6]]
assert(tuple(["aaa":[1], "bbb":[2,3], "ccc":[4,5,6]], copyS3.length) ==
    parse!(int[][string], string, Yes.doCount)(copyS3));

auto s4 = `[]`;
int[int] emptyAA;
writeln(tuple(emptyAA, s4.length)); // parse!(int[int], string, Yes.doCount)(s4)
</pre> </dd>
</dl> </dd> <dt class="d_decl" id="text">string <strong id="text">text</strong>(T...)(T args)<br><small>  Constraints: if (T.length &gt; 0); </small><br><br>wstring <strong id="wtext">wtext</strong>(T...)(T args)<br><small>  Constraints: if (T.length &gt; 0); </small><br><br>dstring <strong id="dtext">dtext</strong>(T...)(T args)<br><small>  Constraints: if (T.length &gt; 0); </small>
</dt> <dd>
<p>Convenience functions for converting one or more arguments of any type into text (the three character widths).</p>
<dl>
<dt>Examples:</dt>
<dd>
<pre data-language="d">writeln(text(42, ' ', 1.5, ": xyz")); // "42 1.5: xyz"c
writeln(wtext(42, ' ', 1.5, ": xyz")); // "42 1.5: xyz"w
writeln(dtext(42, ' ', 1.5, ": xyz")); // "42 1.5: xyz"d
</pre> </dd>
</dl> </dd> <dt class="d_decl" id="octal">template <strong id="octal">octal</strong>(string num) if (isOctalLiteral(num))<br><br>template <strong id="octal">octal</strong>(alias decimalInteger) if (is(typeof(decimalInteger)) &amp;&amp; isIntegral!(typeof(decimalInteger)))</dt> <dd>
<p>The <code>octal</code> facility provides a means to declare a number in base 8. Using <code>octal!177</code> or <code>octal!"177"</code> for 127 represented in octal (same as 0177 in C). </p>
<p>The rules for strings are the usual for literals: If it can fit in an <code>int</code>, it is an <code>int</code>. Otherwise, it is a <code>long</code>. But, if the user specifically asks for a <code>long</code> with the <code>L</code> suffix, always give the <code>long</code>. Give an unsigned iff it is asked for with the <code>U</code> or <code>u</code> suffix. Octals created from integers preserve the type of the passed-in integral. </p> <dl>
<dt>See Also:</dt>
<dd>
<a href="#parse"><code>parse</code></a> for parsing octal strings at runtime.</dd>
</dl> <dl>
<dt>Examples:</dt>
<dd>
<pre data-language="d">// same as 0177
auto x = octal!177;
// octal is a compile-time device
enum y = octal!160;
// Create an unsigned octal
auto z = octal!"1_000_000u";
</pre> </dd>
</dl> </dd> <dt class="d_decl" id="emplace">pure nothrow @safe T* <strong id="emplace">emplace</strong>(T)(T* chunk); </dt> <dd>
<p>Given a pointer <code>chunk</code> to uninitialized memory (but already typed as <code>T</code>), constructs an object of non-<code>class</code> type <code>T</code> at that address. If <code>T</code> is a class, initializes the class reference to null. </p>
<dl>
<dt>Returns:</dt>
<dd>A pointer to the newly constructed object (which is the same as <code>chunk</code>).</dd>
</dl> <dl>
<dt>Examples:</dt>
<dd>
<pre data-language="d">static struct S
{
    int i = 42;
}
S[2] s2 = void;
emplace(&amp;s2);
assert(s2[0].i == 42 &amp;&amp; s2[1].i == 42);
</pre> </dd>
</dl> <dl>
<dt>Examples:</dt>
<dd>
<pre data-language="d">interface I {}
class K : I {}

K k = void;
emplace(&amp;k);
assert(k is null);

I i = void;
emplace(&amp;i);
assert(i is null);
</pre> </dd>
</dl> </dd> <dt class="d_decl" id="emplace.2">T* <strong id="emplace">emplace</strong>(T, Args...)(T* chunk, auto ref Args args)<br><small>  Constraints: if (is(T == struct) || Args.length == 1); </small>
</dt> <dd>
<p>Given a pointer <code>chunk</code> to uninitialized memory (but already typed as a non-class type <code>T</code>), constructs an object of type <code>T</code> at that address from arguments <code>args</code>. If <code>T</code> is a class, initializes the class reference to <code>args[0]</code>. </p>
<p>This function can be <code>@trusted</code> if the corresponding constructor of <code>T</code> is <code>@safe</code>. </p> <dl>
<dt>Returns:</dt>
<dd>A pointer to the newly constructed object (which is the same as <code>chunk</code>).</dd>
</dl> <dl>
<dt>Examples:</dt>
<dd>
<pre data-language="d">int a;
int b = 42;
writeln(*emplace!int(&amp;a, b)); // 42
</pre> </dd>
</dl> </dd> <dt class="d_decl" id="emplace.3">T <strong id="emplace">emplace</strong>(T, Args...)(T chunk, auto ref Args args)<br><small>  Constraints: if (is(T == class)); </small>
</dt> <dd>
<p>Given a raw memory area <code>chunk</code> (but already typed as a class type <code>T</code>), constructs an object of <code>class</code> type <code>T</code> at that address. The constructor is passed the arguments <code>Args</code>. </p>
<p>If <code>T</code> is an inner class whose <code>outer</code> field can be used to access an instance of the enclosing class, then <code>Args</code> must not be empty, and the first member of it must be a valid initializer for that <code>outer</code> field. Correct initialization of this field is essential to access members of the outer class inside <code>T</code> methods. </p> <dl>
<dt>Note</dt>
<dd> This function is <code>@safe</code> if the corresponding constructor of <code>T</code> is <code>@safe</code>. </dd>
</dl> <dl>
<dt>Returns:</dt>
<dd>The newly constructed object.</dd>
</dl> <dl>
<dt>Examples:</dt>
<dd>
<pre data-language="d">() @safe {
    class SafeClass
    {
        int x;
        @safe this(int x) { this.x = x; }
    }

    auto buf = new void[__traits(classInstanceSize, SafeClass)];
    auto support = (() @trusted =&gt; cast(SafeClass)(buf.ptr))();
    auto safeClass = emplace!SafeClass(support, 5);
    writeln(safeClass.x); // 5

    class UnsafeClass
    {
        int x;
        @system this(int x) { this.x = x; }
    }

    auto buf2 = new void[__traits(classInstanceSize, UnsafeClass)];
    auto support2 = (() @trusted =&gt; cast(UnsafeClass)(buf2.ptr))();
    static assert(!__traits(compiles, emplace!UnsafeClass(support2, 5)));
    static assert(!__traits(compiles, emplace!UnsafeClass(buf2, 5)));
}();
</pre> </dd>
</dl> </dd> <dt class="d_decl" id="emplace.4">T <strong id="emplace">emplace</strong>(T, Args...)(void[] chunk, auto ref Args args)<br><small>  Constraints: if (is(T == class)); </small>
</dt> <dd>
<p>Given a raw memory area <code>chunk</code>, constructs an object of <code>class</code> type <code>T</code> at that address. The constructor is passed the arguments <code>Args</code>. </p>
<p>If <code>T</code> is an inner class whose <code>outer</code> field can be used to access an instance of the enclosing class, then <code>Args</code> must not be empty, and the first member of it must be a valid initializer for that <code>outer</code> field. Correct initialization of this field is essential to access members of the outer class inside <code>T</code> methods. </p> <dl>
<dt>Preconditions</dt>
<dd> <code>chunk</code> must be at least as large as <code>T</code> needs and should have an alignment multiple of <code>T</code>'s alignment. (The size of a <code>class</code> instance is obtained by using <code>_traits(classInstanceSize, T)</code>). </dd>
</dl> <dl>
<dt>Note</dt>
<dd> This function can be <code>@trusted</code> if the corresponding constructor of <code>T</code> is <code>@safe</code>. </dd>
</dl> <dl>
<dt>Returns:</dt>
<dd>The newly constructed object.</dd>
</dl> <dl>
<dt>Examples:</dt>
<dd>
<pre data-language="d">static class C
{
    int i;
    this(int i){this.i = i;}
}
auto buf = new void[__traits(classInstanceSize, C)];
auto c = emplace!C(buf, 5);
writeln(c.i); // 5
</pre> </dd>
</dl> </dd> <dt class="d_decl" id="emplace.5">T* <strong id="emplace">emplace</strong>(T, Args...)(void[] chunk, auto ref Args args)<br><small>  Constraints: if (!is(T == class)); </small>
</dt> <dd>
<p>Given a raw memory area <code>chunk</code>, constructs an object of non-<code>class</code> type <code>T</code> at that address. The constructor is passed the arguments <code>args</code>, if any. </p>
<dl>
<dt>Preconditions</dt>
<dd> <code>chunk</code> must be at least as large as <code>T</code> needs and should have an alignment multiple of <code>T</code>'s alignment. </dd>
</dl> <dl>
<dt>Note</dt>
<dd> This function can be <code>@trusted</code> if the corresponding constructor of <code>T</code> is <code>@safe</code>. </dd>
</dl> <dl>
<dt>Returns:</dt>
<dd>A pointer to the newly constructed object.</dd>
</dl> <dl>
<dt>Examples:</dt>
<dd>
<pre data-language="d">struct S
{
    int a, b;
}
auto buf = new void[S.sizeof];
S s;
s.a = 42;
s.b = 43;
auto s1 = emplace!S(buf, s);
assert(s1.a == 42 &amp;&amp; s1.b == 43);
</pre> </dd>
</dl> </dd> <dt class="d_decl" id="unsigned">auto <strong id="unsigned">unsigned</strong>(T)(T x)<br><small>  Constraints: if (isIntegral!T); </small><br><br>auto <strong id="unsigned">unsigned</strong>(T)(T x)<br><small>  Constraints: if (isSomeChar!T); </small>
</dt> <dd>
<p>Returns the corresponding unsigned value for <code>x</code> (e.g. if <code>x</code> has type <code>int</code>, it returns <code>cast(uint) x</code>). The advantage compared to the cast is that you do not need to rewrite the cast if <code>x</code> later changes type (e.g from <code>int</code> to <code>long</code>). </p>
<p>Note that the result is always mutable even if the original type was const or immutable. In order to retain the constness, use <a href="std_traits.html#Unsigned"><code>std.traits.Unsigned</code></a>.</p> <dl>
<dt>Examples:</dt>
<dd>
<pre data-language="d">import std.traits : Unsigned;
immutable int s = 42;
auto u1 = unsigned(s); //not qualified
static assert(is(typeof(u1) == uint));
Unsigned!(typeof(s)) u2 = unsigned(s); //same qualification
static assert(is(typeof(u2) == immutable uint));
immutable u3 = unsigned(s); //explicitly qualified
</pre> </dd>
</dl> </dd> <dt class="d_decl" id="signed">auto <strong id="signed">signed</strong>(T)(T x)<br><small>  Constraints: if (isIntegral!T); </small>
</dt> <dd>
<p>Returns the corresponding signed value for <code>x</code> (e.g. if <code>x</code> has type <code>uint</code>, it returns <code>cast(int) x</code>). The advantage compared to the cast is that you do not need to rewrite the cast if <code>x</code> later changes type (e.g from <code>uint</code> to <code>ulong</code>). </p>
<p>Note that the result is always mutable even if the original type was const or immutable. In order to retain the constness, use <a href="std_traits.html#Signed"><code>std.traits.Signed</code></a>.</p> <dl>
<dt>Examples:</dt>
<dd>
<pre data-language="d">import std.traits : Signed;

immutable uint u = 42;
auto s1 = signed(u); //not qualified
static assert(is(typeof(s1) == int));
Signed!(typeof(u)) s2 = signed(u); //same qualification
static assert(is(typeof(s2) == immutable int));
immutable s3 = signed(u); //explicitly qualified
</pre> </dd>
</dl> </dd> <dt class="d_decl" id="asOriginalType">OriginalType!E <strong id="asOriginalType">asOriginalType</strong>(E)(E value)<br><small>  Constraints: if (is(E == enum)); </small>
</dt> <dd>
<p>Returns the representation of an enumerated value, i.e. the value converted to the base type of the enumeration.</p>
<dl>
<dt>Examples:</dt>
<dd>
<pre data-language="d">enum A { a = 42 }
static assert(is(typeof(A.a.asOriginalType) == int));
writeln(A.a.asOriginalType); // 42
enum B : double { a = 43 }
static assert(is(typeof(B.a.asOriginalType) == double));
writeln(B.a.asOriginalType); // 43
</pre> </dd>
</dl> </dd> <dt class="d_decl" id="castFrom">template <strong id="castFrom">castFrom</strong>(From)</dt> <dd>
<p>A wrapper on top of the built-in cast operator that allows one to restrict casting of the original type of the value. </p>
<p>A common issue with using a raw cast is that it may silently continue to compile even if the value's type has changed during refactoring, which breaks the initial assumption about the cast. </p> <dl>
<dt>Parameters:</dt>
<dd><table>
<tr>
<td>From</td> <td>The type to cast from. The programmer must ensure it is legal to make this cast.</td>
</tr> </table></dd>
</dl> <dl>
<dt>Examples:</dt>
<dd>
<pre data-language="d">// Regular cast, which has been verified to be legal by the programmer:
{
    long x;
    auto y = cast(int) x;
}

// However this will still compile if 'x' is changed to be a pointer:
{
    long* x;
    auto y = cast(int) x;
}

// castFrom provides a more reliable alternative to casting:
{
    long x;
    auto y = castFrom!long.to!int(x);
}

// Changing the type of 'x' will now issue a compiler error,
// allowing bad casts to be caught before it's too late:
{
    long* x;
    static assert(
        !__traits(compiles, castFrom!long.to!int(x))
    );

    // if cast is still needed, must be changed to:
    auto y = castFrom!(long*).to!int(x);
}
</pre> </dd>
</dl> <dl>
<dt class="d_decl" id="castFrom.to">ref @system auto <strong id="to">to</strong>(To, T)(auto ref T value); </dt> <dd>
<dl>
<dt>Parameters:</dt>
<dd><table>
<tr>
<td>To</td> <td>The type to cast to.</td>
</tr> <tr>
<td>T <code>value</code>
</td> <td>The value to cast. It must be of type <code>From</code>, otherwise a compile-time error is emitted.</td>
</tr> </table></dd>
</dl> <dl>
<dt>Returns:</dt>
<dd>the value after the cast, returned by reference if possible.</dd>
</dl> </dd> </dl> </dd> <dt class="d_decl" id="hexString">template <strong id="hexString">hexString</strong>(string hexData) if (hexData.isHexLiteral)<br><br>template <strong id="hexString">hexString</strong>(wstring hexData) if (hexData.isHexLiteral)<br><br>template <strong id="hexString">hexString</strong>(dstring hexData) if (hexData.isHexLiteral)</dt> <dd>
<p>Converts a hex literal to a string at compile time. </p>
<p>Takes a string made of hexadecimal digits and returns the matching string by converting each pair of digits to a character. The input string can also include white characters, which can be used to keep the literal string readable in the source code. <br><br> The function is intended to replace the hexadecimal literal strings starting with <code>'x'</code>, which could be removed to simplify the core language. </p> <dl>
<dt>Parameters:</dt>
<dd><table>
<tr>
<td>hexData</td> <td>string to be converted.</td>
</tr> </table></dd>
</dl> <dl>
<dt>Returns:</dt>
<dd>a <code>string</code>, a <code>wstring</code> or a <code>dstring</code>, according to the type of hexData.</dd>
</dl> <dl>
<dt>Examples:</dt>
<dd>
<pre data-language="d">// conversion at compile time
auto string1 = hexString!"304A314B";
writeln(string1); // "0J1K"
auto string2 = hexString!"304A314B"w;
writeln(string2); // "0J1K"w
auto string3 = hexString!"304A314B"d;
writeln(string3); // "0J1K"d
</pre> </dd>
</dl> </dd> <dt class="d_decl" id="toChars">pure nothrow @nogc @safe auto <strong id="toChars">toChars</strong>(ubyte radix = 10, Char = char, LetterCase letterCase = LetterCase.lower, T)(T value)<br><small>  Constraints: if ((radix == 2 || radix == 8 || radix == 10 || radix == 16) &amp;&amp; (is(immutable(T) == immutable(uint)) || is(immutable(T) == immutable(ulong)) || radix == 10 &amp;&amp; (is(immutable(T) == immutable(int)) || is(immutable(T) == immutable(long))))); </small>
</dt> <dd>
<p>Convert integer to a range of characters. Intended to be lightweight and fast. </p>
<dl>
<dt>Parameters:</dt>
<dd><table>
<tr>
<td>radix</td> <td>2, 8, 10, 16</td>
</tr> <tr>
<td>Char</td> <td>character type for output</td>
</tr> <tr>
<td>letterCase</td> <td>lower for deadbeef, upper for DEADBEEF</td>
</tr> <tr>
<td>T <code>value</code>
</td> <td>integer to convert. Can be uint or ulong. If radix is 10, can also be int or long.</td>
</tr> </table></dd>
</dl> <dl>
<dt>Returns:</dt>
<dd>Random access range with slicing and everything</dd>
</dl> <dl>
<dt>Examples:</dt>
<dd>
<pre data-language="d">import std.algorithm.comparison : equal;

assert(toChars(1).equal("1"));
assert(toChars(1_000_000).equal("1000000"));

assert(toChars!(2)(2U).equal("10"));
assert(toChars!(16)(255U).equal("ff"));
assert(toChars!(16, char, LetterCase.upper)(255U).equal("FF"));
</pre> </dd>
</dl> </dd> </dl><div class="_attribution">
  <p class="_attribution-p">
    &copy; 1999&ndash;2021 The D Language Foundation<br>Licensed under the Boost License 1.0.<br>
    <a href="https://dlang.org/phobos/std_conv.html" class="_attribution-link">https://dlang.org/phobos/std_conv.html</a>
  </p>
</div>
