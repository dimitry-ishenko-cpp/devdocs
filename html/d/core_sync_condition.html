<h1>core.sync.condition</h1>  <p>The condition module provides a primitive for synchronized condition checking. </p>
<dl>
<dt>License:</dt>
<dd>
<a href="http://www.boost.org/LICENSE_1_0.txt">Boost License 1.0</a> </dd>
</dl> <dl>
<dt>Authors:</dt>
<dd>Sean Kelly </dd>
</dl> <dl>
<dt>Source</dt>
<dd> <a href="https://github.com/dlang/druntime/blob/master/src/core/sync/condition.d">core/sync/condition.d</a>
</dd>
</dl> <dl>
<dt class="d_decl" id="Condition">class <strong id="Condition">Condition</strong>; </dt> <dd>
<p>This class represents a condition variable as conceived by C.A.R. Hoare. As per Mesa type monitors however, "signal" has been replaced with "notify" to indicate that control is not transferred to the waiter when a notification is sent.</p>
<dl>
<dt>Examples:</dt>
<dd>
<pre data-language="d">import core.thread;
import core.sync.mutex;
import core.sync.semaphore;


void testNotify()
{
    auto mutex      = new Mutex;
    auto condReady  = new Condition( mutex );
    auto semDone    = new Semaphore;
    auto synLoop    = new Object;
    int  numWaiters = 10;
    int  numTries   = 10;
    int  numReady   = 0;
    int  numTotal   = 0;
    int  numDone    = 0;
    int  numPost    = 0;

    void waiter()
    {
        for ( int i = 0; i &lt; numTries; ++i )
        {
            synchronized( mutex )
            {
                while ( numReady &lt; 1 )
                {
                    condReady.wait();
                }
                --numReady;
                ++numTotal;
            }

            synchronized( synLoop )
            {
                ++numDone;
            }
            semDone.wait();
        }
    }

    auto group = new ThreadGroup;

    for ( int i = 0; i &lt; numWaiters; ++i )
        group.create( &amp;waiter );

    for ( int i = 0; i &lt; numTries; ++i )
    {
        for ( int j = 0; j &lt; numWaiters; ++j )
        {
            synchronized( mutex )
            {
                ++numReady;
                condReady.notify();
            }
        }
        while ( true )
        {
            synchronized( synLoop )
            {
                if ( numDone &gt;= numWaiters )
                    break;
            }
            Thread.yield();
        }
        for ( int j = 0; j &lt; numWaiters; ++j )
        {
            semDone.notify();
        }
    }

    group.joinAll();
    assert( numTotal == numWaiters * numTries );
}


void testNotifyAll()
{
    auto mutex      = new Mutex;
    auto condReady  = new Condition( mutex );
    int  numWaiters = 10;
    int  numReady   = 0;
    int  numDone    = 0;
    bool alert      = false;

    void waiter()
    {
        synchronized( mutex )
        {
            ++numReady;
            while ( !alert )
                condReady.wait();
            ++numDone;
        }
    }

    auto group = new ThreadGroup;

    for ( int i = 0; i &lt; numWaiters; ++i )
        group.create( &amp;waiter );

    while ( true )
    {
        synchronized( mutex )
        {
            if ( numReady &gt;= numWaiters )
            {
                alert = true;
                condReady.notifyAll();
                break;
            }
        }
        Thread.yield();
    }
    group.joinAll();
    assert( numReady == numWaiters &amp;&amp; numDone == numWaiters );
}


void testWaitTimeout()
{
    auto mutex      = new Mutex;
    auto condReady  = new Condition( mutex );
    bool waiting    = false;
    bool alertedOne = true;
    bool alertedTwo = true;

    void waiter()
    {
        synchronized( mutex )
        {
            waiting    = true;
            // we never want to miss the notification (30s)
            alertedOne = condReady.wait( dur!"seconds"(30) );
            // but we don't want to wait long for the timeout (10ms)
            alertedTwo = condReady.wait( dur!"msecs"(10) );
        }
    }

    auto thread = new Thread( &amp;waiter );
    thread.start();

    while ( true )
    {
        synchronized( mutex )
        {
            if ( waiting )
            {
                condReady.notify();
                break;
            }
        }
        Thread.yield();
    }
    thread.join();
    assert( waiting );
    assert( alertedOne );
    assert( !alertedTwo );
}

testNotify();
testNotifyAll();
testWaitTimeout();
</pre> </dd>
</dl> <dl>
<dt class="d_decl" id="Condition.this">nothrow @safe this(Mutex m); <br><br>shared nothrow @safe this(shared Mutex m); </dt> <dd>
<p>Initializes a condition object which is associated with the supplied mutex object. </p>
<dl>
<dt>Parameters:</dt>
<dd><table>
<tr>
<td>Mutex <code>m</code>
</td> <td>The mutex with which this condition will be associated.</td>
</tr> </table></dd>
</dl> <dl>
<dt>Throws:</dt>
<dd>SyncError on error.</dd>
</dl> </dd> <dt class="d_decl" id="Condition.mutex">@property Mutex <strong id="mutex">mutex</strong>(); <br><br>shared @property shared(Mutex) <strong id="mutex">mutex</strong>(); </dt> <dd>
<p>Gets the mutex associated with this condition. </p>
<dl>
<dt>Returns:</dt>
<dd>The mutex associated with this condition.</dd>
</dl> </dd> <dt class="d_decl" id="Condition.wait">void <strong id="wait">wait</strong>(); <br><br>shared void <strong id="wait">wait</strong>(); <br><br>void <strong id="wait">wait</strong>(this Q)(bool _unused_)<br><small>  Constraints: if (is(Q == Condition) || is(Q == shared(Condition))); </small>
</dt> <dd>
<p>Wait until notified. </p>
<dl>
<dt>Throws:</dt>
<dd>SyncError on error.</dd>
</dl> </dd> <dt class="d_decl" id="Condition.wait.2">bool <strong id="wait">wait</strong>(Duration val); <br><br>shared bool <strong id="wait">wait</strong>(Duration val); <br><br>bool <strong id="wait">wait</strong>(this Q)(Duration val, bool _unused_)<br><small>  Constraints: if (is(Q == Condition) || is(Q == shared(Condition))); </small>
</dt> <dd>
<p>Suspends the calling thread until a notification occurs or until the supplied time period has elapsed. </p>
<dl>
<dt>Parameters:</dt>
<dd><table>
<tr>
<td>Duration <code>val</code>
</td> <td>The time to wait.</td>
</tr> </table></dd>
</dl> <dl>
<dt>In</dt>
<dd> val must be non-negative. </dd>
</dl> <dl>
<dt>Throws:</dt>
<dd>SyncError on error. </dd>
</dl> <dl>
<dt>Returns:</dt>
<dd>true if notified before the timeout and false if not.</dd>
</dl> </dd> <dt class="d_decl" id="Condition.notify">void <strong id="notify">notify</strong>(); <br><br>shared void <strong id="notify">notify</strong>(); <br><br>void <strong id="notify">notify</strong>(this Q)(bool _unused_)<br><small>  Constraints: if (is(Q == Condition) || is(Q == shared(Condition))); </small>
</dt> <dd>
<p>Notifies one waiter. </p>
<dl>
<dt>Throws:</dt>
<dd>SyncError on error.</dd>
</dl> </dd> <dt class="d_decl" id="Condition.notifyAll">void <strong id="notifyAll">notifyAll</strong>(); <br><br>shared void <strong id="notifyAll">notifyAll</strong>(); <br><br>void <strong id="notifyAll">notifyAll</strong>(this Q)(bool _unused_)<br><small>  Constraints: if (is(Q == Condition) || is(Q == shared(Condition))); </small>
</dt> <dd>
<p>Notifies all waiters. </p>
<dl>
<dt>Throws:</dt>
<dd>SyncError on error.</dd>
</dl> </dd> </dl> </dd> </dl><div class="_attribution">
  <p class="_attribution-p">
    &copy; 1999&ndash;2021 The D Language Foundation<br>Licensed under the Boost License 1.0.<br>
    <a href="https://dlang.org/phobos/core_sync_condition.html" class="_attribution-link">https://dlang.org/phobos/core_sync_condition.html</a>
  </p>
</div>
