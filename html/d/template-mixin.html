<h1>Template Mixins</h1>       <b>Contents</b>  <ol> <li><a href="#mixin_scope">Mixin Scope</a></li> </ol>   <p>A <i>TemplateMixin</i> takes an arbitrary set of declarations from the body of a <i>TemplateDeclaration</i> and inserts them into the current context.</p>  <pre>TemplateMixinDeclaration:
    mixin template Identifier TemplateParameters Constraintopt { DeclDefsopt }

TemplateMixin:
    mixin MixinTemplateName TemplateArgumentsopt Identifieropt ;

MixinTemplateName:
    . MixinQualifiedIdentifier
    MixinQualifiedIdentifier
    Typeof . MixinQualifiedIdentifier

MixinQualifiedIdentifier:
    Identifier
    Identifier . MixinQualifiedIdentifier
    TemplateInstance . MixinQualifiedIdentifier
</pre>  <p>A <i>TemplateMixin</i> can occur in declaration lists of modules, classes, structs, unions, and as a statement. The <i>MixinTemplateName</i> refers to a <i>TemplateDeclaration</i> or <i>TemplateMixinDeclaration</i>. If the <i>TemplateDeclaration</i> has no parameters, the mixin form that has no !(<i>TemplateArgumentList</i>) can be used. </p>  <p>Unlike a template instantiation, a template mixin's body is evaluated within the scope where the mixin appears, not where the template declaration is defined. It is analogous to cutting and pasting the body of the template into the location of the mixin into a <a href="#mixin_scope">nested scope</a>. It is useful for injecting parameterized ‘boilerplate’ code, as well as for creating templated nested functions, which is not possible with template instantiations. </p>  <pre data-language="d">mixin template Foo()
{
    int x = 5;
}

mixin Foo;

struct Bar
{
    mixin Foo;
}

void test()
{
    writefln("x = %d", x);         // prints 5
    {
        Bar b;
        int x = 3;

        writefln("b.x = %d", b.x); // prints 5
        writefln("x = %d", x);     // prints 3
        {
            mixin Foo;
            writefln("x = %d", x); // prints 5
            x = 4;
            writefln("x = %d", x); // prints 4
        }
        writefln("x = %d", x);     // prints 3
    }
    writefln("x = %d", x);         // prints 5
}
</pre>  <p>Mixins can be parameterized:</p>  <pre data-language="d">mixin template Foo(T)
{
    T x = 5;
}

mixin Foo!(int);           // create x of type int
</pre>  <p>Mixins can add virtual functions to a class:</p>  <pre data-language="d">mixin template Foo()
{
    void func() { writeln("Foo.func()"); }
}

class Bar
{
    mixin Foo;
}

class Code : Bar
{
    override void func() { writeln("Code.func()"); }
}

void test()
{
    Bar b = new Bar();
    b.func();      // calls Foo.func()

    b = new Code();
    b.func();      // calls Code.func()
}
</pre>  <p>Mixins are evaluated in the scope of where they appear, not the scope of the template declaration:</p>  <pre data-language="d">int y = 3;

mixin template Foo()
{
    int abc() { return y; }
}

void test()
{
    int y = 8;
    mixin Foo; // local y is picked up, not global y
    assert(abc() == 8);
}
</pre>  <p>Mixins can parameterize symbols using alias parameters:</p>  <pre data-language="d">mixin template Foo(alias b)
{
    int abc() { return b; }
}

void test()
{
    int y = 8;
    mixin Foo!(y);
    assert(abc() == 8);
}
</pre>  <p>This example uses a mixin to implement a generic Duff's device for an arbitrary statement (in this case, the arbitrary statement is in bold). A nested function is generated as well as a delegate literal, these can be inlined by the compiler:</p>  <pre data-language="d">mixin template duffs_device(alias id1, alias id2, alias s)
{
    void duff_loop()
    {
        if (id1 &lt; id2)
        {
            typeof(id1) n = (id2 - id1 + 7) / 8;
            switch ((id2 - id1) % 8)
            {
                case 0: do { s(); goto case;
                case 7:      s(); goto case;
                case 6:      s(); goto case;
                case 5:      s(); goto case;
                case 4:      s(); goto case;
                case 3:      s(); goto case;
                case 2:      s(); goto case;
                case 1:      s(); continue;
                default:     assert(0, "Impossible");
                        } while (--n &gt; 0);
            }
        }
    }
}

void foo() { writeln("foo"); }

void test()
{
    int i = 1;
    int j = 11;

    mixin duffs_device!(i, j, delegate { foo(); });
    duff_loop();  // executes foo() 10 times
}
</pre>  <h2 id="mixin_scope">Mixin Scope</h2>  <p>The declarations in a mixin are placed in a nested scope and then ‘imported’ into the surrounding scope. If the name of a declaration in a mixin is the same as a declaration in the surrounding scope, the surrounding declaration overrides the mixin one:</p>  <pre data-language="d">int x = 3;

mixin template Foo()
{
    int x = 5;
    int y = 5;
}

mixin Foo;
int y = 3;

void test()
{
    writefln("x = %d", x);  // prints 3
    writefln("y = %d", y);  // prints 3
}
</pre>  <p>If two different mixins are put in the same scope, and each define a declaration with the same name, there is an ambiguity error when the declaration is referenced:</p>  <pre data-language="d">mixin template Foo()
{
    int x = 5;
    void func(int x) { }
}

mixin template Bar()
{
    int x = 4;
    void func(long x) { }
}

mixin Foo;
mixin Bar;

void test()
{
    import std.stdio : writefln;
    writefln("x = %d", x); // error, x is ambiguous
    func(1);               // error, func is ambiguous
}
</pre> <p>The call to <code>func()</code> is ambiguous because Foo.func and Bar.func are in different scopes. </p>  <p>If a mixin has an <i>Identifier</i>, it can be used to disambiguate between conflicting symbols: </p> <pre data-language="d">int x = 6;

mixin template Foo()
{
    int x = 5;
    int y = 7;
    void func() { }
}

mixin template Bar()
{
    int x = 4;
    void func() { }
}

mixin Foo F;
mixin Bar B;

void test()
{
    writefln("y = %d", y);     // prints 7
    writefln("x = %d", x);     // prints 6
    writefln("F.x = %d", F.x); // prints 5
    writefln("B.x = %d", B.x); // prints 4
    F.func();                  // calls Foo.func
    B.func();                  // calls Bar.func
}
</pre> <p>Alias declarations can be used to overload together functions declared in different mixins:</p>  <pre data-language="d">mixin template Foo()
{
    void func(int x) {  }
}

mixin template Bar()
{
    void func(long x) {  }
}

mixin Foo!() F;
mixin Bar!() B;

alias func = F.func;
alias func = B.func;

void main()
{
    func(1);  // calls B.func
    func(1L); // calls F.func
}
</pre>   <p>A mixin has its own scope, even if a declaration is overridden by the enclosing one:</p>  <pre data-language="d">int x = 4;

mixin template Foo()
{
    int x = 5;
    int bar() { return x; }
}

mixin Foo;

void test()
{
    writefln("x = %d", x);         // prints 4
    writefln("bar() = %d", bar()); // prints 5
}
</pre>     <div class="_attribution">
  <p class="_attribution-p">
    &copy; 1999&ndash;2021 The D Language Foundation<br>Licensed under the Boost License 1.0.<br>
    <a href="https://dlang.org/spec/template-mixin.html" class="_attribution-link">https://dlang.org/spec/template-mixin.html</a>
  </p>
</div>
