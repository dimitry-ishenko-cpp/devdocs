<h1>std.meta</h1>  <p>Templates to manipulate <a href="https://dlang.org/spec/template.html#variadic-templates">template parameter sequences</a> (also known as <i>alias sequences</i>). </p>
<p>Some operations on alias sequences are built into the language, such as <code>S[i]</code>, which accesses the element at index <code>i</code> in the sequence. <code>S[low .. high]</code> returns a new alias sequence that is a slice of the old one. <br><br> For more information, see <a href="https://dlang.org/ctarguments.html">Compile-time Sequences</a>. <br><br> <b>Note:</b> Several templates in this module use or operate on eponymous templates that take a single argument and evaluate to a boolean constant. Such templates are referred to as <i>template predicates</i>. <br><br>   <table>
 <tr>
<th scope="col">Category</th> <th scope="col">Templates</th>
</tr> <tr>
<td>Building blocks</td> <td> <a href="#Alias"><code>Alias</code></a> <a href="#AliasSeq"><code>AliasSeq</code></a> <a href="#aliasSeqOf"><code>aliasSeqOf</code></a> </td>
</tr> <tr>
<td>Alias sequence filtering</td> <td> <a href="#Erase"><code>Erase</code></a> <a href="#EraseAll"><code>EraseAll</code></a> <a href="#Filter"><code>Filter</code></a> <a href="#NoDuplicates"><code>NoDuplicates</code></a> <a href="#Stride"><code>Stride</code></a> </td>
</tr> <tr>
<td>Alias sequence type hierarchy</td> <td> <a href="#DerivedToFront"><code>DerivedToFront</code></a> <a href="#MostDerived"><code>MostDerived</code></a> </td>
</tr> <tr>
<td>Alias sequence transformation</td> <td> <a href="#Repeat"><code>Repeat</code></a> <a href="#Replace"><code>Replace</code></a> <a href="#ReplaceAll"><code>ReplaceAll</code></a> <a href="#Reverse"><code>Reverse</code></a> <a href="#staticMap"><code>staticMap</code></a> <a href="#staticSort"><code>staticSort</code></a> </td>
</tr> <tr>
<td>Alias sequence searching</td> <td> <a href="#allSatisfy"><code>allSatisfy</code></a> <a href="#anySatisfy"><code>anySatisfy</code></a> <a href="#staticIndexOf"><code>staticIndexOf</code></a> </td>
</tr> <tr>
<td>Template predicates</td> <td> <a href="#templateAnd"><code>templateAnd</code></a> <a href="#templateNot"><code>templateNot</code></a> <a href="#templateOr"><code>templateOr</code></a> <a href="#staticIsSorted"><code>staticIsSorted</code></a> </td>
</tr> <tr>
<td>Template instantiation</td> <td> <a href="#ApplyLeft"><code>ApplyLeft</code></a> <a href="#ApplyRight"><code>ApplyRight</code></a> <a href="#Instantiate"><code>Instantiate</code></a> </td>
</tr> </table> </p> <dl>
<dt>References</dt>
<dd> Based on ideas in Table 3.1 from <a href="http://amazon.com/exec/obidos/ASIN/0201704315/ref=ase_classicempire/102-2957199-2585768"> Modern C++ Design</a>, Andrei Alexandrescu (Addison-Wesley Professional, 2001) </dd>
</dl> <dl>
<dt>License:</dt>
<dd>
<a href="http://www.boost.org/LICENSE_1_0.txt">Boost License 1.0</a>. </dd>
</dl> <dl>
<dt>Authors:</dt>
<dd>
<a href="http://digitalmars.com">Walter Bright</a>, <a href="http://klickverbot.at">David Nadlinger</a> </dd>
</dl> <dl>
<dt>Source</dt>
<dd> <span class="phobos_src"><a class="https" href="https://github.com/dlang/phobos/blob/master/std/meta.d">std/meta.d</a></span>
</dd>
</dl> <dl>
<dt class="d_decl" id="AliasSeq">template <strong id="AliasSeq">AliasSeq</strong>(TList...)</dt> <dd>
<p>Creates a sequence of zero or more aliases. This is most commonly used as template parameters or arguments. </p>
<p>In previous versions of Phobos, this was known as <code>TypeTuple</code>.</p> <dl>
<dt>Examples:</dt>
<dd>
<pre data-language="d">import std.meta;
alias TL = AliasSeq!(int, double);

int foo(TL td)  // same as int foo(int, double);
{
    return td[0] + cast(int) td[1];
}
</pre> </dd>
</dl> <dl>
<dt>Examples:</dt>
<dd>
<pre data-language="d">alias TL = AliasSeq!(int, double);

alias Types = AliasSeq!(TL, char);
static assert(is(Types == AliasSeq!(int, double, char)));
</pre> </dd>
</dl> <dl>
<dt>Examples:</dt>
<dd>
<pre data-language="d">// Creates a compile-time sequence of function call expressions
// that each call `func` with the next variadic template argument
template Map(alias func, args...)
{
    auto ref lazyItem() {return func(args[0]);}

    static if (args.length == 1)
    {
        alias Map = lazyItem;
    }
    else
    {
        // recurse
        alias Map = AliasSeq!(lazyItem, Map!(func, args[1 .. $]));
    }
}

static void test(int a, int b)
{
    writeln(a); // 4
    writeln(b); // 16
}

static int a = 2;
static int b = 4;

test(Map!(i =&gt; i ^^ 2, a, b));
writeln(a); // 2
writeln(b); // 4

test(Map!((ref i) =&gt; i *= i, a, b));
writeln(a); // 4
writeln(b); // 16

static void testRef(ref int a, ref int b)
{
    writeln(a++); // 16
    writeln(b++); // 256
}

testRef(Map!(function ref(ref i) =&gt; i *= i, a, b));
writeln(a); // 17
writeln(b); // 257
</pre> </dd>
</dl> </dd> <dt class="d_decl" id="Alias">template <strong id="Alias">Alias</strong>(alias a)<br><br>template <strong id="Alias">Alias</strong>(T)</dt> <dd>
<p>Allows <code>alias</code>ing of any single symbol, type or compile-time expression. </p>
<p> Not everything can be directly aliased. An alias cannot be declared of - for example - a literal: <pre data-language="d">alias a = 4; //Error
</pre> With this template any single entity can be aliased: <pre data-language="d">alias b = Alias!4; //OK
</pre> </p> <dl>
<dt>See Also:</dt>
<dd>To alias more than one thing at once, use <a href="#AliasSeq"><code>AliasSeq</code></a>.</dd>
</dl> <dl>
<dt>Examples:</dt>
<dd>
<pre data-language="d">// Without Alias this would fail if Args[0] was e.g. a value and
// some logic would be needed to detect when to use enum instead
alias Head(Args...) = Alias!(Args[0]);
alias Tail(Args...) = Args[1 .. $];

alias Blah = AliasSeq!(3, int, "hello");
static assert(Head!Blah == 3);
static assert(is(Head!(Tail!Blah) == int));
static assert((Tail!Blah)[1] == "hello");
</pre> </dd>
</dl> <dl>
<dt>Examples:</dt>
<dd>
<pre data-language="d">alias a = Alias!(123);
static assert(a == 123);

enum abc = 1;
alias b = Alias!(abc);
static assert(b == 1);

alias c = Alias!(3 + 4);
static assert(c == 7);

alias concat = (s0, s1) =&gt; s0 ~ s1;
alias d = Alias!(concat("Hello", " World!"));
static assert(d == "Hello World!");

alias e = Alias!(int);
static assert(is(e == int));

alias f = Alias!(AliasSeq!(int));
static assert(!is(typeof(f[0]))); //not an AliasSeq
static assert(is(f == int));

auto g = 6;
alias h = Alias!g;
++h;
writeln(g); // 7
</pre> </dd>
</dl> </dd> <dt class="d_decl" id="staticIndexOf">enum auto <strong id="staticIndexOf">staticIndexOf</strong>(T, TList...); <br><br>enum auto <strong id="staticIndexOf">staticIndexOf</strong>(alias T, TList...); </dt> <dd>
<p>Returns the index of the first occurrence of T in the sequence TList. If not found, -1 is returned.</p>
<dl>
<dt>Examples:</dt>
<dd>
<pre data-language="d">import std.stdio;

void foo()
{
    writefln("The index of long is %s",
             staticIndexOf!(long, AliasSeq!(int, long, double)));
    // prints: The index of long is 1
}
</pre> </dd>
</dl> </dd> <dt class="d_decl" id="Erase">template <strong id="Erase">Erase</strong>(T, TList...)<br><br>template <strong id="Erase">Erase</strong>(alias T, TList...)</dt> <dd>
<p>Returns an <code>AliasSeq</code> created from TList with the first occurrence, if any, of T removed.</p>
<dl>
<dt>Examples:</dt>
<dd>
<pre data-language="d">alias Types = AliasSeq!(int, long, double, char);
alias TL = Erase!(long, Types);
static assert(is(TL == AliasSeq!(int, double, char)));
</pre> </dd>
</dl> </dd> <dt class="d_decl" id="EraseAll">template <strong id="EraseAll">EraseAll</strong>(T, TList...)<br><br>template <strong id="EraseAll">EraseAll</strong>(alias T, TList...)</dt> <dd>
<p>Returns an <code>AliasSeq</code> created from TList with the all occurrences, if any, of T removed.</p>
<dl>
<dt>Examples:</dt>
<dd>
<pre data-language="d">alias Types = AliasSeq!(int, long, long, int);

alias TL = EraseAll!(long, Types);
static assert(is(TL == AliasSeq!(int, int)));
</pre> </dd>
</dl> </dd> <dt class="d_decl" id="NoDuplicates">template <strong id="NoDuplicates">NoDuplicates</strong>(TList...)</dt> <dd>
<p>Returns an <code>AliasSeq</code> created from TList with the all duplicate types removed.</p>
<dl>
<dt>Examples:</dt>
<dd>
<pre data-language="d">alias Types = AliasSeq!(int, long, long, int, float);

alias TL = NoDuplicates!(Types);
static assert(is(TL == AliasSeq!(int, long, float)));
</pre> </dd>
</dl> </dd> <dt class="d_decl" id="Replace">template <strong id="Replace">Replace</strong>(T, U, TList...)<br><br>template <strong id="Replace">Replace</strong>(alias T, U, TList...)<br><br>template <strong id="Replace">Replace</strong>(T, alias U, TList...)<br><br>template <strong id="Replace">Replace</strong>(alias T, alias U, TList...)</dt> <dd>
<p>Returns an <code>AliasSeq</code> created from TList with the first occurrence of T, if found, replaced with U.</p>
<dl>
<dt>Examples:</dt>
<dd>
<pre data-language="d">alias Types = AliasSeq!(int, long, long, int, float);

alias TL = Replace!(long, char, Types);
static assert(is(TL == AliasSeq!(int, char, long, int, float)));
</pre> </dd>
</dl> </dd> <dt class="d_decl" id="ReplaceAll">template <strong id="ReplaceAll">ReplaceAll</strong>(T, U, TList...)<br><br>template <strong id="ReplaceAll">ReplaceAll</strong>(alias T, U, TList...)<br><br>template <strong id="ReplaceAll">ReplaceAll</strong>(T, alias U, TList...)<br><br>template <strong id="ReplaceAll">ReplaceAll</strong>(alias T, alias U, TList...)</dt> <dd>
<p>Returns an <code>AliasSeq</code> created from TList with all occurrences of T, if found, replaced with U.</p>
<dl>
<dt>Examples:</dt>
<dd>
<pre data-language="d">alias Types = AliasSeq!(int, long, long, int, float);

alias TL = ReplaceAll!(long, char, Types);
static assert(is(TL == AliasSeq!(int, char, char, int, float)));
</pre> </dd>
</dl> </dd> <dt class="d_decl" id="Reverse">template <strong id="Reverse">Reverse</strong>(TList...)</dt> <dd>
<p>Returns an <code>AliasSeq</code> created from TList with the order reversed.</p>
<dl>
<dt>Examples:</dt>
<dd>
<pre data-language="d">alias Types = AliasSeq!(int, long, long, int, float, byte, ubyte, short, ushort, uint);

alias TL = Reverse!(Types);
static assert(is(TL == AliasSeq!(uint, ushort, short, ubyte, byte, float, int, long, long, int)));
</pre> </dd>
</dl> </dd> <dt class="d_decl" id="MostDerived">template <strong id="MostDerived">MostDerived</strong>(T, TList...)</dt> <dd>
<p>Returns the type from TList that is the most derived from type T. If none are found, T is returned.</p>
<dl>
<dt>Examples:</dt>
<dd>
<pre data-language="d">class A { }
class B : A { }
class C : B { }
alias Types = AliasSeq!(A, C, B);

MostDerived!(Object, Types) x;  // x is declared as type C
static assert(is(typeof(x) == C));
</pre> </dd>
</dl> </dd> <dt class="d_decl" id="DerivedToFront">template <strong id="DerivedToFront">DerivedToFront</strong>(TList...)</dt> <dd>
<p>Returns an <code>AliasSeq</code> with the elements of TList sorted so that the most derived types come first.</p>
<dl>
<dt>Examples:</dt>
<dd>
<pre data-language="d">class A { }
class B : A { }
class C : B { }
alias Types = AliasSeq!(A, C, B);

alias TL = DerivedToFront!(Types);
static assert(is(TL == AliasSeq!(C, B, A)));
</pre> </dd>
</dl> </dd> <dt class="d_decl" id="staticMap">template <strong id="staticMap">staticMap</strong>(alias F, Args...)</dt> <dd>
<p>Evaluates to <code>AliasSeq!(F!(T[0]), F!(T[1]), ..., F!(T[$ - 1]))</code>.</p>
<dl>
<dt>Examples:</dt>
<dd>
<pre data-language="d">import std.traits : Unqual;
alias TL = staticMap!(Unqual, int, const int, immutable int, uint, ubyte, byte, short, ushort);
static assert(is(TL == AliasSeq!(int, int, int, uint, ubyte, byte, short, ushort)));
</pre> </dd>
</dl> </dd> <dt class="d_decl" id="allSatisfy">template <strong id="allSatisfy">allSatisfy</strong>(alias F, T...)</dt> <dd>
<p>Tests whether all given items satisfy a template predicate, i.e. evaluates to <code>F!(T[0]) &amp;&amp; F!(T[1]) &amp;&amp; ... &amp;&amp; F!(T[$ - 1])</code>. </p>
<p>Evaluation is <i>not</i> short-circuited if a false result is encountered; the template predicate must be instantiable with all the given items.</p> <dl>
<dt>Examples:</dt>
<dd>
<pre data-language="d">import std.traits : isIntegral;

static assert(!allSatisfy!(isIntegral, int, double));
static assert( allSatisfy!(isIntegral, int, long));
</pre> </dd>
</dl> </dd> <dt class="d_decl" id="anySatisfy">template <strong id="anySatisfy">anySatisfy</strong>(alias F, T...)</dt> <dd>
<p>Tests whether any given items satisfy a template predicate, i.e. evaluates to <code>F!(T[0]) || F!(T[1]) || ... || F!(T[$ - 1])</code>. </p>
<p>Evaluation is short-circuited if a true result is encountered; the template predicate must be instantiable with one of the given items.</p> <dl>
<dt>Examples:</dt>
<dd>
<pre data-language="d">import std.traits : isIntegral;

static assert(!anySatisfy!(isIntegral, string, double));
static assert( anySatisfy!(isIntegral, int, double));
</pre> </dd>
</dl> </dd> <dt class="d_decl" id="Filter">template <strong id="Filter">Filter</strong>(alias pred, TList...)</dt> <dd>
<p>Filters an <code>AliasSeq</code> using a template predicate. Returns an <code>AliasSeq</code> of the elements which satisfy the predicate.</p>
<dl>
<dt>Examples:</dt>
<dd>
<pre data-language="d">import std.traits : isNarrowString, isUnsigned;

alias Types1 = AliasSeq!(string, wstring, dchar[], char[], dstring, int);
alias TL1 = Filter!(isNarrowString, Types1);
static assert(is(TL1 == AliasSeq!(string, wstring, char[])));

alias Types2 = AliasSeq!(int, byte, ubyte, dstring, dchar, uint, ulong);
alias TL2 = Filter!(isUnsigned, Types2);
static assert(is(TL2 == AliasSeq!(ubyte, uint, ulong)));
</pre> </dd>
</dl> </dd> <dt class="d_decl" id="templateNot">template <strong id="templateNot">templateNot</strong>(alias pred)</dt> <dd>
<p>Negates the passed template predicate.</p>
<dl>
<dt>Examples:</dt>
<dd>
<pre data-language="d">import std.traits : isPointer;

alias isNoPointer = templateNot!isPointer;
static assert(!isNoPointer!(int*));
static assert(allSatisfy!(isNoPointer, string, char, float));
</pre> </dd>
</dl> </dd> <dt class="d_decl" id="templateAnd">template <strong id="templateAnd">templateAnd</strong>(Preds...)</dt> <dd>
<p>Combines several template predicates using logical AND, i.e. constructs a new predicate which evaluates to true for a given input T if and only if all of the passed predicates are true for T. </p>
<p>The predicates are evaluated from left to right, aborting evaluation in a short-cut manner if a false result is encountered, in which case the latter instantiations do not need to compile.</p> <dl>
<dt>Examples:</dt>
<dd>
<pre data-language="d">import std.traits : isNumeric, isUnsigned;

alias storesNegativeNumbers = templateAnd!(isNumeric, templateNot!isUnsigned);
static assert(storesNegativeNumbers!int);
static assert(!storesNegativeNumbers!string &amp;&amp; !storesNegativeNumbers!uint);

// An empty sequence of predicates always yields true.
alias alwaysTrue = templateAnd!();
static assert(alwaysTrue!int);
</pre> </dd>
</dl> </dd> <dt class="d_decl" id="templateOr">template <strong id="templateOr">templateOr</strong>(Preds...)</dt> <dd>
<p>Combines several template predicates using logical OR, i.e. constructs a new predicate which evaluates to true for a given input T if and only at least one of the passed predicates is true for T. </p>
<p>The predicates are evaluated from left to right, aborting evaluation in a short-cut manner if a true result is encountered, in which case the latter instantiations do not need to compile.</p> <dl>
<dt>Examples:</dt>
<dd>
<pre data-language="d">import std.traits : isPointer, isUnsigned;

alias isPtrOrUnsigned = templateOr!(isPointer, isUnsigned);
static assert( isPtrOrUnsigned!uint &amp;&amp;  isPtrOrUnsigned!(short*));
static assert(!isPtrOrUnsigned!int  &amp;&amp; !isPtrOrUnsigned!(string));

// An empty sequence of predicates never yields true.
alias alwaysFalse = templateOr!();
static assert(!alwaysFalse!int);
</pre> </dd>
</dl> </dd> <dt class="d_decl" id="aliasSeqOf">template <strong id="aliasSeqOf">aliasSeqOf</strong>(alias iter) if (isIterable!(typeof(iter)) &amp;&amp; !isInfinite!(typeof(iter)))</dt> <dd>
<p>Converts any foreach-iterable entity (e.g. an input range) to an alias sequence. </p>
<dl>
<dt>Parameters:</dt>
<dd><table>
<tr>
<td>iter</td> <td>the entity to convert into an <code>AliasSeq</code>. It must be able to be able to be iterated over using a <a href="https://dlang.org/spec/statement.html#foreach-statement">foreach-statement</a>.</td>
</tr> </table></dd>
</dl> <dl>
<dt>Returns:</dt>
<dd>An <code>AliasSeq</code> containing the values produced by iterating over <code>iter</code>.</dd>
</dl> <dl>
<dt>Examples:</dt>
<dd>
<pre data-language="d">import std.algorithm.iteration : map;
import std.algorithm.sorting : sort;
import std.string : capitalize;

struct S
{
    int a;
    int c;
    int b;
}

alias capMembers = aliasSeqOf!([__traits(allMembers, S)].sort().map!capitalize());
static assert(capMembers[0] == "A");
static assert(capMembers[1] == "B");
static assert(capMembers[2] == "C");
</pre> </dd>
</dl> <dl>
<dt>Examples:</dt>
<dd>
<pre data-language="d">static immutable REF = [0, 1, 2, 3];
foreach (I, V; aliasSeqOf!([0, 1, 2, 3]))
{
    static assert(V == I);
    static assert(V == REF[I]);
}
</pre> </dd>
</dl> </dd> <dt class="d_decl" id="ApplyLeft">template <strong id="ApplyLeft">ApplyLeft</strong>(alias Template, args...)<br><br>template <strong id="ApplyRight">ApplyRight</strong>(alias Template, args...)</dt> <dd>
<p><a href="http://en.wikipedia.org/wiki/Partial_application">Partially applies</a> <span class="d_param">Template</span> by binding its first (left) or last (right) arguments to <span class="d_param">args</span>. </p>
<p>Behaves like the identity function when <span class="d_param">args</span> is empty. </p> <dl>
<dt>Parameters:</dt>
<dd><table>
<tr>
<td>Template</td> <td>template to partially apply</td>
</tr> <tr>
<td>args</td> <td>arguments to bind</td>
</tr> </table></dd>
</dl> <dl>
<dt>Returns:</dt>
<dd>Template with arity smaller than or equal to <span class="d_param">Template</span>
</dd>
</dl> <dl>
<dt>Examples:</dt>
<dd>
<pre data-language="d">// enum bool isImplicitlyConvertible(From, To)
import std.traits : isImplicitlyConvertible;

static assert(allSatisfy!(
    ApplyLeft!(isImplicitlyConvertible, ubyte),
    short, ushort, int, uint, long, ulong));

static assert(is(Filter!(ApplyRight!(isImplicitlyConvertible, short),
    ubyte, string, short, float, int) == AliasSeq!(ubyte, short)));
</pre> </dd>
</dl> <dl>
<dt>Examples:</dt>
<dd>
<pre data-language="d">import std.traits : hasMember, ifTestable;

struct T1
{
    bool foo;
}

struct T2
{
    struct Test
    {
        bool opCast(T : bool)() { return true; }
    }

    Test foo;
}

static assert(allSatisfy!(ApplyRight!(hasMember, "foo"), T1, T2));
static assert(allSatisfy!(ApplyRight!(ifTestable, a =&gt; a.foo), T1, T2));
</pre> </dd>
</dl> <dl>
<dt>Examples:</dt>
<dd>
<pre data-language="d">import std.traits : Largest;

alias Types = AliasSeq!(byte, short, int, long);

static assert(is(staticMap!(ApplyLeft!(Largest, short), Types) ==
            AliasSeq!(short, short, int, long)));
static assert(is(staticMap!(ApplyLeft!(Largest, int), Types) ==
            AliasSeq!(int, int, int, long)));
</pre> </dd>
</dl> <dl>
<dt>Examples:</dt>
<dd>
<pre data-language="d">import std.traits : FunctionAttribute, SetFunctionAttributes;

static void foo() @system;
static int bar(int) @system;

alias SafeFunctions = AliasSeq!(
    void function() @safe,
    int function(int) @safe);

static assert(is(staticMap!(ApplyRight!(
    SetFunctionAttributes, "D", FunctionAttribute.safe),
    typeof(&amp;foo), typeof(&amp;bar)) == SafeFunctions));
</pre> </dd>
</dl> </dd> <dt class="d_decl" id="Repeat">template <strong id="Repeat">Repeat</strong>(size_t n, TList...)</dt> <dd>
<p>Creates an <code>AliasSeq</code> which repeats <code>TList</code> exactly <code>n</code> times.</p>
<dl>
<dt>Examples:</dt>
<dd>
<pre data-language="d">alias ImInt0 = Repeat!(0, int);
static assert(is(ImInt0 == AliasSeq!()));

alias ImInt1 = Repeat!(1, immutable(int));
static assert(is(ImInt1 == AliasSeq!(immutable(int))));

alias Real3 = Repeat!(3, real);
static assert(is(Real3 == AliasSeq!(real, real, real)));

alias Real12 = Repeat!(4, Real3);
static assert(is(Real12 == AliasSeq!(real, real, real, real, real, real,
    real, real, real, real, real, real)));

alias Composite = AliasSeq!(uint, int);
alias Composite2 = Repeat!(2, Composite);
static assert(is(Composite2 == AliasSeq!(uint, int, uint, int)));

alias ImInt10 = Repeat!(10, int);
static assert(is(ImInt10 == AliasSeq!(int, int, int, int, int, int, int, int, int, int)));
</pre> </dd>
</dl> <dl>
<dt>Examples:</dt>
<dd>
<pre data-language="d">auto staticArray(T, size_t n)(Repeat!(n, T) elems)
{
    T[n] a = [elems];
    return a;
}

auto a = staticArray!(long, 3)(3, 1, 4);
assert(is(typeof(a) == long[3]));
writeln(a); // [3, 1, 4]
</pre> </dd>
</dl> </dd> <dt class="d_decl" id="staticSort">template <strong id="staticSort">staticSort</strong>(alias cmp, Seq...)</dt> <dd>
<p>Sorts an <a href="#AliasSeq"><code>AliasSeq</code></a> using <code>cmp</code>. </p>
<dl>
<dt>Parameters</dt>
<dd> cmp = A template that returns a <code>bool</code> (if its first argument is less than the second one) or an <code>int</code> (-1 means less than, 0 means equal, 1 means greater than) </dd>
</dl> Seq = The <a href="#AliasSeq"><code>AliasSeq</code></a> to sort  <dl>
<dt>Returns:</dt>
<dd>The sorted alias sequence</dd>
</dl> <dl>
<dt>Examples:</dt>
<dd>
<pre data-language="d">alias Nums = AliasSeq!(7, 2, 3, 23);
enum Comp(int N1, int N2) = N1 &lt; N2;
static assert(AliasSeq!(2, 3, 7, 23) == staticSort!(Comp, Nums));
</pre> </dd>
</dl> <dl>
<dt>Examples:</dt>
<dd>
<pre data-language="d">alias Types = AliasSeq!(uint, short, ubyte, long, ulong);
enum Comp(T1, T2) = __traits(isUnsigned, T2) - __traits(isUnsigned, T1);
static assert(is(AliasSeq!(uint, ubyte, ulong, short, long) == staticSort!(Comp,
    Types)));
</pre> </dd>
</dl> </dd> <dt class="d_decl" id="staticIsSorted">template <strong id="staticIsSorted">staticIsSorted</strong>(alias cmp, Seq...)</dt> <dd>
<p>Checks if an <a href="#AliasSeq"><code>AliasSeq</code></a> is sorted according to <code>cmp</code>. </p>
<dl>
<dt>Parameters</dt>
<dd> cmp = A template that returns a <code>bool</code> (if its first argument is less than the second one) or an <code>int</code> (-1 means less than, 0 means equal, 1 means greater than) </dd>
</dl> Seq = The <a href="#AliasSeq"><code>AliasSeq</code></a> to check  <dl>
<dt>Returns:</dt>
<dd>
<code>true</code> if <code>Seq</code> is sorted; otherwise <code>false</code>
</dd>
</dl> <dl>
<dt>Examples:</dt>
<dd>
<pre data-language="d">enum Comp(int N1, int N2) = N1 &lt; N2;
static assert( staticIsSorted!(Comp, 2, 2));
static assert( staticIsSorted!(Comp, 2, 3, 7, 23));
static assert(!staticIsSorted!(Comp, 7, 2, 3, 23));
</pre> </dd>
</dl> <dl>
<dt>Examples:</dt>
<dd>
<pre data-language="d">enum Comp(T1, T2) = __traits(isUnsigned, T2) - __traits(isUnsigned, T1);
static assert( staticIsSorted!(Comp, uint, ubyte, ulong, short, long));
static assert(!staticIsSorted!(Comp, uint, short, ubyte, long, ulong));
</pre> </dd>
</dl> </dd> <dt class="d_decl" id="Stride">template <strong id="Stride">Stride</strong>(int stepSize, Args...) if (stepSize != 0)</dt> <dd>
<p>Selects a subset of <code>Args</code> by stepping with fixed <code>stepSize</code> over the sequence. A negative <code>stepSize</code> starts iteration with the last element. </p>
<dl>
<dt>Parameters:</dt>
<dd><table>
<tr>
<td>stepSize</td> <td>Number of elements to increment on each iteration. Can't be <code>0</code>.</td>
</tr> <tr>
<td>Args</td> <td>Template arguments.</td>
</tr> </table></dd>
</dl> <dl>
<dt>Returns:</dt>
<dd>An <code>AliasSeq</code> filtered by the selected stride.</dd>
</dl> <dl>
<dt>Examples:</dt>
<dd>
<pre data-language="d">static assert(is(Stride!(1, short, int, long) == AliasSeq!(short, int, long)));
static assert(is(Stride!(2, short, int, long) == AliasSeq!(short, long)));
static assert(is(Stride!(-1, short, int, long) == AliasSeq!(long, int, short)));
static assert(is(Stride!(-2, short, int, long) == AliasSeq!(long, short)));

alias attribs = AliasSeq!(short, int, long, ushort, uint, ulong);
static assert(is(Stride!(3, attribs) == AliasSeq!(short, ushort)));
static assert(is(Stride!(3, attribs[1 .. $]) == AliasSeq!(int, uint)));
static assert(is(Stride!(-3, attribs) == AliasSeq!(ulong, long)));
</pre> </dd>
</dl> </dd> <dt class="d_decl" id="Instantiate">template <strong id="Instantiate">Instantiate</strong>(alias Template, Params...)</dt> <dd>
<p>Instantiates the given template with the given parameters. </p>
<p>Used to work around syntactic limitations of D with regard to instantiating a template from an alias sequence (e.g. <code>T[0]!(...)</code> is not valid) or a template returning another template (e.g. <code>Foo!(Bar)!(Baz)</code> is not allowed). </p> <dl>
<dt>Parameters:</dt>
<dd><table>
<tr>
<td>Template</td> <td>The template to instantiate.</td>
</tr> <tr>
<td>Params</td> <td>The parameters with which to instantiate the template.</td>
</tr> </table></dd>
</dl> <dl>
<dt>Returns:</dt>
<dd>The instantiated template.</dd>
</dl> <dl>
<dt>Examples:</dt>
<dd>
<pre data-language="d">// ApplyRight combined with Instantiate can be used to apply various
// templates to the same parameters.
import std.string : leftJustify, center, rightJustify;
alias functions = staticMap!(ApplyRight!(Instantiate, string),
                             leftJustify, center, rightJustify);
string result = "";
static foreach (f; functions)
{
    {
        auto x = &amp;f; // not a template, but a function instantiation
        result ~= x("hello", 7);
        result ~= ";";
    }
}

writeln(result); // "hello  ; hello ;  hello;"
</pre> </dd>
</dl> </dd> </dl><div class="_attribution">
  <p class="_attribution-p">
    &copy; 1999&ndash;2021 The D Language Foundation<br>Licensed under the Boost License 1.0.<br>
    <a href="https://dlang.org/phobos/std_meta.html" class="_attribution-link">https://dlang.org/phobos/std_meta.html</a>
  </p>
</div>
