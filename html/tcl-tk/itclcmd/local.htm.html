<h1>local</h1>  <h2 id="M2">Name</h2> itcl::local — create an object local to a procedure <h2 id="M3">Synopsis</h2> <b>itcl::local </b><i>className objName</i> ?<i>arg arg ...</i>?<br> <h2 id="M4">Description</h2> The <b>local</b> command creates an <b>[incr Tcl]</b> object that is local to the current call frame. When the call frame goes away, the object is automatically deleted. This command is useful for creating objects that are local to a procedure. <p> As a side effect, this command creates a variable named "<b>itcl-local-</b><i>xxx</i>", where <i>xxx</i> is the name of the object that is created. This variable detects when the call frame is destroyed and automatically deletes the associated object. </p>
<h2 id="M5">Example</h2> In the following example, a simple "counter" object is used within the procedure "test". The counter is created as a local object, so it is automatically deleted each time the procedure exits. The <b><a href="../tclcmd/puts.htm.html">puts</a></b> statements included in the constructor/destructor show the object coming and going as the procedure is called. 
<pre>itcl::class counter {
    private variable count 0
    constructor {} {
        puts "created: $this"
    }
    destructor {
        puts "deleted: $this"
    }

    method bump {{by 1}} {
        incr count $by
    }
    method get {} {
        return $count
    }
}

proc test {val} {
    local counter x
    for {set i 0} {$i &lt; $val} {incr i} {
        x bump
    }
    return [x get]
}

set result [test 5]
puts "test: $result"

set result [test 10]
puts "test: $result"

puts "objects: [itcl::find objects *]"</pre>   <div class="_attribution"><p class="_attribution-p">Copyright © 1993-1998 Lucent Technologies, Inc. </p></div><div class="_attribution">
  <p class="_attribution-p">
    Licensed under <a href="http://tcl.tk/software/tcltk/license.html">Tcl/Tk terms</a><br>
    <a href="https://www.tcl.tk/man/tcl/ItclCmd/local.htm" class="_attribution-link">https://www.tcl.tk/man/tcl/ItclCmd/local.htm</a>
  </p>
</div>
