<h1>perlfaq6</h1>   <h2><a id="toc">CONTENTS</a></h2> <ul> <li> <a class="text-decoration-none" href="#NAME">NAME</a> </li> <li> <a class="text-decoration-none" href="#VERSION">VERSION</a> </li> <li> <a class="text-decoration-none" href="#DESCRIPTION">DESCRIPTION</a> <ul> <li> <a class="text-decoration-none" href="#How-can-I-hope-to-use-regular-expressions-without-creating-illegible-and-unmaintainable-code?">How can I hope to use regular expressions without creating illegible and unmaintainable code? </a> </li> <li> <a class="text-decoration-none" href="#I'm-having-trouble-matching-over-more-than-one-line.-What's-wrong?">I'm having trouble matching over more than one line. What's wrong? </a> </li> <li> <a class="text-decoration-none" href="#How-can-I-pull-out-lines-between-two-patterns-that-are-themselves-on-different-lines?">How can I pull out lines between two patterns that are themselves on different lines? </a> </li> <li> <a class="text-decoration-none" href="#How-do-I-match-XML,-HTML,-or-other-nasty,-ugly-things-with-a-regex?">How do I match XML, HTML, or other nasty, ugly things with a regex? </a> </li> <li> <a class="text-decoration-none" href="#I-put-a-regular-expression-into-$/-but-it-didn't-work.-What's-wrong?">I put a regular expression into $/ but it didn't work. What's wrong? </a> </li> <li> <a class="text-decoration-none" href="#How-do-I-substitute-case-insensitively-on-the-LHS-while-preserving-case-on-the-RHS?">How do I substitute case-insensitively on the LHS while preserving case on the RHS? </a> </li> <li> <a class="text-decoration-none" href="#How-can-I-make-%5Cw-match-national-character-sets?">How can I make \w match national character sets? </a> </li> <li> <a class="text-decoration-none" href="#How-can-I-match-a-locale-smart-version-of-/%5Ba-zA-Z%5D/?">How can I match a locale-smart version of /[a-zA-Z]/? </a> </li> <li> <a class="text-decoration-none" href="#How-can-I-quote-a-variable-to-use-in-a-regex?">How can I quote a variable to use in a regex? </a> </li> <li> <a class="text-decoration-none" href="#What-is-/o-really-for?">What is /o really for? </a> </li> <li> <a class="text-decoration-none" href="#How-do-I-use-a-regular-expression-to-strip-C-style-comments-from-a-file?">How do I use a regular expression to strip C-style comments from a file?</a> </li> <li> <a class="text-decoration-none" href="#Can-I-use-Perl-regular-expressions-to-match-balanced-text?">Can I use Perl regular expressions to match balanced text? </a> </li> <li> <a class="text-decoration-none" href="#What-does-it-mean-that-regexes-are-greedy?-How-can-I-get-around-it?">What does it mean that regexes are greedy? How can I get around it? </a> </li> <li> <a class="text-decoration-none" href="#How-do-I-process-each-word-on-each-line?">How do I process each word on each line? </a> </li> <li> <a class="text-decoration-none" href="#How-can-I-print-out-a-word-frequency-or-line-frequency-summary?">How can I print out a word-frequency or line-frequency summary?</a> </li> <li> <a class="text-decoration-none" href="#How-can-I-do-approximate-matching?">How can I do approximate matching? </a> </li> <li> <a class="text-decoration-none" href="#How-do-I-efficiently-match-many-regular-expressions-at-once?">How do I efficiently match many regular expressions at once? </a> </li> <li> <a class="text-decoration-none" href="#Why-don't-word-boundary-searches-with-%5Cb-work-for-me?">Why don't word-boundary searches with \b work for me? </a> </li> <li> <a class="text-decoration-none" href="#Why-does-using-$&amp;,-$%60,-or-$'-slow-my-program-down?">Why does using $&amp;, $`, or $' slow my program down? </a> </li> <li> <a class="text-decoration-none" href="#What-good-is-%5CG-in-a-regular-expression?">What good is \G in a regular expression? </a> </li> <li> <a class="text-decoration-none" href="#Are-Perl-regexes-DFAs-or-NFAs?-Are-they-POSIX-compliant?">Are Perl regexes DFAs or NFAs? Are they POSIX compliant? </a> </li> <li> <a class="text-decoration-none" href="#What's-wrong-with-using-grep-in-a-void-context?">What's wrong with using grep in a void context? </a> </li> <li> <a class="text-decoration-none" href="#How-can-I-match-strings-with-multibyte-characters?">How can I match strings with multibyte characters? </a> </li> <li> <a class="text-decoration-none" href="#How-do-I-match-a-regular-expression-that's-in-a-variable?">How do I match a regular expression that's in a variable? </a> </li> </ul> </li> <li> <a class="text-decoration-none" href="#AUTHOR-AND-COPYRIGHT">AUTHOR AND COPYRIGHT</a> </li> </ul> <h2 id="NAME">NAME</h2> <p>perlfaq6 - Regular Expressions</p> <h2 id="VERSION">VERSION</h2> <p>version 5.20210520</p> <h2 id="DESCRIPTION">DESCRIPTION</h2> <p>This section is surprisingly small because the rest of the FAQ is littered with answers involving regular expressions. For example, decoding a URL and checking whether something is a number can be handled with regular expressions, but those answers are found elsewhere in this document (in <a href="perlfaq9.html">perlfaq9</a>: "How do I decode or create those %-encodings on the web" and <a href="perlfaq4.html">perlfaq4</a>: "How do I determine whether a scalar is a number/whole/integer/float", to be precise).</p> <h3 id="How-can-I-hope-to-use-regular-expressions-without-creating-illegible-and-unmaintainable-code?">
How can I hope to use regular expressions without creating illegible and unmaintainable code? </h3> <p>Three techniques can make regular expressions maintainable and understandable.</p> <dl> <dt id="Comments-Outside-the-Regex">
Comments Outside the Regex</dt> <dd> <p>Describe what you're doing and how you're doing it, using normal Perl comments.</p> <pre data-language="perl"><code># turn the line into the first word, a colon, and the
# number of characters on the rest of the line
s/^(\w+)(.*)/ lc($1) . ":" . length($2) /meg;</code></pre> </dd> <dt id="Comments-Inside-the-Regex">
Comments Inside the Regex</dt> <dd> <p>The <code>/x</code> modifier causes whitespace to be ignored in a regex pattern (except in a character class and a few other places), and also allows you to use normal comments there, too. As you can imagine, whitespace and comments help a lot.</p> <p><code>/x</code> lets you turn this:</p> <pre data-language="perl"><code>s{&lt;(?:[^&gt;'"]*|".*?"|'.*?')+&gt;}{}gs;</code></pre> <p>into this:</p> <pre data-language="perl"><code>s{ &lt;                    # opening angle bracket
    (?:                 # Non-backreffing grouping paren
        [^&gt;'"] *        # 0 or more things that are neither &gt; nor ' nor "
            |           #    or else
        ".*?"           # a section between double quotes (stingy match)
            |           #    or else
        '.*?'           # a section between single quotes (stingy match)
    ) +                 #   all occurring one or more times
    &gt;                   # closing angle bracket
}{}gsx;                 # replace with nothing, i.e. delete</code></pre> <p>It's still not quite so clear as prose, but it is very useful for describing the meaning of each part of the pattern.</p> </dd> <dt id="Different-Delimiters">
Different Delimiters</dt> <dd> <p>While we normally think of patterns as being delimited with <code>/</code> characters, they can be delimited by almost any character. <a href="perlre.html">perlre</a> describes this. For example, the <code>s///</code> above uses braces as delimiters. Selecting another delimiter can avoid quoting the delimiter within the pattern:</p> <pre data-language="perl"><code>s/\/usr\/local/\/usr\/share/g;    # bad delimiter choice
s#/usr/local#/usr/share#g;        # better</code></pre> <p>Using logically paired delimiters can be even more readable:</p> <pre data-language="perl"><code>s{/usr/local/}{/usr/share}g;      # better still</code></pre> </dd> </dl> <h3 id="I'm-having-trouble-matching-over-more-than-one-line.-What's-wrong?">
I'm having trouble matching over more than one line. What's wrong? </h3> <p>Either you don't have more than one line in the string you're looking at (probably), or else you aren't using the correct modifier(s) on your pattern (possibly).</p> <p>There are many ways to get multiline data into a string. If you want it to happen automatically while reading input, you'll want to set $/ (probably to '' for paragraphs or <code>undef</code> for the whole file) to allow you to read more than one line at a time.</p> <p>Read <a href="perlre.html">perlre</a> to help you decide which of <code>/s</code> and <code>/m</code> (or both) you might want to use: <code>/s</code> allows dot to include newline, and <code>/m</code> allows caret and dollar to match next to a newline, not just at the end of the string. You do need to make sure that you've actually got a multiline string in there.</p> <p>For example, this program detects duplicate words, even when they span line breaks (but not paragraph ones). For this example, we don't need <code>/s</code> because we aren't using dot in a regular expression that we want to cross line boundaries. Neither do we need <code>/m</code> because we don't want caret or dollar to match at any point inside the record next to newlines. But it's imperative that $/ be set to something other than the default, or else we won't actually ever have a multiline record read in.</p> <pre data-language="perl"><code>$/ = '';          # read in whole paragraph, not just one line
while ( &lt;&gt; ) {
    while ( /\b([\w'-]+)(\s+\g1)+\b/gi ) {     # word starts alpha
        print "Duplicate $1 at paragraph $.\n";
    }
}</code></pre> <p>Here's some code that finds sentences that begin with "From " (which would be mangled by many mailers):</p> <pre data-language="perl"><code>$/ = '';          # read in whole paragraph, not just one line
while ( &lt;&gt; ) {
    while ( /^From /gm ) { # /m makes ^ match next to \n
    print "leading From in paragraph $.\n";
    }
}</code></pre> <p>Here's code that finds everything between START and END in a paragraph:</p> <pre data-language="perl"><code>undef $/;          # read in whole file, not just one line or paragraph
while ( &lt;&gt; ) {
    while ( /START(.*?)END/sgm ) { # /s makes . cross line boundaries
        print "$1\n";
    }
}</code></pre> <h3 id="How-can-I-pull-out-lines-between-two-patterns-that-are-themselves-on-different-lines?">
How can I pull out lines between two patterns that are themselves on different lines? </h3> <p>You can use Perl's somewhat exotic <code>..</code> operator (documented in <a href="perlop.html">perlop</a>):</p> <pre data-language="perl"><code class="plaintext">perl -ne 'print if /START/ .. /END/' file1 file2 ...</code></pre> <p>If you wanted text and not lines, you would use</p> <pre data-language="perl"><code>perl -0777 -ne 'print "$1\n" while /START(.*?)END/gs' file1 file2 ...</code></pre> <p>But if you want nested occurrences of <code>START</code> through <code>END</code>, you'll run up against the problem described in the question in this section on matching balanced text.</p> <p>Here's another example of using <code>..</code>:</p> <pre data-language="perl"><code>while (&lt;&gt;) {
    my $in_header =   1  .. /^$/;
    my $in_body   = /^$/ .. eof;
# now choose between them
} continue {
    $. = 0 if eof;    # fix $.
}</code></pre> <h3 id="How-do-I-match-XML,-HTML,-or-other-nasty,-ugly-things-with-a-regex?">
How do I match XML, HTML, or other nasty, ugly things with a regex? </h3> <p>Do not use regexes. Use a module and forget about the regular expressions. The <a href="XML::LibXML.html">XML::LibXML</a>, <a href="HTML::TokeParser.html">HTML::TokeParser</a> and <a href="HTML::TreeBuilder.html">HTML::TreeBuilder</a> modules are good starts, although each namespace has other parsing modules specialized for certain tasks and different ways of doing it. Start at CPAN Search ( <a href="http://metacpan.org/">http://metacpan.org/</a> ) and wonder at all the work people have done for you already! :)</p> <h3 id="I-put-a-regular-expression-into-$/-but-it-didn't-work.-What's-wrong?">
I put a regular expression into $/ but it didn't work. What's wrong? </h3> <p>$/ has to be a string. You can use these examples if you really need to do this.</p> <p>If you have <a href="File::Stream.html">File::Stream</a>, this is easy.</p> <pre data-language="perl"><code>use File::Stream;

my $stream = File::Stream-&gt;new(
    $filehandle,
    separator =&gt; qr/\s*,\s*/,
    );

print "$_\n" while &lt;$stream&gt;;</code></pre> <p>If you don't have File::Stream, you have to do a little more work.</p> <p>You can use the four-argument form of sysread to continually add to a buffer. After you add to the buffer, you check if you have a complete line (using your regular expression).</p> <pre data-language="perl"><code>local $_ = "";
while( sysread FH, $_, 8192, length ) {
    while( s/^((?s).*?)your_pattern// ) {
        my $record = $1;
        # do stuff here.
    }
}</code></pre> <p>You can do the same thing with foreach and a match using the c flag and the \G anchor, if you do not mind your entire file being in memory at the end.</p> <pre data-language="perl"><code>local $_ = "";
while( sysread FH, $_, 8192, length ) {
    foreach my $record ( m/\G((?s).*?)your_pattern/gc ) {
        # do stuff here.
    }
    substr( $_, 0, pos ) = "" if pos;
}</code></pre> <h3 id="How-do-I-substitute-case-insensitively-on-the-LHS-while-preserving-case-on-the-RHS?">
How do I substitute case-insensitively on the LHS while preserving case on the RHS? </h3> <p>Here's a lovely Perlish solution by Larry Rosler. It exploits properties of bitwise xor on ASCII strings.</p> <pre data-language="perl"><code>$_= "this is a TEsT case";

$old = 'test';
$new = 'success';

s{(\Q$old\E)}
{ uc $new | (uc $1 ^ $1) .
    (uc(substr $1, -1) ^ substr $1, -1) x
    (length($new) - length $1)
}egi;

print;</code></pre> <p>And here it is as a subroutine, modeled after the above:</p> <pre data-language="perl"><code>sub preserve_case {
    my ($old, $new) = @_;
    my $mask = uc $old ^ $old;

    uc $new | $mask .
        substr($mask, -1) x (length($new) - length($old))
}

$string = "this is a TEsT case";
$string =~ s/(test)/preserve_case($1, "success")/egi;
print "$string\n";</code></pre> <p>This prints:</p> <pre data-language="perl"><code class="plaintext">this is a SUcCESS case</code></pre> <p>As an alternative, to keep the case of the replacement word if it is longer than the original, you can use this code, by Jeff Pinyan:</p> <pre data-language="perl"><code>sub preserve_case {
    my ($from, $to) = @_;
    my ($lf, $lt) = map length, @_;

    if ($lt &lt; $lf) { $from = substr $from, 0, $lt }
    else { $from .= substr $to, $lf }

    return uc $to | ($from ^ uc $from);
}</code></pre> <p>This changes the sentence to "this is a SUcCess case."</p> <p>Just to show that C programmers can write C in any programming language, if you prefer a more C-like solution, the following script makes the substitution have the same case, letter by letter, as the original. (It also happens to run about 240% slower than the Perlish solution runs.) If the substitution has more characters than the string being substituted, the case of the last character is used for the rest of the substitution.</p> <pre data-language="perl"><code># Original by Nathan Torkington, massaged by Jeffrey Friedl
#
sub preserve_case
{
    my ($old, $new) = @_;
    my $state = 0; # 0 = no change; 1 = lc; 2 = uc
    my ($i, $oldlen, $newlen, $c) = (0, length($old), length($new));
    my $len = $oldlen &lt; $newlen ? $oldlen : $newlen;

    for ($i = 0; $i &lt; $len; $i++) {
        if ($c = substr($old, $i, 1), $c =~ /[\W\d_]/) {
            $state = 0;
        } elsif (lc $c eq $c) {
            substr($new, $i, 1) = lc(substr($new, $i, 1));
            $state = 1;
        } else {
            substr($new, $i, 1) = uc(substr($new, $i, 1));
            $state = 2;
        }
    }
    # finish up with any remaining new (for when new is longer than old)
    if ($newlen &gt; $oldlen) {
        if ($state == 1) {
            substr($new, $oldlen) = lc(substr($new, $oldlen));
        } elsif ($state == 2) {
            substr($new, $oldlen) = uc(substr($new, $oldlen));
        }
    }
    return $new;
}</code></pre> <h3 id="How-can-I-make-\w-match-national-character-sets?">
How can I make <code>\w</code> match national character sets? </h3> <p>Put <code>use locale;</code> in your script. The \w character class is taken from the current locale.</p> <p>See <a href="perllocale.html">perllocale</a> for details.</p> <h3 id="How-can-I-match-a-locale-smart-version-of-/[a-zA-Z]/?">
How can I match a locale-smart version of <code>/[a-zA-Z]/</code>? </h3> <p>You can use the POSIX character class syntax <code>/[[:alpha:]]/</code> documented in <a href="perlre.html">perlre</a>.</p> <p>No matter which locale you are in, the alphabetic characters are the characters in \w without the digits and the underscore. As a regex, that looks like <code>/[^\W\d_]/</code>. Its complement, the non-alphabetics, is then everything in \W along with the digits and the underscore, or <code>/[\W\d_]/</code>.</p> <h3 id="How-can-I-quote-a-variable-to-use-in-a-regex?">
How can I quote a variable to use in a regex? </h3> <p>The Perl parser will expand $variable and @variable references in regular expressions unless the delimiter is a single quote. Remember, too, that the right-hand side of a <code>s///</code> substitution is considered a double-quoted string (see <a href="perlop.html">perlop</a> for more details). Remember also that any regex special characters will be acted on unless you precede the substitution with \Q. Here's an example:</p> <pre data-language="perl"><code>$string = "Placido P. Octopus";
$regex  = "P.";

$string =~ s/$regex/Polyp/;
# $string is now "Polypacido P. Octopus"</code></pre> <p>Because <code>.</code> is special in regular expressions, and can match any single character, the regex <code>P.</code> here has matched the &lt;Pl&gt; in the original string.</p> <p>To escape the special meaning of <code>.</code>, we use <code>\Q</code>:</p> <pre data-language="perl"><code>$string = "Placido P. Octopus";
$regex  = "P.";

$string =~ s/\Q$regex/Polyp/;
# $string is now "Placido Polyp Octopus"</code></pre> <p>The use of <code>\Q</code> causes the <code>.</code> in the regex to be treated as a regular character, so that <code>P.</code> matches a <code>P</code> followed by a dot.</p> <h3 id="What-is-/o-really-for?">
What is <code>/o</code> really for? </h3> <p>(contributed by brian d foy)</p> <p>The <code>/o</code> option for regular expressions (documented in <a href="perlop.html">perlop</a> and <a href="perlreref.html">perlreref</a>) tells Perl to compile the regular expression only once. This is only useful when the pattern contains a variable. Perls 5.6 and later handle this automatically if the pattern does not change.</p> <p>Since the match operator <code>m//</code>, the substitution operator <code>s///</code>, and the regular expression quoting operator <code>qr//</code> are double-quotish constructs, you can interpolate variables into the pattern. See the answer to "How can I quote a variable to use in a regex?" for more details.</p> <p>This example takes a regular expression from the argument list and prints the lines of input that match it:</p> <pre data-language="perl"><code>my $pattern = shift @ARGV;

while( &lt;&gt; ) {
    print if m/$pattern/;
}</code></pre> <p>Versions of Perl prior to 5.6 would recompile the regular expression for each iteration, even if <code>$pattern</code> had not changed. The <code>/o</code> would prevent this by telling Perl to compile the pattern the first time, then reuse that for subsequent iterations:</p> <pre data-language="perl"><code>my $pattern = shift @ARGV;

while( &lt;&gt; ) {
    print if m/$pattern/o; # useful for Perl &lt; 5.6
}</code></pre> <p>In versions 5.6 and later, Perl won't recompile the regular expression if the variable hasn't changed, so you probably don't need the <code>/o</code> option. It doesn't hurt, but it doesn't help either. If you want any version of Perl to compile the regular expression only once even if the variable changes (thus, only using its initial value), you still need the <code>/o</code>.</p> <p>You can watch Perl's regular expression engine at work to verify for yourself if Perl is recompiling a regular expression. The <code>use re 'debug'</code> pragma (comes with Perl 5.005 and later) shows the details. With Perls before 5.6, you should see <code>re</code> reporting that its compiling the regular expression on each iteration. With Perl 5.6 or later, you should only see <code>re</code> report that for the first iteration.</p> <pre data-language="perl"><code>use re 'debug';

my $regex = 'Perl';
foreach ( qw(Perl Java Ruby Python) ) {
    print STDERR "-" x 73, "\n";
    print STDERR "Trying $_...\n";
    print STDERR "\t$_ is good!\n" if m/$regex/;
}</code></pre> <h3 id="How-do-I-use-a-regular-expression-to-strip-C-style-comments-from-a-file?">
How do I use a regular expression to strip C-style comments from a file?</h3> <p>While this actually can be done, it's much harder than you'd think. For example, this one-liner</p> <pre data-language="perl"><code class="plaintext">perl -0777 -pe 's{/\*.*?\*/}{}gs' foo.c</code></pre> <p>will work in many but not all cases. You see, it's too simple-minded for certain kinds of C programs, in particular, those with what appear to be comments in quoted strings. For that, you'd need something like this, created by Jeffrey Friedl and later modified by Fred Curtis.</p> <pre data-language="perl"><code>$/ = undef;
$_ = &lt;&gt;;
s#/\*[^*]*\*+([^/*][^*]*\*+)*/|("(\\.|[^"\\])*"|'(\\.|[^'\\])*'|.[^/"'\\]*)#defined $2 ? $2 : ""#gse;
print;</code></pre> <p>This could, of course, be more legibly written with the <code>/x</code> modifier, adding whitespace and comments. Here it is expanded, courtesy of Fred Curtis.</p> <pre data-language="perl"><code>s{
   /\*         ##  Start of /* ... */ comment
   [^*]*\*+    ##  Non-* followed by 1-or-more *'s
   (
     [^/*][^*]*\*+
   )*          ##  0-or-more things which don't start with /
               ##    but do end with '*'
   /           ##  End of /* ... */ comment

 |         ##     OR  various things which aren't comments:

   (
     "           ##  Start of " ... " string
     (
       \\.           ##  Escaped char
     |               ##    OR
       [^"\\]        ##  Non "\
     )*
     "           ##  End of " ... " string

   |         ##     OR

     '           ##  Start of ' ... ' string
     (
       \\.           ##  Escaped char
     |               ##    OR
       [^'\\]        ##  Non '\
     )*
     '           ##  End of ' ... ' string

   |         ##     OR

     .           ##  Anything other char
     [^/"'\\]*   ##  Chars which doesn't start a comment, string or escape
   )
 }{defined $2 ? $2 : ""}gxse;</code></pre> <p>A slight modification also removes C++ comments, possibly spanning multiple lines using a continuation character:</p> <pre data-language="perl"><code>s#/\*[^*]*\*+([^/*][^*]*\*+)*/|//([^\\]|[^\n][\n]?)*?\n|("(\\.|[^"\\])*"|'(\\.|[^'\\])*'|.[^/"'\\]*)#defined $3 ? $3 : ""#gse;</code></pre> <h3 id="Can-I-use-Perl-regular-expressions-to-match-balanced-text?">
Can I use Perl regular expressions to match balanced text? </h3> <p>(contributed by brian d foy)</p> <p>Your first try should probably be the <a href="Text::Balanced.html">Text::Balanced</a> module, which is in the Perl standard library since Perl 5.8. It has a variety of functions to deal with tricky text. The <a href="Regexp::Common.html">Regexp::Common</a> module can also help by providing canned patterns you can use.</p> <p>As of Perl 5.10, you can match balanced text with regular expressions using recursive patterns. Before Perl 5.10, you had to resort to various tricks such as using Perl code in <code>(??{})</code> sequences.</p> <p>Here's an example using a recursive regular expression. The goal is to capture all of the text within angle brackets, including the text in nested angle brackets. This sample text has two "major" groups: a group with one level of nesting and a group with two levels of nesting. There are five total groups in angle brackets:</p> <pre data-language="perl"><code class="plaintext">I have some &lt;brackets in &lt;nested brackets&gt; &gt; and
&lt;another group &lt;nested once &lt;nested twice&gt; &gt; &gt;
and that's it.</code></pre> <p>The regular expression to match the balanced text uses two new (to Perl 5.10) regular expression features. These are covered in <a href="perlre.html">perlre</a> and this example is a modified version of one in that documentation.</p> <p>First, adding the new possessive <code>+</code> to any quantifier finds the longest match and does not backtrack. That's important since you want to handle any angle brackets through the recursion, not backtracking. The group <code>[^&lt;&gt;]++</code> finds one or more non-angle brackets without backtracking.</p> <p>Second, the new <code>(?PARNO)</code> refers to the sub-pattern in the particular capture group given by <code>PARNO</code>. In the following regex, the first capture group finds (and remembers) the balanced text, and you need that same pattern within the first buffer to get past the nested text. That's the recursive part. The <code>(?1)</code> uses the pattern in the outer capture group as an independent part of the regex.</p> <p>Putting it all together, you have:</p> <pre data-language="perl"><code>#!/usr/local/bin/perl5.10.0

my $string =&lt;&lt;"HERE";
I have some &lt;brackets in &lt;nested brackets&gt; &gt; and
&lt;another group &lt;nested once &lt;nested twice&gt; &gt; &gt;
and that's it.
HERE

my @groups = $string =~ m/
        (                   # start of capture group 1
        &lt;                   # match an opening angle bracket
            (?:
                [^&lt;&gt;]++     # one or more non angle brackets, non backtracking
                  |
                (?1)        # found &lt; or &gt;, so recurse to capture group 1
            )*
        &gt;                   # match a closing angle bracket
        )                   # end of capture group 1
        /xg;

$" = "\n\t";
print "Found:\n\t@groups\n";</code></pre> <p>The output shows that Perl found the two major groups:</p> <pre data-language="perl"><code class="plaintext">Found:
    &lt;brackets in &lt;nested brackets&gt; &gt;
    &lt;another group &lt;nested once &lt;nested twice&gt; &gt; &gt;</code></pre> <p>With a little extra work, you can get all of the groups in angle brackets even if they are in other angle brackets too. Each time you get a balanced match, remove its outer delimiter (that's the one you just matched so don't match it again) and add it to a queue of strings to process. Keep doing that until you get no matches:</p> <pre data-language="perl"><code>#!/usr/local/bin/perl5.10.0

my @queue =&lt;&lt;"HERE";
I have some &lt;brackets in &lt;nested brackets&gt; &gt; and
&lt;another group &lt;nested once &lt;nested twice&gt; &gt; &gt;
and that's it.
HERE

my $regex = qr/
        (                   # start of bracket 1
        &lt;                   # match an opening angle bracket
            (?:
                [^&lt;&gt;]++     # one or more non angle brackets, non backtracking
                  |
                (?1)        # recurse to bracket 1
            )*
        &gt;                   # match a closing angle bracket
        )                   # end of bracket 1
        /x;

$" = "\n\t";

while( @queue ) {
    my $string = shift @queue;

    my @groups = $string =~ m/$regex/g;
    print "Found:\n\t@groups\n\n" if @groups;

    unshift @queue, map { s/^&lt;//; s/&gt;$//; $_ } @groups;
}</code></pre> <p>The output shows all of the groups. The outermost matches show up first and the nested matches show up later:</p> <pre data-language="perl"><code class="plaintext">Found:
    &lt;brackets in &lt;nested brackets&gt; &gt;
    &lt;another group &lt;nested once &lt;nested twice&gt; &gt; &gt;

Found:
    &lt;nested brackets&gt;

Found:
    &lt;nested once &lt;nested twice&gt; &gt;

Found:
    &lt;nested twice&gt;</code></pre> <h3 id="What-does-it-mean-that-regexes-are-greedy?-How-can-I-get-around-it?">
What does it mean that regexes are greedy? How can I get around it? </h3> <p>Most people mean that greedy regexes match as much as they can. Technically speaking, it's actually the quantifiers (<code>?</code>, <code>*</code>, <code>+</code>, <code>{}</code>) that are greedy rather than the whole pattern; Perl prefers local greed and immediate gratification to overall greed. To get non-greedy versions of the same quantifiers, use (<code>??</code>, <code>*?</code>, <code>+?</code>, <code>{}?</code>).</p> <p>An example:</p> <pre data-language="perl"><code>my $s1 = my $s2 = "I am very very cold";
$s1 =~ s/ve.*y //;      # I am cold
$s2 =~ s/ve.*?y //;     # I am very cold</code></pre> <p>Notice how the second substitution stopped matching as soon as it encountered "y ". The <code>*?</code> quantifier effectively tells the regular expression engine to find a match as quickly as possible and pass control on to whatever is next in line, as you would if you were playing hot potato.</p> <h3 id="How-do-I-process-each-word-on-each-line?">
How do I process each word on each line? </h3> <p>Use the split function:</p> <pre data-language="perl"><code>while (&lt;&gt;) {
    foreach my $word ( split ) {
        # do something with $word here
    }
}</code></pre> <p>Note that this isn't really a word in the English sense; it's just chunks of consecutive non-whitespace characters.</p> <p>To work with only alphanumeric sequences (including underscores), you might consider</p> <pre data-language="perl"><code>while (&lt;&gt;) {
    foreach $word (m/(\w+)/g) {
        # do something with $word here
    }
}</code></pre> <h3 id="How-can-I-print-out-a-word-frequency-or-line-frequency-summary?">
How can I print out a word-frequency or line-frequency summary?</h3> <p>To do this, you have to parse out each word in the input stream. We'll pretend that by word you mean chunk of alphabetics, hyphens, or apostrophes, rather than the non-whitespace chunk idea of a word given in the previous question:</p> <pre data-language="perl"><code>my (%seen);
while (&lt;&gt;) {
    while ( /(\b[^\W_\d][\w'-]+\b)/g ) {   # misses "`sheep'"
        $seen{$1}++;
    }
}

while ( my ($word, $count) = each %seen ) {
    print "$count $word\n";
}</code></pre> <p>If you wanted to do the same thing for lines, you wouldn't need a regular expression:</p> <pre data-language="perl"><code>my (%seen);

while (&lt;&gt;) {
    $seen{$_}++;
}

while ( my ($line, $count) = each %seen ) {
    print "$count $line";
}</code></pre> <p>If you want these output in a sorted order, see <a href="perlfaq4.html">perlfaq4</a>: "How do I sort a hash (optionally by value instead of key)?".</p> <h3 id="How-can-I-do-approximate-matching?">
How can I do approximate matching? </h3> <p>See the module <a href="String::Approx.html">String::Approx</a> available from CPAN.</p> <h3 id="How-do-I-efficiently-match-many-regular-expressions-at-once?">
How do I efficiently match many regular expressions at once? </h3> <p>(contributed by brian d foy)</p> <p>You want to avoid compiling a regular expression every time you want to match it. In this example, perl must recompile the regular expression for every iteration of the <code>foreach</code> loop since <code>$pattern</code> can change:</p> <pre data-language="perl"><code>my @patterns = qw( fo+ ba[rz] );

LINE: while( my $line = &lt;&gt; ) {
    foreach my $pattern ( @patterns ) {
        if( $line =~ m/\b$pattern\b/i ) {
            print $line;
            next LINE;
        }
    }
}</code></pre> <p>The <code>qr//</code> operator compiles a regular expression, but doesn't apply it. When you use the pre-compiled version of the regex, perl does less work. In this example, I inserted a <code>map</code> to turn each pattern into its pre-compiled form. The rest of the script is the same, but faster:</p> <pre data-language="perl"><code>my @patterns = map { qr/\b$_\b/i } qw( fo+ ba[rz] );

LINE: while( my $line = &lt;&gt; ) {
    foreach my $pattern ( @patterns ) {
        if( $line =~ m/$pattern/ ) {
            print $line;
            next LINE;
        }
    }
}</code></pre> <p>In some cases, you may be able to make several patterns into a single regular expression. Beware of situations that require backtracking though. In this example, the regex is only compiled once because <code>$regex</code> doesn't change between iterations:</p> <pre data-language="perl"><code>my $regex = join '|', qw( fo+ ba[rz] );

while( my $line = &lt;&gt; ) {
    print if $line =~ m/\b(?:$regex)\b/i;
}</code></pre> <p>The function <a href="Data::Munge.html#list2re">"list2re" in Data::Munge</a> on CPAN can also be used to form a single regex that matches a list of literal strings (not regexes).</p> <p>For more details on regular expression efficiency, see <i>Mastering Regular Expressions</i> by Jeffrey Friedl. He explains how the regular expressions engine works and why some patterns are surprisingly inefficient. Once you understand how perl applies regular expressions, you can tune them for individual situations.</p> <h3 id="Why-don't-word-boundary-searches-with-\b-work-for-me?">
Why don't word-boundary searches with <code>\b</code> work for me? </h3> <p>(contributed by brian d foy)</p> <p>Ensure that you know what \b really does: it's the boundary between a word character, \w, and something that isn't a word character. That thing that isn't a word character might be \W, but it can also be the start or end of the string.</p> <p>It's not (not!) the boundary between whitespace and non-whitespace, and it's not the stuff between words we use to create sentences.</p> <p>In regex speak, a word boundary (\b) is a "zero width assertion", meaning that it doesn't represent a character in the string, but a condition at a certain position.</p> <p>For the regular expression, /\bPerl\b/, there has to be a word boundary before the "P" and after the "l". As long as something other than a word character precedes the "P" and succeeds the "l", the pattern will match. These strings match /\bPerl\b/.</p> <pre data-language="perl"><code class="plaintext">"Perl"    # no word char before "P" or after "l"
"Perl "   # same as previous (space is not a word char)
"'Perl'"  # the "'" char is not a word char
"Perl's"  # no word char before "P", non-word char after "l"</code></pre> <p>These strings do not match /\bPerl\b/.</p> <pre data-language="perl"><code class="plaintext">"Perl_"   # "_" is a word char!
"Perler"  # no word char before "P", but one after "l"</code></pre> <p>You don't have to use \b to match words though. You can look for non-word characters surrounded by word characters. These strings match the pattern /\b'\b/.</p> <pre data-language="perl"><code class="plaintext">"don't"   # the "'" char is surrounded by "n" and "t"
"qep'a'"  # the "'" char is surrounded by "p" and "a"</code></pre> <p>These strings do not match /\b'\b/.</p> <pre data-language="perl"><code class="plaintext">"foo'"    # there is no word char after non-word "'"</code></pre> <p>You can also use the complement of \b, \B, to specify that there should not be a word boundary.</p> <p>In the pattern /\Bam\B/, there must be a word character before the "a" and after the "m". These patterns match /\Bam\B/:</p> <pre data-language="perl"><code class="plaintext">"llama"   # "am" surrounded by word chars
"Samuel"  # same</code></pre> <p>These strings do not match /\Bam\B/</p> <pre data-language="perl"><code class="plaintext">"Sam"      # no word boundary before "a", but one after "m"
"I am Sam" # "am" surrounded by non-word chars</code></pre> <h3 id="Why-does-using-$&amp;,-$`,-or-$'-slow-my-program-down?">
Why does using $&amp;, $`, or $' slow my program down? </h3> <p>(contributed by Anno Siegel)</p> <p>Once Perl sees that you need one of these variables anywhere in the program, it provides them on each and every pattern match. That means that on every pattern match the entire string will be copied, part of it to $`, part to $&amp;, and part to $'. Thus the penalty is most severe with long strings and patterns that match often. Avoid $&amp;, $', and $` if you can, but if you can't, once you've used them at all, use them at will because you've already paid the price. Remember that some algorithms really appreciate them. As of the 5.005 release, the $&amp; variable is no longer "expensive" the way the other two are.</p> <p>Since Perl 5.6.1 the special variables @- and @+ can functionally replace $`, $&amp; and $'. These arrays contain pointers to the beginning and end of each match (see perlvar for the full story), so they give you essentially the same information, but without the risk of excessive string copying.</p> <p>Perl 5.10 added three specials, <code>${^MATCH}</code>, <code>${^PREMATCH}</code>, and <code>${^POSTMATCH}</code> to do the same job but without the global performance penalty. Perl 5.10 only sets these variables if you compile or execute the regular expression with the <code>/p</code> modifier.</p> <h3 id="What-good-is-\G-in-a-regular-expression?">
What good is <code>\G</code> in a regular expression? </h3> <p>You use the <code>\G</code> anchor to start the next match on the same string where the last match left off. The regular expression engine cannot skip over any characters to find the next match with this anchor, so <code>\G</code> is similar to the beginning of string anchor, <code>^</code>. The <code>\G</code> anchor is typically used with the <code>g</code> modifier. It uses the value of <code>pos()</code> as the position to start the next match. As the match operator makes successive matches, it updates <code>pos()</code> with the position of the next character past the last match (or the first character of the next match, depending on how you like to look at it). Each string has its own <code>pos()</code> value.</p> <p>Suppose you want to match all of consecutive pairs of digits in a string like "1122a44" and stop matching when you encounter non-digits. You want to match <code>11</code> and <code>22</code> but the letter <code>a</code> shows up between <code>22</code> and <code>44</code> and you want to stop at <code>a</code>. Simply matching pairs of digits skips over the <code>a</code> and still matches <code>44</code>.</p> <pre data-language="perl"><code>$_ = "1122a44";
my @pairs = m/(\d\d)/g;   # qw( 11 22 44 )</code></pre> <p>If you use the <code>\G</code> anchor, you force the match after <code>22</code> to start with the <code>a</code>. The regular expression cannot match there since it does not find a digit, so the next match fails and the match operator returns the pairs it already found.</p> <pre data-language="perl"><code>$_ = "1122a44";
my @pairs = m/\G(\d\d)/g; # qw( 11 22 )</code></pre> <p>You can also use the <code>\G</code> anchor in scalar context. You still need the <code>g</code> modifier.</p> <pre data-language="perl"><code>$_ = "1122a44";
while( m/\G(\d\d)/g ) {
    print "Found $1\n";
}</code></pre> <p>After the match fails at the letter <code>a</code>, perl resets <code>pos()</code> and the next match on the same string starts at the beginning.</p> <pre data-language="perl"><code>$_ = "1122a44";
while( m/\G(\d\d)/g ) {
    print "Found $1\n";
}

print "Found $1 after while" if m/(\d\d)/g; # finds "11"</code></pre> <p>You can disable <code>pos()</code> resets on fail with the <code>c</code> modifier, documented in <a href="perlop.html">perlop</a> and <a href="perlreref.html">perlreref</a>. Subsequent matches start where the last successful match ended (the value of <code>pos()</code>) even if a match on the same string has failed in the meantime. In this case, the match after the <code>while()</code> loop starts at the <code>a</code> (where the last match stopped), and since it does not use any anchor it can skip over the <code>a</code> to find <code>44</code>.</p> <pre data-language="perl"><code>$_ = "1122a44";
while( m/\G(\d\d)/gc ) {
    print "Found $1\n";
}

print "Found $1 after while" if m/(\d\d)/g; # finds "44"</code></pre> <p>Typically you use the <code>\G</code> anchor with the <code>c</code> modifier when you want to try a different match if one fails, such as in a tokenizer. Jeffrey Friedl offers this example which works in 5.004 or later.</p> <pre data-language="perl"><code>while (&lt;&gt;) {
    chomp;
    PARSER: {
        m/ \G( \d+\b    )/gcx   &amp;&amp; do { print "number: $1\n";  redo; };
        m/ \G( \w+      )/gcx   &amp;&amp; do { print "word:   $1\n";  redo; };
        m/ \G( \s+      )/gcx   &amp;&amp; do { print "space:  $1\n";  redo; };
        m/ \G( [^\w\d]+ )/gcx   &amp;&amp; do { print "other:  $1\n";  redo; };
    }
}</code></pre> <p>For each line, the <code>PARSER</code> loop first tries to match a series of digits followed by a word boundary. This match has to start at the place the last match left off (or the beginning of the string on the first match). Since <code>m/ \G( \d+\b )/gcx</code> uses the <code>c</code> modifier, if the string does not match that regular expression, perl does not reset pos() and the next match starts at the same position to try a different pattern.</p> <h3 id="Are-Perl-regexes-DFAs-or-NFAs?-Are-they-POSIX-compliant?">
Are Perl regexes DFAs or NFAs? Are they POSIX compliant? </h3> <p>While it's true that Perl's regular expressions resemble the DFAs (deterministic finite automata) of the egrep(1) program, they are in fact implemented as NFAs (non-deterministic finite automata) to allow backtracking and backreferencing. And they aren't POSIX-style either, because those guarantee worst-case behavior for all cases. (It seems that some people prefer guarantees of consistency, even when what's guaranteed is slowness.) See the book "Mastering Regular Expressions" (from O'Reilly) by Jeffrey Friedl for all the details you could ever hope to know on these matters (a full citation appears in <a href="perlfaq2.html">perlfaq2</a>).</p> <h3 id="What's-wrong-with-using-grep-in-a-void-context?">
What's wrong with using grep in a void context? </h3> <p>The problem is that grep builds a return list, regardless of the context. This means you're making Perl go to the trouble of building a list that you then just throw away. If the list is large, you waste both time and space. If your intent is to iterate over the list, then use a for loop for this purpose.</p> <p>In perls older than 5.8.1, map suffers from this problem as well. But since 5.8.1, this has been fixed, and map is context aware - in void context, no lists are constructed.</p> <h3 id="How-can-I-match-strings-with-multibyte-characters?">
How can I match strings with multibyte characters? </h3> <p>Starting from Perl 5.6 Perl has had some level of multibyte character support. Perl 5.8 or later is recommended. Supported multibyte character repertoires include Unicode, and legacy encodings through the Encode module. See <a href="perluniintro.html">perluniintro</a>, <a href="perlunicode.html">perlunicode</a>, and <a href="encode.html">Encode</a>.</p> <p>If you are stuck with older Perls, you can do Unicode with the <a href="Unicode::String.html">Unicode::String</a> module, and character conversions using the <a href="Unicode::Map8.html">Unicode::Map8</a> and <a href="Unicode::Map.html">Unicode::Map</a> modules. If you are using Japanese encodings, you might try using the jperl 5.005_03.</p> <p>Finally, the following set of approaches was offered by Jeffrey Friedl, whose article in issue #5 of The Perl Journal talks about this very matter.</p> <p>Let's suppose you have some weird Martian encoding where pairs of ASCII uppercase letters encode single Martian letters (i.e. the two bytes "CV" make a single Martian letter, as do the two bytes "SG", "VS", "XX", etc.). Other bytes represent single characters, just like ASCII.</p> <p>So, the string of Martian "I am CVSGXX!" uses 12 bytes to encode the nine characters 'I', ' ', 'a', 'm', ' ', 'CV', 'SG', 'XX', '!'.</p> <p>Now, say you want to search for the single character <code>/GX/</code>. Perl doesn't know about Martian, so it'll find the two bytes "GX" in the "I am CVSGXX!" string, even though that character isn't there: it just looks like it is because "SG" is next to "XX", but there's no real "GX". This is a big problem.</p> <p>Here are a few ways, all painful, to deal with it:</p> <pre data-language="perl"><code># Make sure adjacent "martian" bytes are no longer adjacent.
$martian =~ s/([A-Z][A-Z])/ $1 /g;

print "found GX!\n" if $martian =~ /GX/;</code></pre> <p>Or like this:</p> <pre data-language="perl"><code>my @chars = $martian =~ m/([A-Z][A-Z]|[^A-Z])/g;
# above is conceptually similar to:     my @chars = $text =~ m/(.)/g;
#
foreach my $char (@chars) {
    print "found GX!\n", last if $char eq 'GX';
}</code></pre> <p>Or like this:</p> <pre data-language="perl"><code>while ($martian =~ m/\G([A-Z][A-Z]|.)/gs) {  # \G probably unneeded
    if ($1 eq 'GX') {
        print "found GX!\n";
        last;
    }
}</code></pre> <p>Here's another, slightly less painful, way to do it from Benjamin Goldberg, who uses a zero-width negative look-behind assertion.</p> <pre data-language="perl"><code>print "found GX!\n" if    $martian =~ m/
    (?&lt;![A-Z])
    (?:[A-Z][A-Z])*?
    GX
    /x;</code></pre> <p>This succeeds if the "martian" character GX is in the string, and fails otherwise. If you don't like using (?&lt;!), a zero-width negative look-behind assertion, you can replace (?&lt;![A-Z]) with (?:^|[^A-Z]).</p> <p>It does have the drawback of putting the wrong thing in $-[0] and $+[0], but this usually can be worked around.</p> <h3 id="How-do-I-match-a-regular-expression-that's-in-a-variable?">
How do I match a regular expression that's in a variable? </h3> <p>(contributed by brian d foy)</p> <p>We don't have to hard-code patterns into the match operator (or anything else that works with regular expressions). We can put the pattern in a variable for later use.</p> <p>The match operator is a double quote context, so you can interpolate your variable just like a double quoted string. In this case, you read the regular expression as user input and store it in <code>$regex</code>. Once you have the pattern in <code>$regex</code>, you use that variable in the match operator.</p> <pre data-language="perl"><code>chomp( my $regex = &lt;STDIN&gt; );

if( $string =~ m/$regex/ ) { ... }</code></pre> <p>Any regular expression special characters in <code>$regex</code> are still special, and the pattern still has to be valid or Perl will complain. For instance, in this pattern there is an unpaired parenthesis.</p> <pre data-language="perl"><code>my $regex = "Unmatched ( paren";

"Two parens to bind them all" =~ m/$regex/;</code></pre> <p>When Perl compiles the regular expression, it treats the parenthesis as the start of a memory match. When it doesn't find the closing parenthesis, it complains:</p> <pre data-language="perl"><code class="plaintext">Unmatched ( in regex; marked by &lt;-- HERE in m/Unmatched ( &lt;-- HERE  paren/ at script line 3.</code></pre> <p>You can get around this in several ways depending on our situation. First, if you don't want any of the characters in the string to be special, you can escape them with <code>quotemeta</code> before you use the string.</p> <pre data-language="perl"><code>chomp( my $regex = &lt;STDIN&gt; );
$regex = quotemeta( $regex );

if( $string =~ m/$regex/ ) { ... }</code></pre> <p>You can also do this directly in the match operator using the <code>\Q</code> and <code>\E</code> sequences. The <code>\Q</code> tells Perl where to start escaping special characters, and the <code>\E</code> tells it where to stop (see <a href="perlop.html">perlop</a> for more details).</p> <pre data-language="perl"><code>chomp( my $regex = &lt;STDIN&gt; );

if( $string =~ m/\Q$regex\E/ ) { ... }</code></pre> <p>Alternately, you can use <code>qr//</code>, the regular expression quote operator (see <a href="perlop.html">perlop</a> for more details). It quotes and perhaps compiles the pattern, and you can apply regular expression flags to the pattern.</p> <pre data-language="perl"><code>chomp( my $input = &lt;STDIN&gt; );

my $regex = qr/$input/is;

$string =~ m/$regex/  # same as m/$input/is;</code></pre> <p>You might also want to trap any errors by wrapping an <code>eval</code> block around the whole thing.</p> <pre data-language="perl"><code>chomp( my $input = &lt;STDIN&gt; );

eval {
    if( $string =~ m/\Q$input\E/ ) { ... }
};
warn $@ if $@;</code></pre> <p>Or...</p> <pre data-language="perl"><code>my $regex = eval { qr/$input/is };
if( defined $regex ) {
    $string =~ m/$regex/;
}
else {
    warn $@;
}</code></pre> <h2 id="AUTHOR-AND-COPYRIGHT">
AUTHOR AND COPYRIGHT</h2> <p>Copyright (c) 1997-2010 Tom Christiansen, Nathan Torkington, and other authors as noted. All rights reserved.</p> <p>This documentation is free; you can redistribute it and/or modify it under the same terms as Perl itself.</p> <p>Irrespective of its distribution, all code examples in this file are hereby placed into the public domain. You are permitted and encouraged to use this code in your own programs for fun or for profit as you see fit. A simple comment in the code giving credit would be courteous but is not required.</p><div class="_attribution">
  <p class="_attribution-p">
    &copy; 1993&ndash;2023 Larry Wall and others<br>Licensed under the GNU General Public License version 1 or later, or the Artistic License.<br>The Perl logo is a trademark of the Perl Foundation.<br>
    <a href="https://perldoc.perl.org/5.38.0/perlfaq6" class="_attribution-link">https://perldoc.perl.org/5.38.0/perlfaq6</a>
  </p>
</div>
