<h1>perlop</h1>   <h2><a id="toc">CONTENTS</a></h2> <ul> <li> <a class="text-decoration-none" href="#NAME">NAME </a> </li> <li> <a class="text-decoration-none" href="#DESCRIPTION">DESCRIPTION</a> <ul> <li> <a class="text-decoration-none" href="#Operator-Precedence-and-Associativity">Operator Precedence and Associativity </a> </li> <li> <a class="text-decoration-none" href="#Terms-and-List-Operators-(Leftward)">Terms and List Operators (Leftward) </a> </li> <li> <a class="text-decoration-none" href="#The-Arrow-Operator">The Arrow Operator </a> </li> <li> <a class="text-decoration-none" href="#Auto-increment-and-Auto-decrement">Auto-increment and Auto-decrement </a> </li> <li> <a class="text-decoration-none" href="#Exponentiation">Exponentiation </a> </li> <li> <a class="text-decoration-none" href="#Symbolic-Unary-Operators">Symbolic Unary Operators </a> </li> <li> <a class="text-decoration-none" href="#Binding-Operators">Binding Operators </a> </li> <li> <a class="text-decoration-none" href="#Multiplicative-Operators">Multiplicative Operators </a> </li> <li> <a class="text-decoration-none" href="#Additive-Operators">Additive Operators </a> </li> <li> <a class="text-decoration-none" href="#Shift-Operators">Shift Operators </a> </li> <li> <a class="text-decoration-none" href="#Named-Unary-Operators">Named Unary Operators </a> </li> <li> <a class="text-decoration-none" href="#Relational-Operators">Relational Operators </a> </li> <li> <a class="text-decoration-none" href="#Equality-Operators">Equality Operators </a> </li> <li> <a class="text-decoration-none" href="#Class-Instance-Operator">Class Instance Operator </a> </li> <li> <a class="text-decoration-none" href="#Smartmatch-Operator">Smartmatch Operator</a> <ul> <li> <a class="text-decoration-none" href="#Smartmatching-of-Objects">Smartmatching of Objects</a> </li> </ul> </li> <li> <a class="text-decoration-none" href="#Bitwise-And">Bitwise And </a> </li> <li> <a class="text-decoration-none" href="#Bitwise-Or-and-Exclusive-Or">Bitwise Or and Exclusive Or </a> </li> <li> <a class="text-decoration-none" href="#C-style-Logical-And">C-style Logical And </a> </li> <li> <a class="text-decoration-none" href="#C-style-Logical-Or">C-style Logical Or </a> </li> <li> <a class="text-decoration-none" href="#Logical-Defined-Or">Logical Defined-Or </a> </li> <li> <a class="text-decoration-none" href="#Range-Operators">Range Operators </a> </li> <li> <a class="text-decoration-none" href="#Conditional-Operator">Conditional Operator </a> </li> <li> <a class="text-decoration-none" href="#Assignment-Operators">Assignment Operators </a> </li> <li> <a class="text-decoration-none" href="#Comma-Operator">Comma Operator </a> </li> <li> <a class="text-decoration-none" href="#List-Operators-(Rightward)">List Operators (Rightward) </a> </li> <li> <a class="text-decoration-none" href="#Logical-Not">Logical Not </a> </li> <li> <a class="text-decoration-none" href="#Logical-And">Logical And </a> </li> <li> <a class="text-decoration-none" href="#Logical-or-and-Exclusive-Or">Logical or and Exclusive Or </a> </li> <li> <a class="text-decoration-none" href="#C-Operators-Missing-From-Perl">C Operators Missing From Perl </a> </li> <li> <a class="text-decoration-none" href="#Quote-and-Quote-like-Operators">Quote and Quote-like Operators </a> </li> <li> <a class="text-decoration-none" href="#Regexp-Quote-Like-Operators">Regexp Quote-Like Operators </a> </li> <li> <a class="text-decoration-none" href="#Quote-Like-Operators">Quote-Like Operators </a> </li> <li> <a class="text-decoration-none" href="#Gory-details-of-parsing-quoted-constructs">Gory details of parsing quoted constructs </a> </li> <li> <a class="text-decoration-none" href="#I/O-Operators">I/O Operators </a> </li> <li> <a class="text-decoration-none" href="#Constant-Folding">Constant Folding </a> </li> <li> <a class="text-decoration-none" href="#No-ops">No-ops </a> </li> <li> <a class="text-decoration-none" href="#Bitwise-String-Operators">Bitwise String Operators </a> </li> <li> <a class="text-decoration-none" href="#Integer-Arithmetic">Integer Arithmetic </a> </li> <li> <a class="text-decoration-none" href="#Floating-point-Arithmetic">Floating-point Arithmetic</a> </li> <li> <a class="text-decoration-none" href="#Bigger-Numbers">Bigger Numbers </a> </li> </ul> </li> </ul> <h2 id="NAME">NAME </h2> <p>perlop - Perl operators and precedence</p> <h2 id="DESCRIPTION">DESCRIPTION</h2> <p>In Perl, the operator determines what operation is performed, independent of the type of the operands. For example <span style="white-space: nowrap;"><code>$x + $y</code></span> is always a numeric addition, and if <code>$x</code> or <code>$y</code> do not contain numbers, an attempt is made to convert them to numbers first.</p> <p>This is in contrast to many other dynamic languages, where the operation is determined by the type of the first argument. It also means that Perl has two versions of some operators, one for numeric and one for string comparison. For example <span style="white-space: nowrap;"><code>$x == $y</code></span> compares two numbers for equality, and <span style="white-space: nowrap;"><code>$x eq $y</code></span> compares two strings.</p> <p>There are a few exceptions though: <code>x</code> can be either string repetition or list repetition, depending on the type of the left operand, and <code>&amp;</code>, <code>|</code>, <code>^</code> and <code>~</code> can be either string or numeric bit operations.</p> <h3 id="Operator-Precedence-and-Associativity">
Operator Precedence and Associativity </h3> <p>Operator precedence and associativity work in Perl more or less like they do in mathematics.</p> <p><i>Operator precedence</i> means some operators group more tightly than others. For example, in <code>2 + 4 * 5</code>, the multiplication has higher precedence, so <code>4 * 5</code> is grouped together as the right-hand operand of the addition, rather than <code>2 + 4</code> being grouped together as the left-hand operand of the multiplication. It is as if the expression were written <code>2 + (4 * 5)</code>, not <code>(2 + 4) * 5</code>. So the expression yields <code>2 + 20 == 22</code>, rather than <code>6 * 5 == 30</code>.</p> <p><i>Operator associativity</i> defines what happens if a sequence of the same operators is used one after another: usually that they will be grouped at the left or the right. For example, in <code>9 - 3 - 2</code>, subtraction is left associative, so <code>9 - 3</code> is grouped together as the left-hand operand of the second subtraction, rather than <code>3 - 2</code> being grouped together as the right-hand operand of the first subtraction. It is as if the expression were written <code>(9 - 3) - 2</code>, not <code>9 - (3 - 2)</code>. So the expression yields <code>6 - 2 == 4</code>, rather than <code>9 - 1 == 8</code>.</p> <p>For simple operators that evaluate all their operands and then combine the values in some way, precedence and associativity (and parentheses) imply some ordering requirements on those combining operations. For example, in <code>2 + 4 * 5</code>, the grouping implied by precedence means that the multiplication of 4 and 5 must be performed before the addition of 2 and 20, simply because the result of that multiplication is required as one of the operands of the addition. But the order of operations is not fully determined by this: in <code>2 * 2 + 4 * 5</code> both multiplications must be performed before the addition, but the grouping does not say anything about the order in which the two multiplications are performed. In fact Perl has a general rule that the operands of an operator are evaluated in left-to-right order. A few operators such as <code>&amp;&amp;=</code> have special evaluation rules that can result in an operand not being evaluated at all; in general, the top-level operator in an expression has control of operand evaluation.</p> <p>Some comparison operators, as their associativity, <i>chain</i> with some operators of the same precedence (but never with operators of different precedence). This chaining means that each comparison is performed on the two arguments surrounding it, with each interior argument taking part in two comparisons, and the comparison results are implicitly ANDed. Thus <span style="white-space: nowrap;"><code>"$x &lt; $y &lt;= $z"</code></span> behaves exactly like <span style="white-space: nowrap;"><code>"$x &lt; $y &amp;&amp; $y &lt;= $z"</code></span>, assuming that <code>"$y"</code> is as simple a scalar as it looks. The ANDing short-circuits just like <code>"&amp;&amp;"</code> does, stopping the sequence of comparisons as soon as one yields false.</p> <p>In a chained comparison, each argument expression is evaluated at most once, even if it takes part in two comparisons, but the result of the evaluation is fetched for each comparison. (It is not evaluated at all if the short-circuiting means that it's not required for any comparisons.) This matters if the computation of an interior argument is expensive or non-deterministic. For example,</p> <pre data-language="perl"><code>if($x &lt; expensive_sub() &lt;= $z) { ...</code></pre> <p>is not entirely like</p> <pre data-language="perl"><code>if($x &lt; expensive_sub() &amp;&amp; expensive_sub() &lt;= $z) { ...</code></pre> <p>but instead closer to</p> <pre data-language="perl"><code>my $tmp = expensive_sub();
if($x &lt; $tmp &amp;&amp; $tmp &lt;= $z) { ...</code></pre> <p>in that the subroutine is only called once. However, it's not exactly like this latter code either, because the chained comparison doesn't actually involve any temporary variable (named or otherwise): there is no assignment. This doesn't make much difference where the expression is a call to an ordinary subroutine, but matters more with an lvalue subroutine, or if the argument expression yields some unusual kind of scalar by other means. For example, if the argument expression yields a tied scalar, then the expression is evaluated to produce that scalar at most once, but the value of that scalar may be fetched up to twice, once for each comparison in which it is actually used.</p> <p>In this example, the expression is evaluated only once, and the tied scalar (the result of the expression) is fetched for each comparison that uses it.</p> <pre data-language="perl"><code>if ($x &lt; $tied_scalar &lt; $z) { ...</code></pre> <p>In the next example, the expression is evaluated only once, and the tied scalar is fetched once as part of the operation within the expression. The result of that operation is fetched for each comparison, which normally doesn't matter unless that expression result is also magical due to operator overloading.</p> <pre data-language="perl"><code>if ($x &lt; $tied_scalar + 42 &lt; $z) { ...</code></pre> <p>Some operators are instead non-associative, meaning that it is a syntax error to use a sequence of those operators of the same precedence. For example, <span style="white-space: nowrap;"><code>"$x .. $y .. $z"</code></span> is an error.</p> <p>Perl operators have the following associativity and precedence, listed from highest precedence to lowest. Operators borrowed from C keep the same precedence relationship with each other, even where C's precedence is slightly screwy. (This makes learning Perl easier for C folks.) With very few exceptions, these all operate on scalar values only, not array values.</p> <pre data-language="perl"><code class="plaintext">left	terms and list operators (leftward)
left	-&gt;
nonassoc	++ --
right	**
right	! ~ ~. \ and unary + and -
left	=~ !~
left	* / % x
left	+ - .
left	&lt;&lt; &gt;&gt;
nonassoc	named unary operators
nonassoc    isa
chained 	&lt; &gt; &lt;= &gt;= lt gt le ge
chain/na	== != eq ne &lt;=&gt; cmp ~~
left	&amp; &amp;.
left	| |. ^ ^.
left	&amp;&amp;
left	|| //
nonassoc	..  ...
right	?:
right	= += -= *= etc. goto last next redo dump
left	, =&gt;
nonassoc	list operators (rightward)
right	not
left	and
left	or xor</code></pre> <p>In the following sections, these operators are covered in detail, in the same order in which they appear in the table above.</p> <p>Many operators can be overloaded for objects. See <a href="overload.html">overload</a>.</p> <h3 id="Terms-and-List-Operators-(Leftward)">
Terms and List Operators (Leftward) </h3> <p>A TERM has the highest precedence in Perl. They include variables, quote and quote-like operators, any expression in parentheses, and any function whose arguments are parenthesized. Actually, there aren't really functions in this sense, just list operators and unary operators behaving as functions because you put parentheses around the arguments. These are all documented in <a href="perlfunc.html">perlfunc</a>.</p> <p>If any list operator (<code>print()</code>, etc.) or any unary operator (<code>chdir()</code>, etc.) is followed by a left parenthesis as the next token, the operator and arguments within parentheses are taken to be of highest precedence, just like a normal function call.</p> <p>In the absence of parentheses, the precedence of list operators such as <code>print</code>, <code>sort</code>, or <code>chmod</code> is either very high or very low depending on whether you are looking at the left side or the right side of the operator. For example, in</p> <pre data-language="perl"><code>@ary = (1, 3, sort 4, 2);
print @ary;		# prints 1324</code></pre> <p>the commas on the right of the <code>sort</code> are evaluated before the <code>sort</code>, but the commas on the left are evaluated after. In other words, list operators tend to gobble up all arguments that follow, and then act like a simple TERM with regard to the preceding expression. Be careful with parentheses:</p> <pre data-language="perl"><code># These evaluate exit before doing the print:
print($foo, exit);	# Obviously not what you want.
print $foo, exit;	# Nor is this.

# These do the print before evaluating exit:
(print $foo), exit;	# This is what you want.
print($foo), exit;	# Or this.
print ($foo), exit;	# Or even this.</code></pre> <p>Also note that</p> <pre data-language="perl"><code>print ($foo &amp; 255) + 1, "\n";</code></pre> <p>probably doesn't do what you expect at first glance. The parentheses enclose the argument list for <code>print</code> which is evaluated (printing the result of <span style="white-space: nowrap;"><code>$foo &amp; 255</code></span>). Then one is added to the return value of <code>print</code> (usually 1). The result is something like this:</p> <pre data-language="perl"><code>1 + 1, "\n";    # Obviously not what you meant.</code></pre> <p>To do what you meant properly, you must write:</p> <pre data-language="perl"><code>print(($foo &amp; 255) + 1, "\n");</code></pre> <p>See <a href="#Named-Unary-Operators">"Named Unary Operators"</a> for more discussion of this.</p> <p>Also parsed as terms are the <span style="white-space: nowrap;"><code>do {}</code></span> and <span style="white-space: nowrap;"><code>eval {}</code></span> constructs, as well as subroutine and method calls, and the anonymous constructors <code>[]</code> and <code>{}</code>.</p> <p>See also <a href="#Quote-and-Quote-like-Operators">"Quote and Quote-like Operators"</a> toward the end of this section, as well as <a href="#I%2FO-Operators">"I/O Operators"</a>.</p> <h3 id="The-Arrow-Operator">
The Arrow Operator </h3> <p>"<code>-&gt;</code>" is an infix dereference operator, just as it is in C and C++. If the right side is either a <code>[...]</code>, <code>{...}</code>, or a <code>(...)</code> subscript, then the left side must be either a hard or symbolic reference to an array, a hash, or a subroutine respectively. (Or technically speaking, a location capable of holding a hard reference, if it's an array or hash reference being used for assignment.) See <a href="perlreftut.html">perlreftut</a> and <a href="perlref.html">perlref</a>.</p> <p>Otherwise, the right side is a method name or a simple scalar variable containing either the method name or a subroutine reference, and (if it is a method name) the left side must be either an object (a blessed reference) or a class name (that is, a package name). See <a href="perlobj.html">perlobj</a>.</p> <p>The dereferencing cases (as opposed to method-calling cases) are somewhat extended by the <code>postderef</code> feature. For the details of that feature, consult <a href="perlref.html#Postfix-Dereference-Syntax">"Postfix Dereference Syntax" in perlref</a>.</p> <h3 id="Auto-increment-and-Auto-decrement">
Auto-increment and Auto-decrement </h3> <p><code>"++"</code> and <code>"--"</code> work as in C. That is, if placed before a variable, they increment or decrement the variable by one before returning the value, and if placed after, increment or decrement after returning the value.</p> <pre data-language="perl"><code>$i = 0;  $j = 0;
print $i++;  # prints 0
print ++$j;  # prints 1</code></pre> <p>Note that just as in C, Perl doesn't define <b>when</b> the variable is incremented or decremented. You just know it will be done sometime before or after the value is returned. This also means that modifying a variable twice in the same statement will lead to undefined behavior. Avoid statements like:</p> <pre data-language="perl"><code>$i = $i ++;
print ++ $i + $i ++;</code></pre> <p>Perl will not guarantee what the result of the above statements is.</p> <p>The auto-increment operator has a little extra builtin magic to it. If you increment a variable that is numeric, or that has ever been used in a numeric context, you get a normal increment. If, however, the variable has been used in only string contexts since it was set, and has a value that is not the empty string and matches the pattern <code>/^[a-zA-Z]*[0-9]*\z/</code>, the increment is done as a string, preserving each character within its range, with carry:</p> <pre data-language="perl"><code>print ++($foo = "99");	# prints "100"
print ++($foo = "a0");	# prints "a1"
print ++($foo = "Az");	# prints "Ba"
print ++($foo = "zz");	# prints "aaa"</code></pre> <p><code>undef</code> is always treated as numeric, and in particular is changed to <code>0</code> before incrementing (so that a post-increment of an undef value will return <code>0</code> rather than <code>undef</code>).</p> <p>The auto-decrement operator is not magical.</p> <h3 id="Exponentiation">Exponentiation </h3> <p>Binary <code>"**"</code> is the exponentiation operator. It binds even more tightly than unary minus, so <code>-2**4</code> is <code>-(2**4)</code>, not <code>(-2)**4</code>. (This is implemented using C's <code>pow(3)</code> function, which actually works on doubles internally.)</p> <p>Note that certain exponentiation expressions are ill-defined: these include <code>0**0</code>, <code>1**Inf</code>, and <code>Inf**0</code>. Do not expect any particular results from these special cases, the results are platform-dependent.</p> <h3 id="Symbolic-Unary-Operators">
Symbolic Unary Operators </h3> <p>Unary <code>"!"</code> performs logical negation, that is, "not". See also <a href="#Logical-Not"><code>not</code></a> for a lower precedence version of this. </p> <p>Unary <code>"-"</code> performs arithmetic negation if the operand is numeric, including any string that looks like a number. If the operand is an identifier, a string consisting of a minus sign concatenated with the identifier is returned. Otherwise, if the string starts with a plus or minus, a string starting with the opposite sign is returned. One effect of these rules is that <code>-bareword</code> is equivalent to the string <code>"-bareword"</code>. If, however, the string begins with a non-alphabetic character (excluding <code>"+"</code> or <code>"-"</code>), Perl will attempt to convert the string to a numeric, and the arithmetic negation is performed. If the string cannot be cleanly converted to a numeric, Perl will give the warning <b>Argument "the string" isn't numeric in negation (-) at ...</b>. </p> <p>Unary <code>"~"</code> performs bitwise negation, that is, 1's complement. For example, <span style="white-space: nowrap;"><code>0666 &amp; ~027</code></span> is 0640. (See also <a href="#Integer-Arithmetic">"Integer Arithmetic"</a> and <a href="#Bitwise-String-Operators">"Bitwise String Operators"</a>.) Note that the width of the result is platform-dependent: <code>~0</code> is 32 bits wide on a 32-bit platform, but 64 bits wide on a 64-bit platform, so if you are expecting a certain bit width, remember to use the <code>"&amp;"</code> operator to mask off the excess bits. </p> <p>Starting in Perl 5.28, it is a fatal error to try to complement a string containing a character with an ordinal value above 255.</p> <p>If the "bitwise" feature is enabled via <span style="white-space: nowrap;"><code>use feature 'bitwise'</code></span> or <code>use v5.28</code>, then unary <code>"~"</code> always treats its argument as a number, and an alternate form of the operator, <code>"~."</code>, always treats its argument as a string. So <code>~0</code> and <code>~"0"</code> will both give 2**32-1 on 32-bit platforms, whereas <code>~.0</code> and <code>~."0"</code> will both yield <code>"\xff"</code>. Until Perl 5.28, this feature produced a warning in the <code>"experimental::bitwise"</code> category.</p> <p>Unary <code>"+"</code> has no effect whatsoever, even on strings. It is useful syntactically for separating a function name from a parenthesized expression that would otherwise be interpreted as the complete list of function arguments. (See examples above under <a href="#Terms-and-List-Operators-%28Leftward%29">"Terms and List Operators (Leftward)"</a>.) </p> <p>Unary <code>"\"</code> creates references. If its operand is a single sigilled thing, it creates a reference to that object. If its operand is a parenthesised list, then it creates references to the things mentioned in the list. Otherwise it puts its operand in list context, and creates a list of references to the scalars in the list provided by the operand. See <a href="perlreftut.html">perlreftut</a> and <a href="perlref.html">perlref</a>. Do not confuse this behavior with the behavior of backslash within a string, although both forms do convey the notion of protecting the next thing from interpolation. </p> <h3 id="Binding-Operators">
Binding Operators </h3> <p>Binary <code>"=~"</code> binds a scalar expression to a pattern match. Certain operations search or modify the string <code>$_</code> by default. This operator makes that kind of operation work on some other string. The right argument is a search pattern, substitution, or transliteration. The left argument is what is supposed to be searched, substituted, or transliterated instead of the default <code>$_</code>. When used in scalar context, the return value generally indicates the success of the operation. The exceptions are substitution (<code>s///</code>) and transliteration (<code>y///</code>) with the <code>/r</code> (non-destructive) option, which cause the <b>r</b>eturn value to be the result of the substitution. Behavior in list context depends on the particular operator. See <a href="#Regexp-Quote-Like-Operators">"Regexp Quote-Like Operators"</a> for details and <a href="perlretut.html">perlretut</a> for examples using these operators.</p> <p>If the right argument is an expression rather than a search pattern, substitution, or transliteration, it is interpreted as a search pattern at run time. Note that this means that its contents will be interpolated twice, so</p> <pre data-language="perl"><code>'\\' =~ q'\\';</code></pre> <p>is not ok, as the regex engine will end up trying to compile the pattern <code>\</code>, which it will consider a syntax error.</p> <p>Binary <code>"!~"</code> is just like <code>"=~"</code> except the return value is negated in the logical sense.</p> <p>Binary <code>"!~"</code> with a non-destructive substitution (<code>s///r</code>) or transliteration (<code>y///r</code>) is a syntax error.</p> <h3 id="Multiplicative-Operators">
Multiplicative Operators </h3> <p>Binary <code>"*"</code> multiplies two numbers. </p> <p>Binary <code>"/"</code> divides two numbers. </p> <p>Binary <code>"%"</code> is the modulo operator, which computes the division remainder of its first argument with respect to its second argument. Given integer operands <code>$m</code> and <code>$n</code>: If <code>$n</code> is positive, then <span style="white-space: nowrap;"><code>$m % $n</code></span> is <code>$m</code> minus the largest multiple of <code>$n</code> less than or equal to <code>$m</code>. If <code>$n</code> is negative, then <span style="white-space: nowrap;"><code>$m % $n</code></span> is <code>$m</code> minus the smallest multiple of <code>$n</code> that is not less than <code>$m</code> (that is, the result will be less than or equal to zero). If the operands <code>$m</code> and <code>$n</code> are floating point values and the absolute value of <code>$n</code> (that is <code>abs($n)</code>) is less than <span style="white-space: nowrap;"><code>(UV_MAX + 1)</code></span>, only the integer portion of <code>$m</code> and <code>$n</code> will be used in the operation (Note: here <code>UV_MAX</code> means the maximum of the unsigned integer type). If the absolute value of the right operand (<code>abs($n)</code>) is greater than or equal to <span style="white-space: nowrap;"><code>(UV_MAX + 1)</code></span>, <code>"%"</code> computes the floating-point remainder <code>$r</code> in the equation <span style="white-space: nowrap;"><code>($r = $m - $i*$n)</code></span> where <code>$i</code> is a certain integer that makes <code>$r</code> have the same sign as the right operand <code>$n</code> (<b>not</b> as the left operand <code>$m</code> like C function <code>fmod()</code>) and the absolute value less than that of <code>$n</code>. Note that when <span style="white-space: nowrap;"><code>use integer</code></span> is in scope, <code>"%"</code> gives you direct access to the modulo operator as implemented by your C compiler. This operator is not as well defined for negative operands, but it will execute faster. </p> <p>Binary <code>x</code> is the repetition operator. In scalar context, or if the left operand is neither enclosed in parentheses nor a <code>qw//</code> list, it performs a string repetition. In that case it supplies scalar context to the left operand, and returns a string consisting of the left operand string repeated the number of times specified by the right operand. If the <code>x</code> is in list context, and the left operand is either enclosed in parentheses or a <code>qw//</code> list, it performs a list repetition. In that case it supplies list context to the left operand, and returns a list consisting of the left operand list repeated the number of times specified by the right operand. If the right operand is zero or negative (raising a warning on negative), it returns an empty string or an empty list, depending on the context. </p> <pre data-language="perl"><code>print '-' x 80;		# print row of dashes

print "\t" x ($tab/8), ' ' x ($tab%8);	# tab over

@ones = (1) x 80;		# a list of 80 1's
@ones = (5) x @ones;	# set all elements to 5</code></pre> <h3 id="Additive-Operators">
Additive Operators </h3> <p>Binary <code>"+"</code> returns the sum of two numbers. </p> <p>Binary <code>"-"</code> returns the difference of two numbers. </p> <p>Binary <code>"."</code> concatenates two strings. </p> <h3 id="Shift-Operators">
Shift Operators </h3> <p>Binary <code>"&lt;&lt;"</code> returns the value of its left argument shifted left by the number of bits specified by the right argument. Arguments should be integers. (See also <a href="#Integer-Arithmetic">"Integer Arithmetic"</a>.)</p> <p>Binary <code>"&gt;&gt;"</code> returns the value of its left argument shifted right by the number of bits specified by the right argument. Arguments should be integers. (See also <a href="#Integer-Arithmetic">"Integer Arithmetic"</a>.)</p> <p>If <span style="white-space: nowrap;"><code>use integer</code></span> (see <a href="#Integer-Arithmetic">"Integer Arithmetic"</a>) is in force then signed C integers are used (<i>arithmetic shift</i>), otherwise unsigned C integers are used (<i>logical shift</i>), even for negative shiftees. In arithmetic right shift the sign bit is replicated on the left, in logical shift zero bits come in from the left.</p> <p>Either way, the implementation isn't going to generate results larger than the size of the integer type Perl was built with (32 bits or 64 bits).</p> <p>Shifting by negative number of bits means the reverse shift: left shift becomes right shift, right shift becomes left shift. This is unlike in C, where negative shift is undefined.</p> <p>Shifting by more bits than the size of the integers means most of the time zero (all bits fall off), except that under <span style="white-space: nowrap;"><code>use integer</code></span> right overshifting a negative shiftee results in -1. This is unlike in C, where shifting by too many bits is undefined. A common C behavior is "shift by modulo wordbits", so that for example</p> <pre data-language="perl"><code class="plaintext">1 &gt;&gt; 64 == 1 &gt;&gt; (64 % 64) == 1 &gt;&gt; 0 == 1  # Common C behavior.</code></pre> <p>but that is completely accidental.</p> <p>If you get tired of being subject to your platform's native integers, the <span style="white-space: nowrap;"><code>use bigint</code></span> pragma neatly sidesteps the issue altogether:</p> <pre data-language="perl"><code>print 20 &lt;&lt; 20;  # 20971520
print 20 &lt;&lt; 40;  # 5120 on 32-bit machines,
                 # 21990232555520 on 64-bit machines
use bigint;
print 20 &lt;&lt; 100; # 25353012004564588029934064107520</code></pre> <h3 id="Named-Unary-Operators">
Named Unary Operators </h3> <p>The various named unary operators are treated as functions with one argument, with optional parentheses.</p> <p>If any list operator (<code>print()</code>, etc.) or any unary operator (<code>chdir()</code>, etc.) is followed by a left parenthesis as the next token, the operator and arguments within parentheses are taken to be of highest precedence, just like a normal function call. For example, because named unary operators are higher precedence than <code>||</code>:</p> <pre data-language="perl"><code>chdir $foo    || die;	# (chdir $foo) || die
chdir($foo)   || die;	# (chdir $foo) || die
chdir ($foo)  || die;	# (chdir $foo) || die
chdir +($foo) || die;	# (chdir $foo) || die</code></pre> <p>but, because <code>"*"</code> is higher precedence than named operators:</p> <pre data-language="perl"><code>chdir $foo * 20;	# chdir ($foo * 20)
chdir($foo) * 20;	# (chdir $foo) * 20
chdir ($foo) * 20;	# (chdir $foo) * 20
chdir +($foo) * 20;	# chdir ($foo * 20)

rand 10 * 20;	# rand (10 * 20)
rand(10) * 20;	# (rand 10) * 20
rand (10) * 20;	# (rand 10) * 20
rand +(10) * 20;	# rand (10 * 20)</code></pre> <p>Regarding precedence, the filetest operators, like <code>-f</code>, <code>-M</code>, etc. are treated like named unary operators, but they don't follow this functional parenthesis rule. That means, for example, that <code>-f($file).".bak"</code> is equivalent to <span style="white-space: nowrap;"><code>-f "$file.bak"</code></span>. </p> <p>See also <a href="#Terms-and-List-Operators-%28Leftward%29">"Terms and List Operators (Leftward)"</a>.</p> <h3 id="Relational-Operators">
Relational Operators </h3> <p>Perl operators that return true or false generally return values that can be safely used as numbers. For example, the relational operators in this section and the equality operators in the next one return <code>1</code> for true and a special version of the defined empty string, <code>""</code>, which counts as a zero but is exempt from warnings about improper numeric conversions, just as <span style="white-space: nowrap;"><code>"0 but true"</code></span> is.</p> <p>Binary <code>"&lt;"</code> returns true if the left argument is numerically less than the right argument. </p> <p>Binary <code>"&gt;"</code> returns true if the left argument is numerically greater than the right argument. </p> <p>Binary <code>"&lt;="</code> returns true if the left argument is numerically less than or equal to the right argument. </p> <p>Binary <code>"&gt;="</code> returns true if the left argument is numerically greater than or equal to the right argument. </p> <p>Binary <code>"lt"</code> returns true if the left argument is stringwise less than the right argument. </p> <p>Binary <code>"gt"</code> returns true if the left argument is stringwise greater than the right argument. </p> <p>Binary <code>"le"</code> returns true if the left argument is stringwise less than or equal to the right argument. </p> <p>Binary <code>"ge"</code> returns true if the left argument is stringwise greater than or equal to the right argument. </p> <p>A sequence of relational operators, such as <span style="white-space: nowrap;"><code>"$x &lt; $y &lt;= $z"</code></span>, performs chained comparisons, in the manner described above in the section <a href="#Operator-Precedence-and-Associativity">"Operator Precedence and Associativity"</a>. Beware that they do not chain with equality operators, which have lower precedence.</p> <h3 id="Equality-Operators">
Equality Operators </h3> <p>Binary <code>"=="</code> returns true if the left argument is numerically equal to the right argument. </p> <p>Binary <code>"!="</code> returns true if the left argument is numerically not equal to the right argument. </p> <p>Binary <code>"eq"</code> returns true if the left argument is stringwise equal to the right argument. </p> <p>Binary <code>"ne"</code> returns true if the left argument is stringwise not equal to the right argument. </p> <p>A sequence of the above equality operators, such as <span style="white-space: nowrap;"><code>"$x == $y == $z"</code></span>, performs chained comparisons, in the manner described above in the section <a href="#Operator-Precedence-and-Associativity">"Operator Precedence and Associativity"</a>. Beware that they do not chain with relational operators, which have higher precedence.</p> <p>Binary <code>"&lt;=&gt;"</code> returns -1, 0, or 1 depending on whether the left argument is numerically less than, equal to, or greater than the right argument. If your platform supports <code>NaN</code>'s (not-a-numbers) as numeric values, using them with <code>"&lt;=&gt;"</code> returns undef. <code>NaN</code> is not <code>"&lt;"</code>, <code>"=="</code>, <code>"&gt;"</code>, <code>"&lt;="</code> or <code>"&gt;="</code> anything (even <code>NaN</code>), so those 5 return false. <span style="white-space: nowrap;"><code>NaN != NaN</code></span> returns true, as does <span style="white-space: nowrap;"><code>NaN !=</code> <i>anything else</i></span>. If your platform doesn't support <code>NaN</code>'s then <code>NaN</code> is just a string with numeric value 0. </p> <pre data-language="perl"><code>$ perl -le '$x = "NaN"; print "No NaN support here" if $x == $x'
$ perl -le '$x = "NaN"; print "NaN support here" if $x != $x'</code></pre> <p>(Note that the <a href="bigint.html">bigint</a>, <a href="bigrat.html">bigrat</a>, and <a href="bignum.html">bignum</a> pragmas all support <code>"NaN"</code>.)</p> <p>Binary <code>"cmp"</code> returns -1, 0, or 1 depending on whether the left argument is stringwise less than, equal to, or greater than the right argument.</p> <p>Here we can see the difference between &lt;=&gt; and cmp,</p> <pre data-language="perl"><code class="plaintext">print 10 &lt;=&gt; 2 #prints 1
print 10 cmp 2 #prints -1</code></pre> <p>(likewise between gt and &gt;, lt and &lt;, etc.) </p> <p>Binary <code>"~~"</code> does a smartmatch between its arguments. Smart matching is described in the next section. </p> <p>The two-sided ordering operators <code>"&lt;=&gt;"</code> and <code>"cmp"</code>, and the smartmatch operator <code>"~~"</code>, are non-associative with respect to each other and with respect to the equality operators of the same precedence.</p> <p><code>"lt"</code>, <code>"le"</code>, <code>"ge"</code>, <code>"gt"</code> and <code>"cmp"</code> use the collation (sort) order specified by the current <code>LC_COLLATE</code> locale if a <span style="white-space: nowrap;"><code>use locale</code></span> form that includes collation is in effect. See <a href="perllocale.html">perllocale</a>. Do not mix these with Unicode, only use them with legacy 8-bit locale encodings. The standard <code><a href="Unicode::Collate.html">Unicode::Collate</a></code> and <code><a href="Unicode::Collate::Locale.html">Unicode::Collate::Locale</a></code> modules offer much more powerful solutions to collation issues.</p> <p>For case-insensitive comparisons, look at the <a href="perlfunc.html#fc">"fc" in perlfunc</a> case-folding function, available in Perl v5.16 or later:</p> <pre data-language="perl"><code>if ( fc($x) eq fc($y) ) { ... }</code></pre> <h3 id="Class-Instance-Operator">
Class Instance Operator </h3> <p>Binary <code>isa</code> evaluates to true when the left argument is an object instance of the class (or a subclass derived from that class) given by the right argument. If the left argument is not defined, not a blessed object instance, nor does not derive from the class given by the right argument, the operator evaluates as false. The right argument may give the class either as a bareword or a scalar expression that yields a string class name:</p> <pre data-language="perl"><code>if( $obj isa Some::Class ) { ... }

if( $obj isa "Different::Class" ) { ... }
if( $obj isa $name_of_class ) { ... }</code></pre> <p>This feature is available from Perl 5.31.6 onwards when enabled by <code>use feature 'isa'</code>. This feature is enabled automatically by a <code>use v5.36</code> (or higher) declaration in the current scope.</p> <h3 id="Smartmatch-Operator">
Smartmatch Operator</h3> <p>First available in Perl 5.10.1 (the 5.10.0 version behaved differently), binary <code>~~</code> does a "smartmatch" between its arguments. This is mostly used implicitly in the <code>when</code> construct described in <a href="perlsyn.html">perlsyn</a>, although not all <code>when</code> clauses call the smartmatch operator. Unique among all of Perl's operators, the smartmatch operator can recurse. The smartmatch operator is <a href="perlpolicy.html#experimental">experimental</a> and its behavior is subject to change.</p> <p>It is also unique in that all other Perl operators impose a context (usually string or numeric context) on their operands, autoconverting those operands to those imposed contexts. In contrast, smartmatch <i>infers</i> contexts from the actual types of its operands and uses that type information to select a suitable comparison mechanism.</p> <p>The <code>~~</code> operator compares its operands "polymorphically", determining how to compare them according to their actual types (numeric, string, array, hash, etc.). Like the equality operators with which it shares the same precedence, <code>~~</code> returns 1 for true and <code>""</code> for false. It is often best read aloud as "in", "inside of", or "is contained in", because the left operand is often looked for <i>inside</i> the right operand. That makes the order of the operands to the smartmatch operand often opposite that of the regular match operator. In other words, the "smaller" thing is usually placed in the left operand and the larger one in the right.</p> <p>The behavior of a smartmatch depends on what type of things its arguments are, as determined by the following table. The first row of the table whose types apply determines the smartmatch behavior. Because what actually happens is mostly determined by the type of the second operand, the table is sorted on the right operand instead of on the left.</p> <pre data-language="perl"><code>Left      Right      Description and pseudocode
===============================================================
Any       undef      check whether Any is undefined
               like: !defined Any

Any       Object     invoke ~~ overloading on Object, or die

Right operand is an ARRAY:

Left      Right      Description and pseudocode
===============================================================
ARRAY1    ARRAY2     recurse on paired elements of ARRAY1 and ARRAY2[2]
               like: (ARRAY1[0] ~~ ARRAY2[0])
                       &amp;&amp; (ARRAY1[1] ~~ ARRAY2[1]) &amp;&amp; ...
HASH      ARRAY      any ARRAY elements exist as HASH keys
               like: grep { exists HASH-&gt;{$_} } ARRAY
Regexp    ARRAY      any ARRAY elements pattern match Regexp
               like: grep { /Regexp/ } ARRAY
undef     ARRAY      undef in ARRAY
               like: grep { !defined } ARRAY
Any       ARRAY      smartmatch each ARRAY element[3]
               like: grep { Any ~~ $_ } ARRAY

Right operand is a HASH:

Left      Right      Description and pseudocode
===============================================================
HASH1     HASH2      all same keys in both HASHes
               like: keys HASH1 ==
                        grep { exists HASH2-&gt;{$_} } keys HASH1
ARRAY     HASH       any ARRAY elements exist as HASH keys
               like: grep { exists HASH-&gt;{$_} } ARRAY
Regexp    HASH       any HASH keys pattern match Regexp
               like: grep { /Regexp/ } keys HASH
undef     HASH       always false (undef cannot be a key)
               like: 0 == 1
Any       HASH       HASH key existence
               like: exists HASH-&gt;{Any}

Right operand is CODE:

Left      Right      Description and pseudocode
===============================================================
ARRAY     CODE       sub returns true on all ARRAY elements[1]
               like: !grep { !CODE-&gt;($_) } ARRAY
HASH      CODE       sub returns true on all HASH keys[1]
               like: !grep { !CODE-&gt;($_) } keys HASH
Any       CODE       sub passed Any returns true
               like: CODE-&gt;(Any)

Right operand is a Regexp:

Left      Right      Description and pseudocode
===============================================================
ARRAY     Regexp     any ARRAY elements match Regexp
               like: grep { /Regexp/ } ARRAY
HASH      Regexp     any HASH keys match Regexp
               like: grep { /Regexp/ } keys HASH
Any       Regexp     pattern match
               like: Any =~ /Regexp/

Other:

Left      Right      Description and pseudocode
===============================================================
Object    Any        invoke ~~ overloading on Object,
                     or fall back to...

Any       Num        numeric equality
                like: Any == Num
Num       nummy[4]    numeric equality
                like: Num == nummy
undef     Any        check whether undefined
                like: !defined(Any)
Any       Any        string equality
                like: Any eq Any</code></pre> <p>Notes:</p> <dl> <dt id="1.-Empty-hashes-or-arrays-match.">
1. Empty hashes or arrays match.</dt>  <dt id="2.-That-is,-each-element-smartmatches-the-element-of-the-same-index-in-the-other-array.[3]">
2. That is, each element smartmatches the element of the same index in the other array.[3]</dt>  <dt id="3.-If-a-circular-reference-is-found,-fall-back-to-referential-equality.">
3. If a circular reference is found, fall back to referential equality.</dt>  <dt id="4.-Either-an-actual-number,-or-a-string-that-looks-like-one.">
4. Either an actual number, or a string that looks like one.</dt>  </dl> <p>The smartmatch implicitly dereferences any non-blessed hash or array reference, so the <code><i>HASH</i></code> and <code><i>ARRAY</i></code> entries apply in those cases. For blessed references, the <code><i>Object</i></code> entries apply. Smartmatches involving hashes only consider hash keys, never hash values.</p> <p>The "like" code entry is not always an exact rendition. For example, the smartmatch operator short-circuits whenever possible, but <code>grep</code> does not. Also, <code>grep</code> in scalar context returns the number of matches, but <code>~~</code> returns only true or false.</p> <p>Unlike most operators, the smartmatch operator knows to treat <code>undef</code> specially:</p> <pre data-language="perl"><code>use v5.10.1;
@array = (1, 2, 3, undef, 4, 5);
say "some elements undefined" if undef ~~ @array;</code></pre> <p>Each operand is considered in a modified scalar context, the modification being that array and hash variables are passed by reference to the operator, which implicitly dereferences them. Both elements of each pair are the same:</p> <pre data-language="perl"><code>use v5.10.1;

my %hash = (red    =&gt; 1, blue   =&gt; 2, green  =&gt; 3,
            orange =&gt; 4, yellow =&gt; 5, purple =&gt; 6,
            black  =&gt; 7, grey   =&gt; 8, white  =&gt; 9);

my @array = qw(red blue green);

say "some array elements in hash keys" if  @array ~~  %hash;
say "some array elements in hash keys" if \@array ~~ \%hash;

say "red in array" if "red" ~~  @array;
say "red in array" if "red" ~~ \@array;

say "some keys end in e" if /e$/ ~~  %hash;
say "some keys end in e" if /e$/ ~~ \%hash;</code></pre> <p>Two arrays smartmatch if each element in the first array smartmatches (that is, is "in") the corresponding element in the second array, recursively.</p> <pre data-language="perl"><code>use v5.10.1;
my @little = qw(red blue green);
my @bigger = ("red", "blue", [ "orange", "green" ] );
if (@little ~~ @bigger) {  # true!
    say "little is contained in bigger";
}</code></pre> <p>Because the smartmatch operator recurses on nested arrays, this will still report that "red" is in the array.</p> <pre data-language="perl"><code>use v5.10.1;
my @array = qw(red blue green);
my $nested_array = [[[[[[[ @array ]]]]]]];
say "red in array" if "red" ~~ $nested_array;</code></pre> <p>If two arrays smartmatch each other, then they are deep copies of each others' values, as this example reports:</p> <pre data-language="perl"><code>use v5.12.0;
my @a = (0, 1, 2, [3, [4, 5], 6], 7);
my @b = (0, 1, 2, [3, [4, 5], 6], 7);

if (@a ~~ @b &amp;&amp; @b ~~ @a) {
    say "a and b are deep copies of each other";
}
elsif (@a ~~ @b) {
    say "a smartmatches in b";
}
elsif (@b ~~ @a) {
    say "b smartmatches in a";
}
else {
    say "a and b don't smartmatch each other at all";
}</code></pre> <p>If you were to set <span style="white-space: nowrap;"><code>$b[3] = 4</code></span>, then instead of reporting that "a and b are deep copies of each other", it now reports that <code>"b smartmatches in a"</code>. That's because the corresponding position in <code>@a</code> contains an array that (eventually) has a 4 in it.</p> <p>Smartmatching one hash against another reports whether both contain the same keys, no more and no less. This could be used to see whether two records have the same field names, without caring what values those fields might have. For example:</p> <pre data-language="perl"><code>use v5.10.1;
sub make_dogtag {
    state $REQUIRED_FIELDS = { name=&gt;1, rank=&gt;1, serial_num=&gt;1 };

    my ($class, $init_fields) = @_;

    die "Must supply (only) name, rank, and serial number"
        unless $init_fields ~~ $REQUIRED_FIELDS;

    ...
}</code></pre> <p>However, this only does what you mean if <code>$init_fields</code> is indeed a hash reference. The condition <code>$init_fields ~~ $REQUIRED_FIELDS</code> also allows the strings <code>"name"</code>, <code>"rank"</code>, <code>"serial_num"</code> as well as any array reference that contains <code>"name"</code> or <code>"rank"</code> or <code>"serial_num"</code> anywhere to pass through.</p> <p>The smartmatch operator is most often used as the implicit operator of a <code>when</code> clause. See the section on "Switch Statements" in <a href="perlsyn.html">perlsyn</a>.</p> <h4 id="Smartmatching-of-Objects">
Smartmatching of Objects</h4> <p>To avoid relying on an object's underlying representation, if the smartmatch's right operand is an object that doesn't overload <code>~~</code>, it raises the exception "<code>Smartmatching a non-overloaded object breaks encapsulation</code>". That's because one has no business digging around to see whether something is "in" an object. These are all illegal on objects without a <code>~~</code> overload:</p> <pre data-language="perl"><code> %hash ~~ $object
    42 ~~ $object
"fred" ~~ $object</code></pre> <p>However, you can change the way an object is smartmatched by overloading the <code>~~</code> operator. This is allowed to extend the usual smartmatch semantics. For objects that do have an <code>~~</code> overload, see <a href="overload.html">overload</a>.</p> <p>Using an object as the left operand is allowed, although not very useful. Smartmatching rules take precedence over overloading, so even if the object in the left operand has smartmatch overloading, this will be ignored. A left operand that is a non-overloaded object falls back on a string or numeric comparison of whatever the <code>ref</code> operator returns. That means that</p> <pre data-language="perl"><code>$object ~~ X</code></pre> <p>does <i>not</i> invoke the overload method with <code><i>X</i></code> as an argument. Instead the above table is consulted as normal, and based on the type of <code><i>X</i></code>, overloading may or may not be invoked. For simple strings or numbers, "in" becomes equivalent to this:</p> <pre data-language="perl"><code>$object ~~ $number          ref($object) == $number
$object ~~ $string          ref($object) eq $string</code></pre> <p>For example, this reports that the handle smells IOish (but please don't really do this!):</p> <pre data-language="perl"><code>use IO::Handle;
my $fh = IO::Handle-&gt;new();
if ($fh ~~ /\bIO\b/) {
    say "handle smells IOish";
}</code></pre> <p>That's because it treats <code>$fh</code> as a string like <code>"IO::Handle=GLOB(0x8039e0)"</code>, then pattern matches against that.</p> <h3 id="Bitwise-And">
Bitwise And </h3> <p>Binary <code>"&amp;"</code> returns its operands ANDed together bit by bit. Although no warning is currently raised, the result is not well defined when this operation is performed on operands that aren't either numbers (see <a href="#Integer-Arithmetic">"Integer Arithmetic"</a>) nor bitstrings (see <a href="#Bitwise-String-Operators">"Bitwise String Operators"</a>).</p> <p>Note that <code>"&amp;"</code> has lower priority than relational operators, so for example the parentheses are essential in a test like</p> <pre data-language="perl"><code>print "Even\n" if ($x &amp; 1) == 0;</code></pre> <p>If the "bitwise" feature is enabled via <span style="white-space: nowrap;"><code>use feature 'bitwise'</code></span> or <code>use v5.28</code>, then this operator always treats its operands as numbers. Before Perl 5.28 this feature produced a warning in the <code>"experimental::bitwise"</code> category.</p> <h3 id="Bitwise-Or-and-Exclusive-Or">
Bitwise Or and Exclusive Or </h3> <p>Binary <code>"|"</code> returns its operands ORed together bit by bit.</p> <p>Binary <code>"^"</code> returns its operands XORed together bit by bit.</p> <p>Although no warning is currently raised, the results are not well defined when these operations are performed on operands that aren't either numbers (see <a href="#Integer-Arithmetic">"Integer Arithmetic"</a>) nor bitstrings (see <a href="#Bitwise-String-Operators">"Bitwise String Operators"</a>).</p> <p>Note that <code>"|"</code> and <code>"^"</code> have lower priority than relational operators, so for example the parentheses are essential in a test like</p> <pre data-language="perl"><code>print "false\n" if (8 | 2) != 10;</code></pre> <p>If the "bitwise" feature is enabled via <span style="white-space: nowrap;"><code>use feature 'bitwise'</code></span> or <code>use v5.28</code>, then this operator always treats its operands as numbers. Before Perl 5.28. this feature produced a warning in the <code>"experimental::bitwise"</code> category.</p> <h3 id="C-style-Logical-And">
C-style Logical And </h3> <p>Binary <code>"&amp;&amp;"</code> performs a short-circuit logical AND operation. That is, if the left operand is false, the right operand is not even evaluated. Scalar or list context propagates down to the right operand if it is evaluated.</p> <h3 id="C-style-Logical-Or">
C-style Logical Or </h3> <p>Binary <code>"||"</code> performs a short-circuit logical OR operation. That is, if the left operand is true, the right operand is not even evaluated. Scalar or list context propagates down to the right operand if it is evaluated.</p> <h3 id="Logical-Defined-Or">
Logical Defined-Or </h3> <p>Although it has no direct equivalent in C, Perl's <code>//</code> operator is related to its C-style "or". In fact, it's exactly the same as <code>||</code>, except that it tests the left hand side's definedness instead of its truth. Thus, <span style="white-space: nowrap;"><code>EXPR1 // EXPR2</code></span> returns the value of <code>EXPR1</code> if it's defined, otherwise, the value of <code>EXPR2</code> is returned. (<code>EXPR1</code> is evaluated in scalar context, <code>EXPR2</code> in the context of <code>//</code> itself). Usually, this is the same result as <span style="white-space: nowrap;"><code>defined(EXPR1) ? EXPR1 : EXPR2</code></span> (except that the ternary-operator form can be used as a lvalue, while <span style="white-space: nowrap;"><code>EXPR1 // EXPR2</code></span> cannot). This is very useful for providing default values for variables. If you actually want to test if at least one of <code>$x</code> and <code>$y</code> is defined, use <span style="white-space: nowrap;"><code>defined($x // $y)</code></span>.</p> <p>The <code>||</code>, <code>//</code> and <code>&amp;&amp;</code> operators return the last value evaluated (unlike C's <code>||</code> and <code>&amp;&amp;</code>, which return 0 or 1). Thus, a reasonably portable way to find out the home directory might be:</p> <pre data-language="perl"><code>    $home =  $ENV{HOME}
	  // $ENV{LOGDIR}
	  // (getpwuid($&lt;))[7]
	  // die "You're homeless!\n";</code></pre> <p>In particular, this means that you shouldn't use this for selecting between two aggregates for assignment:</p> <pre data-language="perl"><code>@a = @b || @c;            # This doesn't do the right thing
@a = scalar(@b) || @c;    # because it really means this.
@a = @b ? @b : @c;        # This works fine, though.</code></pre> <p>As alternatives to <code>&amp;&amp;</code> and <code>||</code> when used for control flow, Perl provides the <code>and</code> and <code>or</code> operators (see below). The short-circuit behavior is identical. The precedence of <code>"and"</code> and <code>"or"</code> is much lower, however, so that you can safely use them after a list operator without the need for parentheses:</p> <pre data-language="perl"><code>    unlink "alpha", "beta", "gamma"
	    or gripe(), next LINE;</code></pre> <p>With the C-style operators that would have been written like this:</p> <pre data-language="perl"><code>    unlink("alpha", "beta", "gamma")
	    || (gripe(), next LINE);</code></pre> <p>It would be even more readable to write that this way:</p> <pre data-language="perl"><code>unless(unlink("alpha", "beta", "gamma")) {
    gripe();
    next LINE;
}</code></pre> <p>Using <code>"or"</code> for assignment is unlikely to do what you want; see below.</p> <h3 id="Range-Operators">
Range Operators </h3> <p>Binary <code>".."</code> is the range operator, which is really two different operators depending on the context. In list context, it returns a list of values counting (up by ones) from the left value to the right value. If the left value is greater than the right value then it returns the empty list. The range operator is useful for writing <span style="white-space: nowrap;"><code>foreach (1..10)</code></span> loops and for doing slice operations on arrays. In the current implementation, no temporary array is created when the range operator is used as the expression in <code>foreach</code> loops, but older versions of Perl might burn a lot of memory when you write something like this:</p> <pre data-language="perl"><code>    for (1 .. 1_000_000) {
	# code
    }</code></pre> <p>The range operator also works on strings, using the magical auto-increment, see below.</p> <p>In scalar context, <code>".."</code> returns a boolean value. The operator is bistable, like a flip-flop, and emulates the line-range (comma) operator of <b>sed</b>, <b>awk</b>, and various editors. Each <code>".."</code> operator maintains its own boolean state, even across calls to a subroutine that contains it. It is false as long as its left operand is false. Once the left operand is true, the range operator stays true until the right operand is true, <i>AFTER</i> which the range operator becomes false again. It doesn't become false till the next time the range operator is evaluated. It can test the right operand and become false on the same evaluation it became true (as in <b>awk</b>), but it still returns true once. If you don't want it to test the right operand until the next evaluation, as in <b>sed</b>, just use three dots (<code>"..."</code>) instead of two. In all other regards, <code>"..."</code> behaves just like <code>".."</code> does.</p> <p>The right operand is not evaluated while the operator is in the "false" state, and the left operand is not evaluated while the operator is in the "true" state. The precedence is a little lower than || and &amp;&amp;. The value returned is either the empty string for false, or a sequence number (beginning with 1) for true. The sequence number is reset for each range encountered. The final sequence number in a range has the string <code>"E0"</code> appended to it, which doesn't affect its numeric value, but gives you something to search for if you want to exclude the endpoint. You can exclude the beginning point by waiting for the sequence number to be greater than 1.</p> <p>If either operand of scalar <code>".."</code> is a constant expression, that operand is considered true if it is equal (<code>==</code>) to the current input line number (the <code>$.</code> variable).</p> <p>To be pedantic, the comparison is actually <span style="white-space: nowrap;"><code>int(EXPR) == int(EXPR)</code></span>, but that is only an issue if you use a floating point expression; when implicitly using <code>$.</code> as described in the previous paragraph, the comparison is <span style="white-space: nowrap;"><code>int(EXPR) == int($.)</code></span> which is only an issue when <code>$.</code> is set to a floating point value and you are not reading from a file. Furthermore, <span style="white-space: nowrap;"><code>"span" .. "spat"</code></span> or <span style="white-space: nowrap;"><code>2.18 .. 3.14</code></span> will not do what you want in scalar context because each of the operands are evaluated using their integer representation.</p> <p>Examples:</p> <p>As a scalar operator:</p> <pre data-language="perl"><code>if (101 .. 200) { print; } # print 2nd hundred lines, short for
                           #  if ($. == 101 .. $. == 200) { print; }

next LINE if (1 .. /^$/);  # skip header lines, short for
                           #   next LINE if ($. == 1 .. /^$/);
                           # (typically in a loop labeled LINE)

s/^/&gt; / if (/^$/ .. eof());  # quote body

# parse mail messages
while (&lt;&gt;) {
    $in_header =   1  .. /^$/;
    $in_body   = /^$/ .. eof;
    if ($in_header) {
        # do something
    } else { # in body
        # do something else
    }
} continue {
    close ARGV if eof;             # reset $. each file
}</code></pre> <p>Here's a simple example to illustrate the difference between the two range operators:</p> <pre data-language="perl"><code>@lines = ("   - Foo",
          "01 - Bar",
          "1  - Baz",
          "   - Quux");

foreach (@lines) {
    if (/0/ .. /1/) {
        print "$_\n";
    }
}</code></pre> <p>This program will print only the line containing "Bar". If the range operator is changed to <code>...</code>, it will also print the "Baz" line.</p> <p>And now some examples as a list operator:</p> <pre data-language="perl"><code>for (101 .. 200) { print }      # print $_ 100 times
@foo = @foo[0 .. $#foo];        # an expensive no-op
@foo = @foo[$#foo-4 .. $#foo];  # slice last 5 items</code></pre> <p>Because each operand is evaluated in integer form, <span style="white-space: nowrap;"><code>2.18 .. 3.14</code></span> will return two elements in list context.</p> <pre data-language="perl"><code>@list = (2.18 .. 3.14); # same as @list = (2 .. 3);</code></pre> <p>The range operator in list context can make use of the magical auto-increment algorithm if both operands are strings, subject to the following rules:</p> <ul> <li>
<p>With one exception (below), if both strings look like numbers to Perl, the magic increment will not be applied, and the strings will be treated as numbers (more specifically, integers) instead.</p> <p>For example, <code>"-2".."2"</code> is the same as <code>-2..2</code>, and <code>"2.18".."3.14"</code> produces <code>2, 3</code>.</p> </li> <li>
<p>The exception to the above rule is when the left-hand string begins with <code>0</code> and is longer than one character, in this case the magic increment <i>will</i> be applied, even though strings like <code>"01"</code> would normally look like a number to Perl.</p> <p>For example, <code>"01".."04"</code> produces <code>"01", "02", "03", "04"</code>, and <code>"00".."-1"</code> produces <code>"00"</code> through <code>"99"</code> - this may seem surprising, but see the following rules for why it works this way. To get dates with leading zeros, you can say:</p> <pre data-language="perl"><code>@z2 = ("01" .. "31");
print $z2[$mday];</code></pre> <p>If you want to force strings to be interpreted as numbers, you could say</p> <pre data-language="perl"><code>@numbers = ( 0+$first .. 0+$last );</code></pre> <p><b>Note:</b> In Perl versions 5.30 and below, <i>any</i> string on the left-hand side beginning with <code>"0"</code>, including the string <code>"0"</code> itself, would cause the magic string increment behavior. This means that on these Perl versions, <code>"0".."-1"</code> would produce <code>"0"</code> through <code>"99"</code>, which was inconsistent with <code>0..-1</code>, which produces the empty list. This also means that <code>"0".."9"</code> now produces a list of integers instead of a list of strings.</p> </li> <li>
<p>If the initial value specified isn't part of a magical increment sequence (that is, a non-empty string matching <code>/^[a-zA-Z]*[0-9]*\z/</code>), only the initial value will be returned.</p> <p>For example, <code>"ax".."az"</code> produces <code>"ax", "ay", "az"</code>, but <code>"*x".."az"</code> produces only <code>"*x"</code>.</p> </li> <li>
<p>For other initial values that are strings that do follow the rules of the magical increment, the corresponding sequence will be returned.</p> <p>For example, you can say</p> <pre data-language="perl"><code>@alphabet = ("A" .. "Z");</code></pre> <p>to get all normal letters of the English alphabet, or</p> <pre data-language="perl"><code>$hexdigit = (0 .. 9, "a" .. "f")[$num &amp; 15];</code></pre> <p>to get a hexadecimal digit.</p> </li> <li>
<p>If the final value specified is not in the sequence that the magical increment would produce, the sequence goes until the next value would be longer than the final value specified. If the length of the final string is shorter than the first, the empty list is returned.</p> <p>For example, <code>"a".."--"</code> is the same as <code>"a".."zz"</code>, <code>"0".."xx"</code> produces <code>"0"</code> through <code>"99"</code>, and <code>"aaa".."--"</code> returns the empty list.</p> </li> </ul> <p>As of Perl 5.26, the list-context range operator on strings works as expected in the scope of <a href="feature.html#The-%27unicode_strings%27-feature"><span style="white-space: nowrap;"><code>"use feature 'unicode_strings"</code></span></a>. In previous versions, and outside the scope of that feature, it exhibits <a href="perlunicode.html#The-%22Unicode-Bug%22">"The "Unicode Bug"" in perlunicode</a>: its behavior depends on the internal encoding of the range endpoint.</p> <p>Because the magical increment only works on non-empty strings matching <code>/^[a-zA-Z]*[0-9]*\z/</code>, the following will only return an alpha:</p> <pre data-language="perl"><code>use charnames "greek";
my @greek_small =  ("\N{alpha}" .. "\N{omega}");</code></pre> <p>To get the 25 traditional lowercase Greek letters, including both sigmas, you could use this instead:</p> <pre data-language="perl"><code>use charnames "greek";
my @greek_small =  map { chr } ( ord("\N{alpha}")
                                    ..
                                 ord("\N{omega}")
                               );</code></pre> <p>However, because there are <i>many</i> other lowercase Greek characters than just those, to match lowercase Greek characters in a regular expression, you could use the pattern <code>/(?:(?=\p{Greek})\p{Lower})+/</code> (or the <a href="perlrecharclass.html#Extended-Bracketed-Character-Classes">experimental feature</a> <code><span style="white-space: nowrap;">/(?[ \p{Greek} &amp; \p{Lower} ])+/</span></code>).</p> <h3 id="Conditional-Operator">
Conditional Operator </h3> <p>Ternary <code>"?:"</code> is the conditional operator, just as in C. It works much like an if-then-else. If the argument before the <code>?</code> is true, the argument before the <code>:</code> is returned, otherwise the argument after the <code>:</code> is returned. For example:</p> <pre data-language="perl"><code>    printf "I have %d dog%s.\n", $n,
	    ($n == 1) ? "" : "s";</code></pre> <p>Scalar or list context propagates downward into the 2nd or 3rd argument, whichever is selected.</p> <pre data-language="perl"><code>$x = $ok ? $y : $z;  # get a scalar
@x = $ok ? @y : @z;  # get an array
$x = $ok ? @y : @z;  # oops, that's just a count!</code></pre> <p>The operator may be assigned to if both the 2nd and 3rd arguments are legal lvalues (meaning that you can assign to them):</p> <pre data-language="perl"><code>($x_or_y ? $x : $y) = $z;</code></pre> <p>Because this operator produces an assignable result, using assignments without parentheses will get you in trouble. For example, this:</p> <pre data-language="perl"><code>$x % 2 ? $x += 10 : $x += 2</code></pre> <p>Really means this:</p> <pre data-language="perl"><code>(($x % 2) ? ($x += 10) : $x) += 2</code></pre> <p>Rather than this:</p> <pre data-language="perl"><code>($x % 2) ? ($x += 10) : ($x += 2)</code></pre> <p>That should probably be written more simply as:</p> <pre data-language="perl"><code>$x += ($x % 2) ? 10 : 2;</code></pre> <h3 id="Assignment-Operators">
Assignment Operators </h3> <p><code>"="</code> is the ordinary assignment operator.</p> <p>Assignment operators work as in C. That is,</p> <pre data-language="perl"><code>$x += 2;</code></pre> <p>is equivalent to</p> <pre data-language="perl"><code>$x = $x + 2;</code></pre> <p>although without duplicating any side effects that dereferencing the lvalue might trigger, such as from <code>tie()</code>. Other assignment operators work similarly. The following are recognized:</p> <pre data-language="perl"><code class="plaintext">**=    +=    *=    &amp;=    &amp;.=    &lt;&lt;=    &amp;&amp;=
       -=    /=    |=    |.=    &gt;&gt;=    ||=
       .=    %=    ^=    ^.=           //=
             x=</code></pre> <p>Although these are grouped by family, they all have the precedence of assignment. These combined assignment operators can only operate on scalars, whereas the ordinary assignment operator can assign to arrays, hashes, lists and even references. (See <a href="perldata.html#Context">"Context"</a> and <a href="perldata.html#List-value-constructors">"List value constructors" in perldata</a>, and <a href="perlref.html#Assigning-to-References">"Assigning to References" in perlref</a>.)</p> <p>Unlike in C, the scalar assignment operator produces a valid lvalue. Modifying an assignment is equivalent to doing the assignment and then modifying the variable that was assigned to. This is useful for modifying a copy of something, like this:</p> <pre data-language="perl"><code>($tmp = $global) =~ tr/13579/24680/;</code></pre> <p>Although as of 5.14, that can be also be accomplished this way:</p> <pre data-language="perl"><code>use v5.14;
$tmp = ($global =~  tr/13579/24680/r);</code></pre> <p>Likewise,</p> <pre data-language="perl"><code>($x += 2) *= 3;</code></pre> <p>is equivalent to</p> <pre data-language="perl"><code>$x += 2;
$x *= 3;</code></pre> <p>Similarly, a list assignment in list context produces the list of lvalues assigned to, and a list assignment in scalar context returns the number of elements produced by the expression on the right hand side of the assignment.</p> <p>The three dotted bitwise assignment operators (<code>&amp;.=</code> <code>|.=</code> <code>^.=</code>) are new in Perl 5.22. See <a href="#Bitwise-String-Operators">"Bitwise String Operators"</a>.</p> <h3 id="Comma-Operator">
Comma Operator </h3> <p>Binary <code>","</code> is the comma operator. In scalar context it evaluates its left argument, throws that value away, then evaluates its right argument and returns that value. This is just like C's comma operator.</p> <p>In list context, it's just the list argument separator, and inserts both its arguments into the list. These arguments are also evaluated from left to right.</p> <p>The <code>=&gt;</code> operator (sometimes pronounced "fat comma") is a synonym for the comma except that it causes a word on its left to be interpreted as a string if it begins with a letter or underscore and is composed only of letters, digits and underscores. This includes operands that might otherwise be interpreted as operators, constants, single number v-strings or function calls. If in doubt about this behavior, the left operand can be quoted explicitly.</p> <p>Otherwise, the <code>=&gt;</code> operator behaves exactly as the comma operator or list argument separator, according to context.</p> <p>For example:</p> <pre data-language="perl"><code>use constant FOO =&gt; "something";

my %h = ( FOO =&gt; 23 );</code></pre> <p>is equivalent to:</p> <pre data-language="perl"><code>my %h = ("FOO", 23);</code></pre> <p>It is <i>NOT</i>:</p> <pre data-language="perl"><code>my %h = ("something", 23);</code></pre> <p>The <code>=&gt;</code> operator is helpful in documenting the correspondence between keys and values in hashes, and other paired elements in lists.</p> <pre data-language="perl"><code>%hash = ( $key =&gt; $value );
login( $username =&gt; $password );</code></pre> <p>The special quoting behavior ignores precedence, and hence may apply to <i>part</i> of the left operand:</p> <pre data-language="perl"><code>print time.shift =&gt; "bbb";</code></pre> <p>That example prints something like <code>"1314363215shiftbbb"</code>, because the <code>=&gt;</code> implicitly quotes the <code>shift</code> immediately on its left, ignoring the fact that <code>time.shift</code> is the entire left operand.</p> <h3 id="List-Operators-(Rightward)">
List Operators (Rightward) </h3> <p>On the right side of a list operator, the comma has very low precedence, such that it controls all comma-separated expressions found there. The only operators with lower precedence are the logical operators <code>"and"</code>, <code>"or"</code>, and <code>"not"</code>, which may be used to evaluate calls to list operators without the need for parentheses:</p> <pre data-language="perl"><code>open HANDLE, "&lt; :encoding(UTF-8)", "filename"
    or die "Can't open: $!\n";</code></pre> <p>However, some people find that code harder to read than writing it with parentheses:</p> <pre data-language="perl"><code>open(HANDLE, "&lt; :encoding(UTF-8)", "filename")
    or die "Can't open: $!\n";</code></pre> <p>in which case you might as well just use the more customary <code>"||"</code> operator:</p> <pre data-language="perl"><code>open(HANDLE, "&lt; :encoding(UTF-8)", "filename")
    || die "Can't open: $!\n";</code></pre> <p>See also discussion of list operators in <a href="#Terms-and-List-Operators-%28Leftward%29">"Terms and List Operators (Leftward)"</a>.</p> <h3 id="Logical-Not">
Logical Not </h3> <p>Unary <code>"not"</code> returns the logical negation of the expression to its right. It's the equivalent of <code>"!"</code> except for the very low precedence.</p> <h3 id="Logical-And">
Logical And </h3> <p>Binary <code>"and"</code> returns the logical conjunction of the two surrounding expressions. It's equivalent to <code>&amp;&amp;</code> except for the very low precedence. This means that it short-circuits: the right expression is evaluated only if the left expression is true.</p> <h3 id="Logical-or-and-Exclusive-Or">
Logical or and Exclusive Or </h3> <p>Binary <code>"or"</code> returns the logical disjunction of the two surrounding expressions. It's equivalent to <code>||</code> except for the very low precedence. This makes it useful for control flow:</p> <pre data-language="perl"><code>print FH $data		or die "Can't write to FH: $!";</code></pre> <p>This means that it short-circuits: the right expression is evaluated only if the left expression is false. Due to its precedence, you must be careful to avoid using it as replacement for the <code>||</code> operator. It usually works out better for flow control than in assignments:</p> <pre data-language="perl"><code>$x = $y or $z;              # bug: this is wrong
($x = $y) or $z;            # really means this
$x = $y || $z;              # better written this way</code></pre> <p>However, when it's a list-context assignment and you're trying to use <code>||</code> for control flow, you probably need <code>"or"</code> so that the assignment takes higher precedence.</p> <pre data-language="perl"><code>@info = stat($file) || die;     # oops, scalar sense of stat!
@info = stat($file) or die;     # better, now @info gets its due</code></pre> <p>Then again, you could always use parentheses.</p> <p>Binary <code>"xor"</code> returns the exclusive-OR of the two surrounding expressions. It cannot short-circuit (of course).</p> <p>There is no low precedence operator for defined-OR.</p> <h3 id="C-Operators-Missing-From-Perl">
C Operators Missing From Perl </h3> <p>Here is what C has that Perl doesn't:</p> <dl> <dt id="unary-&amp;">
unary &amp;</dt> <dd> <p>Address-of operator. (But see the <code>"\"</code> operator for taking a reference.)</p> </dd> <dt id="unary-*">
unary *</dt> <dd> <p>Dereference-address operator. (Perl's prefix dereferencing operators are typed: <code>$</code>, <code>@</code>, <code>%</code>, and <code>&amp;</code>.)</p> </dd> <dt id="(TYPE)">
(TYPE)</dt> <dd> <p>Type-casting operator.</p> </dd> </dl> <h3 id="Quote-and-Quote-like-Operators">
Quote and Quote-like Operators </h3> <p>While we usually think of quotes as literal values, in Perl they function as operators, providing various kinds of interpolating and pattern matching capabilities. Perl provides customary quote characters for these behaviors, but also provides a way for you to choose your quote character for any of them. In the following table, a <code>{}</code> represents any pair of delimiters you choose.</p> <pre data-language="perl"><code class="plaintext">    Customary  Generic        Meaning	     Interpolates
	''	 q{}	      Literal		  no
	""	qq{}	      Literal		  yes
	``	qx{}	      Command		  yes*
		qw{}	     Word list		  no
	//	 m{}	   Pattern match	  yes*
		qr{}	      Pattern		  yes*
		 s{}{}	    Substitution	  yes*
		tr{}{}	  Transliteration	  no (but see below)
		 y{}{}	  Transliteration	  no (but see below)
        &lt;&lt;EOF                 here-doc            yes*

	* unless the delimiter is ''.</code></pre> <p>Non-bracketing delimiters use the same character fore and aft, but the four sorts of ASCII brackets (round, angle, square, curly) all nest, which means that</p> <pre data-language="perl"><code class="plaintext">q{foo{bar}baz}</code></pre> <p>is the same as</p> <pre data-language="perl"><code class="plaintext">'foo{bar}baz'</code></pre> <p>Note, however, that this does not always work for quoting Perl code:</p> <pre data-language="perl"><code>$s = q{ if($x eq "}") ... }; # WRONG</code></pre> <p>is a syntax error. The <code><a href="Text::Balanced.html">Text::Balanced</a></code> module (standard as of v5.8, and from CPAN before then) is able to do this properly.</p> <p>There can (and in some cases, must) be whitespace between the operator and the quoting characters, except when <code>#</code> is being used as the quoting character. <code>q#foo#</code> is parsed as the string <code>foo</code>, while <span style="white-space: nowrap;"><code>q #foo#</code></span> is the operator <code>q</code> followed by a comment. Its argument will be taken from the next line. This allows you to write:</p> <pre data-language="perl"><code class="plaintext">s {foo}  # Replace foo
  {bar}  # with bar.</code></pre> <p>The cases where whitespace must be used are when the quoting character is a word character (meaning it matches <code>/\w/</code>):</p> <pre data-language="perl"><code class="plaintext">q XfooX # Works: means the string 'foo'
qXfooX  # WRONG!</code></pre> <p>The following escape sequences are available in constructs that interpolate, and in transliterations whose delimiters aren't single quotes (<code>"'"</code>). In all the ones with braces, any number of blanks and/or tabs adjoining and within the braces are allowed (and ignored). </p> <pre data-language="perl"><code class="plaintext">Sequence     Note  Description
\t                  tab               (HT, TAB)
\n                  newline           (NL)
\r                  return            (CR)
\f                  form feed         (FF)
\b                  backspace         (BS)
\a                  alarm (bell)      (BEL)
\e                  escape            (ESC)
\x{263A}     [1,8]  hex char          (example shown: SMILEY)
\x{ 263A }          Same, but shows optional blanks inside and
                    adjoining the braces
\x1b         [2,8]  restricted range hex char (example: ESC)
\N{name}     [3]    named Unicode character or character sequence
\N{U+263D}   [4,8]  Unicode character (example: FIRST QUARTER MOON)
\c[          [5]    control char      (example: chr(27))
\o{23072}    [6,8]  octal char        (example: SMILEY)
\033         [7,8]  restricted range octal char  (example: ESC)</code></pre> <p>Note that any escape sequence using braces inside interpolated constructs may have optional blanks (tab or space characters) adjoining with and inside of the braces, as illustrated above by the second <span style="white-space: nowrap;"><code>\x{ }</code></span> example.</p> <dl> <dt id="[1]">
[1]</dt> <dd> <p>The result is the character specified by the hexadecimal number between the braces. See <a href="#%5B8%5D">"[8]"</a> below for details on which character.</p> <p>Blanks (tab or space characters) may separate the number from either or both of the braces.</p> <p>Otherwise, only hexadecimal digits are valid between the braces. If an invalid character is encountered, a warning will be issued and the invalid character and all subsequent characters (valid or invalid) within the braces will be discarded.</p> <p>If there are no valid digits between the braces, the generated character is the NULL character (<code>\x{00}</code>). However, an explicit empty brace (<code>\x{}</code>) will not cause a warning (currently).</p> </dd> <dt id="[2]">
[2]</dt> <dd> <p>The result is the character specified by the hexadecimal number in the range 0x00 to 0xFF. See <a href="#%5B8%5D">"[8]"</a> below for details on which character.</p> <p>Only hexadecimal digits are valid following <code>\x</code>. When <code>\x</code> is followed by fewer than two valid digits, any valid digits will be zero-padded. This means that <code>\x7</code> will be interpreted as <code>\x07</code>, and a lone <code>"\x"</code> will be interpreted as <code>\x00</code>. Except at the end of a string, having fewer than two valid digits will result in a warning. Note that although the warning says the illegal character is ignored, it is only ignored as part of the escape and will still be used as the subsequent character in the string. For example:</p> <pre data-language="perl"><code class="plaintext">Original    Result    Warns?
"\x7"       "\x07"    no
"\x"        "\x00"    no
"\x7q"      "\x07q"   yes
"\xq"       "\x00q"   yes</code></pre> </dd> <dt id="[3]">
[3]</dt> <dd> <p>The result is the Unicode character or character sequence given by <i>name</i>. See <a href="charnames.html">charnames</a>.</p> </dd> <dt id="[4]">
[4]</dt> <dd> <p><span style="white-space: nowrap;"><code>\N{U+<i>hexadecimal number</i>}</code></span> means the Unicode character whose Unicode code point is <i>hexadecimal number</i>.</p> </dd> <dt id="[5]">
[5]</dt> <dd> <p>The character following <code>\c</code> is mapped to some other character as shown in the table:</p> <pre data-language="perl"><code class="plaintext">Sequence   Value
  \c@      chr(0)
  \cA      chr(1)
  \ca      chr(1)
  \cB      chr(2)
  \cb      chr(2)
  ...
  \cZ      chr(26)
  \cz      chr(26)
  \c[      chr(27)
                    # See below for chr(28)
  \c]      chr(29)
  \c^      chr(30)
  \c_      chr(31)
  \c?      chr(127) # (on ASCII platforms; see below for link to
                    #  EBCDIC discussion)</code></pre> <p>In other words, it's the character whose code point has had 64 xor'd with its uppercase. <code>\c?</code> is DELETE on ASCII platforms because <span style="white-space: nowrap;"><code>ord("?") ^ 64</code></span> is 127, and <code>\c@</code> is NULL because the ord of <code>"@"</code> is 64, so xor'ing 64 itself produces 0.</p> <p>Also, <code>\c\<i>X</i></code> yields <span style="white-space: nowrap;"><code> chr(28) . "<i>X</i>"</code></span> for any <i>X</i>, but cannot come at the end of a string, because the backslash would be parsed as escaping the end quote.</p> <p>On ASCII platforms, the resulting characters from the list above are the complete set of ASCII controls. This isn't the case on EBCDIC platforms; see <a href="perlebcdic.html#OPERATOR-DIFFERENCES">"OPERATOR DIFFERENCES" in perlebcdic</a> for a full discussion of the differences between these for ASCII versus EBCDIC platforms.</p> <p>Use of any other character following the <code>"c"</code> besides those listed above is discouraged, and as of Perl v5.20, the only characters actually allowed are the printable ASCII ones, minus the left brace <code>"{"</code>. What happens for any of the allowed other characters is that the value is derived by xor'ing with the seventh bit, which is 64, and a warning raised if enabled. Using the non-allowed characters generates a fatal error.</p> <p>To get platform independent controls, you can use <code>\N{...}</code>.</p> </dd> <dt id="[6]">
[6]</dt> <dd> <p>The result is the character specified by the octal number between the braces. See <a href="#%5B8%5D">"[8]"</a> below for details on which character.</p> <p>Blanks (tab or space characters) may separate the number from either or both of the braces.</p> <p>Otherwise, if a character that isn't an octal digit is encountered, a warning is raised, and the value is based on the octal digits before it, discarding it and all following characters up to the closing brace. It is a fatal error if there are no octal digits at all.</p> </dd> <dt id="[7]">
[7]</dt> <dd> <p>The result is the character specified by the three-digit octal number in the range 000 to 777 (but best to not use above 077, see next paragraph). See <a href="#%5B8%5D">"[8]"</a> below for details on which character.</p> <p>Some contexts allow 2 or even 1 digit, but any usage without exactly three digits, the first being a zero, may give unintended results. (For example, in a regular expression it may be confused with a backreference; see <a href="perlrebackslash.html#Octal-escapes">"Octal escapes" in perlrebackslash</a>.) Starting in Perl 5.14, you may use <code>\o{}</code> instead, which avoids all these problems. Otherwise, it is best to use this construct only for ordinals <code>\077</code> and below, remembering to pad to the left with zeros to make three digits. For larger ordinals, either use <code>\o{}</code>, or convert to something else, such as to hex and use <code>\N{U+}</code> (which is portable between platforms with different character sets) or <code>\x{}</code> instead.</p> </dd> <dt id="[8]">
[8]</dt> <dd> <p>Several constructs above specify a character by a number. That number gives the character's position in the character set encoding (indexed from 0). This is called synonymously its ordinal, code position, or code point. Perl works on platforms that have a native encoding currently of either ASCII/Latin1 or EBCDIC, each of which allow specification of 256 characters. In general, if the number is 255 (0xFF, 0377) or below, Perl interprets this in the platform's native encoding. If the number is 256 (0x100, 0400) or above, Perl interprets it as a Unicode code point and the result is the corresponding Unicode character. For example <code>\x{50}</code> and <code>\o{120}</code> both are the number 80 in decimal, which is less than 256, so the number is interpreted in the native character set encoding. In ASCII the character in the 80th position (indexed from 0) is the letter <code>"P"</code>, and in EBCDIC it is the ampersand symbol <code>"&amp;"</code>. <code>\x{100}</code> and <code>\o{400}</code> are both 256 in decimal, so the number is interpreted as a Unicode code point no matter what the native encoding is. The name of the character in the 256th position (indexed by 0) in Unicode is <code>LATIN CAPITAL LETTER A WITH MACRON</code>.</p> <p>An exception to the above rule is that <span style="white-space: nowrap;"><code>\N{U+<i>hex number</i>}</code></span> is always interpreted as a Unicode code point, so that <code>\N{U+0050}</code> is <code>"P"</code> even on EBCDIC platforms.</p> </dd> </dl> <p><b>NOTE</b>: Unlike C and other languages, Perl has no <code>\v</code> escape sequence for the vertical tab (VT, which is 11 in both ASCII and EBCDIC), but you may use <code>\N{VT}</code>, <code>\ck</code>, <code>\N{U+0b}</code>, or <code>\x0b</code>. (<code>\v</code> does have meaning in regular expression patterns in Perl, see <a href="perlre.html">perlre</a>.)</p> <p>The following escape sequences are available in constructs that interpolate, but not in transliterations. </p> <pre data-language="perl"><code class="plaintext">    \l		lowercase next character only
    \u		titlecase (not uppercase!) next character only
    \L		lowercase all characters till \E or end of string
    \U		uppercase all characters till \E or end of string
    \F		foldcase all characters till \E or end of string
    \Q          quote (disable) pattern metacharacters till \E or
                end of string
    \E		end either case modification or quoted section
		(whichever was last seen)</code></pre> <p>See <a href="perlfunc.html#quotemeta">"quotemeta" in perlfunc</a> for the exact definition of characters that are quoted by <code>\Q</code>.</p> <p><code>\L</code>, <code>\U</code>, <code>\F</code>, and <code>\Q</code> can stack, in which case you need one <code>\E</code> for each. For example:</p> <pre data-language="perl"><code>say "This \Qquoting \ubusiness \Uhere isn't quite\E done yet,\E is it?";
This quoting\ Business\ HERE\ ISN\'T\ QUITE\ done\ yet\, is it?</code></pre> <p>If a <span style="white-space: nowrap;"><code>use locale</code></span> form that includes <code>LC_CTYPE</code> is in effect (see <a href="perllocale.html">perllocale</a>), the case map used by <code>\l</code>, <code>\L</code>, <code>\u</code>, and <code>\U</code> is taken from the current locale. If Unicode (for example, <code>\N{}</code> or code points of 0x100 or beyond) is being used, the case map used by <code>\l</code>, <code>\L</code>, <code>\u</code>, and <code>\U</code> is as defined by Unicode. That means that case-mapping a single character can sometimes produce a sequence of several characters. Under <span style="white-space: nowrap;"><code>use locale</code></span>, <code>\F</code> produces the same results as <code>\L</code> for all locales but a UTF-8 one, where it instead uses the Unicode definition.</p> <p>All systems use the virtual <code>"\n"</code> to represent a line terminator, called a "newline". There is no such thing as an unvarying, physical newline character. It is only an illusion that the operating system, device drivers, C libraries, and Perl all conspire to preserve. Not all systems read <code>"\r"</code> as ASCII CR and <code>"\n"</code> as ASCII LF. For example, on the ancient Macs (pre-MacOS X) of yesteryear, these used to be reversed, and on systems without a line terminator, printing <code>"\n"</code> might emit no actual data. In general, use <code>"\n"</code> when you mean a "newline" for your system, but use the literal ASCII when you need an exact character. For example, most networking protocols expect and prefer a CR+LF (<code>"\015\012"</code> or <code>"\cM\cJ"</code>) for line terminators, and although they often accept just <code>"\012"</code>, they seldom tolerate just <code>"\015"</code>. If you get in the habit of using <code>"\n"</code> for networking, you may be burned some day. </p> <p>For constructs that do interpolate, variables beginning with "<code>$</code>" or "<code>@</code>" are interpolated. Subscripted variables such as <code>$a[3]</code> or <code>$href-&gt;{key}[0]</code> are also interpolated, as are array and hash slices. But method calls such as <code>$obj-&gt;meth</code> are not.</p> <p>Interpolating an array or slice interpolates the elements in order, separated by the value of <code>$"</code>, so is equivalent to interpolating <span style="white-space: nowrap;"><code>join $", @array</code></span>. "Punctuation" arrays such as <code>@*</code> are usually interpolated only if the name is enclosed in braces <code>@{*}</code>, but the arrays <code>@_</code>, <code>@+</code>, and <code>@-</code> are interpolated even without braces.</p> <p>For double-quoted strings, the quoting from <code>\Q</code> is applied after interpolation and escapes are processed.</p> <pre data-language="perl"><code>"abc\Qfoo\tbar$s\Exyz"</code></pre> <p>is equivalent to</p> <pre data-language="perl"><code>"abc" . quotemeta("foo\tbar$s") . "xyz"</code></pre> <p>For the pattern of regex operators (<code>qr//</code>, <code>m//</code> and <code>s///</code>), the quoting from <code>\Q</code> is applied after interpolation is processed, but before escapes are processed. This allows the pattern to match literally (except for <code>$</code> and <code>@</code>). For example, the following matches:</p> <pre data-language="perl"><code class="plaintext">'\s\t' =~ /\Q\s\t/</code></pre> <p>Because <code>$</code> or <code>@</code> trigger interpolation, you'll need to use something like <code>/\Quser\E\@\Qhost/</code> to match them literally.</p> <p>Patterns are subject to an additional level of interpretation as a regular expression. This is done as a second pass, after variables are interpolated, so that regular expressions may be incorporated into the pattern from the variables. If this is not what you want, use <code>\Q</code> to interpolate a variable literally.</p> <p>Apart from the behavior described above, Perl does not expand multiple levels of interpolation. In particular, contrary to the expectations of shell programmers, back-quotes do <i>NOT</i> interpolate within double quotes, nor do single quotes impede evaluation of variables when used within double quotes.</p> <h3 id="Regexp-Quote-Like-Operators">
Regexp Quote-Like Operators </h3> <p>Here are the quote-like operators that apply to pattern matching and related activities.</p> <dl> <dt id="qr/STRING/msixpodualn">
<code>qr/<i>STRING</i>/msixpodualn</code> </dt> <dd> <p>This operator quotes (and possibly compiles) its <i>STRING</i> as a regular expression. <i>STRING</i> is interpolated the same way as <i>PATTERN</i> in <code>m/<i>PATTERN</i>/</code>. If <code>"'"</code> is used as the delimiter, no variable interpolation is done. Returns a Perl value which may be used instead of the corresponding <code>/<i>STRING</i>/msixpodualn</code> expression. The returned value is a normalized version of the original pattern. It magically differs from a string containing the same characters: <code>ref(qr/x/)</code> returns "Regexp"; however, dereferencing it is not well defined (you currently get the normalized version of the original pattern, but this may change).</p> <p>For example,</p> <pre data-language="perl"><code>$rex = qr/my.STRING/is;
print $rex;                 # prints (?si-xm:my.STRING)
s/$rex/foo/;</code></pre> <p>is equivalent to</p> <pre data-language="perl"><code>s/my.STRING/foo/is;</code></pre> <p>The result may be used as a subpattern in a match:</p> <pre data-language="perl"><code>$re = qr/$pattern/;
$string =~ /foo${re}bar/;	# can be interpolated in other
                            # patterns
$string =~ $re;		# or used standalone
$string =~ /$re/;		# or this way</code></pre> <p>Since Perl may compile the pattern at the moment of execution of the <code>qr()</code> operator, using <code>qr()</code> may have speed advantages in some situations, notably if the result of <code>qr()</code> is used standalone:</p> <pre data-language="perl"><code>    sub match {
	my $patterns = shift;
	my @compiled = map qr/$_/i, @$patterns;
	grep {
	    my $success = 0;
	    foreach my $pat (@compiled) {
		$success = 1, last if /$pat/;
	    }
	    $success;
	} @_;
    }</code></pre> <p>Precompilation of the pattern into an internal representation at the moment of <code>qr()</code> avoids the need to recompile the pattern every time a match <code>/$pat/</code> is attempted. (Perl has many other internal optimizations, but none would be triggered in the above example if we did not use <code>qr()</code> operator.)</p> <p>Options (specified by the following modifiers) are:</p> <pre data-language="perl"><code>m	Treat string as multiple lines.
s	Treat string as single line. (Make . match a newline)
i	Do case-insensitive pattern matching.
x   Use extended regular expressions; specifying two
    x's means \t and the SPACE character are ignored within
    square-bracketed character classes
p	When matching preserve a copy of the matched string so
    that ${^PREMATCH}, ${^MATCH}, ${^POSTMATCH} will be
    defined (ignored starting in v5.20 as these are always
    defined starting in that release)
o	Compile pattern only once.
a   ASCII-restrict: Use ASCII for \d, \s, \w and [[:posix:]]
    character classes; specifying two a's adds the further
    restriction that no ASCII character will match a
    non-ASCII one under /i.
l   Use the current run-time locale's rules.
u   Use Unicode rules.
d   Use Unicode or native charset, as in 5.12 and earlier.
n   Non-capture mode. Don't let () fill in $1, $2, etc...</code></pre> <p>If a precompiled pattern is embedded in a larger pattern then the effect of <code>"msixpluadn"</code> will be propagated appropriately. The effect that the <code>/o</code> modifier has is not propagated, being restricted to those patterns explicitly using it.</p> <p>The <code>/a</code>, <code>/d</code>, <code>/l</code>, and <code>/u</code> modifiers (added in Perl 5.14) control the character set rules, but <code>/a</code> is the only one you are likely to want to specify explicitly; the other three are selected automatically by various pragmas.</p> <p>See <a href="perlre.html">perlre</a> for additional information on valid syntax for <i>STRING</i>, and for a detailed look at the semantics of regular expressions. In particular, all modifiers except the largely obsolete <code>/o</code> are further explained in <a href="perlre.html#Modifiers">"Modifiers" in perlre</a>. <code>/o</code> is described in the next section.</p> </dd> <dt id="m/PATTERN/msixpodualngc">
<code>m/<i>PATTERN</i>/msixpodualngc</code> </dt>  <dt id="/PATTERN/msixpodualngc">
<code>/<i>PATTERN</i>/msixpodualngc</code>
</dt> <dd> <p>Searches a string for a pattern match, and in scalar context returns true if it succeeds, false if it fails. If no string is specified via the <code>=~</code> or <code>!~</code> operator, the <code>$_</code> string is searched. (The string specified with <code>=~</code> need not be an lvalue--it may be the result of an expression evaluation, but remember the <code>=~</code> binds rather tightly.) See also <a href="perlre.html">perlre</a>.</p> <p>Options are as described in <code>qr//</code> above; in addition, the following match process modifiers are available:</p> <pre data-language="perl"><code class="plaintext">g  Match globally, i.e., find all occurrences.
c  Do not reset search position on a failed match when /g is
   in effect.</code></pre> <p>If <code>"/"</code> is the delimiter then the initial <code>m</code> is optional. With the <code>m</code> you can use any pair of non-whitespace (ASCII) characters as delimiters. This is particularly useful for matching path names that contain <code>"/"</code>, to avoid LTS (leaning toothpick syndrome). If <code>"?"</code> is the delimiter, then a match-only-once rule applies, described in <code>m?<i>PATTERN</i>?</code> below. If <code>"'"</code> (single quote) is the delimiter, no variable interpolation is performed on the <i>PATTERN</i>. When using a delimiter character valid in an identifier, whitespace is required after the <code>m</code>.</p> <p><i>PATTERN</i> may contain variables, which will be interpolated every time the pattern search is evaluated, except for when the delimiter is a single quote. (Note that <code>$(</code>, <code>$)</code>, and <code>$|</code> are not interpolated because they look like end-of-string tests.) Perl will not recompile the pattern unless an interpolated variable that it contains changes. You can force Perl to skip the test and never recompile by adding a <code>/o</code> (which stands for "once") after the trailing delimiter. Once upon a time, Perl would recompile regular expressions unnecessarily, and this modifier was useful to tell it not to do so, in the interests of speed. But now, the only reasons to use <code>/o</code> are one of:</p> <ol> <li>
<p>The variables are thousands of characters long and you know that they don't change, and you need to wring out the last little bit of speed by having Perl skip testing for that. (There is a maintenance penalty for doing this, as mentioning <code>/o</code> constitutes a promise that you won't change the variables in the pattern. If you do change them, Perl won't even notice.)</p> </li> <li>
<p>you want the pattern to use the initial values of the variables regardless of whether they change or not. (But there are saner ways of accomplishing this than using <code>/o</code>.)</p> </li> <li>
<p>If the pattern contains embedded code, such as</p> <pre data-language="perl"><code>use re 'eval';
$code = 'foo(?{ $x })';
/$code/</code></pre> <p>then perl will recompile each time, even though the pattern string hasn't changed, to ensure that the current value of <code>$x</code> is seen each time. Use <code>/o</code> if you want to avoid this.</p> </li> </ol> <p>The bottom line is that using <code>/o</code> is almost never a good idea.</p> </dd> <dt id="The-empty-pattern-//">
The empty pattern <code>//</code>
</dt> <dd> <p>If the <i>PATTERN</i> evaluates to the empty string, the last <i>successfully</i> matched regular expression is used instead. In this case, only the <code>g</code> and <code>c</code> flags on the empty pattern are honored; the other flags are taken from the original pattern. If no match has previously succeeded, this will (silently) act instead as a genuine empty pattern (which will always match). Using a user supplied string as a pattern has the risk that if the string is empty that it triggers the "last successful match" behavior, which can be very confusing. In such cases you are recommended to replace <code>m/$pattern/</code> with <code>m/(?:$pattern)/</code> to avoid this behavior.</p> <p>The last successful pattern may be accessed as a variable via <code>${^LAST_SUCCESSFUL_PATTERN}</code>. Matching against it, or the empty pattern should have the same effect, with the exception that when there is no last successful pattern the empty pattern will silently match, whereas using the <code>${^LAST_SUCCESSFUL_PATTERN}</code> variable will produce undefined warnings (if warnings are enabled). You can check <code>defined(${^LAST_SUCCESSFUL_PATTERN})</code> to test if there is a "last successful match" in the current scope.</p> <p>Note that it's possible to confuse Perl into thinking <code>//</code> (the empty regex) is really <code>//</code> (the defined-or operator). Perl is usually pretty good about this, but some pathological cases might trigger this, such as <code>$x///</code> (is that <span style="white-space: nowrap;"><code>($x) / (//)</code></span> or <span style="white-space: nowrap;"><code>$x // /</code></span>?) and <span style="white-space: nowrap;"><code>print $fh //</code></span> (<span style="white-space: nowrap;"><code>print $fh(//</code></span> or <span style="white-space: nowrap;"><code>print($fh //</code></span>?). In all of these examples, Perl will assume you meant defined-or. If you meant the empty regex, just use parentheses or spaces to disambiguate, or even prefix the empty regex with an <code>m</code> (so <code>//</code> becomes <code>m//</code>).</p> </dd> <dt id="Matching-in-list-context">
Matching in list context</dt> <dd> <p>If the <code>/g</code> option is not used, <code>m//</code> in list context returns a list consisting of the subexpressions matched by the parentheses in the pattern, that is, (<code>$1</code>, <code>$2</code>, <code>$3</code>...) (Note that here <code>$1</code> etc. are also set). When there are no parentheses in the pattern, the return value is the list <code>(1)</code> for success. With or without parentheses, an empty list is returned upon failure.</p> <p>Examples:</p> <pre data-language="perl"><code>open(TTY, "+&lt;/dev/tty")
   || die "can't access /dev/tty: $!";

&lt;TTY&gt; =~ /^y/i &amp;&amp; foo();	# do foo if desired

if (/Version: *([0-9.]*)/) { $version = $1; }

next if m#^/usr/spool/uucp#;

# poor man's grep
$arg = shift;
while (&lt;&gt;) {
   print if /$arg/o; # compile only once (no longer needed!)
}

if (($F1, $F2, $Etc) = ($foo =~ /^(\S+)\s+(\S+)\s*(.*)/))</code></pre> <p>This last example splits <code>$foo</code> into the first two words and the remainder of the line, and assigns those three fields to <code>$F1</code>, <code>$F2</code>, and <code>$Etc</code>. The conditional is true if any variables were assigned; that is, if the pattern matched.</p> <p>The <code>/g</code> modifier specifies global pattern matching--that is, matching as many times as possible within the string. How it behaves depends on the context. In list context, it returns a list of the substrings matched by any capturing parentheses in the regular expression. If there are no parentheses, it returns a list of all the matched strings, as if there were parentheses around the whole pattern.</p> <p>In scalar context, each execution of <code>m//g</code> finds the next match, returning true if it matches, and false if there is no further match. The position after the last match can be read or set using the <code>pos()</code> function; see <a href="perlfunc.html#pos">"pos" in perlfunc</a>. A failed match normally resets the search position to the beginning of the string, but you can avoid that by adding the <code>/c</code> modifier (for example, <code>m//gc</code>). Modifying the target string also resets the search position.</p> </dd> <dt id="\G-assertion">
<code>\G <i>assertion</i></code>
</dt> <dd> <p>You can intermix <code>m//g</code> matches with <code>m/\G.../g</code>, where <code>\G</code> is a zero-width assertion that matches the exact position where the previous <code>m//g</code>, if any, left off. Without the <code>/g</code> modifier, the <code>\G</code> assertion still anchors at <code>pos()</code> as it was at the start of the operation (see <a href="perlfunc.html#pos">"pos" in perlfunc</a>), but the match is of course only attempted once. Using <code>\G</code> without <code>/g</code> on a target string that has not previously had a <code>/g</code> match applied to it is the same as using the <code>\A</code> assertion to match the beginning of the string. Note also that, currently, <code>\G</code> is only properly supported when anchored at the very beginning of the pattern.</p> <p>Examples:</p> <pre data-language="perl"><code>    # list context
    ($one,$five,$fifteen) = (`uptime` =~ /(\d+\.\d+)/g);

    # scalar context
    local $/ = "";
    while ($paragraph = &lt;&gt;) {
	while ($paragraph =~ /\p{Ll}['")]*[.!?]+['")]*\s/g) {
	    $sentences++;
	}
    }
    say $sentences;</code></pre> <p>Here's another way to check for sentences in a paragraph:</p> <pre data-language="perl"><code>my $sentence_rx = qr{
   (?: (?&lt;= ^ ) | (?&lt;= \s ) )  # after start-of-string or
                               # whitespace
   \p{Lu}                      # capital letter
   .*?                         # a bunch of anything
   (?&lt;= \S )                   # that ends in non-
                               # whitespace
   (?&lt;! \b [DMS]r  )           # but isn't a common abbr.
   (?&lt;! \b Mrs )
   (?&lt;! \b Sra )
   (?&lt;! \b St  )
   [.?!]                       # followed by a sentence
                               # ender
   (?= $ | \s )                # in front of end-of-string
                               # or whitespace
}sx;
local $/ = "";
while (my $paragraph = &lt;&gt;) {
   say "NEW PARAGRAPH";
   my $count = 0;
   while ($paragraph =~ /($sentence_rx)/g) {
       printf "\tgot sentence %d: &lt;%s&gt;\n", ++$count, $1;
   }
}</code></pre> <p>Here's how to use <code>m//gc</code> with <code>\G</code>:</p> <pre data-language="perl"><code>$_ = "ppooqppqq";
while ($i++ &lt; 2) {
    print "1: '";
    print $1 while /(o)/gc; print "', pos=", pos, "\n";
    print "2: '";
    print $1 if /\G(q)/gc;  print "', pos=", pos, "\n";
    print "3: '";
    print $1 while /(p)/gc; print "', pos=", pos, "\n";
}
print "Final: '$1', pos=",pos,"\n" if /\G(.)/;</code></pre> <p>The last example should print:</p> <pre data-language="perl"><code class="plaintext">1: 'oo', pos=4
2: 'q', pos=5
3: 'pp', pos=7
1: '', pos=7
2: 'q', pos=8
3: '', pos=8
Final: 'q', pos=8</code></pre> <p>Notice that the final match matched <code>q</code> instead of <code>p</code>, which a match without the <code>\G</code> anchor would have done. Also note that the final match did not update <code>pos</code>. <code>pos</code> is only updated on a <code>/g</code> match. If the final match did indeed match <code>p</code>, it's a good bet that you're running an ancient (pre-5.6.0) version of Perl.</p> <p>A useful idiom for <code>lex</code>-like scanners is <code>/\G.../gc</code>. You can combine several regexps like this to process a string part-by-part, doing different actions depending on which regexp matched. Each regexp tries to match where the previous one leaves off.</p> <pre data-language="perl"><code>$_ = &lt;&lt;'EOL';
   $url = URI::URL-&gt;new( "http://example.com/" );
   die if $url eq "xXx";
EOL

LOOP: {
    print(" digits"),       redo LOOP if /\G\d+\b[,.;]?\s*/gc;
    print(" lowercase"),    redo LOOP
                                   if /\G\p{Ll}+\b[,.;]?\s*/gc;
    print(" UPPERCASE"),    redo LOOP
                                   if /\G\p{Lu}+\b[,.;]?\s*/gc;
    print(" Capitalized"),  redo LOOP
                             if /\G\p{Lu}\p{Ll}+\b[,.;]?\s*/gc;
    print(" MiXeD"),        redo LOOP if /\G\pL+\b[,.;]?\s*/gc;
    print(" alphanumeric"), redo LOOP
                           if /\G[\p{Alpha}\pN]+\b[,.;]?\s*/gc;
    print(" line-noise"),   redo LOOP if /\G\W+/gc;
    print ". That's all!\n";
}</code></pre> <p>Here is the output (split into several lines):</p> <pre data-language="perl"><code class="plaintext">line-noise lowercase line-noise UPPERCASE line-noise UPPERCASE
line-noise lowercase line-noise lowercase line-noise lowercase
lowercase line-noise lowercase lowercase line-noise lowercase
lowercase line-noise MiXeD line-noise. That's all!</code></pre> </dd> <dt id="m?PATTERN?msixpodualngc">
<code>m?<i>PATTERN</i>?msixpodualngc</code> </dt> <dd> <p>This is just like the <code>m/<i>PATTERN</i>/</code> search, except that it matches only once between calls to the <code>reset()</code> operator. This is a useful optimization when you want to see only the first occurrence of something in each file of a set of files, for instance. Only <code>m??</code> patterns local to the current package are reset.</p> <pre data-language="perl"><code>    while (&lt;&gt;) {
	if (m?^$?) {
			    # blank line between header and body
	}
    } continue {
	reset if eof;	    # clear m?? status for next file
    }</code></pre> <p>Another example switched the first "latin1" encoding it finds to "utf8" in a pod file:</p> <pre data-language="perl"><code>s//utf8/ if m? ^ =encoding \h+ \K latin1 ?x;</code></pre> <p>The match-once behavior is controlled by the match delimiter being <code>?</code>; with any other delimiter this is the normal <code>m//</code> operator.</p> <p>In the past, the leading <code>m</code> in <code>m?<i>PATTERN</i>?</code> was optional, but omitting it would produce a deprecation warning. As of v5.22.0, omitting it produces a syntax error. If you encounter this construct in older code, you can just add <code>m</code>.</p> </dd> <dt id="s/PATTERN/REPLACEMENT/msixpodualngcer">
<code>s/<i>PATTERN</i>/<i>REPLACEMENT</i>/msixpodualngcer</code> </dt> <dd> <p>Searches a string for a pattern, and if found, replaces that pattern with the replacement text and returns the number of substitutions made. Otherwise it returns false (a value that is both an empty string (<code>""</code>) and numeric zero (<code>0</code>) as described in <a href="#Relational-Operators">"Relational Operators"</a>).</p> <p>If the <code>/r</code> (non-destructive) option is used then it runs the substitution on a copy of the string and instead of returning the number of substitutions, it returns the copy whether or not a substitution occurred. The original string is never changed when <code>/r</code> is used. The copy will always be a plain string, even if the input is an object or a tied variable.</p> <p>If no string is specified via the <code>=~</code> or <code>!~</code> operator, the <code>$_</code> variable is searched and modified. Unless the <code>/r</code> option is used, the string specified must be a scalar variable, an array element, a hash element, or an assignment to one of those; that is, some sort of scalar lvalue.</p> <p>If the delimiter chosen is a single quote, no variable interpolation is done on either the <i>PATTERN</i> or the <i>REPLACEMENT</i>. Otherwise, if the <i>PATTERN</i> contains a <code>$</code> that looks like a variable rather than an end-of-string test, the variable will be interpolated into the pattern at run-time. If you want the pattern compiled only once the first time the variable is interpolated, use the <code>/o</code> option. If the pattern evaluates to the empty string, the last successfully executed regular expression is used instead. See <a href="perlre.html">perlre</a> for further explanation on these.</p> <p>Options are as with <code>m//</code> with the addition of the following replacement specific options:</p> <pre data-language="perl"><code class="plaintext">e	Evaluate the right side as an expression.
ee  Evaluate the right side as a string then eval the
    result.
r   Return substitution and leave the original string
    untouched.</code></pre> <p>Any non-whitespace delimiter may replace the slashes. Add space after the <code>s</code> when using a character allowed in identifiers. If single quotes are used, no interpretation is done on the replacement string (the <code>/e</code> modifier overrides this, however). Note that Perl treats backticks as normal delimiters; the replacement text is not evaluated as a command. If the <i>PATTERN</i> is delimited by bracketing quotes, the <i>REPLACEMENT</i> has its own pair of quotes, which may or may not be bracketing quotes, for example, <code>s(foo)(bar)</code> or <code>s&lt;foo&gt;/bar/</code>. A <code>/e</code> will cause the replacement portion to be treated as a full-fledged Perl expression and evaluated right then and there. It is, however, syntax checked at compile-time. A second <code>e</code> modifier will cause the replacement portion to be <code>eval</code>ed before being run as a Perl expression.</p> <p>Examples:</p> <pre data-language="perl"><code>    s/\bgreen\b/mauve/g;	      # don't change wintergreen

    $path =~ s|/usr/bin|/usr/local/bin|;

    s/Login: $foo/Login: $bar/; # run-time pattern

    ($foo = $bar) =~ s/this/that/;	# copy first, then
                                        # change
    ($foo = "$bar") =~ s/this/that/;	# convert to string,
                                        # copy, then change
    $foo = $bar =~ s/this/that/r;	# Same as above using /r
    $foo = $bar =~ s/this/that/r
                =~ s/that/the other/r;	# Chained substitutes
                                        # using /r
    @foo = map { s/this/that/r } @bar	# /r is very useful in
                                        # maps

    $count = ($paragraph =~ s/Mister\b/Mr./g);  # get change-cnt

    $_ = 'abc123xyz';
    s/\d+/$&amp;*2/e;		# yields 'abc246xyz'
    s/\d+/sprintf("%5d",$&amp;)/e;	# yields 'abc  246xyz'
    s/\w/$&amp; x 2/eg;		# yields 'aabbcc  224466xxyyzz'

    s/%(.)/$percent{$1}/g;	# change percent escapes; no /e
    s/%(.)/$percent{$1} || $&amp;/ge;	# expr now, so /e
    s/^=(\w+)/pod($1)/ge;	# use function call

    $_ = 'abc123xyz';
    $x = s/abc/def/r;           # $x is 'def123xyz' and
                                # $_ remains 'abc123xyz'.

    # expand variables in $_, but dynamics only, using
    # symbolic dereferencing
    s/\$(\w+)/${$1}/g;

    # Add one to the value of any numbers in the string
    s/(\d+)/1 + $1/eg;

    # Titlecase words in the last 30 characters only (presuming
    # that the substring doesn't start in the middle of a word)
    substr($str, -30) =~ s/\b(\p{Alpha})(\p{Alpha}*)\b/\u$1\L$2/g;

    # This will expand any embedded scalar variable
    # (including lexicals) in $_ : First $1 is interpolated
    # to the variable name, and then evaluated
    s/(\$\w+)/$1/eeg;

    # Delete (most) C comments.
    $program =~ s {
	/\*	# Match the opening delimiter.
	.*?	# Match a minimal number of characters.
	\*/	# Match the closing delimiter.
    } []gsx;

    s/^\s*(.*?)\s*$/$1/;	# trim whitespace in $_,
                                # expensively

    for ($variable) {		# trim whitespace in $variable,
                                # cheap
	s/^\s+//;
	s/\s+$//;
    }

    s/([^ ]*) *([^ ]*)/$2 $1/;	# reverse 1st two fields

    $foo !~ s/A/a/g;    # Lowercase all A's in $foo; return
                        # 0 if any were found and changed;
                        # otherwise return 1</code></pre> <p>Note the use of <code>$</code> instead of <code>\</code> in the last example. Unlike <b>sed</b>, we use the \&lt;<i>digit</i>&gt; form only in the left hand side. Anywhere else it's $&lt;<i>digit</i>&gt;.</p> <p>Occasionally, you can't use just a <code>/g</code> to get all the changes to occur that you might want. Here are two common cases:</p> <pre data-language="perl"><code># put commas in the right places in an integer
1 while s/(\d)(\d\d\d)(?!\d)/$1,$2/g;

# expand tabs to 8-column spacing
1 while s/\t+/' ' x (length($&amp;)*8 - length($`)%8)/e;</code></pre> <p>While <code>s///</code> accepts the <code>/c</code> flag, it has no effect beyond producing a warning if warnings are enabled.</p> </dd> </dl> <h3 id="Quote-Like-Operators">
Quote-Like Operators </h3> <dl> <dt id="q/STRING/">
<code>q/<i>STRING</i>/</code> </dt>  <dt id="'STRING'">
<code>'<i>STRING</i>'</code>
</dt> <dd> <p>A single-quoted, literal string. A backslash represents a backslash unless followed by the delimiter or another backslash, in which case the delimiter or backslash is interpolated.</p> <pre data-language="perl"><code>$foo = q!I said, "You said, 'She said it.'"!;
$bar = q('This is it.');
$baz = '\n';		# a two-character string</code></pre> </dd> <dt id="qq/STRING/">
<code>qq/<i>STRING</i>/</code> </dt>  <dt id='"STRING"'>
<code>"<i>STRING</i>"</code>
</dt> <dd> <p>A double-quoted, interpolated string.</p> <pre data-language="perl"><code>    $_ .= qq
     (*** The previous line contains the naughty word "$1".\n)
		if /\b(tcl|java|python)\b/i;      # :-)
    $baz = "\n";		# a one-character string</code></pre> </dd> <dt id="qx/STRING/">
<code>qx/<i>STRING</i>/</code> </dt>  <dt id="`STRING`">
<code>`<i>STRING</i>`</code>
</dt> <dd> <p>A string which is (possibly) interpolated and then executed as a system command, via <i>/bin/sh</i> or its equivalent if required. Shell wildcards, pipes, and redirections will be honored. Similarly to <code>system</code>, if the string contains no shell metacharacters then it will executed directly. The collected standard output of the command is returned; standard error is unaffected. In scalar context, it comes back as a single (potentially multi-line) string, or <code>undef</code> if the shell (or command) could not be started. In list context, returns a list of lines (however you've defined lines with <code>$/</code> or <code>$INPUT_RECORD_SEPARATOR</code>), or an empty list if the shell (or command) could not be started.</p> <p>Because backticks do not affect standard error, use shell file descriptor syntax (assuming the shell supports this) if you care to address this. To capture a command's STDERR and STDOUT together:</p> <pre data-language="perl"><code>$output = `cmd 2&gt;&amp;1`;</code></pre> <p>To capture a command's STDOUT but discard its STDERR:</p> <pre data-language="perl"><code>$output = `cmd 2&gt;/dev/null`;</code></pre> <p>To capture a command's STDERR but discard its STDOUT (ordering is important here):</p> <pre data-language="perl"><code>$output = `cmd 2&gt;&amp;1 1&gt;/dev/null`;</code></pre> <p>To exchange a command's STDOUT and STDERR in order to capture the STDERR but leave its STDOUT to come out the old STDERR:</p> <pre data-language="perl"><code>$output = `cmd 3&gt;&amp;1 1&gt;&amp;2 2&gt;&amp;3 3&gt;&amp;-`;</code></pre> <p>To read both a command's STDOUT and its STDERR separately, it's easiest to redirect them separately to files, and then read from those files when the program is done:</p> <pre data-language="perl"><code>system("program args 1&gt;program.stdout 2&gt;program.stderr");</code></pre> <p>The STDIN filehandle used by the command is inherited from Perl's STDIN. For example:</p> <pre data-language="perl"><code>open(SPLAT, "stuff")   || die "can't open stuff: $!";
open(STDIN, "&lt;&amp;SPLAT") || die "can't dupe SPLAT: $!";
print STDOUT `sort`;</code></pre> <p>will print the sorted contents of the file named <i>"stuff"</i>.</p> <p>Using single-quote as a delimiter protects the command from Perl's double-quote interpolation, passing it on to the shell instead:</p> <pre data-language="perl"><code>$perl_info  = qx(ps $$);            # that's Perl's $$
$shell_info = qx'ps $$';            # that's the new shell's $$</code></pre> <p>How that string gets evaluated is entirely subject to the command interpreter on your system. On most platforms, you will have to protect shell metacharacters if you want them treated literally. This is in practice difficult to do, as it's unclear how to escape which characters. See <a href="perlsec.html">perlsec</a> for a clean and safe example of a manual <code>fork()</code> and <code>exec()</code> to emulate backticks safely.</p> <p>On some platforms (notably DOS-like ones), the shell may not be capable of dealing with multiline commands, so putting newlines in the string may not get you what you want. You may be able to evaluate multiple commands in a single line by separating them with the command separator character, if your shell supports that (for example, <code>;</code> on many Unix shells and <code>&amp;</code> on the Windows NT <code>cmd</code> shell).</p> <p>Perl will attempt to flush all files opened for output before starting the child process, but this may not be supported on some platforms (see <a href="perlport.html">perlport</a>). To be safe, you may need to set <code>$|</code> (<code>$AUTOFLUSH</code> in <code><a href="english.html">English</a></code>) or call the <code>autoflush()</code> method of <code><a href="IO::Handle.html">IO::Handle</a></code> on any open handles.</p> <p>Beware that some command shells may place restrictions on the length of the command line. You must ensure your strings don't exceed this limit after any necessary interpolations. See the platform-specific release notes for more details about your particular environment.</p> <p>Using this operator can lead to programs that are difficult to port, because the shell commands called vary between systems, and may in fact not be present at all. As one example, the <code>type</code> command under the POSIX shell is very different from the <code>type</code> command under DOS. That doesn't mean you should go out of your way to avoid backticks when they're the right way to get something done. Perl was made to be a glue language, and one of the things it glues together is commands. Just understand what you're getting yourself into.</p> <p>Like <code>system</code>, backticks put the child process exit code in <code>$?</code>. If you'd like to manually inspect failure, you can check all possible failure modes by inspecting <code>$?</code> like this:</p> <pre data-language="perl"><code>if ($? == -1) {
    print "failed to execute: $!\n";
}
elsif ($? &amp; 127) {
    printf "child died with signal %d, %s coredump\n",
        ($? &amp; 127),  ($? &amp; 128) ? 'with' : 'without';
}
else {
    printf "child exited with value %d\n", $? &gt;&gt; 8;
}</code></pre> <p>Use the <a href="open.html">open</a> pragma to control the I/O layers used when reading the output of the command, for example:</p> <pre data-language="perl"><code>use open IN =&gt; ":encoding(UTF-8)";
my $x = `cmd-producing-utf-8`;</code></pre> <p><code>qx//</code> can also be called like a function with <a href="perlfunc.html#readpipe">"readpipe" in perlfunc</a>.</p> <p>See <a href="#I%2FO-Operators">"I/O Operators"</a> for more discussion.</p> </dd> <dt id="qw/STRING/">
<code>qw/<i>STRING</i>/</code> </dt> <dd> <p>Evaluates to a list of the words extracted out of <i>STRING</i>, using embedded whitespace as the word delimiters. It can be understood as being roughly equivalent to:</p> <pre data-language="perl"><code>split(" ", q/STRING/);</code></pre> <p>the differences being that it only splits on ASCII whitespace, generates a real list at compile time, and in scalar context it returns the last element in the list. So this expression:</p> <pre data-language="perl"><code class="plaintext">qw(foo bar baz)</code></pre> <p>is semantically equivalent to the list:</p> <pre data-language="perl"><code class="plaintext">"foo", "bar", "baz"</code></pre> <p>Some frequently seen examples:</p> <pre data-language="perl"><code>use POSIX qw( setlocale localeconv )
@EXPORT = qw( foo bar baz );</code></pre> <p>A common mistake is to try to separate the words with commas or to put comments into a multi-line <code>qw</code>-string. For this reason, the <span style="white-space: nowrap;"><code>use warnings</code></span> pragma and the <b>-w</b> switch (that is, the <code>$^W</code> variable) produces warnings if the <i>STRING</i> contains the <code>","</code> or the <code>"#"</code> character.</p> </dd> <dt id="tr/SEARCHLIST/REPLACEMENTLIST/cdsr">
<code>tr/<i>SEARCHLIST</i>/<i>REPLACEMENTLIST</i>/cdsr</code> </dt>  <dt id="y/SEARCHLIST/REPLACEMENTLIST/cdsr">
<code>y/<i>SEARCHLIST</i>/<i>REPLACEMENTLIST</i>/cdsr</code>
</dt> <dd> <p>Transliterates all occurrences of the characters found (or not found if the <code>/c</code> modifier is specified) in the search list with the positionally corresponding character in the replacement list, possibly deleting some, depending on the modifiers specified. It returns the number of characters replaced or deleted. If no string is specified via the <code>=~</code> or <code>!~</code> operator, the <code>$_</code> string is transliterated.</p> <p>For <b>sed</b> devotees, <code>y</code> is provided as a synonym for <code>tr</code>.</p> <p>If the <code>/r</code> (non-destructive) option is present, a new copy of the string is made and its characters transliterated, and this copy is returned no matter whether it was modified or not: the original string is always left unchanged. The new copy is always a plain string, even if the input string is an object or a tied variable.</p> <p>Unless the <code>/r</code> option is used, the string specified with <code>=~</code> must be a scalar variable, an array element, a hash element, or an assignment to one of those; in other words, an lvalue.</p> <p>The characters delimitting <i>SEARCHLIST</i> and <i>REPLACEMENTLIST</i> can be any printable character, not just forward slashes. If they are single quotes (<code>tr'<i>SEARCHLIST</i>'<i>REPLACEMENTLIST</i>'</code>), the only interpolation is removal of <code>\</code> from pairs of <code>\\</code>; so hyphens are interpreted literally rather than specifying a character range.</p> <p>Otherwise, a character range may be specified with a hyphen, so <code>tr/A-J/0-9/</code> does the same replacement as <code>tr/ACEGIBDFHJ/0246813579/</code>.</p> <p>If the <i>SEARCHLIST</i> is delimited by bracketing quotes, the <i>REPLACEMENTLIST</i> must have its own pair of quotes, which may or may not be bracketing quotes; for example, <code>tr(aeiouy)(yuoiea)</code> or <code>tr[+\-*/]"ABCD"</code>. This final example shows a way to visually clarify what is going on for people who are more familiar with regular expression patterns than with <code>tr</code>, and who may think forward slash delimiters imply that <code>tr</code> is more like a regular expression pattern than it actually is. (Another option might be to use <code>tr[...][...]</code>.)</p> <p><code>tr</code> isn't fully like bracketed character classes, just (significantly) more like them than it is to full patterns. For example, characters appearing more than once in either list behave differently here than in patterns, and <code>tr</code> lists do not allow backslashed character classes such as <code>\d</code> or <code>\pL</code>, nor variable interpolation, so <code>"$"</code> and <code>"@"</code> are always treated as literals.</p> <p>The allowed elements are literals plus <code>\'</code> (meaning a single quote). If the delimiters aren't single quotes, also allowed are any of the escape sequences accepted in double-quoted strings. Escape sequence details are in <a href="#Quote-and-Quote-like-Operators">the table near the beginning of this section</a>.</p> <p>A hyphen at the beginning or end, or preceded by a backslash is also always considered a literal. Precede a delimiter character with a backslash to allow it.</p> <p>The <code>tr</code> operator is not equivalent to the <code><a href="http://man.he.net/man1/tr">tr(1)</a></code> utility. <code>tr[a-z][A-Z]</code> will uppercase the 26 letters "a" through "z", but for case changing not confined to ASCII, use <a href="perlfunc.html#lc"><code>lc</code></a>, <a href="perlfunc.html#uc"><code>uc</code></a>, <a href="perlfunc.html#lcfirst"><code>lcfirst</code></a>, <a href="perlfunc.html#ucfirst"><code>ucfirst</code></a> (all documented in <a href="perlfunc.html">perlfunc</a>), or the <a href="#s%2FPATTERN%2FREPLACEMENT%2Fmsixpodualngcer">substitution operator <code>s/<i>PATTERN</i>/<i>REPLACEMENT</i>/</code></a> (with <code>\U</code>, <code>\u</code>, <code>\L</code>, and <code>\l</code> string-interpolation escapes in the <i>REPLACEMENT</i> portion).</p> <p>Most ranges are unportable between character sets, but certain ones signal Perl to do special handling to make them portable. There are two classes of portable ranges. The first are any subsets of the ranges <code>A-Z</code>, <code>a-z</code>, and <code>0-9</code>, when expressed as literal characters.</p> <pre data-language="perl"><code class="plaintext">tr/h-k/H-K/</code></pre> <p>capitalizes the letters <code>"h"</code>, <code>"i"</code>, <code>"j"</code>, and <code>"k"</code> and nothing else, no matter what the platform's character set is. In contrast, all of</p> <pre data-language="perl"><code class="plaintext">tr/\x68-\x6B/\x48-\x4B/
tr/h-\x6B/H-\x4B/
tr/\x68-k/\x48-K/</code></pre> <p>do the same capitalizations as the previous example when run on ASCII platforms, but something completely different on EBCDIC ones.</p> <p>The second class of portable ranges is invoked when one or both of the range's end points are expressed as <code>\N{...}</code></p> <pre data-language="perl"><code>$string =~ tr/\N{U+20}-\N{U+7E}//d;</code></pre> <p>removes from <code>$string</code> all the platform's characters which are equivalent to any of Unicode U+0020, U+0021, ... U+007D, U+007E. This is a portable range, and has the same effect on every platform it is run on. In this example, these are the ASCII printable characters. So after this is run, <code>$string</code> has only controls and characters which have no ASCII equivalents.</p> <p>But, even for portable ranges, it is not generally obvious what is included without having to look things up in the manual. A sound principle is to use only ranges that both begin from, and end at, either ASCII alphabetics of equal case (<code>b-e</code>, <code>B-E</code>), or digits (<code>1-4</code>). Anything else is unclear (and unportable unless <code>\N{...}</code> is used). If in doubt, spell out the character sets in full.</p> <p>Options:</p> <pre data-language="perl"><code class="plaintext">    c	Complement the SEARCHLIST.
    d	Delete found but unreplaced characters.
    r	Return the modified string and leave the original string
	untouched.
    s	Squash duplicate replaced characters.</code></pre> <p>If the <code>/d</code> modifier is specified, any characters specified by <i>SEARCHLIST</i> not found in <i>REPLACEMENTLIST</i> are deleted. (Note that this is slightly more flexible than the behavior of some <b>tr</b> programs, which delete anything they find in the <i>SEARCHLIST</i>, period.)</p> <p>If the <code>/s</code> modifier is specified, sequences of characters, all in a row, that were transliterated to the same character are squashed down to a single instance of that character.</p> <pre data-language="perl"><code>my $a = "aaabbbca";
$a =~ tr/ab/dd/s;     # $a now is "dcd"</code></pre> <p>If the <code>/d</code> modifier is used, the <i>REPLACEMENTLIST</i> is always interpreted exactly as specified. Otherwise, if the <i>REPLACEMENTLIST</i> is shorter than the <i>SEARCHLIST</i>, the final character, if any, is replicated until it is long enough. There won't be a final character if and only if the <i>REPLACEMENTLIST</i> is empty, in which case <i>REPLACEMENTLIST</i> is copied from <i>SEARCHLIST</i>. An empty <i>REPLACEMENTLIST</i> is useful for counting characters in a class, or for squashing character sequences in a class.</p> <pre data-language="perl"><code class="plaintext">tr/abcd//            tr/abcd/abcd/
tr/abcd/AB/          tr/abcd/ABBB/
tr/abcd//d           s/[abcd]//g
tr/abcd/AB/d         (tr/ab/AB/ + s/[cd]//g)  - but run together</code></pre> <p>If the <code>/c</code> modifier is specified, the characters to be transliterated are the ones NOT in <i>SEARCHLIST</i>, that is, it is complemented. If <code>/d</code> and/or <code>/s</code> are also specified, they apply to the complemented <i>SEARCHLIST</i>. Recall, that if <i>REPLACEMENTLIST</i> is empty (except under <code>/d</code>) a copy of <i>SEARCHLIST</i> is used instead. That copy is made after complementing under <code>/c</code>. <i>SEARCHLIST</i> is sorted by code point order after complementing, and any <i>REPLACEMENTLIST</i> is applied to that sorted result. This means that under <code>/c</code>, the order of the characters specified in <i>SEARCHLIST</i> is irrelevant. This can lead to different results on EBCDIC systems if <i>REPLACEMENTLIST</i> contains more than one character, hence it is generally non-portable to use <code>/c</code> with such a <i>REPLACEMENTLIST</i>.</p> <p>Another way of describing the operation is this: If <code>/c</code> is specified, the <i>SEARCHLIST</i> is sorted by code point order, then complemented. If <i>REPLACEMENTLIST</i> is empty and <code>/d</code> is not specified, <i>REPLACEMENTLIST</i> is replaced by a copy of <i>SEARCHLIST</i> (as modified under <code>/c</code>), and these potentially modified lists are used as the basis for what follows. Any character in the target string that isn't in <i>SEARCHLIST</i> is passed through unchanged. Every other character in the target string is replaced by the character in <i>REPLACEMENTLIST</i> that positionally corresponds to its mate in <i>SEARCHLIST</i>, except that under <code>/s</code>, the 2nd and following characters are squeezed out in a sequence of characters in a row that all translate to the same character. If <i>SEARCHLIST</i> is longer than <i>REPLACEMENTLIST</i>, characters in the target string that match a character in <i>SEARCHLIST</i> that doesn't have a correspondence in <i>REPLACEMENTLIST</i> are either deleted from the target string if <code>/d</code> is specified; or replaced by the final character in <i>REPLACEMENTLIST</i> if <code>/d</code> isn't specified.</p> <p>Some examples:</p> <pre data-language="perl"><code>$ARGV[1] =~ tr/A-Z/a-z/;   # canonicalize to lower case ASCII

$cnt = tr/*/*/;            # count the stars in $_
$cnt = tr/*//;             # same thing

$cnt = $sky =~ tr/*/*/;    # count the stars in $sky
$cnt = $sky =~ tr/*//;     # same thing

$cnt = $sky =~ tr/*//c;    # count all the non-stars in $sky
$cnt = $sky =~ tr/*/*/c;   # same, but transliterate each non-star
                           # into a star, leaving the already-stars
                           # alone.  Afterwards, everything in $sky
                           # is a star.

$cnt = tr/0-9//;           # count the ASCII digits in $_

tr/a-zA-Z//s;              # bookkeeper -&gt; bokeper
tr/o/o/s;                  # bookkeeper -&gt; bokkeeper
tr/oe/oe/s;                # bookkeeper -&gt; bokkeper
tr/oe//s;                  # bookkeeper -&gt; bokkeper
tr/oe/o/s;                 # bookkeeper -&gt; bokkopor

($HOST = $host) =~ tr/a-z/A-Z/;
 $HOST = $host  =~ tr/a-z/A-Z/r; # same thing

$HOST = $host =~ tr/a-z/A-Z/r   # chained with s///r
              =~ s/:/ -p/r;

tr/a-zA-Z/ /cs;                 # change non-alphas to single space

@stripped = map tr/a-zA-Z/ /csr, @original;
                                # /r with map

tr [\200-\377]
   [\000-\177];                 # wickedly delete 8th bit

$foo !~ tr/A/a/    # transliterate all the A's in $foo to 'a',
                   # return 0 if any were found and changed.
                   # Otherwise return 1</code></pre> <p>If multiple transliterations are given for a character, only the first one is used:</p> <pre data-language="perl"><code class="plaintext">tr/AAA/XYZ/</code></pre> <p>will transliterate any A to X.</p> <p>Because the transliteration table is built at compile time, neither the <i>SEARCHLIST</i> nor the <i>REPLACEMENTLIST</i> are subjected to double quote interpolation. That means that if you want to use variables, you must use an <code>eval()</code>:</p> <pre data-language="perl"><code>eval "tr/$oldlist/$newlist/";
die $@ if $@;

eval "tr/$oldlist/$newlist/, 1" or die $@;</code></pre> </dd> <dt id="EOF">
<code>&lt;&lt;<i>EOF</i></code> </dt> <dd> <p>A line-oriented form of quoting is based on the shell "here-document" syntax. Following a <code>&lt;&lt;</code> you specify a string to terminate the quoted material, and all lines following the current line down to the terminating string are the value of the item.</p> <p>Prefixing the terminating string with a <code>~</code> specifies that you want to use <a href="#Indented-Here-docs">"Indented Here-docs"</a> (see below).</p> <p>The terminating string may be either an identifier (a word), or some quoted text. An unquoted identifier works like double quotes. There may not be a space between the <code>&lt;&lt;</code> and the identifier, unless the identifier is explicitly quoted. The terminating string must appear by itself (unquoted and with no surrounding whitespace) on the terminating line.</p> <p>If the terminating string is quoted, the type of quotes used determine the treatment of the text.</p> <dl> <dt id="Double-Quotes">
Double Quotes</dt> <dd> <p>Double quotes indicate that the text will be interpolated using exactly the same rules as normal double quoted strings.</p> <pre data-language="perl"><code>   print &lt;&lt;EOF;
The price is $Price.
EOF

   print &lt;&lt; "EOF"; # same as above
The price is $Price.
EOF</code></pre> </dd> <dt id="Single-Quotes">
Single Quotes</dt> <dd> <p>Single quotes indicate the text is to be treated literally with no interpolation of its content. This is similar to single quoted strings except that backslashes have no special meaning, with <code>\\</code> being treated as two backslashes and not one as they would in every other quoting construct.</p> <p>Just as in the shell, a backslashed bareword following the <code>&lt;&lt;</code> means the same thing as a single-quoted string does:</p> <pre data-language="perl"><code>	$cost = &lt;&lt;'VISTA';  # hasta la ...
    That'll be $10 please, ma'am.
    VISTA

	$cost = &lt;&lt;\VISTA;   # Same thing!
    That'll be $10 please, ma'am.
    VISTA</code></pre> <p>This is the only form of quoting in perl where there is no need to worry about escaping content, something that code generators can and do make good use of.</p> </dd> <dt id="Backticks">Backticks</dt> <dd> <p>The content of the here doc is treated just as it would be if the string were embedded in backticks. Thus the content is interpolated as though it were double quoted and then executed via the shell, with the results of the execution returned.</p> <pre data-language="perl"><code>   print &lt;&lt; `EOC`; # execute command and get results
echo hi there
EOC</code></pre> </dd> </dl> <dl> <dt id="Indented-Here-docs">
Indented Here-docs</dt> <dd> <p>The here-doc modifier <code>~</code> allows you to indent your here-docs to make the code more readable:</p> <pre data-language="perl"><code>if ($some_var) {
  print &lt;&lt;~EOF;
    This is a here-doc
    EOF
}</code></pre> <p>This will print...</p> <pre data-language="perl"><code class="plaintext">This is a here-doc</code></pre> <p>...with no leading whitespace.</p> <p>The line containing the delimiter that marks the end of the here-doc determines the indentation template for the whole thing. Compilation croaks if any non-empty line inside the here-doc does not begin with the precise indentation of the terminating line. (An empty line consists of the single character "\n".) For example, suppose the terminating line begins with a tab character followed by 4 space characters. Every non-empty line in the here-doc must begin with a tab followed by 4 spaces. They are stripped from each line, and any leading white space remaining on a line serves as the indentation for that line. Currently, only the TAB and SPACE characters are treated as whitespace for this purpose. Tabs and spaces may be mixed, but are matched exactly; tabs remain tabs and are not expanded.</p> <p>Additional beginning whitespace (beyond what preceded the delimiter) will be preserved:</p> <pre data-language="perl"><code>print &lt;&lt;~EOF;
  This text is not indented
    This text is indented with two spaces
  		This text is indented with two tabs
  EOF</code></pre> <p>Finally, the modifier may be used with all of the forms mentioned above:</p> <pre data-language="perl"><code>&lt;&lt;~\EOF;
&lt;&lt;~'EOF'
&lt;&lt;~"EOF"
&lt;&lt;~`EOF`</code></pre> <p>And whitespace may be used between the <code>~</code> and quoted delimiters:</p> <pre data-language="perl"><code>&lt;&lt;~ 'EOF'; # ... "EOF", `EOF`</code></pre> </dd> </dl> <p>It is possible to stack multiple here-docs in a row:</p> <pre data-language="perl"><code>   print &lt;&lt;"foo", &lt;&lt;"bar"; # you can stack them
I said foo.
foo
I said bar.
bar

   myfunc(&lt;&lt; "THIS", 23, &lt;&lt;'THAT');
Here's a line
or two.
THIS
and here's another.
THAT</code></pre> <p>Just don't forget that you have to put a semicolon on the end to finish the statement, as Perl doesn't know you're not going to try to do this:</p> <pre data-language="perl"><code>   print &lt;&lt;ABC
179231
ABC
   + 20;</code></pre> <p>If you want to remove the line terminator from your here-docs, use <code>chomp()</code>.</p> <pre data-language="perl"><code>chomp($string = &lt;&lt;'END');
This is a string.
END</code></pre> <p>If you want your here-docs to be indented with the rest of the code, use the <code>&lt;&lt;~FOO</code> construct described under <a href="#Indented-Here-docs">"Indented Here-docs"</a>:</p> <pre data-language="perl"><code>$quote = &lt;&lt;~'FINIS';
   The Road goes ever on and on,
   down from the door where it began.
   FINIS</code></pre> <p>If you use a here-doc within a delimited construct, such as in <code>s///eg</code>, the quoted material must still come on the line following the <code>&lt;&lt;FOO</code> marker, which means it may be inside the delimited construct:</p> <pre data-language="perl"><code>s/this/&lt;&lt;E . 'that'
the other
E
 . 'more '/eg;</code></pre> <p>It works this way as of Perl 5.18. Historically, it was inconsistent, and you would have to write</p> <pre data-language="perl"><code>s/this/&lt;&lt;E . 'that'
 . 'more '/eg;
the other
E</code></pre> <p>outside of string evals.</p> <p>Additionally, quoting rules for the end-of-string identifier are unrelated to Perl's quoting rules. <code>q()</code>, <code>qq()</code>, and the like are not supported in place of <code>''</code> and <code>""</code>, and the only interpolation is for backslashing the quoting character:</p> <pre data-language="perl"><code>print &lt;&lt; "abc\"def";
testing...
abc"def</code></pre> <p>Finally, quoted strings cannot span multiple lines. The general rule is that the identifier must be a string literal. Stick with that, and you should be safe.</p> </dd> </dl> <h3 id="Gory-details-of-parsing-quoted-constructs">
Gory details of parsing quoted constructs </h3> <p>When presented with something that might have several different interpretations, Perl uses the <b>DWIM</b> (that's "Do What I Mean") principle to pick the most probable interpretation. This strategy is so successful that Perl programmers often do not suspect the ambivalence of what they write. But from time to time, Perl's notions differ substantially from what the author honestly meant.</p> <p>This section hopes to clarify how Perl handles quoted constructs. Although the most common reason to learn this is to unravel labyrinthine regular expressions, because the initial steps of parsing are the same for all quoting operators, they are all discussed together.</p> <p>The most important Perl parsing rule is the first one discussed below: when processing a quoted construct, Perl first finds the end of that construct, then interprets its contents. If you understand this rule, you may skip the rest of this section on the first reading. The other rules are likely to contradict the user's expectations much less frequently than this first one.</p> <p>Some passes discussed below are performed concurrently, but because their results are the same, we consider them individually. For different quoting constructs, Perl performs different numbers of passes, from one to four, but these passes are always performed in the same order.</p> <dl> <dt id="Finding-the-end">
Finding the end</dt> <dd> <p>The first pass is finding the end of the quoted construct. This results in saving to a safe location a copy of the text (between the starting and ending delimiters), normalized as necessary to avoid needing to know what the original delimiters were.</p> <p>If the construct is a here-doc, the ending delimiter is a line that has a terminating string as the content. Therefore <code>&lt;&lt;EOF</code> is terminated by <code>EOF</code> immediately followed by <code>"\n"</code> and starting from the first column of the terminating line. When searching for the terminating line of a here-doc, nothing is skipped. In other words, lines after the here-doc syntax are compared with the terminating string line by line.</p> <p>For the constructs except here-docs, single characters are used as starting and ending delimiters. If the starting delimiter is an opening punctuation (that is <code>(</code>, <code>[</code>, <code>{</code>, or <code>&lt;</code>), the ending delimiter is the corresponding closing punctuation (that is <code>)</code>, <code>]</code>, <code>}</code>, or <code>&gt;</code>). If the starting delimiter is an unpaired character like <code>/</code> or a closing punctuation, the ending delimiter is the same as the starting delimiter. Therefore a <code>/</code> terminates a <code>qq//</code> construct, while a <code>]</code> terminates both <code>qq[]</code> and <code>qq]]</code> constructs.</p> <p>When searching for single-character delimiters, escaped delimiters and <code>\\</code> are skipped. For example, while searching for terminating <code>/</code>, combinations of <code>\\</code> and <code>\/</code> are skipped. If the delimiters are bracketing, nested pairs are also skipped. For example, while searching for a closing <code>]</code> paired with the opening <code>[</code>, combinations of <code>\\</code>, <code>\]</code>, and <code>\[</code> are all skipped, and nested <code>[</code> and <code>]</code> are skipped as well. However, when backslashes are used as the delimiters (like <code>qq\\</code> and <code>tr\\\</code>), nothing is skipped. During the search for the end, backslashes that escape delimiters or other backslashes are removed (exactly speaking, they are not copied to the safe location).</p> <p>For constructs with three-part delimiters (<code>s///</code>, <code>y///</code>, and <code>tr///</code>), the search is repeated once more. If the first delimiter is not an opening punctuation, the three delimiters must be the same, such as <code>s!!!</code> and <code>tr)))</code>, in which case the second delimiter terminates the left part and starts the right part at once. If the left part is delimited by bracketing punctuation (that is <code>()</code>, <code>[]</code>, <code>{}</code>, or <code>&lt;&gt;</code>), the right part needs another pair of delimiters such as <code>s(){}</code> and <code>tr[]//</code>. In these cases, whitespace and comments are allowed between the two parts, although the comment must follow at least one whitespace character; otherwise a character expected as the start of the comment may be regarded as the starting delimiter of the right part.</p> <p>During this search no attention is paid to the semantics of the construct. Thus:</p> <pre data-language="perl"><code>"$hash{"$foo/$bar"}"</code></pre> <p>or:</p> <pre data-language="perl"><code class="plaintext">m/
  bar	# NOT a comment, this slash / terminated m//!
 /x</code></pre> <p>do not form legal quoted expressions. The quoted part ends on the first <code>"</code> and <code>/</code>, and the rest happens to be a syntax error. Because the slash that terminated <code>m//</code> was followed by a <code>SPACE</code>, the example above is not <code>m//x</code>, but rather <code>m//</code> with no <code>/x</code> modifier. So the embedded <code>#</code> is interpreted as a literal <code>#</code>.</p> <p>Also no attention is paid to <code>\c\</code> (multichar control char syntax) during this search. Thus the second <code>\</code> in <code>qq/\c\/</code> is interpreted as a part of <code>\/</code>, and the following <code>/</code> is not recognized as a delimiter. Instead, use <code>\034</code> or <code>\x1c</code> at the end of quoted constructs.</p> </dd> <dt id="Interpolation">Interpolation </dt> <dd> <p>The next step is interpolation in the text obtained, which is now delimiter-independent. There are multiple cases.</p> <dl> <dt id="">
<code>&lt;&lt;'EOF'</code>
</dt> <dd> <p>No interpolation is performed. Note that the combination <code>\\</code> is left intact, since escaped delimiters are not available for here-docs.</p> </dd> <dt id="m'',-the-pattern-of-s'''">
<code>m''</code>, the pattern of <code>s'''</code>
</dt> <dd> <p>No interpolation is performed at this stage. Any backslashed sequences including <code>\\</code> are treated at the stage of <a href="#Parsing-regular-expressions">"Parsing regular expressions"</a>.</p> </dd> <dt id="'',-q//,-tr''',-y''',-the-replacement-of-s'''">
<code>''</code>, <code>q//</code>, <code>tr'''</code>, <code>y'''</code>, the replacement of <code>s'''</code>
</dt> <dd> <p>The only interpolation is removal of <code>\</code> from pairs of <code>\\</code>. Therefore <code>"-"</code> in <code>tr'''</code> and <code>y'''</code> is treated literally as a hyphen and no character range is available. <code>\1</code> in the replacement of <code>s'''</code> does not work as <code>$1</code>.</p> </dd> <dt id="tr///,-y///">
<code>tr///</code>, <code>y///</code>
</dt> <dd> <p>No variable interpolation occurs. String modifying combinations for case and quoting such as <code>\Q</code>, <code>\U</code>, and <code>\E</code> are not recognized. The other escape sequences such as <code>\200</code> and <code>\t</code> and backslashed characters such as <code>\\</code> and <code>\-</code> are converted to appropriate literals. The character <code>"-"</code> is treated specially and therefore <code>\-</code> is treated as a literal <code>"-"</code>.</p> </dd> <dt id='"",-``,-qq//,-qx//,-,'>
<code>""</code>, <code>``</code>, <code>qq//</code>, <code>qx//</code>, <code>&lt;file*glob&gt;</code>, <code>&lt;&lt;"EOF"</code>
</dt> <dd> <p><code>\Q</code>, <code>\U</code>, <code>\u</code>, <code>\L</code>, <code>\l</code>, <code>\F</code> (possibly paired with <code>\E</code>) are converted to corresponding Perl constructs. Thus, <code>"$foo\Qbaz$bar"</code> is converted to <span style="white-space: nowrap;"><code>$foo . (quotemeta("baz" . $bar))</code></span> internally. The other escape sequences such as <code>\200</code> and <code>\t</code> and backslashed characters such as <code>\\</code> and <code>\-</code> are replaced with appropriate expansions.</p> <p>Let it be stressed that <i>whatever falls between <code>\Q</code> and <code>\E</code></i> is interpolated in the usual way. Something like <code>"\Q\\E"</code> has no <code>\E</code> inside. Instead, it has <code>\Q</code>, <code>\\</code>, and <code>E</code>, so the result is the same as for <code>"\\\\E"</code>. As a general rule, backslashes between <code>\Q</code> and <code>\E</code> may lead to counterintuitive results. So, <code>"\Q\t\E"</code> is converted to <code>quotemeta("\t")</code>, which is the same as <code>"\\\t"</code> (since TAB is not alphanumeric). Note also that:</p> <pre data-language="perl"><code>$str = '\t';
return "\Q$str";</code></pre> <p>may be closer to the conjectural <i>intention</i> of the writer of <code>"\Q\t\E"</code>.</p> <p>Interpolated scalars and arrays are converted internally to the <code>join</code> and <code>"."</code> catenation operations. Thus, <span style="white-space: nowrap;"><code>"$foo XXX '@arr'"</code></span> becomes:</p> <pre data-language="perl"><code>$foo . " XXX '" . (join $", @arr) . "'";</code></pre> <p>All operations above are performed simultaneously, left to right.</p> <p>Because the result of <span style="white-space: nowrap;"><code>"\Q <i>STRING</i> \E"</code></span> has all metacharacters quoted, there is no way to insert a literal <code>$</code> or <code>@</code> inside a <code>\Q\E</code> pair. If protected by <code>\</code>, <code>$</code> will be quoted to become <code>"\\\$"</code>; if not, it is interpreted as the start of an interpolated scalar.</p> <p>Note also that the interpolation code needs to make a decision on where the interpolated scalar ends. For instance, whether <span style="white-space: nowrap;"><code>"a $x -&gt; {c}"</code></span> really means:</p> <pre data-language="perl"><code>"a " . $x . " -&gt; {c}";</code></pre> <p>or:</p> <pre data-language="perl"><code>"a " . $x -&gt; {c};</code></pre> <p>Most of the time, the longest possible text that does not include spaces between components and which contains matching braces or brackets. because the outcome may be determined by voting based on heuristic estimators, the result is not strictly predictable. Fortunately, it's usually correct for ambiguous cases.</p> </dd> <dt id="The-replacement-of-s///">
The replacement of <code>s///</code>
</dt> <dd> <p>Processing of <code>\Q</code>, <code>\U</code>, <code>\u</code>, <code>\L</code>, <code>\l</code>, <code>\F</code> and interpolation happens as with <code>qq//</code> constructs.</p> <p>It is at this step that <code>\1</code> is begrudgingly converted to <code>$1</code> in the replacement text of <code>s///</code>, in order to correct the incorrigible <i>sed</i> hackers who haven't picked up the saner idiom yet. A warning is emitted if the <span style="white-space: nowrap;"><code>use warnings</code></span> pragma or the <b>-w</b> command-line flag (that is, the <code>$^W</code> variable) was set.</p> </dd> <dt id="RE-in-m?RE?,-/RE/,-m/RE/,-s/RE/foo/,">
<code>RE</code> in <code>m?RE?</code>, <code>/RE/</code>, <code>m/RE/</code>, <code>s/RE/foo/</code>,</dt> <dd> <p>Processing of <code>\Q</code>, <code>\U</code>, <code>\u</code>, <code>\L</code>, <code>\l</code>, <code>\F</code>, <code>\E</code>, and interpolation happens (almost) as with <code>qq//</code> constructs.</p> <p>Processing of <code>\N{...}</code> is also done here, and compiled into an intermediate form for the regex compiler. (This is because, as mentioned below, the regex compilation may be done at execution time, and <code>\N{...}</code> is a compile-time construct.)</p> <p>However any other combinations of <code>\</code> followed by a character are not substituted but only skipped, in order to parse them as regular expressions at the following step. As <code>\c</code> is skipped at this step, <code>@</code> of <code>\c@</code> in RE is possibly treated as an array symbol (for example <code>@foo</code>), even though the same text in <code>qq//</code> gives interpolation of <code>\c@</code>.</p> <p>Code blocks such as <code>(?{BLOCK})</code> are handled by temporarily passing control back to the perl parser, in a similar way that an interpolated array subscript expression such as <code>"foo$array[1+f("[xyz")]bar"</code> would be.</p> <p>Moreover, inside <code>(?{BLOCK})</code>, <span style="white-space: nowrap;"><code>(?# comment )</code></span>, and a <code>#</code>-comment in a <code>/x</code>-regular expression, no processing is performed whatsoever. This is the first step at which the presence of the <code>/x</code> modifier is relevant.</p> <p>Interpolation in patterns has several quirks: <code>$|</code>, <code>$(</code>, <code>$)</code>, <code>@+</code> and <code>@-</code> are not interpolated, and constructs <code>$var[SOMETHING]</code> are voted (by several different estimators) to be either an array element or <code>$var</code> followed by an RE alternative. This is where the notation <code>${arr[$bar]}</code> comes handy: <code>/${arr[0-9]}/</code> is interpreted as array element <code>-9</code>, not as a regular expression from the variable <code>$arr</code> followed by a digit, which would be the interpretation of <code>/$arr[0-9]/</code>. Since voting among different estimators may occur, the result is not predictable.</p> <p>The lack of processing of <code>\\</code> creates specific restrictions on the post-processed text. If the delimiter is <code>/</code>, one cannot get the combination <code>\/</code> into the result of this step. <code>/</code> will finish the regular expression, <code>\/</code> will be stripped to <code>/</code> on the previous step, and <code>\\/</code> will be left as is. Because <code>/</code> is equivalent to <code>\/</code> inside a regular expression, this does not matter unless the delimiter happens to be character special to the RE engine, such as in <code>s*foo*bar*</code>, <code>m[foo]</code>, or <code>m?foo?</code>; or an alphanumeric char, as in:</p> <pre data-language="perl"><code>m m ^ a \s* b mmx;</code></pre> <p>In the RE above, which is intentionally obfuscated for illustration, the delimiter is <code>m</code>, the modifier is <code>mx</code>, and after delimiter-removal the RE is the same as for <span style="white-space: nowrap;"><code>m/ ^ a \s* b /mx</code></span>. There's more than one reason you're encouraged to restrict your delimiters to non-alphanumeric, non-whitespace choices.</p> </dd> </dl> <p>This step is the last one for all constructs except regular expressions, which are processed further.</p> </dd> <dt id="Parsing-regular-expressions">
Parsing regular expressions </dt> <dd> <p>Previous steps were performed during the compilation of Perl code, but this one happens at run time, although it may be optimized to be calculated at compile time if appropriate. After preprocessing described above, and possibly after evaluation if concatenation, joining, casing translation, or metaquoting are involved, the resulting <i>string</i> is passed to the RE engine for compilation.</p> <p>Whatever happens in the RE engine might be better discussed in <a href="perlre.html">perlre</a>, but for the sake of continuity, we shall do so here.</p> <p>This is another step where the presence of the <code>/x</code> modifier is relevant. The RE engine scans the string from left to right and converts it into a finite automaton.</p> <p>Backslashed characters are either replaced with corresponding literal strings (as with <code>\{</code>), or else they generate special nodes in the finite automaton (as with <code>\b</code>). Characters special to the RE engine (such as <code>|</code>) generate corresponding nodes or groups of nodes. <code>(?#...)</code> comments are ignored. All the rest is either converted to literal strings to match, or else is ignored (as is whitespace and <code>#</code>-style comments if <code>/x</code> is present).</p> <p>Parsing of the bracketed character class construct, <code>[...]</code>, is rather different than the rule used for the rest of the pattern. The terminator of this construct is found using the same rules as for finding the terminator of a <code>{}</code>-delimited construct, the only exception being that <code>]</code> immediately following <code>[</code> is treated as though preceded by a backslash.</p> <p>The terminator of runtime <code>(?{...})</code> is found by temporarily switching control to the perl parser, which should stop at the point where the logically balancing terminating <code>}</code> is found.</p> <p>It is possible to inspect both the string given to RE engine and the resulting finite automaton. See the arguments <code>debug</code>/<code>debugcolor</code> in the <span style="white-space: nowrap;"><code>use <a href="re.html">re</a></code></span> pragma, as well as Perl's <b>-Dr</b> command-line switch documented in <a href="perlrun.html#Command-Switches">"Command Switches" in perlrun</a>.</p> </dd> <dt id="Optimization-of-regular-expressions">
Optimization of regular expressions </dt> <dd> <p>This step is listed for completeness only. Since it does not change semantics, details of this step are not documented and are subject to change without notice. This step is performed over the finite automaton that was generated during the previous pass.</p> <p>It is at this stage that <code>split()</code> silently optimizes <code>/^/</code> to mean <code>/^/m</code>.</p> </dd> </dl> <h3 id="I/O-Operators">
I/O Operators </h3> <p>There are several I/O operators you should know about.</p> <p>A string enclosed by backticks (grave accents) first undergoes double-quote interpolation. It is then interpreted as an external command, and the output of that command is the value of the backtick string, like in a shell. In scalar context, a single string consisting of all output is returned. In list context, a list of values is returned, one per line of output. (You can set <code>$/</code> to use a different line terminator.) The command is executed each time the pseudo-literal is evaluated. The status value of the command is returned in <code>$?</code> (see <a href="perlvar.html">perlvar</a> for the interpretation of <code>$?</code>). Unlike in <b>csh</b>, no translation is done on the return data--newlines remain newlines. Unlike in any of the shells, single quotes do not hide variable names in the command from interpretation. To pass a literal dollar-sign through to the shell you need to hide it with a backslash. The generalized form of backticks is <code>qx//</code>, or you can call the <a href="perlfunc.html#readpipe">"readpipe" in perlfunc</a> function. (Because backticks always undergo shell expansion as well, see <a href="perlsec.html">perlsec</a> for security concerns.) </p> <p>In scalar context, evaluating a filehandle in angle brackets yields the next line from that file (the newline, if any, included), or <code>undef</code> at end-of-file or on error. When <code>$/</code> is set to <code>undef</code> (sometimes known as file-slurp mode) and the file is empty, it returns <code>''</code> the first time, followed by <code>undef</code> subsequently.</p> <p>Ordinarily you must assign the returned value to a variable, but there is one situation where an automatic assignment happens. If and only if the input symbol is the only thing inside the conditional of a <code>while</code> statement (even if disguised as a <code>for(;;)</code> loop), the value is automatically assigned to the global variable <code>$_</code>, destroying whatever was there previously. (This may seem like an odd thing to you, but you'll use the construct in almost every Perl script you write.) The <code>$_</code> variable is not implicitly localized. You'll have to put a <span style="white-space: nowrap;"><code>local $_;</code></span> before the loop if you want that to happen. Furthermore, if the input symbol or an explicit assignment of the input symbol to a scalar is used as a <code>while</code>/<code>for</code> condition, then the condition actually tests for definedness of the expression's value, not for its regular truth value.</p> <p>Thus the following lines are equivalent:</p> <pre data-language="perl"><code>while (defined($_ = &lt;STDIN&gt;)) { print; }
while ($_ = &lt;STDIN&gt;) { print; }
while (&lt;STDIN&gt;) { print; }
for (;&lt;STDIN&gt;;) { print; }
print while defined($_ = &lt;STDIN&gt;);
print while ($_ = &lt;STDIN&gt;);
print while &lt;STDIN&gt;;</code></pre> <p>This also behaves similarly, but assigns to a lexical variable instead of to <code>$_</code>:</p> <pre data-language="perl"><code>while (my $line = &lt;STDIN&gt;) { print $line }</code></pre> <p>In these loop constructs, the assigned value (whether assignment is automatic or explicit) is then tested to see whether it is defined. The defined test avoids problems where the line has a string value that would be treated as false by Perl; for example a "" or a <code>"0"</code> with no trailing newline. If you really mean for such values to terminate the loop, they should be tested for explicitly:</p> <pre data-language="perl"><code>while (($_ = &lt;STDIN&gt;) ne '0') { ... }
while (&lt;STDIN&gt;) { last unless $_; ... }</code></pre> <p>In other boolean contexts, <code>&lt;<i>FILEHANDLE</i>&gt;</code> without an explicit <code>defined</code> test or comparison elicits a warning if the <span style="white-space: nowrap;"><code>use warnings</code></span> pragma or the <b>-w</b> command-line switch (the <code>$^W</code> variable) is in effect.</p> <p>The filehandles STDIN, STDOUT, and STDERR are predefined. (The filehandles <code>stdin</code>, <code>stdout</code>, and <code>stderr</code> will also work except in packages, where they would be interpreted as local identifiers rather than global.) Additional filehandles may be created with the <code>open()</code> function, amongst others. See <a href="perlopentut.html">perlopentut</a> and <a href="perlfunc.html#open">"open" in perlfunc</a> for details on this. </p> <p>If a <code>&lt;<i>FILEHANDLE</i>&gt;</code> is used in a context that is looking for a list, a list comprising all input lines is returned, one line per list element. It's easy to grow to a rather large data space this way, so use with care.</p> <p><code>&lt;<i>FILEHANDLE</i>&gt;</code> may also be spelled <code>readline(*<i>FILEHANDLE</i>)</code>. See <a href="perlfunc.html#readline">"readline" in perlfunc</a>.</p> <p>The null filehandle <code>&lt;&gt;</code> (sometimes called the diamond operator) is special: it can be used to emulate the behavior of <b>sed</b> and <b>awk</b>, and any other Unix filter program that takes a list of filenames, doing the same to each line of input from all of them. Input from <code>&lt;&gt;</code> comes either from standard input, or from each file listed on the command line. Here's how it works: the first time <code>&lt;&gt;</code> is evaluated, the <code>@ARGV</code> array is checked, and if it is empty, <code>$ARGV[0]</code> is set to <code>"-"</code>, which when opened gives you standard input. The <code>@ARGV</code> array is then processed as a list of filenames. The loop</p> <pre data-language="perl"><code>    while (&lt;&gt;) {
	...			# code for each line
    }</code></pre> <p>is equivalent to the following Perl-like pseudo code:</p> <pre data-language="perl"><code>    unshift(@ARGV, '-') unless @ARGV;
    while ($ARGV = shift) {
	open(ARGV, $ARGV);
	while (&lt;ARGV&gt;) {
	    ...		# code for each line
	}
    }</code></pre> <p>except that it isn't so cumbersome to say, and will actually work. It really does shift the <code>@ARGV</code> array and put the current filename into the <code>$ARGV</code> variable. It also uses filehandle <i>ARGV</i> internally. <code>&lt;&gt;</code> is just a synonym for <code>&lt;ARGV&gt;</code>, which is magical. (The pseudo code above doesn't work because it treats <code>&lt;ARGV&gt;</code> as non-magical.)</p> <p>Since the null filehandle uses the two argument form of <a href="perlfunc.html#open">"open" in perlfunc</a> it interprets special characters, so if you have a script like this:</p> <pre data-language="perl"><code>while (&lt;&gt;) {
    print;
}</code></pre> <p>and call it with <span style="white-space: nowrap;"><code>perl dangerous.pl 'rm -rfv *|'</code></span>, it actually opens a pipe, executes the <code>rm</code> command and reads <code>rm</code>'s output from that pipe. If you want all items in <code>@ARGV</code> to be interpreted as file names, you can use the module <code>ARGV::readonly</code> from CPAN, or use the double diamond bracket:</p> <pre data-language="perl"><code>while (&lt;&lt;&gt;&gt;) {
    print;
}</code></pre> <p>Using double angle brackets inside of a while causes the open to use the three argument form (with the second argument being <code>&lt;</code>), so all arguments in <code>ARGV</code> are treated as literal filenames (including <code>"-"</code>). (Note that for convenience, if you use <code>&lt;&lt;&gt;&gt;</code> and if <code>@ARGV</code> is empty, it will still read from the standard input.)</p> <p>You can modify <code>@ARGV</code> before the first <code>&lt;&gt;</code> as long as the array ends up containing the list of filenames you really want. Line numbers (<code>$.</code>) continue as though the input were one big happy file. See the example in <a href="perlfunc.html#eof">"eof" in perlfunc</a> for how to reset line numbers on each file.</p> <p>If you want to set <code>@ARGV</code> to your own list of files, go right ahead. This sets <code>@ARGV</code> to all plain text files if no <code>@ARGV</code> was given:</p> <pre data-language="perl"><code>@ARGV = grep { -f &amp;&amp; -T } glob('*') unless @ARGV;</code></pre> <p>You can even set them to pipe commands. For example, this automatically filters compressed arguments through <b>gzip</b>:</p> <pre data-language="perl"><code>@ARGV = map { /\.(gz|Z)$/ ? "gzip -dc &lt; $_ |" : $_ } @ARGV;</code></pre> <p>If you want to pass switches into your script, you can use one of the <code>Getopts</code> modules or put a loop on the front like this:</p> <pre data-language="perl"><code>    while ($_ = $ARGV[0], /^-/) {
	shift;
        last if /^--$/;
	if (/^-D(.*)/) { $debug = $1 }
	if (/^-v/)     { $verbose++  }
	# ...		# other switches
    }

    while (&lt;&gt;) {
	# ...		# code for each line
    }</code></pre> <p>The <code>&lt;&gt;</code> symbol will return <code>undef</code> for end-of-file only once. If you call it again after this, it will assume you are processing another <code>@ARGV</code> list, and if you haven't set <code>@ARGV</code>, will read input from STDIN.</p> <p>If what the angle brackets contain is a simple scalar variable (for example, <code>$foo</code>), then that variable contains the name of the filehandle to input from, or its typeglob, or a reference to the same. For example:</p> <pre data-language="perl"><code>$fh = \*STDIN;
$line = &lt;$fh&gt;;</code></pre> <p>If what's within the angle brackets is neither a filehandle nor a simple scalar variable containing a filehandle name, typeglob, or typeglob reference, it is interpreted as a filename pattern to be globbed, and either a list of filenames or the next filename in the list is returned, depending on context. This distinction is determined on syntactic grounds alone. That means <code>&lt;$x&gt;</code> is always a <code>readline()</code> from an indirect handle, but <code>&lt;$hash{key}&gt;</code> is always a <code>glob()</code>. That's because <code>$x</code> is a simple scalar variable, but <code>$hash{key}</code> is not--it's a hash element. Even <code>&lt;$x &gt;</code> (note the extra space) is treated as <code>glob("$x ")</code>, not <code>readline($x)</code>.</p> <p>One level of double-quote interpretation is done first, but you can't say <code>&lt;$foo&gt;</code> because that's an indirect filehandle as explained in the previous paragraph. (In older versions of Perl, programmers would insert curly brackets to force interpretation as a filename glob: <code>&lt;${foo}&gt;</code>. These days, it's considered cleaner to call the internal function directly as <code>glob($foo)</code>, which is probably the right way to have done it in the first place.) For example:</p> <pre data-language="perl"><code>    while (&lt;*.c&gt;) {
	chmod 0644, $_;
    }</code></pre> <p>is roughly equivalent to:</p> <pre data-language="perl"><code>    open(FOO, "echo *.c | tr -s ' \t\r\f' '\\012\\012\\012\\012'|");
    while (&lt;FOO&gt;) {
	chomp;
	chmod 0644, $_;
    }</code></pre> <p>except that the globbing is actually done internally using the standard <code><a href="File::Glob.html">File::Glob</a></code> extension. Of course, the shortest way to do the above is:</p> <pre data-language="perl"><code>chmod 0644, &lt;*.c&gt;;</code></pre> <p>A (file)glob evaluates its (embedded) argument only when it is starting a new list. All values must be read before it will start over. In list context, this isn't important because you automatically get them all anyway. However, in scalar context the operator returns the next value each time it's called, or <code>undef</code> when the list has run out. As with filehandle reads, an automatic <code>defined</code> is generated when the glob occurs in the test part of a <code>while</code>, because legal glob returns (for example, a file called <i>0</i>) would otherwise terminate the loop. Again, <code>undef</code> is returned only once. So if you're expecting a single value from a glob, it is much better to say</p> <pre data-language="perl"><code>($file) = &lt;blurch*&gt;;</code></pre> <p>than</p> <pre data-language="perl"><code>$file = &lt;blurch*&gt;;</code></pre> <p>because the latter will alternate between returning a filename and returning false.</p> <p>If you're trying to do variable interpolation, it's definitely better to use the <code>glob()</code> function, because the older notation can cause people to become confused with the indirect filehandle notation.</p> <pre data-language="perl"><code>@files = glob("$dir/*.[ch]");
@files = glob($files[$i]);</code></pre> <p>If an angle-bracket-based globbing expression is used as the condition of a <code>while</code> or <code>for</code> loop, then it will be implicitly assigned to <code>$_</code>. If either a globbing expression or an explicit assignment of a globbing expression to a scalar is used as a <code>while</code>/<code>for</code> condition, then the condition actually tests for definedness of the expression's value, not for its regular truth value.</p> <h3 id="Constant-Folding">
Constant Folding </h3> <p>Like C, Perl does a certain amount of expression evaluation at compile time whenever it determines that all arguments to an operator are static and have no side effects. In particular, string concatenation happens at compile time between literals that don't do variable substitution. Backslash interpolation also happens at compile time. You can say</p> <pre data-language="perl"><code class="plaintext">  'Now is the time for all'
. "\n"
.  'good men to come to.'</code></pre> <p>and this all reduces to one string internally. Likewise, if you say</p> <pre data-language="perl"><code>    foreach $file (@filenames) {
	if (-s $file &gt; 5 + 100 * 2**16) {  }
    }</code></pre> <p>the compiler precomputes the number which that expression represents so that the interpreter won't have to.</p> <h3 id="No-ops">
No-ops </h3> <p>Perl doesn't officially have a no-op operator, but the bare constants <code>0</code> and <code>1</code> are special-cased not to produce a warning in void context, so you can for example safely do</p> <pre data-language="perl"><code>1 while foo();</code></pre> <h3 id="Bitwise-String-Operators">
Bitwise String Operators </h3> <p>Bitstrings of any size may be manipulated by the bitwise operators (<code>~ | &amp; ^</code>).</p> <p>If the operands to a binary bitwise op are strings of different sizes, <b>|</b> and <b>^</b> ops act as though the shorter operand had additional zero bits on the right, while the <b>&amp;</b> op acts as though the longer operand were truncated to the length of the shorter. The granularity for such extension or truncation is one or more bytes.</p> <pre data-language="perl"><code># ASCII-based examples
print "j p \n" ^ " a h";        	# prints "JAPH\n"
print "JA" | "  ph\n";          	# prints "japh\n"
print "japh\nJunk" &amp; '_____';   	# prints "JAPH\n";
print 'p N$' ^ " E&lt;H\n";		# prints "Perl\n";</code></pre> <p>If you are intending to manipulate bitstrings, be certain that you're supplying bitstrings: If an operand is a number, that will imply a <b>numeric</b> bitwise operation. You may explicitly show which type of operation you intend by using <code>""</code> or <code>0+</code>, as in the examples below.</p> <pre data-language="perl"><code>$foo =  150  |  105;	# yields 255  (0x96 | 0x69 is 0xFF)
$foo = '150' |  105;	# yields 255
$foo =  150  | '105';	# yields 255
$foo = '150' | '105';	# yields string '155' (under ASCII)

$baz = 0+$foo &amp; 0+$bar;	# both ops explicitly numeric
$biz = "$foo" ^ "$bar";	# both ops explicitly stringy</code></pre> <p>This somewhat unpredictable behavior can be avoided with the "bitwise" feature, new in Perl 5.22. You can enable it via <span style="white-space: nowrap;"><code>use feature 'bitwise'</code></span> or <code>use v5.28</code>. Before Perl 5.28, it used to emit a warning in the <code>"experimental::bitwise"</code> category. Under this feature, the four standard bitwise operators (<code>~ | &amp; ^</code>) are always numeric. Adding a dot after each operator (<code>~. |. &amp;. ^.</code>) forces it to treat its operands as strings:</p> <pre data-language="perl"><code>use feature "bitwise";
$foo =  150  |  105;	# yields 255  (0x96 | 0x69 is 0xFF)
$foo = '150' |  105;	# yields 255
$foo =  150  | '105';	# yields 255
$foo = '150' | '105';	# yields 255
$foo =  150  |. 105;	# yields string '155'
$foo = '150' |. 105;	# yields string '155'
$foo =  150  |.'105';	# yields string '155'
$foo = '150' |.'105';	# yields string '155'

$baz = $foo &amp;  $bar;	# both operands numeric
$biz = $foo ^. $bar;	# both operands stringy</code></pre> <p>The assignment variants of these operators (<code>&amp;= |= ^= &amp;.= |.= ^.=</code>) behave likewise under the feature.</p> <p>It is a fatal error if an operand contains a character whose ordinal value is above 0xFF, and hence not expressible except in UTF-8. The operation is performed on a non-UTF-8 copy for other operands encoded in UTF-8. See <a href="perlunicode.html#Byte-and-Character-Semantics">"Byte and Character Semantics" in perlunicode</a>.</p> <p>See <a href="perlfunc.html#vec">"vec" in perlfunc</a> for information on how to manipulate individual bits in a bit vector.</p> <h3 id="Integer-Arithmetic">
Integer Arithmetic </h3> <p>By default, Perl assumes that it must do most of its arithmetic in floating point. But by saying</p> <pre data-language="perl"><code>use integer;</code></pre> <p>you may tell the compiler to use integer operations (see <a href="integer.html">integer</a> for a detailed explanation) from here to the end of the enclosing BLOCK. An inner BLOCK may countermand this by saying</p> <pre data-language="perl"><code>no integer;</code></pre> <p>which lasts until the end of that BLOCK. Note that this doesn't mean everything is an integer, merely that Perl will use integer operations for arithmetic, comparison, and bitwise operators. For example, even under <span style="white-space: nowrap;"><code>use integer</code></span>, if you take the <code>sqrt(2)</code>, you'll still get <code>1.4142135623731</code> or so.</p> <p>Used on numbers, the bitwise operators (<code>&amp;</code> <code>|</code> <code>^</code> <code>~</code> <code>&lt;&lt;</code> <code>&gt;&gt;</code>) always produce integral results. (But see also <a href="#Bitwise-String-Operators">"Bitwise String Operators"</a>.) However, <span style="white-space: nowrap;"><code>use integer</code></span> still has meaning for them. By default, their results are interpreted as unsigned integers, but if <span style="white-space: nowrap;"><code>use integer</code></span> is in effect, their results are interpreted as signed integers. For example, <code>~0</code> usually evaluates to a large integral value. However, <span style="white-space: nowrap;"><code>use integer; ~0</code></span> is <code>-1</code> on two's-complement machines.</p> <h3 id="Floating-point-Arithmetic">
Floating-point Arithmetic</h3>  <p>While <span style="white-space: nowrap;"><code>use integer</code></span> provides integer-only arithmetic, there is no analogous mechanism to provide automatic rounding or truncation to a certain number of decimal places. For rounding to a certain number of digits, <code>sprintf()</code> or <code>printf()</code> is usually the easiest route. See <a href="perlfaq4.html">perlfaq4</a>.</p> <p>Floating-point numbers are only approximations to what a mathematician would call real numbers. There are infinitely more reals than floats, so some corners must be cut. For example:</p> <pre data-language="perl"><code>printf "%.20g\n", 123456789123456789;
#        produces 123456789123456784</code></pre> <p>Testing for exact floating-point equality or inequality is not a good idea. Here's a (relatively expensive) work-around to compare whether two floating-point numbers are equal to a particular number of decimal places. See Knuth, volume II, for a more robust treatment of this topic.</p> <pre data-language="perl"><code>    sub fp_equal {
	my ($X, $Y, $POINTS) = @_;
	my ($tX, $tY);
	$tX = sprintf("%.${POINTS}g", $X);
	$tY = sprintf("%.${POINTS}g", $Y);
	return $tX eq $tY;
    }</code></pre> <p>The POSIX module (part of the standard perl distribution) implements <code>ceil()</code>, <code>floor()</code>, and other mathematical and trigonometric functions. The <code><a href="Math::Complex.html">Math::Complex</a></code> module (part of the standard perl distribution) defines mathematical functions that work on both the reals and the imaginary numbers. <code>Math::Complex</code> is not as efficient as POSIX, but POSIX can't work with complex numbers.</p> <p>Rounding in financial applications can have serious implications, and the rounding method used should be specified precisely. In these cases, it probably pays not to trust whichever system rounding is being used by Perl, but to instead implement the rounding function you need yourself.</p> <h3 id="Bigger-Numbers">
Bigger Numbers </h3> <p>The standard <code><a href="Math::BigInt.html">Math::BigInt</a></code>, <code><a href="Math::BigRat.html">Math::BigRat</a></code>, and <code><a href="Math::BigFloat.html">Math::BigFloat</a></code> modules, along with the <code>bignum</code>, <code>bigint</code>, and <code>bigrat</code> pragmas, provide variable-precision arithmetic and overloaded operators, although they're currently pretty slow. At the cost of some space and considerable speed, they avoid the normal pitfalls associated with limited-precision representations.</p> <pre data-language="perl"><code>	use 5.010;
	use bigint;  # easy interface to Math::BigInt
	$x = 123456789123456789;
	say $x * $x;
    +15241578780673678515622620750190521</code></pre> <p>Or with rationals:</p> <pre data-language="perl"><code>use 5.010;
use bigrat;
$x = 3/22;
$y = 4/6;
say "x/y is ", $x/$y;
say "x*y is ", $x*$y;
x/y is 9/44
x*y is 1/11</code></pre> <p>Several modules let you calculate with unlimited or fixed precision (bound only by memory and CPU time). There are also some non-standard modules that provide faster implementations via external C libraries.</p> <p>Here is a short, but incomplete summary:</p> <pre data-language="perl"><code class="plaintext">Math::String           treat string sequences like numbers
Math::FixedPrecision   calculate with a fixed precision
Math::Currency         for currency calculations
Bit::Vector            manipulate bit vectors fast (uses C)
Math::BigIntFast       Bit::Vector wrapper for big numbers
Math::Pari             provides access to the Pari C library
Math::Cephes           uses the external Cephes C library (no
                       big numbers)
Math::Cephes::Fraction fractions via the Cephes library
Math::GMP              another one using an external C library
Math::GMPz             an alternative interface to libgmp's big ints
Math::GMPq             an interface to libgmp's fraction numbers
Math::GMPf             an interface to libgmp's floating point numbers</code></pre> <p>Choose wisely.</p><div class="_attribution">
  <p class="_attribution-p">
    &copy; 1993&ndash;2023 Larry Wall and others<br>Licensed under the GNU General Public License version 1 or later, or the Artistic License.<br>The Perl logo is a trademark of the Perl Foundation.<br>
    <a href="https://perldoc.perl.org/5.38.0/perlop" class="_attribution-link">https://perldoc.perl.org/5.38.0/perlop</a>
  </p>
</div>
