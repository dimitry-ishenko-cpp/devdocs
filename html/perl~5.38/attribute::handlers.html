<h1>Attribute::Handlers</h1>   <h2><a id="toc">CONTENTS</a></h2> <ul> <li> <a class="text-decoration-none" href="#NAME">NAME</a> </li> <li> <a class="text-decoration-none" href="#VERSION">VERSION</a> </li> <li> <a class="text-decoration-none" href="#SYNOPSIS">SYNOPSIS</a> </li> <li> <a class="text-decoration-none" href="#DESCRIPTION">DESCRIPTION</a> <ul> <li> <a class="text-decoration-none" href="#Typed-lexicals">Typed lexicals</a> </li> <li> <a class="text-decoration-none" href="#Type-specific-attribute-handlers">Type-specific attribute handlers</a> </li> <li> <a class="text-decoration-none" href="#Non-interpretive-attribute-handlers">Non-interpretive attribute handlers</a> </li> <li> <a class="text-decoration-none" href="#Phase-specific-attribute-handlers">Phase-specific attribute handlers</a> </li> <li> <a class="text-decoration-none" href="#Attributes-as-tie-interfaces">Attributes as tie interfaces</a> <ul> <li> <a class="text-decoration-none" href="#Passing-the-tied-object-to-tie">Passing the tied object to tie</a> </li> </ul> </li> </ul> </li> <li> <a class="text-decoration-none" href="#EXAMPLES">EXAMPLES</a> </li> <li> <a class="text-decoration-none" href="#UTILITY-FUNCTIONS">UTILITY FUNCTIONS</a> </li> <li> <a class="text-decoration-none" href="#DIAGNOSTICS">DIAGNOSTICS</a> </li> <li> <a class="text-decoration-none" href="#AUTHOR">AUTHOR</a> </li> <li> <a class="text-decoration-none" href="#BUGS">BUGS</a> </li> <li> <a class="text-decoration-none" href="#COPYRIGHT-AND-LICENSE">COPYRIGHT AND LICENSE</a> </li> </ul> <h2 id="NAME">NAME</h2> <p>Attribute::Handlers - Simpler definition of attribute handlers</p> <h2 id="VERSION">VERSION</h2> <p>This document describes version 1.03 of Attribute::Handlers.</p> <h2 id="SYNOPSIS">SYNOPSIS</h2> <pre data-language="perl"><code>    package MyClass;
    require 5.006;
    use Attribute::Handlers;
    no warnings 'redefine';


    sub Good : ATTR(SCALAR) {
	my ($package, $symbol, $referent, $attr, $data) = @_;

	# Invoked for any scalar variable with a :Good attribute,
	# provided the variable was declared in MyClass (or
	# a derived class) or typed to MyClass.

	# Do whatever to $referent here (executed in CHECK phase).
	...
    }

    sub Bad : ATTR(SCALAR) {
	# Invoked for any scalar variable with a :Bad attribute,
	# provided the variable was declared in MyClass (or
	# a derived class) or typed to MyClass.
	...
    }

    sub Good : ATTR(ARRAY) {
	# Invoked for any array variable with a :Good attribute,
	# provided the variable was declared in MyClass (or
	# a derived class) or typed to MyClass.
	...
    }

    sub Good : ATTR(HASH) {
	# Invoked for any hash variable with a :Good attribute,
	# provided the variable was declared in MyClass (or
	# a derived class) or typed to MyClass.
	...
    }

    sub Ugly : ATTR(CODE) {
	# Invoked for any subroutine declared in MyClass (or a 
	# derived class) with an :Ugly attribute.
	...
    }

    sub Omni : ATTR {
	# Invoked for any scalar, array, hash, or subroutine
	# with an :Omni attribute, provided the variable or
	# subroutine was declared in MyClass (or a derived class)
	# or the variable was typed to MyClass.
	# Use ref($_[2]) to determine what kind of referent it was.
	...
    }


    use Attribute::Handlers autotie =&gt; { Cycle =&gt; Tie::Cycle };

    my $next : Cycle(['A'..'Z']);</code></pre> <h2 id="DESCRIPTION">DESCRIPTION</h2> <p>This module, when inherited by a package, allows that package's class to define attribute handler subroutines for specific attributes. Variables and subroutines subsequently defined in that package, or in packages derived from that package may be given attributes with the same names as the attribute handler subroutines, which will then be called in one of the compilation phases (i.e. in a <code>BEGIN</code>, <code>CHECK</code>, <code>INIT</code>, or <code>END</code> block). (<code>UNITCHECK</code> blocks don't correspond to a global compilation phase, so they can't be specified here.)</p> <p>To create a handler, define it as a subroutine with the same name as the desired attribute, and declare the subroutine itself with the attribute <code>:ATTR</code>. For example:</p> <pre data-language="perl"><code>    package LoudDecl;
    use Attribute::Handlers;

    sub Loud :ATTR {
	my ($package, $symbol, $referent, $attr, $data, $phase,
	    $filename, $linenum) = @_;
	print STDERR
	    ref($referent), " ",
	    *{$symbol}{NAME}, " ",
	    "($referent) ", "was just declared ",
	    "and ascribed the ${attr} attribute ",
	    "with data ($data)\n",
	    "in phase $phase\n",
	    "in file $filename at line $linenum\n";
    }</code></pre> <p>This creates a handler for the attribute <code>:Loud</code> in the class LoudDecl. Thereafter, any subroutine declared with a <code>:Loud</code> attribute in the class LoudDecl:</p> <pre data-language="perl"><code>package LoudDecl;

sub foo: Loud {...}</code></pre> <p>causes the above handler to be invoked, and passed:</p> <dl> <dt id="[0]">
[0]</dt> <dd> <p>the name of the package into which it was declared;</p> </dd> <dt id="[1]">
[1]</dt> <dd> <p>a reference to the symbol table entry (typeglob) containing the subroutine;</p> </dd> <dt id="[2]">
[2]</dt> <dd> <p>a reference to the subroutine;</p> </dd> <dt id="[3]">
[3]</dt> <dd> <p>the name of the attribute;</p> </dd> <dt id="[4]">
[4]</dt> <dd> <p>any data associated with that attribute;</p> </dd> <dt id="[5]">
[5]</dt> <dd> <p>the name of the phase in which the handler is being invoked;</p> </dd> <dt id="[6]">
[6]</dt> <dd> <p>the filename in which the handler is being invoked;</p> </dd> <dt id="[7]">
[7]</dt> <dd> <p>the line number in this file.</p> </dd> </dl> <p>Likewise, declaring any variables with the <code>:Loud</code> attribute within the package:</p> <pre data-language="perl"><code>package LoudDecl;

my $foo :Loud;
my @foo :Loud;
my %foo :Loud;</code></pre> <p>will cause the handler to be called with a similar argument list (except, of course, that <code>$_[2]</code> will be a reference to the variable).</p> <p>The package name argument will typically be the name of the class into which the subroutine was declared, but it may also be the name of a derived class (since handlers are inherited).</p> <p>If a lexical variable is given an attribute, there is no symbol table to which it belongs, so the symbol table argument (<code>$_[1]</code>) is set to the string <code>'LEXICAL'</code> in that case. Likewise, ascribing an attribute to an anonymous subroutine results in a symbol table argument of <code>'ANON'</code>.</p> <p>The data argument passes in the value (if any) associated with the attribute. For example, if <code>&amp;foo</code> had been declared:</p> <pre data-language="perl"><code class="plaintext">sub foo :Loud("turn it up to 11, man!") {...}</code></pre> <p>then a reference to an array containing the string <code>"turn it up to 11, man!"</code> would be passed as the last argument.</p> <p>Attribute::Handlers makes strenuous efforts to convert the data argument (<code>$_[4]</code>) to a usable form before passing it to the handler (but see <a href="#Non-interpretive-attribute-handlers">"Non-interpretive attribute handlers"</a>). If those efforts succeed, the interpreted data is passed in an array reference; if they fail, the raw data is passed as a string. For example, all of these:</p> <pre data-language="perl"><code class="plaintext">sub foo :Loud(till=&gt;ears=&gt;are=&gt;bleeding) {...}
sub foo :Loud(qw/till ears are bleeding/) {...}
sub foo :Loud(qw/till, ears, are, bleeding/) {...}
sub foo :Loud(till,ears,are,bleeding) {...}</code></pre> <p>causes it to pass <code>['till','ears','are','bleeding']</code> as the handler's data argument. While:</p> <pre data-language="perl"><code class="plaintext">sub foo :Loud(['till','ears','are','bleeding']) {...}</code></pre> <p>causes it to pass <code>[ ['till','ears','are','bleeding'] ]</code>; the array reference specified in the data being passed inside the standard array reference indicating successful interpretation.</p> <p>However, if the data can't be parsed as valid Perl, then it is passed as an uninterpreted string. For example:</p> <pre data-language="perl"><code class="plaintext">sub foo :Loud(my,ears,are,bleeding) {...}
sub foo :Loud(qw/my ears are bleeding) {...}</code></pre> <p>cause the strings <code>'my,ears,are,bleeding'</code> and <code>'qw/my ears are bleeding'</code> respectively to be passed as the data argument.</p> <p>If no value is associated with the attribute, <code>undef</code> is passed.</p> <h3 id="Typed-lexicals">
Typed lexicals</h3> <p>Regardless of the package in which it is declared, if a lexical variable is ascribed an attribute, the handler that is invoked is the one belonging to the package to which it is typed. For example, the following declarations:</p> <pre data-language="perl"><code>package OtherClass;

my LoudDecl $loudobj : Loud;
my LoudDecl @loudobjs : Loud;
my LoudDecl %loudobjex : Loud;</code></pre> <p>causes the LoudDecl::Loud handler to be invoked (even if OtherClass also defines a handler for <code>:Loud</code> attributes).</p> <h3 id="Type-specific-attribute-handlers">
Type-specific attribute handlers</h3> <p>If an attribute handler is declared and the <code>:ATTR</code> specifier is given the name of a built-in type (<code>SCALAR</code>, <code>ARRAY</code>, <code>HASH</code>, or <code>CODE</code>), the handler is only applied to declarations of that type. For example, the following definition:</p> <pre data-language="perl"><code>package LoudDecl;

sub RealLoud :ATTR(SCALAR) { print "Yeeeeow!" }</code></pre> <p>creates an attribute handler that applies only to scalars:</p> <pre data-language="perl"><code>package Painful;
use base LoudDecl;

my $metal : RealLoud;           # invokes &amp;LoudDecl::RealLoud
my @metal : RealLoud;           # error: unknown attribute
my %metal : RealLoud;           # error: unknown attribute
sub metal : RealLoud {...}      # error: unknown attribute</code></pre> <p>You can, of course, declare separate handlers for these types as well (but you'll need to specify <code>no warnings 'redefine'</code> to do it quietly):</p> <pre data-language="perl"><code>package LoudDecl;
use Attribute::Handlers;
no warnings 'redefine';

sub RealLoud :ATTR(SCALAR) { print "Yeeeeow!" }
sub RealLoud :ATTR(ARRAY) { print "Urrrrrrrrrr!" }
sub RealLoud :ATTR(HASH) { print "Arrrrrgggghhhhhh!" }
sub RealLoud :ATTR(CODE) { croak "Real loud sub torpedoed" }</code></pre> <p>You can also explicitly indicate that a single handler is meant to be used for all types of referents like so:</p> <pre data-language="perl"><code>package LoudDecl;
use Attribute::Handlers;

sub SeriousLoud :ATTR(ANY) { warn "Hearing loss imminent" }</code></pre> <p>(I.e. <code>ATTR(ANY)</code> is a synonym for <code>:ATTR</code>).</p> <h3 id="Non-interpretive-attribute-handlers">
Non-interpretive attribute handlers</h3> <p>Occasionally the strenuous efforts Attribute::Handlers makes to convert the data argument (<code>$_[4]</code>) to a usable form before passing it to the handler get in the way.</p> <p>You can turn off that eagerness-to-help by declaring an attribute handler with the keyword <code>RAWDATA</code>. For example:</p> <pre data-language="perl"><code class="plaintext">sub Raw          : ATTR(RAWDATA) {...}
sub Nekkid       : ATTR(SCALAR,RAWDATA) {...}
sub Au::Naturale : ATTR(RAWDATA,ANY) {...}</code></pre> <p>Then the handler makes absolutely no attempt to interpret the data it receives and simply passes it as a string:</p> <pre data-language="perl"><code>my $power : Raw(1..100);        # handlers receives "1..100"</code></pre> <h3 id="Phase-specific-attribute-handlers">
Phase-specific attribute handlers</h3> <p>By default, attribute handlers are called at the end of the compilation phase (in a <code>CHECK</code> block). This seems to be optimal in most cases because most things that can be defined are defined by that point but nothing has been executed.</p> <p>However, it is possible to set up attribute handlers that are called at other points in the program's compilation or execution, by explicitly stating the phase (or phases) in which you wish the attribute handler to be called. For example:</p> <pre data-language="perl"><code class="plaintext">sub Early    :ATTR(SCALAR,BEGIN) {...}
sub Normal   :ATTR(SCALAR,CHECK) {...}
sub Late     :ATTR(SCALAR,INIT) {...}
sub Final    :ATTR(SCALAR,END) {...}
sub Bookends :ATTR(SCALAR,BEGIN,END) {...}</code></pre> <p>As the last example indicates, a handler may be set up to be (re)called in two or more phases. The phase name is passed as the handler's final argument.</p> <p>Note that attribute handlers that are scheduled for the <code>BEGIN</code> phase are handled as soon as the attribute is detected (i.e. before any subsequently defined <code>BEGIN</code> blocks are executed).</p> <h3 id="Attributes-as-tie-interfaces">
Attributes as <code>tie</code> interfaces</h3> <p>Attributes make an excellent and intuitive interface through which to tie variables. For example:</p> <pre data-language="perl"><code>    use Attribute::Handlers;
    use Tie::Cycle;

    sub UNIVERSAL::Cycle : ATTR(SCALAR) {
	my ($package, $symbol, $referent, $attr, $data, $phase) = @_;
	$data = [ $data ] unless ref $data eq 'ARRAY';
	tie $$referent, 'Tie::Cycle', $data;
    }

    # and thereafter...

    package main;

    my $next : Cycle('A'..'Z');     # $next is now a tied variable

    while (&lt;&gt;) {
	print $next;
    }</code></pre> <p>Note that, because the <code>Cycle</code> attribute receives its arguments in the <code>$data</code> variable, if the attribute is given a list of arguments, <code>$data</code> will consist of a single array reference; otherwise, it will consist of the single argument directly. Since Tie::Cycle requires its cycling values to be passed as an array reference, this means that we need to wrap non-array-reference arguments in an array constructor:</p> <pre data-language="perl"><code>$data = [ $data ] unless ref $data eq 'ARRAY';</code></pre> <p>Typically, however, things are the other way around: the tieable class expects its arguments as a flattened list, so the attribute looks like:</p> <pre data-language="perl"><code>    sub UNIVERSAL::Cycle : ATTR(SCALAR) {
	my ($package, $symbol, $referent, $attr, $data, $phase) = @_;
	my @data = ref $data eq 'ARRAY' ? @$data : $data;
	tie $$referent, 'Tie::Whatever', @data;
    }</code></pre> <p>This software pattern is so widely applicable that Attribute::Handlers provides a way to automate it: specifying <code>'autotie'</code> in the <code>use Attribute::Handlers</code> statement. So, the cycling example, could also be written:</p> <pre data-language="perl"><code>    use Attribute::Handlers autotie =&gt; { Cycle =&gt; 'Tie::Cycle' };

    # and thereafter...

    package main;

    my $next : Cycle(['A'..'Z']);     # $next is now a tied variable

    while (&lt;&gt;) {
	print $next;
    }</code></pre> <p>Note that we now have to pass the cycling values as an array reference, since the <code>autotie</code> mechanism passes <code>tie</code> a list of arguments as a list (as in the Tie::Whatever example), <i>not</i> as an array reference (as in the original Tie::Cycle example at the start of this section).</p> <p>The argument after <code>'autotie'</code> is a reference to a hash in which each key is the name of an attribute to be created, and each value is the class to which variables ascribed that attribute should be tied.</p> <p>Note that there is no longer any need to import the Tie::Cycle module -- Attribute::Handlers takes care of that automagically. You can even pass arguments to the module's <code>import</code> subroutine, by appending them to the class name. For example:</p> <pre data-language="perl"><code>    use Attribute::Handlers
	 autotie =&gt; { Dir =&gt; 'Tie::Dir qw(DIR_UNLINK)' };</code></pre> <p>If the attribute name is unqualified, the attribute is installed in the current package. Otherwise it is installed in the qualifier's package:</p> <pre data-language="perl"><code>package Here;

use Attribute::Handlers autotie =&gt; {
     Other::Good =&gt; Tie::SecureHash, # tie attr installed in Other::
             Bad =&gt; Tie::Taxes,      # tie attr installed in Here::
 UNIVERSAL::Ugly =&gt; Software::Patent # tie attr installed everywhere
};</code></pre> <p>Autoties are most commonly used in the module to which they actually tie, and need to export their attributes to any module that calls them. To facilitate this, Attribute::Handlers recognizes a special "pseudo-class" -- <code>__CALLER__</code>, which may be specified as the qualifier of an attribute:</p> <pre data-language="perl"><code>    package Tie::Me::Kangaroo::Down::Sport;

    use Attribute::Handlers autotie =&gt;
	 { '__CALLER__::Roo' =&gt; __PACKAGE__ };</code></pre> <p>This causes Attribute::Handlers to define the <code>Roo</code> attribute in the package that imports the Tie::Me::Kangaroo::Down::Sport module.</p> <p>Note that it is important to quote the __CALLER__::Roo identifier because a bug in perl 5.8 will refuse to parse it and cause an unknown error.</p> <h4 id="Passing-the-tied-object-to-tie">
Passing the tied object to <code>tie</code>
</h4> <p>Occasionally it is important to pass a reference to the object being tied to the TIESCALAR, TIEHASH, etc. that ties it.</p> <p>The <code>autotie</code> mechanism supports this too. The following code:</p> <pre data-language="perl"><code>use Attribute::Handlers autotieref =&gt; { Selfish =&gt; Tie::Selfish };
my $var : Selfish(@args);</code></pre> <p>has the same effect as:</p> <pre data-language="perl"><code>tie my $var, 'Tie::Selfish', @args;</code></pre> <p>But when <code>"autotieref"</code> is used instead of <code>"autotie"</code>:</p> <pre data-language="perl"><code>use Attribute::Handlers autotieref =&gt; { Selfish =&gt; Tie::Selfish };
my $var : Selfish(@args);</code></pre> <p>the effect is to pass the <code>tie</code> call an extra reference to the variable being tied:</p> <pre data-language="perl"><code>tie my $var, 'Tie::Selfish', \$var, @args;</code></pre> <h2 id="EXAMPLES">EXAMPLES</h2> <p>If the class shown in <a href="#SYNOPSIS">"SYNOPSIS"</a> were placed in the MyClass.pm module, then the following code:</p> <pre data-language="perl"><code>package main;
use MyClass;

my MyClass $slr :Good :Bad(1**1-1) :Omni(-vorous);

package SomeOtherClass;
use base MyClass;

sub tent { 'acle' }

sub fn :Ugly(sister) :Omni('po',tent()) {...}
my @arr :Good :Omni(s/cie/nt/);
my %hsh :Good(q/bye/) :Omni(q/bus/);</code></pre> <p>would cause the following handlers to be invoked:</p> <pre data-language="perl"><code># my MyClass $slr :Good :Bad(1**1-1) :Omni(-vorous);

MyClass::Good:ATTR(SCALAR)( 'MyClass',          # class
                            'LEXICAL',          # no typeglob
                            \$slr,              # referent
                            'Good',             # attr name
                            undef               # no attr data
                            'CHECK',            # compiler phase
                          );

MyClass::Bad:ATTR(SCALAR)( 'MyClass',           # class
                           'LEXICAL',           # no typeglob
                           \$slr,               # referent
                           'Bad',               # attr name
                           0                    # eval'd attr data
                           'CHECK',             # compiler phase
                         );

MyClass::Omni:ATTR(SCALAR)( 'MyClass',          # class
                            'LEXICAL',          # no typeglob
                            \$slr,              # referent
                            'Omni',             # attr name
                            '-vorous'           # eval'd attr data
                            'CHECK',            # compiler phase
                          );


# sub fn :Ugly(sister) :Omni('po',tent()) {...}

MyClass::UGLY:ATTR(CODE)( 'SomeOtherClass',     # class
                          \*SomeOtherClass::fn, # typeglob
                          \&amp;SomeOtherClass::fn, # referent
                          'Ugly',               # attr name
                          'sister'              # eval'd attr data
                          'CHECK',              # compiler phase
                        );

MyClass::Omni:ATTR(CODE)( 'SomeOtherClass',     # class
                          \*SomeOtherClass::fn, # typeglob
                          \&amp;SomeOtherClass::fn, # referent
                          'Omni',               # attr name
                          ['po','acle']         # eval'd attr data
                          'CHECK',              # compiler phase
                        );


# my @arr :Good :Omni(s/cie/nt/);

MyClass::Good:ATTR(ARRAY)( 'SomeOtherClass',    # class
                           'LEXICAL',           # no typeglob
                           \@arr,               # referent
                           'Good',              # attr name
                           undef                # no attr data
                           'CHECK',             # compiler phase
                         );

MyClass::Omni:ATTR(ARRAY)( 'SomeOtherClass',    # class
                           'LEXICAL',           # no typeglob
                           \@arr,               # referent
                           'Omni',              # attr name
                           ""                   # eval'd attr data 
                           'CHECK',             # compiler phase
                         );


# my %hsh :Good(q/bye) :Omni(q/bus/);

MyClass::Good:ATTR(HASH)( 'SomeOtherClass',     # class
                          'LEXICAL',            # no typeglob
                          \%hsh,                # referent
                          'Good',               # attr name
                          'q/bye'               # raw attr data
                          'CHECK',              # compiler phase
                        );

MyClass::Omni:ATTR(HASH)( 'SomeOtherClass',     # class
                          'LEXICAL',            # no typeglob
                          \%hsh,                # referent
                          'Omni',               # attr name
                          'bus'                 # eval'd attr data
                          'CHECK',              # compiler phase
                        );</code></pre> <p>Installing handlers into UNIVERSAL, makes them...err..universal. For example:</p> <pre data-language="perl"><code>package Descriptions;
use Attribute::Handlers;

my %name;
sub name { return $name{$_[2]}||*{$_[1]}{NAME} }

sub UNIVERSAL::Name :ATTR {
    $name{$_[2]} = $_[4];
}

sub UNIVERSAL::Purpose :ATTR {
    print STDERR "Purpose of ", &amp;name, " is $_[4]\n";
}

sub UNIVERSAL::Unit :ATTR {
    print STDERR &amp;name, " measured in $_[4]\n";
}</code></pre> <p>Let's you write:</p> <pre data-language="perl"><code>use Descriptions;

my $capacity : Name(capacity)
             : Purpose(to store max storage capacity for files)
             : Unit(Gb);


package Other;

sub foo : Purpose(to foo all data before barring it) { }

# etc.</code></pre> <h2 id="UTILITY-FUNCTIONS">
UTILITY FUNCTIONS</h2> <p>This module offers a single utility function, <code>findsym()</code>.</p> <dl> <dt id="findsym">findsym</dt> <dd> <pre data-language="perl"><code>my $symbol = Attribute::Handlers::findsym($package, $referent);</code></pre> <p>The function looks in the symbol table of <code>$package</code> for the typeglob for <code>$referent</code>, which is a reference to a variable or subroutine (SCALAR, ARRAY, HASH, or CODE). If it finds the typeglob, it returns it. Otherwise, it returns undef. Note that <code>findsym</code> memoizes the typeglobs it has previously successfully found, so subsequent calls with the same arguments should be much faster.</p> </dd> </dl> <h2 id="DIAGNOSTICS">DIAGNOSTICS</h2> <dl> <dt id="Bad-attribute-type:-ATTR(%s)">
<code>Bad attribute type: ATTR(%s)</code>
</dt> <dd> <p>An attribute handler was specified with an <code>:ATTR(<i>ref_type</i>)</code>, but the type of referent it was defined to handle wasn't one of the five permitted: <code>SCALAR</code>, <code>ARRAY</code>, <code>HASH</code>, <code>CODE</code>, or <code>ANY</code>.</p> </dd> <dt id="Attribute-handler-%s-doesn't-handle-%s-attributes">
<code>Attribute handler %s doesn't handle %s attributes</code>
</dt> <dd> <p>A handler for attributes of the specified name <i>was</i> defined, but not for the specified type of declaration. Typically encountered when trying to apply a <code>VAR</code> attribute handler to a subroutine, or a <code>SCALAR</code> attribute handler to some other type of variable.</p> </dd> <dt id="Declaration-of-%s-attribute-in-package-%s-may-clash-with-future-reserved-word">
<code>Declaration of %s attribute in package %s may clash with future reserved word</code>
</dt> <dd> <p>A handler for an attributes with an all-lowercase name was declared. An attribute with an all-lowercase name might have a meaning to Perl itself some day, even though most don't yet. Use a mixed-case attribute name, instead.</p> </dd> <dt id="Can't-have-two-ATTR-specifiers-on-one-subroutine">
<code>Can't have two ATTR specifiers on one subroutine</code>
</dt> <dd> <p>You just can't, okay? Instead, put all the specifications together with commas between them in a single <code>ATTR(<i>specification</i>)</code>.</p> </dd> <dt id="Can't-autotie-a-%s">
<code>Can't autotie a %s</code>
</dt> <dd> <p>You can only declare autoties for types <code>"SCALAR"</code>, <code>"ARRAY"</code>, and <code>"HASH"</code>. They're the only things (apart from typeglobs -- which are not declarable) that Perl can tie.</p> </dd> <dt id="Internal-error:-%s-symbol-went-missing">
<code>Internal error: %s symbol went missing</code>
</dt> <dd> <p>Something is rotten in the state of the program. An attributed subroutine ceased to exist between the point it was declared and the point at which its attribute handler(s) would have been called.</p> </dd> <dt id="Won't-be-able-to-apply-END-handler">
<code>Won't be able to apply END handler</code>
</dt> <dd> <p>You have defined an END handler for an attribute that is being applied to a lexical variable. Since the variable may not be available during END this won't happen.</p> </dd> </dl> <h2 id="AUTHOR">AUTHOR</h2> <p>Damian Conway (damian@conway.org). The maintainer of this module is now Rafael Garcia-Suarez (rgarciasuarez@gmail.com).</p> <p>Maintainer of the CPAN release is Steffen Mueller (smueller@cpan.org). Contact him with technical difficulties with respect to the packaging of the CPAN module.</p> <h2 id="BUGS">BUGS</h2> <p>There are undoubtedly serious bugs lurking somewhere in code this funky :-) Bug reports and other feedback are most welcome.</p> <h2 id="COPYRIGHT-AND-LICENSE">
COPYRIGHT AND LICENSE</h2> <pre data-language="perl"><code class="plaintext">  Copyright (c) 2001-2014, Damian Conway. All Rights Reserved.
This module is free software. It may be used, redistributed
    and/or modified under the same terms as Perl itself.</code></pre><div class="_attribution">
  <p class="_attribution-p">
    &copy; 1993&ndash;2023 Larry Wall and others<br>Licensed under the GNU General Public License version 1 or later, or the Artistic License.<br>The Perl logo is a trademark of the Perl Foundation.<br>
    <a href="https://perldoc.perl.org/5.38.0/Attribute::Handlers" class="_attribution-link">https://perldoc.perl.org/5.38.0/Attribute::Handlers</a>
  </p>
</div>
