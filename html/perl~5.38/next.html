<h1>NEXT</h1>    <h2><a id="toc">CONTENTS</a></h2> <ul> <li> <a class="text-decoration-none" href="#NAME">NAME</a> </li> <li> <a class="text-decoration-none" href="#SYNOPSIS">SYNOPSIS</a> </li> <li> <a class="text-decoration-none" href="#DESCRIPTION">DESCRIPTION</a> <ul> <li> <a class="text-decoration-none" href="#Enforcing-redispatch">Enforcing redispatch</a> </li> <li> <a class="text-decoration-none" href="#Avoiding-repetitions">Avoiding repetitions</a> </li> <li> <a class="text-decoration-none" href="#Invoking-all-versions-of-a-method-with-a-single-call">Invoking all versions of a method with a single call</a> </li> <li> <a class="text-decoration-none" href="#Using-EVERY-methods">Using EVERY methods</a> </li> </ul> </li> <li> <a class="text-decoration-none" href="#SEE-ALSO">SEE ALSO</a> </li> <li> <a class="text-decoration-none" href="#AUTHOR">AUTHOR</a> </li> <li> <a class="text-decoration-none" href="#BUGS-AND-IRRITATIONS">BUGS AND IRRITATIONS</a> </li> <li> <a class="text-decoration-none" href="#COPYRIGHT">COPYRIGHT</a> </li> </ul> <h2 id="NAME">NAME</h2> <p>NEXT - Provide a pseudo-class NEXT (et al) that allows method redispatch</p> <h2 id="SYNOPSIS">SYNOPSIS</h2> <pre data-language="perl"><code>use NEXT;

package P;
sub P::method   { print "$_[0]: P method\n";   $_[0]-&gt;NEXT::method() }
sub P::DESTROY  { print "$_[0]: P dtor\n";     $_[0]-&gt;NEXT::DESTROY() }

package Q;
use base qw( P );
sub Q::AUTOLOAD { print "$_[0]: Q AUTOLOAD\n"; $_[0]-&gt;NEXT::AUTOLOAD() }
sub Q::DESTROY  { print "$_[0]: Q dtor\n";     $_[0]-&gt;NEXT::DESTROY() }

package R;
sub R::method   { print "$_[0]: R method\n";   $_[0]-&gt;NEXT::method() }
sub R::AUTOLOAD { print "$_[0]: R AUTOLOAD\n"; $_[0]-&gt;NEXT::AUTOLOAD() }
sub R::DESTROY  { print "$_[0]: R dtor\n";     $_[0]-&gt;NEXT::DESTROY() }

package S;
use base qw( Q R );
sub S::method   { print "$_[0]: S method\n";   $_[0]-&gt;NEXT::method() }
sub S::AUTOLOAD { print "$_[0]: S AUTOLOAD\n"; $_[0]-&gt;NEXT::AUTOLOAD() }
sub S::DESTROY  { print "$_[0]: S dtor\n";     $_[0]-&gt;NEXT::DESTROY() }

package main;

my $obj = bless {}, "S";

$obj-&gt;method();		# Calls S::method, P::method, R::method
$obj-&gt;missing_method(); # Calls S::AUTOLOAD, Q::AUTOLOAD, R::AUTOLOAD

# Clean-up calls S::DESTROY, Q::DESTROY, P::DESTROY, R::DESTROY</code></pre> <h2 id="DESCRIPTION">DESCRIPTION</h2> <p>The <code>NEXT</code> module adds a pseudoclass named <code>NEXT</code> to any program that uses it. If a method <code>m</code> calls <code>$self-&gt;NEXT::m()</code>, the call to <code>m</code> is redispatched as if the calling method had not originally been found.</p> <p><b>Note:</b> before using this module, you should look at <a href="https://metacpan.org/pod/mro#next::method">next::method</a> in the core <a href="mro.html">mro</a> module. <code>mro</code> has been a core module since Perl 5.9.5.</p> <p>In other words, a call to <code>$self-&gt;NEXT::m()</code> resumes the depth-first, left-to-right search of <code>$self</code>'s class hierarchy that resulted in the original call to <code>m</code>.</p> <p>Note that this is not the same thing as <code>$self-&gt;SUPER::m()</code>, which begins a new dispatch that is restricted to searching the ancestors of the current class. <code>$self-&gt;NEXT::m()</code> can backtrack past the current class -- to look for a suitable method in other ancestors of <code>$self</code> -- whereas <code>$self-&gt;SUPER::m()</code> cannot.</p> <p>A typical use would be in the destructors of a class hierarchy, as illustrated in the SYNOPSIS above. Each class in the hierarchy has a DESTROY method that performs some class-specific action and then redispatches the call up the hierarchy. As a result, when an object of class S is destroyed, the destructors of <i>all</i> its parent classes are called (in depth-first, left-to-right order).</p> <p>Another typical use of redispatch would be in <code>AUTOLOAD</code>'ed methods. If such a method determined that it was not able to handle a particular call, it might choose to redispatch that call, in the hope that some other <code>AUTOLOAD</code> (above it, or to its left) might do better.</p> <p>By default, if a redispatch attempt fails to find another method elsewhere in the objects class hierarchy, it quietly gives up and does nothing (but see <a href="#Enforcing-redispatch">"Enforcing redispatch"</a>). This gracious acquiescence is also unlike the (generally annoying) behaviour of <code>SUPER</code>, which throws an exception if it cannot redispatch.</p> <p>Note that it is a fatal error for any method (including <code>AUTOLOAD</code>) to attempt to redispatch any method that does not have the same name. For example:</p> <pre data-language="perl"><code>sub S::oops { print "oops!\n"; $_[0]-&gt;NEXT::other_method() }</code></pre> <h3 id="Enforcing-redispatch">
Enforcing redispatch</h3> <p>It is possible to make <code>NEXT</code> redispatch more demandingly (i.e. like <code>SUPER</code> does), so that the redispatch throws an exception if it cannot find a "next" method to call.</p> <p>To do this, simple invoke the redispatch as:</p> <pre data-language="perl"><code>$self-&gt;NEXT::ACTUAL::method();</code></pre> <p>rather than:</p> <pre data-language="perl"><code>$self-&gt;NEXT::method();</code></pre> <p>The <code>ACTUAL</code> tells <code>NEXT</code> that there must actually be a next method to call, or it should throw an exception.</p> <p><code>NEXT::ACTUAL</code> is most commonly used in <code>AUTOLOAD</code> methods, as a means to decline an <code>AUTOLOAD</code> request, but preserve the normal exception-on-failure semantics:</p> <pre data-language="perl"><code>sub AUTOLOAD {
	if ($AUTOLOAD =~ /foo|bar/) {
		# handle here
	}
	else {  # try elsewhere
		shift()-&gt;NEXT::ACTUAL::AUTOLOAD(@_);
	}
}</code></pre> <p>By using <code>NEXT::ACTUAL</code>, if there is no other <code>AUTOLOAD</code> to handle the method call, an exception will be thrown (as usually happens in the absence of a suitable <code>AUTOLOAD</code>).</p> <h3 id="Avoiding-repetitions">
Avoiding repetitions</h3> <p>If <code>NEXT</code> redispatching is used in the methods of a "diamond" class hierarchy:</p> <pre data-language="perl"><code>#     A   B
#    / \ /
#   C   D
#    \ /
#     E

use NEXT;

package A;                 
sub foo { print "called A::foo\n"; shift-&gt;NEXT::foo() }

package B;                 
sub foo { print "called B::foo\n"; shift-&gt;NEXT::foo() }

package C; @ISA = qw( A );
sub foo { print "called C::foo\n"; shift-&gt;NEXT::foo() }

package D; @ISA = qw(A B);
sub foo { print "called D::foo\n"; shift-&gt;NEXT::foo() }

package E; @ISA = qw(C D);
sub foo { print "called E::foo\n"; shift-&gt;NEXT::foo() }

E-&gt;foo();</code></pre> <p>then derived classes may (re-)inherit base-class methods through two or more distinct paths (e.g. in the way <code>E</code> inherits <code>A::foo</code> twice -- through <code>C</code> and <code>D</code>). In such cases, a sequence of <code>NEXT</code> redispatches will invoke the multiply inherited method as many times as it is inherited. For example, the above code prints:</p> <pre data-language="perl"><code class="plaintext">called E::foo
called C::foo
called A::foo
called D::foo
called A::foo
called B::foo</code></pre> <p>(i.e. <code>A::foo</code> is called twice).</p> <p>In some cases this <i>may</i> be the desired effect within a diamond hierarchy, but in others (e.g. for destructors) it may be more appropriate to call each method only once during a sequence of redispatches.</p> <p>To cover such cases, you can redispatch methods via:</p> <pre data-language="perl"><code>$self-&gt;NEXT::DISTINCT::method();</code></pre> <p>rather than:</p> <pre data-language="perl"><code>$self-&gt;NEXT::method();</code></pre> <p>This causes the redispatcher to only visit each distinct <code>method</code> method once. That is, to skip any classes in the hierarchy that it has already visited during redispatch. So, for example, if the previous example were rewritten:</p> <pre data-language="perl"><code>package A;                 
sub foo { print "called A::foo\n"; shift-&gt;NEXT::DISTINCT::foo() }

package B;                 
sub foo { print "called B::foo\n"; shift-&gt;NEXT::DISTINCT::foo() }

package C; @ISA = qw( A );
sub foo { print "called C::foo\n"; shift-&gt;NEXT::DISTINCT::foo() }

package D; @ISA = qw(A B);
sub foo { print "called D::foo\n"; shift-&gt;NEXT::DISTINCT::foo() }

package E; @ISA = qw(C D);
sub foo { print "called E::foo\n"; shift-&gt;NEXT::DISTINCT::foo() }

E-&gt;foo();</code></pre> <p>then it would print:</p> <pre data-language="perl"><code class="plaintext">called E::foo
called C::foo
called A::foo
called D::foo
called B::foo</code></pre> <p>and omit the second call to <code>A::foo</code> (since it would not be distinct from the first call to <code>A::foo</code>).</p> <p>Note that you can also use:</p> <pre data-language="perl"><code>$self-&gt;NEXT::DISTINCT::ACTUAL::method();</code></pre> <p>or:</p> <pre data-language="perl"><code>$self-&gt;NEXT::ACTUAL::DISTINCT::method();</code></pre> <p>to get both unique invocation <i>and</i> exception-on-failure.</p> <p>Note that, for historical compatibility, you can also use <code>NEXT::UNSEEN</code> instead of <code>NEXT::DISTINCT</code>.</p> <h3 id="Invoking-all-versions-of-a-method-with-a-single-call">
Invoking all versions of a method with a single call</h3> <p>Yet another pseudo-class that <code>NEXT</code> provides is <code>EVERY</code>. Its behaviour is considerably simpler than that of the <code>NEXT</code> family. A call to:</p> <pre data-language="perl"><code>$obj-&gt;EVERY::foo();</code></pre> <p>calls <i>every</i> method named <code>foo</code> that the object in <code>$obj</code> has inherited. That is:</p> <pre data-language="perl"><code>use NEXT;

package A; @ISA = qw(B D X);
sub foo { print "A::foo " }

package B; @ISA = qw(D X);
sub foo { print "B::foo " }

package X; @ISA = qw(D);
sub foo { print "X::foo " }

package D;
sub foo { print "D::foo " }

package main;

my $obj = bless {}, 'A';
$obj-&gt;EVERY::foo();        # prints" A::foo B::foo X::foo D::foo</code></pre> <p>Prefixing a method call with <code>EVERY::</code> causes every method in the object's hierarchy with that name to be invoked. As the above example illustrates, they are not called in Perl's usual "left-most-depth-first" order. Instead, they are called "breadth-first-dependency-wise".</p> <p>That means that the inheritance tree of the object is traversed breadth-first and the resulting order of classes is used as the sequence in which methods are called. However, that sequence is modified by imposing a rule that the appropriate method of a derived class must be called before the same method of any ancestral class. That's why, in the above example, <code>X::foo</code> is called before <code>D::foo</code>, even though <code>D</code> comes before <code>X</code> in <code>@B::ISA</code>.</p> <p>In general, there's no need to worry about the order of calls. They will be left-to-right, breadth-first, most-derived-first. This works perfectly for most inherited methods (including destructors), but is inappropriate for some kinds of methods (such as constructors, cloners, debuggers, and initializers) where it's more appropriate that the least-derived methods be called first (as more-derived methods may rely on the behaviour of their "ancestors"). In that case, instead of using the <code>EVERY</code> pseudo-class:</p> <pre data-language="perl"><code>$obj-&gt;EVERY::foo();        # prints" A::foo B::foo X::foo D::foo      </code></pre> <p>you can use the <code>EVERY::LAST</code> pseudo-class:</p> <pre data-language="perl"><code>$obj-&gt;EVERY::LAST::foo();  # prints" D::foo X::foo B::foo A::foo      </code></pre> <p>which reverses the order of method call.</p> <p>Whichever version is used, the actual methods are called in the same context (list, scalar, or void) as the original call via <code>EVERY</code>, and return:</p> <ul> <li>
<p>A hash of array references in list context. Each entry of the hash has the fully qualified method name as its key and a reference to an array containing the method's list-context return values as its value.</p> </li> <li>
<p>A reference to a hash of scalar values in scalar context. Each entry of the hash has the fully qualified method name as its key and the method's scalar-context return values as its value.</p> </li> <li>
<p>Nothing in void context (obviously).</p> </li> </ul> <h3 id="Using-EVERY-methods">
Using <code>EVERY</code> methods</h3> <p>The typical way to use an <code>EVERY</code> call is to wrap it in another base method, that all classes inherit. For example, to ensure that every destructor an object inherits is actually called (as opposed to just the left-most-depth-first-est one):</p> <pre data-language="perl"><code>package Base;
sub DESTROY { $_[0]-&gt;EVERY::Destroy }

package Derived1; 
use base 'Base';
sub Destroy {...}

package Derived2; 
use base 'Base', 'Derived1';
sub Destroy {...}</code></pre> <p>et cetera. Every derived class than needs its own clean-up behaviour simply adds its own <code>Destroy</code> method (<i>not</i> a <code>DESTROY</code> method), which the call to <code>EVERY::LAST::Destroy</code> in the inherited destructor then correctly picks up.</p> <p>Likewise, to create a class hierarchy in which every initializer inherited by a new object is invoked:</p> <pre data-language="perl"><code>        package Base;
        sub new {
		my ($class, %args) = @_;
		my $obj = bless {}, $class;
		$obj-&gt;EVERY::LAST::Init(\%args);
	}

        package Derived1; 
        use base 'Base';
        sub Init {
		my ($argsref) = @_;
		...
	}

        package Derived2; 
        use base 'Base', 'Derived1';
        sub Init {
		my ($argsref) = @_;
		...
	}</code></pre> <p>et cetera. Every derived class than needs some additional initialization behaviour simply adds its own <code>Init</code> method (<i>not</i> a <code>new</code> method), which the call to <code>EVERY::LAST::Init</code> in the inherited constructor then correctly picks up.</p> <h2 id="SEE-ALSO">
SEE ALSO</h2> <p><a href="mro.html">mro</a> (in particular <a href="https://metacpan.org/pod/mro#next::method">next::method</a>), which has been a core module since Perl 5.9.5.</p> <h2 id="AUTHOR">AUTHOR</h2> <p>Damian Conway (damian@conway.org)</p> <h2 id="BUGS-AND-IRRITATIONS">
BUGS AND IRRITATIONS</h2> <p>Because it's a module, not an integral part of the interpreter, <code>NEXT</code> has to guess where the surrounding call was found in the method look-up sequence. In the presence of diamond inheritance patterns it occasionally guesses wrong.</p> <p>It's also too slow (despite caching).</p> <p>Comment, suggestions, and patches welcome.</p> <h2 id="COPYRIGHT">COPYRIGHT</h2> <pre data-language="perl"><code class="plaintext">Copyright (c) 2000-2001, Damian Conway. All Rights Reserved.
This module is free software. It may be used, redistributed
   and/or modified under the same terms as Perl itself.</code></pre><div class="_attribution">
  <p class="_attribution-p">
    &copy; 1993&ndash;2023 Larry Wall and others<br>Licensed under the GNU General Public License version 1 or later, or the Artistic License.<br>The Perl logo is a trademark of the Perl Foundation.<br>
    <a href="https://perldoc.perl.org/5.38.0/NEXT" class="_attribution-link">https://perldoc.perl.org/5.38.0/NEXT</a>
  </p>
</div>
