<h1>perlfaq7</h1>   <h2><a id="toc">CONTENTS</a></h2> <ul> <li> <a class="text-decoration-none" href="#NAME">NAME</a> </li> <li> <a class="text-decoration-none" href="#VERSION">VERSION</a> </li> <li> <a class="text-decoration-none" href="#DESCRIPTION">DESCRIPTION</a> <ul> <li> <a class="text-decoration-none" href="#Can-I-get-a-BNF/yacc/RE-for-the-Perl-language?">Can I get a BNF/yacc/RE for the Perl language?</a> </li> <li> <a class="text-decoration-none" href="#What-are-all-these-$@%25&amp;*-punctuation-signs,-and-how-do-I-know-when-to-use-them?">What are all these $@%&amp;* punctuation signs, and how do I know when to use them?</a> </li> <li> <a class="text-decoration-none" href="#Do-I-always/never-have-to-quote-my-strings-or-use-semicolons-and-commas?">Do I always/never have to quote my strings or use semicolons and commas?</a> </li> <li> <a class="text-decoration-none" href="#How-do-I-skip-some-return-values?">How do I skip some return values?</a> </li> <li> <a class="text-decoration-none" href="#How-do-I-temporarily-block-warnings?">How do I temporarily block warnings?</a> </li> <li> <a class="text-decoration-none" href="#What's-an-extension?">What's an extension?</a> </li> <li> <a class="text-decoration-none" href="#Why-do-Perl-operators-have-different-precedence-than-C-operators?">Why do Perl operators have different precedence than C operators?</a> </li> <li> <a class="text-decoration-none" href="#How-do-I-declare/create-a-structure?">How do I declare/create a structure?</a> </li> <li> <a class="text-decoration-none" href="#How-do-I-create-a-module?">How do I create a module?</a> </li> <li> <a class="text-decoration-none" href="#How-do-I-adopt-or-take-over-a-module-already-on-CPAN?">How do I adopt or take over a module already on CPAN?</a> </li> <li> <a class="text-decoration-none" href="#How-do-I-create-a-class?">How do I create a class? </a> </li> <li> <a class="text-decoration-none" href="#How-can-I-tell-if-a-variable-is-tainted?">How can I tell if a variable is tainted?</a> </li> <li> <a class="text-decoration-none" href="#What's-a-closure?">What's a closure?</a> </li> <li> <a class="text-decoration-none" href="#What-is-variable-suicide-and-how-can-I-prevent-it?">What is variable suicide and how can I prevent it?</a> </li> <li> <a class="text-decoration-none" href="#How-can-I-pass/return-a-%7BFunction,-FileHandle,-Array,-Hash,-Method,-Regex%7D?">How can I pass/return a {Function, FileHandle, Array, Hash, Method, Regex}?</a> </li> <li> <a class="text-decoration-none" href="#How-do-I-create-a-static-variable?">How do I create a static variable?</a> </li> <li> <a class="text-decoration-none" href="#What's-the-difference-between-dynamic-and-lexical-(static)-scoping?-Between-local()-and-my()?">What's the difference between dynamic and lexical (static) scoping? Between local() and my()?</a> </li> <li> <a class="text-decoration-none" href="#How-can-I-access-a-dynamic-variable-while-a-similarly-named-lexical-is-in-scope?">How can I access a dynamic variable while a similarly named lexical is in scope?</a> </li> <li> <a class="text-decoration-none" href="#What's-the-difference-between-deep-and-shallow-binding?">What's the difference between deep and shallow binding?</a> </li> <li> <a class="text-decoration-none" href="#Why-doesn't-%22my($foo)-=-%3C$fh%3E;%22-work-right?">Why doesn't "my($foo) = &lt;$fh&gt;;" work right?</a> </li> <li> <a class="text-decoration-none" href="#How-do-I-redefine-a-builtin-function,-operator,-or-method?">How do I redefine a builtin function, operator, or method?</a> </li> <li> <a class="text-decoration-none" href="#What's-the-difference-between-calling-a-function-as-&amp;foo-and-foo()?">What's the difference between calling a function as &amp;foo and foo()?</a> </li> <li> <a class="text-decoration-none" href="#How-do-I-create-a-switch-or-case-statement?">How do I create a switch or case statement?</a> </li> <li> <a class="text-decoration-none" href="#How-can-I-catch-accesses-to-undefined-variables,-functions,-or-methods?">How can I catch accesses to undefined variables, functions, or methods?</a> </li> <li> <a class="text-decoration-none" href="#Why-can't-a-method-included-in-this-same-file-be-found?">Why can't a method included in this same file be found?</a> </li> <li> <a class="text-decoration-none" href="#How-can-I-find-out-my-current-or-calling-package?">How can I find out my current or calling package?</a> </li> <li> <a class="text-decoration-none" href="#How-can-I-comment-out-a-large-block-of-Perl-code?">How can I comment out a large block of Perl code?</a> </li> <li> <a class="text-decoration-none" href="#How-do-I-clear-a-package?">How do I clear a package?</a> </li> <li> <a class="text-decoration-none" href="#How-can-I-use-a-variable-as-a-variable-name?">How can I use a variable as a variable name?</a> </li> <li> <a class="text-decoration-none" href="#What-does-%22bad-interpreter%22-mean?">What does "bad interpreter" mean?</a> </li> <li> <a class="text-decoration-none" href="#Do-I-need-to-recompile-XS-modules-when-there-is-a-change-in-the-C-library?">Do I need to recompile XS modules when there is a change in the C library?</a> </li> </ul> </li> <li> <a class="text-decoration-none" href="#AUTHOR-AND-COPYRIGHT">AUTHOR AND COPYRIGHT</a> </li> </ul> <h2 id="NAME">NAME</h2> <p>perlfaq7 - General Perl Language Issues</p> <h2 id="VERSION">VERSION</h2> <p>version 5.20210520</p> <h2 id="DESCRIPTION">DESCRIPTION</h2> <p>This section deals with general Perl language issues that don't clearly fit into any of the other sections.</p> <h3 id="Can-I-get-a-BNF/yacc/RE-for-the-Perl-language?">
Can I get a BNF/yacc/RE for the Perl language?</h3> <p>There is no BNF, but you can paw your way through the yacc grammar in perly.y in the source distribution if you're particularly brave. The grammar relies on very smart tokenizing code, so be prepared to venture into toke.c as well.</p> <p>In the words of Chaim Frenkel: "Perl's grammar can not be reduced to BNF. The work of parsing perl is distributed between yacc, the lexer, smoke and mirrors."</p> <h3 id="What-are-all-these-$@%&amp;*-punctuation-signs,-and-how-do-I-know-when-to-use-them?">
What are all these $@%&amp;* punctuation signs, and how do I know when to use them?</h3> <p>They are type specifiers, as detailed in <a href="perldata.html">perldata</a>:</p> <pre data-language="perl"><code class="plaintext">$ for scalar values (number, string or reference)
@ for arrays
% for hashes (associative arrays)
&amp; for subroutines (aka functions, procedures, methods)
* for all types of that symbol name. In version 4 you used them like
  pointers, but in modern perls you can just use references.</code></pre> <p>There are a couple of other symbols that you're likely to encounter that aren't really type specifiers:</p> <pre data-language="perl"><code class="plaintext">&lt;&gt; are used for inputting a record from a filehandle.
\  takes a reference to something.</code></pre> <p>Note that &lt;FILE&gt; is <i>neither</i> the type specifier for files nor the name of the handle. It is the <code>&lt;&gt;</code> operator applied to the handle FILE. It reads one line (well, record--see <a href="perlvar.html#%24%2F">"$/" in perlvar</a>) from the handle FILE in scalar context, or <i>all</i> lines in list context. When performing open, close, or any other operation besides <code>&lt;&gt;</code> on files, or even when talking about the handle, do <i>not</i> use the brackets. These are correct: <code>eof(FH)</code>, <code>seek(FH, 0, 2)</code> and "copying from STDIN to FILE".</p> <h3 id="Do-I-always/never-have-to-quote-my-strings-or-use-semicolons-and-commas?">
Do I always/never have to quote my strings or use semicolons and commas?</h3> <p>Normally, a bareword doesn't need to be quoted, but in most cases probably should be (and must be under <code>use strict</code>). But a hash key consisting of a simple word and the left-hand operand to the <code>=&gt;</code> operator both count as though they were quoted:</p> <pre data-language="perl"><code>This                    is like this
------------            ---------------
$foo{line}              $foo{'line'}
bar =&gt; stuff            'bar' =&gt; stuff</code></pre> <p>The final semicolon in a block is optional, as is the final comma in a list. Good style (see <a href="perlstyle.html">perlstyle</a>) says to put them in except for one-liners:</p> <pre data-language="perl"><code>if ($whoops) { exit 1 }
my @nums = (1, 2, 3);

if ($whoops) {
    exit 1;
}

my @lines = (
    "There Beren came from mountains cold",
    "And lost he wandered under leaves",
);</code></pre> <h3 id="How-do-I-skip-some-return-values?">
How do I skip some return values?</h3> <p>One way is to treat the return values as a list and index into it:</p> <pre data-language="perl"><code>$dir = (getpwnam($user))[7];</code></pre> <p>Another way is to use undef as an element on the left-hand-side:</p> <pre data-language="perl"><code>($dev, $ino, undef, undef, $uid, $gid) = stat($file);</code></pre> <p>You can also use a list slice to select only the elements that you need:</p> <pre data-language="perl"><code>($dev, $ino, $uid, $gid) = ( stat($file) )[0,1,4,5];</code></pre> <h3 id="How-do-I-temporarily-block-warnings?">
How do I temporarily block warnings?</h3> <p>If you are running Perl 5.6.0 or better, the <code>use warnings</code> pragma allows fine control of what warnings are produced. See <a href="perllexwarn.html">perllexwarn</a> for more details.</p> <pre data-language="perl"><code>{
    no warnings;          # temporarily turn off warnings
    $x = $y + $z;         # I know these might be undef
}</code></pre> <p>Additionally, you can enable and disable categories of warnings. You turn off the categories you want to ignore and you can still get other categories of warnings. See <a href="perllexwarn.html">perllexwarn</a> for the complete details, including the category names and hierarchy.</p> <pre data-language="perl"><code>{
    no warnings 'uninitialized';
    $x = $y + $z;
}</code></pre> <p>If you have an older version of Perl, the <code>$^W</code> variable (documented in <a href="perlvar.html">perlvar</a>) controls runtime warnings for a block:</p> <pre data-language="perl"><code>{
    local $^W = 0;        # temporarily turn off warnings
    $x = $y + $z;         # I know these might be undef
}</code></pre> <p>Note that like all the punctuation variables, you cannot currently use my() on <code>$^W</code>, only local().</p> <h3 id="What's-an-extension?">
What's an extension?</h3> <p>An extension is a way of calling compiled C code from Perl. Reading <a href="perlxstut.html">perlxstut</a> is a good place to learn more about extensions.</p> <h3 id="Why-do-Perl-operators-have-different-precedence-than-C-operators?">
Why do Perl operators have different precedence than C operators?</h3> <p>Actually, they don't. All C operators that Perl copies have the same precedence in Perl as they do in C. The problem is with operators that C doesn't have, especially functions that give a list context to everything on their right, eg. print, chmod, exec, and so on. Such functions are called "list operators" and appear as such in the precedence table in <a href="perlop.html">perlop</a>.</p> <p>A common mistake is to write:</p> <pre data-language="perl"><code>unlink $file || die "snafu";</code></pre> <p>This gets interpreted as:</p> <pre data-language="perl"><code>unlink ($file || die "snafu");</code></pre> <p>To avoid this problem, either put in extra parentheses or use the super low precedence <code>or</code> operator:</p> <pre data-language="perl"><code>(unlink $file) || die "snafu";
unlink $file or die "snafu";</code></pre> <p>The "English" operators (<code>and</code>, <code>or</code>, <code>xor</code>, and <code>not</code>) deliberately have precedence lower than that of list operators for just such situations as the one above.</p> <p>Another operator with surprising precedence is exponentiation. It binds more tightly even than unary minus, making <code>-2**2</code> produce a negative four and not a positive one. It is also right-associating, meaning that <code>2**3**2</code> is two raised to the ninth power, not eight squared.</p> <p>Although it has the same precedence as in C, Perl's <code>?:</code> operator produces an lvalue. This assigns $x to either $if_true or $if_false, depending on the trueness of $maybe:</p> <pre data-language="perl"><code>($maybe ? $if_true : $if_false) = $x;</code></pre> <h3 id="How-do-I-declare/create-a-structure?">
How do I declare/create a structure?</h3> <p>In general, you don't "declare" a structure. Just use a (probably anonymous) hash reference. See <a href="perlref.html">perlref</a> and <a href="perldsc.html">perldsc</a> for details. Here's an example:</p> <pre data-language="perl"><code>$person = {};                   # new anonymous hash
$person-&gt;{AGE}  = 24;           # set field AGE to 24
$person-&gt;{NAME} = "Nat";        # set field NAME to "Nat"</code></pre> <p>If you're looking for something a bit more rigorous, try <a href="perlootut.html">perlootut</a>.</p> <h3 id="How-do-I-create-a-module?">
How do I create a module?</h3> <p><a href="perlnewmod.html">perlnewmod</a> is a good place to start, ignore the bits about uploading to CPAN if you don't want to make your module publicly available.</p> <p><a href="ExtUtils::ModuleMaker.html">ExtUtils::ModuleMaker</a> and <a href="Module::Starter.html">Module::Starter</a> are also good places to start. Many CPAN authors now use <a href="Dist::Zilla.html">Dist::Zilla</a> to automate as much as possible.</p> <p>Detailed documentation about modules can be found at: <a href="perlmod.html">perlmod</a>, <a href="perlmodlib.html">perlmodlib</a>, <a href="perlmodstyle.html">perlmodstyle</a>.</p> <p>If you need to include C code or C library interfaces use h2xs. h2xs will create the module distribution structure and the initial interface files. <a href="perlxs.html">perlxs</a> and <a href="perlxstut.html">perlxstut</a> explain the details.</p> <h3 id="How-do-I-adopt-or-take-over-a-module-already-on-CPAN?">
How do I adopt or take over a module already on CPAN?</h3> <p>Ask the current maintainer to make you a co-maintainer or transfer the module to you.</p> <p>If you can not reach the author for some reason contact the PAUSE admins at modules@perl.org who may be able to help, but each case is treated separately.</p> <ul> <li>
<p>Get a login for the Perl Authors Upload Server (PAUSE) if you don't already have one: <a href="http://pause.perl.org">http://pause.perl.org</a></p> </li> <li>
<p>Write to modules@perl.org explaining what you did to contact the current maintainer. The PAUSE admins will also try to reach the maintainer.</p> </li> <li>
<p>Post a public message in a heavily trafficked site announcing your intention to take over the module.</p> </li> <li>
<p>Wait a bit. The PAUSE admins don't want to act too quickly in case the current maintainer is on holiday. If there's no response to private communication or the public post, a PAUSE admin can transfer it to you.</p> </li> </ul> <h3 id="How-do-I-create-a-class?">
How do I create a class? </h3> <p>(contributed by brian d foy)</p> <p>In Perl, a class is just a package, and methods are just subroutines. Perl doesn't get more formal than that and lets you set up the package just the way that you like it (that is, it doesn't set up anything for you).</p> <p>See also <a href="perlootut.html">perlootut</a>, a tutorial that covers class creation, and <a href="perlobj.html">perlobj</a>.</p> <h3 id="How-can-I-tell-if-a-variable-is-tainted?">
How can I tell if a variable is tainted?</h3> <p>You can use the tainted() function of the Scalar::Util module, available from CPAN (or included with Perl since release 5.8.0). See also <a href="perlsec.html#Laundering-and-Detecting-Tainted-Data">"Laundering and Detecting Tainted Data" in perlsec</a>.</p> <h3 id="What's-a-closure?">
What's a closure?</h3> <p>Closures are documented in <a href="perlref.html">perlref</a>.</p> <p><i>Closure</i> is a computer science term with a precise but hard-to-explain meaning. Usually, closures are implemented in Perl as anonymous subroutines with lasting references to lexical variables outside their own scopes. These lexicals magically refer to the variables that were around when the subroutine was defined (deep binding).</p> <p>Closures are most often used in programming languages where you can have the return value of a function be itself a function, as you can in Perl. Note that some languages provide anonymous functions but are not capable of providing proper closures: the Python language, for example. For more information on closures, check out any textbook on functional programming. Scheme is a language that not only supports but encourages closures.</p> <p>Here's a classic non-closure function-generating function:</p> <pre data-language="perl"><code>sub add_function_generator {
    return sub { shift() + shift() };
}

my $add_sub = add_function_generator();
my $sum = $add_sub-&gt;(4,5);                # $sum is 9 now.</code></pre> <p>The anonymous subroutine returned by add_function_generator() isn't technically a closure because it refers to no lexicals outside its own scope. Using a closure gives you a <i>function template</i> with some customization slots left out to be filled later.</p> <p>Contrast this with the following make_adder() function, in which the returned anonymous function contains a reference to a lexical variable outside the scope of that function itself. Such a reference requires that Perl return a proper closure, thus locking in for all time the value that the lexical had when the function was created.</p> <pre data-language="perl"><code>sub make_adder {
    my $addpiece = shift;
    return sub { shift() + $addpiece };
}

my $f1 = make_adder(20);
my $f2 = make_adder(555);</code></pre> <p>Now <code>$f1-&gt;($n)</code> is always 20 plus whatever $n you pass in, whereas <code>$f2-&gt;($n)</code> is always 555 plus whatever $n you pass in. The $addpiece in the closure sticks around.</p> <p>Closures are often used for less esoteric purposes. For example, when you want to pass in a bit of code into a function:</p> <pre data-language="perl"><code>my $line;
timeout( 30, sub { $line = &lt;STDIN&gt; } );</code></pre> <p>If the code to execute had been passed in as a string, <code>'$line = &lt;STDIN&gt;'</code>, there would have been no way for the hypothetical timeout() function to access the lexical variable $line back in its caller's scope.</p> <p>Another use for a closure is to make a variable <i>private</i> to a named subroutine, e.g. a counter that gets initialized at creation time of the sub and can only be modified from within the sub. This is sometimes used with a BEGIN block in package files to make sure a variable doesn't get meddled with during the lifetime of the package:</p> <pre data-language="perl"><code>BEGIN {
    my $id = 0;
    sub next_id { ++$id }
}</code></pre> <p>This is discussed in more detail in <a href="perlsub.html">perlsub</a>; see the entry on <i>Persistent Private Variables</i>.</p> <h3 id="What-is-variable-suicide-and-how-can-I-prevent-it?">
What is variable suicide and how can I prevent it?</h3> <p>This problem was fixed in perl 5.004_05, so preventing it means upgrading your version of perl. ;)</p> <p>Variable suicide is when you (temporarily or permanently) lose the value of a variable. It is caused by scoping through my() and local() interacting with either closures or aliased foreach() iterator variables and subroutine arguments. It used to be easy to inadvertently lose a variable's value this way, but now it's much harder. Take this code:</p> <pre data-language="perl"><code>my $f = 'foo';
sub T {
    while ($i++ &lt; 3) { my $f = $f; $f .= "bar"; print $f, "\n" }
}

T;
print "Finally $f\n";</code></pre> <p>If you are experiencing variable suicide, that <code>my $f</code> in the subroutine doesn't pick up a fresh copy of the <code>$f</code> whose value is <code>'foo'</code>. The output shows that inside the subroutine the value of <code>$f</code> leaks through when it shouldn't, as in this output:</p> <pre data-language="perl"><code class="plaintext">foobar
foobarbar
foobarbarbar
Finally foo</code></pre> <p>The $f that has "bar" added to it three times should be a new <code>$f</code> <code>my $f</code> should create a new lexical variable each time through the loop. The expected output is:</p> <pre data-language="perl"><code class="plaintext">foobar
foobar
foobar
Finally foo</code></pre> <h3 id="How-can-I-pass/return-a-{Function,-FileHandle,-Array,-Hash,-Method,-Regex}?">
How can I pass/return a {Function, FileHandle, Array, Hash, Method, Regex}?</h3> <p>You need to pass references to these objects. See <a href="perlsub.html#Pass-by-Reference">"Pass by Reference" in perlsub</a> for this particular question, and <a href="perlref.html">perlref</a> for information on references.</p> <dl> <dt id="Passing-Variables-and-Functions">
Passing Variables and Functions</dt> <dd> <p>Regular variables and functions are quite easy to pass: just pass in a reference to an existing or anonymous variable or function:</p> <pre data-language="perl"><code>func( \$some_scalar );

func( \@some_array  );
func( [ 1 .. 10 ]   );

func( \%some_hash   );
func( { this =&gt; 10, that =&gt; 20 }   );

func( \&amp;some_func   );
func( sub { $_[0] ** $_[1] }   );</code></pre> </dd> <dt id="Passing-Filehandles">
Passing Filehandles</dt> <dd> <p>As of Perl 5.6, you can represent filehandles with scalar variables which you treat as any other scalar.</p> <pre data-language="perl"><code>open my $fh, $filename or die "Cannot open $filename! $!";
func( $fh );

sub func {
    my $passed_fh = shift;

    my $line = &lt;$passed_fh&gt;;
}</code></pre> <p>Before Perl 5.6, you had to use the <code>*FH</code> or <code>\*FH</code> notations. These are "typeglobs"--see <a href="perldata.html#Typeglobs-and-Filehandles">"Typeglobs and Filehandles" in perldata</a> and especially <a href="perlsub.html#Pass-by-Reference">"Pass by Reference" in perlsub</a> for more information.</p> </dd> <dt id="Passing-Regexes">
Passing Regexes</dt> <dd> <p>Here's an example of how to pass in a string and a regular expression for it to match against. You construct the pattern with the <code>qr//</code> operator:</p> <pre data-language="perl"><code>sub compare {
    my ($val1, $regex) = @_;
    my $retval = $val1 =~ /$regex/;
    return $retval;
}
$match = compare("old McDonald", qr/d.*D/i);</code></pre> </dd> <dt id="Passing-Methods">
Passing Methods</dt> <dd> <p>To pass an object method into a subroutine, you can do this:</p> <pre data-language="perl"><code>call_a_lot(10, $some_obj, "methname")
sub call_a_lot {
    my ($count, $widget, $trick) = @_;
    for (my $i = 0; $i &lt; $count; $i++) {
        $widget-&gt;$trick();
    }
}</code></pre> <p>Or, you can use a closure to bundle up the object, its method call, and arguments:</p> <pre data-language="perl"><code>my $whatnot = sub { $some_obj-&gt;obfuscate(@args) };
func($whatnot);
sub func {
    my $code = shift;
    &amp;$code();
}</code></pre> <p>You could also investigate the can() method in the UNIVERSAL class (part of the standard perl distribution).</p> </dd> </dl> <h3 id="How-do-I-create-a-static-variable?">
How do I create a static variable?</h3> <p>(contributed by brian d foy)</p> <p>In Perl 5.10, declare the variable with <code>state</code>. The <code>state</code> declaration creates the lexical variable that persists between calls to the subroutine:</p> <pre data-language="perl"><code>sub counter { state $count = 1; $count++ }</code></pre> <p>You can fake a static variable by using a lexical variable which goes out of scope. In this example, you define the subroutine <code>counter</code>, and it uses the lexical variable <code>$count</code>. Since you wrap this in a BEGIN block, <code>$count</code> is defined at compile-time, but also goes out of scope at the end of the BEGIN block. The BEGIN block also ensures that the subroutine and the value it uses is defined at compile-time so the subroutine is ready to use just like any other subroutine, and you can put this code in the same place as other subroutines in the program text (i.e. at the end of the code, typically). The subroutine <code>counter</code> still has a reference to the data, and is the only way you can access the value (and each time you do, you increment the value). The data in chunk of memory defined by <code>$count</code> is private to <code>counter</code>.</p> <pre data-language="perl"><code>BEGIN {
    my $count = 1;
    sub counter { $count++ }
}

my $start = counter();

.... # code that calls counter();

my $end = counter();</code></pre> <p>In the previous example, you created a function-private variable because only one function remembered its reference. You could define multiple functions while the variable is in scope, and each function can share the "private" variable. It's not really "static" because you can access it outside the function while the lexical variable is in scope, and even create references to it. In this example, <code>increment_count</code> and <code>return_count</code> share the variable. One function adds to the value and the other simply returns the value. They can both access <code>$count</code>, and since it has gone out of scope, there is no other way to access it.</p> <pre data-language="perl"><code>BEGIN {
    my $count = 1;
    sub increment_count { $count++ }
    sub return_count    { $count }
}</code></pre> <p>To declare a file-private variable, you still use a lexical variable. A file is also a scope, so a lexical variable defined in the file cannot be seen from any other file.</p> <p>See <a href="perlsub.html#Persistent-Private-Variables">"Persistent Private Variables" in perlsub</a> for more information. The discussion of closures in <a href="perlref.html">perlref</a> may help you even though we did not use anonymous subroutines in this answer. See <a href="perlsub.html#Persistent-Private-Variables">"Persistent Private Variables" in perlsub</a> for details.</p> <h3 id="What's-the-difference-between-dynamic-and-lexical-(static)-scoping?-Between-local()-and-my()?">
What's the difference between dynamic and lexical (static) scoping? Between local() and my()?</h3> <p><code>local($x)</code> saves away the old value of the global variable <code>$x</code> and assigns a new value for the duration of the subroutine <i>which is visible in other functions called from that subroutine</i>. This is done at run-time, so is called dynamic scoping. local() always affects global variables, also called package variables or dynamic variables.</p> <p><code>my($x)</code> creates a new variable that is only visible in the current subroutine. This is done at compile-time, so it is called lexical or static scoping. my() always affects private variables, also called lexical variables or (improperly) static(ly scoped) variables.</p> <p>For instance:</p> <pre data-language="perl"><code>sub visible {
    print "var has value $var\n";
}

sub dynamic {
    local $var = 'local';    # new temporary value for the still-global
    visible();              #   variable called $var
}

sub lexical {
    my $var = 'private';    # new private variable, $var
    visible();              # (invisible outside of sub scope)
}

$var = 'global';

visible();              # prints global
dynamic();              # prints local
lexical();              # prints global</code></pre> <p>Notice how at no point does the value "private" get printed. That's because $var only has that value within the block of the lexical() function, and it is hidden from the called subroutine.</p> <p>In summary, local() doesn't make what you think of as private, local variables. It gives a global variable a temporary value. my() is what you're looking for if you want private variables.</p> <p>See <a href="perlsub.html#Private-Variables-via-my%28%29">"Private Variables via my()" in perlsub</a> and <a href="perlsub.html#Temporary-Values-via-local%28%29">"Temporary Values via local()" in perlsub</a> for excruciating details.</p> <h3 id="How-can-I-access-a-dynamic-variable-while-a-similarly-named-lexical-is-in-scope?">
How can I access a dynamic variable while a similarly named lexical is in scope?</h3> <p>If you know your package, you can just mention it explicitly, as in $Some_Pack::var. Note that the notation $::var is <b>not</b> the dynamic $var in the current package, but rather the one in the "main" package, as though you had written $main::var.</p> <pre data-language="perl"><code>use vars '$var';
local $var = "global";
my    $var = "lexical";

print "lexical is $var\n";
print "global  is $main::var\n";</code></pre> <p>Alternatively you can use the compiler directive our() to bring a dynamic variable into the current lexical scope.</p> <pre data-language="perl"><code>require 5.006; # our() did not exist before 5.6
use vars '$var';

local $var = "global";
my $var    = "lexical";

print "lexical is $var\n";

{
    our $var;
    print "global  is $var\n";
}</code></pre> <h3 id="What's-the-difference-between-deep-and-shallow-binding?">
What's the difference between deep and shallow binding?</h3> <p>In deep binding, lexical variables mentioned in anonymous subroutines are the same ones that were in scope when the subroutine was created. In shallow binding, they are whichever variables with the same names happen to be in scope when the subroutine is called. Perl always uses deep binding of lexical variables (i.e., those created with my()). However, dynamic variables (aka global, local, or package variables) are effectively shallowly bound. Consider this just one more reason not to use them. See the answer to <a href="#What%27s-a-closure%3F">"What's a closure?"</a>.</p> <h3 id="Why-doesn't-&quot;my($foo)-=-&lt;$fh&gt;;&quot;-work-right?">
Why doesn't "my($foo) = &lt;$fh&gt;;" work right?</h3> <p><code>my()</code> and <code>local()</code> give list context to the right hand side of <code>=</code>. The &lt;$fh&gt; read operation, like so many of Perl's functions and operators, can tell which context it was called in and behaves appropriately. In general, the scalar() function can help. This function does nothing to the data itself (contrary to popular myth) but rather tells its argument to behave in whatever its scalar fashion is. If that function doesn't have a defined scalar behavior, this of course doesn't help you (such as with sort()).</p> <p>To enforce scalar context in this particular case, however, you need merely omit the parentheses:</p> <pre data-language="perl"><code>local($foo) = &lt;$fh&gt;;        # WRONG
local($foo) = scalar(&lt;$fh&gt;);   # ok
local $foo  = &lt;$fh&gt;;        # right</code></pre> <p>You should probably be using lexical variables anyway, although the issue is the same here:</p> <pre data-language="perl"><code>my($foo) = &lt;$fh&gt;;    # WRONG
my $foo  = &lt;$fh&gt;;    # right</code></pre> <h3 id="How-do-I-redefine-a-builtin-function,-operator,-or-method?">
How do I redefine a builtin function, operator, or method?</h3> <p>Why do you want to do that? :-)</p> <p>If you want to override a predefined function, such as open(), then you'll have to import the new definition from a different module. See <a href="perlsub.html#Overriding-Built-in-Functions">"Overriding Built-in Functions" in perlsub</a>.</p> <p>If you want to overload a Perl operator, such as <code>+</code> or <code>**</code>, then you'll want to use the <code>use overload</code> pragma, documented in <a href="overload.html">overload</a>.</p> <p>If you're talking about obscuring method calls in parent classes, see <a href="perlootut.html#Overriding-methods-and-method-resolution">"Overriding methods and method resolution" in perlootut</a>.</p> <h3 id="What's-the-difference-between-calling-a-function-as-&amp;foo-and-foo()?">
What's the difference between calling a function as &amp;foo and foo()?</h3> <p>(contributed by brian d foy)</p> <p>Calling a subroutine as <code>&amp;foo</code> with no trailing parentheses ignores the prototype of <code>foo</code> and passes it the current value of the argument list, <code>@_</code>. Here's an example; the <code>bar</code> subroutine calls <code>&amp;foo</code>, which prints its arguments list:</p> <pre data-language="perl"><code>sub foo { print "Args in foo are: @_\n"; }

sub bar { &amp;foo; }

bar( "a", "b", "c" );</code></pre> <p>When you call <code>bar</code> with arguments, you see that <code>foo</code> got the same <code>@_</code>:</p> <pre data-language="perl"><code class="plaintext">Args in foo are: a b c</code></pre> <p>Calling the subroutine with trailing parentheses, with or without arguments, does not use the current <code>@_</code>. Changing the example to put parentheses after the call to <code>foo</code> changes the program:</p> <pre data-language="perl"><code>sub foo { print "Args in foo are: @_\n"; }

sub bar { &amp;foo(); }

bar( "a", "b", "c" );</code></pre> <p>Now the output shows that <code>foo</code> doesn't get the <code>@_</code> from its caller.</p> <pre data-language="perl"><code class="plaintext">Args in foo are:</code></pre> <p>However, using <code>&amp;</code> in the call still overrides the prototype of <code>foo</code> if present:</p> <pre data-language="perl"><code>sub foo ($$$) { print "Args infoo are: @_\n"; }

sub bar_1 { &amp;foo; }
sub bar_2 { &amp;foo(); }
sub bar_3 { foo( $_[0], $_[1], $_[2] ); }
# sub bar_4 { foo(); }
# bar_4 doesn't compile: "Not enough arguments for main::foo at ..."

bar_1( "a", "b", "c" );
# Args in foo are: a b c

bar_2( "a", "b", "c" );
# Args in foo are:

bar_3( "a", "b", "c" );
# Args in foo are: a b c</code></pre> <p>The main use of the <code>@_</code> pass-through feature is to write subroutines whose main job it is to call other subroutines for you. For further details, see <a href="perlsub.html">perlsub</a>.</p> <h3 id="How-do-I-create-a-switch-or-case-statement?">
How do I create a switch or case statement?</h3> <p>There is a given/when statement in Perl, but it is experimental and likely to change in future. See <a href="perlsyn.html">perlsyn</a> for more details.</p> <p>The general answer is to use a CPAN module such as <a href="Switch::Plain.html">Switch::Plain</a>:</p> <pre data-language="perl"><code>use Switch::Plain;
sswitch($variable_holding_a_string) {
    case 'first': { }
    case 'second': { }
    default: { }
}</code></pre> <p>or for more complicated comparisons, <code>if-elsif-else</code>:</p> <pre data-language="perl"><code>for ($variable_to_test) {
    if    (/pat1/)  { }     # do something
    elsif (/pat2/)  { }     # do something else
    elsif (/pat3/)  { }     # do something else
    else            { }     # default
}</code></pre> <p>Here's a simple example of a switch based on pattern matching, lined up in a way to make it look more like a switch statement. We'll do a multiway conditional based on the type of reference stored in $whatchamacallit:</p> <pre data-language="perl"><code>SWITCH: for (ref $whatchamacallit) {

    /^$/           &amp;&amp; die "not a reference";

    /SCALAR/       &amp;&amp; do {
                    print_scalar($$ref);
                    last SWITCH;
                  };

    /ARRAY/        &amp;&amp; do {
                    print_array(@$ref);
                    last SWITCH;
                  };

    /HASH/        &amp;&amp; do {
                    print_hash(%$ref);
                    last SWITCH;
                  };

    /CODE/        &amp;&amp; do {
                    warn "can't print function ref";
                    last SWITCH;
                  };

    # DEFAULT

    warn "User defined type skipped";

}</code></pre> <p>See <a href="perlsyn.html">perlsyn</a> for other examples in this style.</p> <p>Sometimes you should change the positions of the constant and the variable. For example, let's say you wanted to test which of many answers you were given, but in a case-insensitive way that also allows abbreviations. You can use the following technique if the strings all start with different characters or if you want to arrange the matches so that one takes precedence over another, as <code>"SEND"</code> has precedence over <code>"STOP"</code> here:</p> <pre data-language="perl"><code>chomp($answer = &lt;&gt;);
if    ("SEND"  =~ /^\Q$answer/i) { print "Action is send\n"  }
elsif ("STOP"  =~ /^\Q$answer/i) { print "Action is stop\n"  }
elsif ("ABORT" =~ /^\Q$answer/i) { print "Action is abort\n" }
elsif ("LIST"  =~ /^\Q$answer/i) { print "Action is list\n"  }
elsif ("EDIT"  =~ /^\Q$answer/i) { print "Action is edit\n"  }</code></pre> <p>A totally different approach is to create a hash of function references.</p> <pre data-language="perl"><code>my %commands = (
    "happy" =&gt; \&amp;joy,
    "sad",  =&gt; \&amp;sullen,
    "done"  =&gt; sub { die "See ya!" },
    "mad"   =&gt; \&amp;angry,
);

print "How are you? ";
chomp($string = &lt;STDIN&gt;);
if ($commands{$string}) {
    $commands{$string}-&gt;();
} else {
    print "No such command: $string\n";
}</code></pre> <p>Starting from Perl 5.8, a source filter module, <code>Switch</code>, can also be used to get switch and case. Its use is now discouraged, because it's not fully compatible with the native switch of Perl 5.10, and because, as it's implemented as a source filter, it doesn't always work as intended when complex syntax is involved.</p> <h3 id="How-can-I-catch-accesses-to-undefined-variables,-functions,-or-methods?">
How can I catch accesses to undefined variables, functions, or methods?</h3> <p>The AUTOLOAD method, discussed in <a href="perlsub.html#Autoloading">"Autoloading" in perlsub</a> lets you capture calls to undefined functions and methods.</p> <p>When it comes to undefined variables that would trigger a warning under <code>use warnings</code>, you can promote the warning to an error.</p> <pre data-language="perl"><code>use warnings FATAL =&gt; qw(uninitialized);</code></pre> <h3 id="Why-can't-a-method-included-in-this-same-file-be-found?">
Why can't a method included in this same file be found?</h3> <p>Some possible reasons: your inheritance is getting confused, you've misspelled the method name, or the object is of the wrong type. Check out <a href="perlootut.html">perlootut</a> for details about any of the above cases. You may also use <code>print ref($object)</code> to find out the class <code>$object</code> was blessed into.</p> <p>Another possible reason for problems is that you've used the indirect object syntax (eg, <code>find Guru "Samy"</code>) on a class name before Perl has seen that such a package exists. It's wisest to make sure your packages are all defined before you start using them, which will be taken care of if you use the <code>use</code> statement instead of <code>require</code>. If not, make sure to use arrow notation (eg., <code>Guru-&gt;find("Samy")</code>) instead. Object notation is explained in <a href="perlobj.html">perlobj</a>.</p> <p>Make sure to read about creating modules in <a href="perlmod.html">perlmod</a> and the perils of indirect objects in <a href="perlobj.html#Method-Invocation">"Method Invocation" in perlobj</a>.</p> <h3 id="How-can-I-find-out-my-current-or-calling-package?">
How can I find out my current or calling package?</h3> <p>(contributed by brian d foy)</p> <p>To find the package you are currently in, use the special literal <code>__PACKAGE__</code>, as documented in <a href="perldata.html">perldata</a>. You can only use the special literals as separate tokens, so you can't interpolate them into strings like you can with variables:</p> <pre data-language="perl"><code>my $current_package = __PACKAGE__;
print "I am in package $current_package\n";</code></pre> <p>If you want to find the package calling your code, perhaps to give better diagnostics as <a href="carp.html">Carp</a> does, use the <code>caller</code> built-in:</p> <pre data-language="perl"><code>sub foo {
    my @args = ...;
    my( $package, $filename, $line ) = caller;

    print "I was called from package $package\n";
    );</code></pre> <p>By default, your program starts in package <code>main</code>, so you will always be in some package.</p> <p>This is different from finding out the package an object is blessed into, which might not be the current package. For that, use <code>blessed</code> from <a href="Scalar::Util.html">Scalar::Util</a>, part of the Standard Library since Perl 5.8:</p> <pre data-language="perl"><code>use Scalar::Util qw(blessed);
my $object_package = blessed( $object );</code></pre> <p>Most of the time, you shouldn't care what package an object is blessed into, however, as long as it claims to inherit from that class:</p> <pre data-language="perl"><code>my $is_right_class = eval { $object-&gt;isa( $package ) }; # true or false</code></pre> <p>And, with Perl 5.10 and later, you don't have to check for an inheritance to see if the object can handle a role. For that, you can use <code>DOES</code>, which comes from <code>UNIVERSAL</code>:</p> <pre data-language="perl"><code>my $class_does_it = eval { $object-&gt;DOES( $role ) }; # true or false</code></pre> <p>You can safely replace <code>isa</code> with <code>DOES</code> (although the converse is not true).</p> <h3 id="How-can-I-comment-out-a-large-block-of-Perl-code?">
How can I comment out a large block of Perl code?</h3> <p>(contributed by brian d foy)</p> <p>The quick-and-dirty way to comment out more than one line of Perl is to surround those lines with Pod directives. You have to put these directives at the beginning of the line and somewhere where Perl expects a new statement (so not in the middle of statements like the <code>#</code> comments). You end the comment with <code>=cut</code>, ending the Pod section:</p> <pre data-language="perl"><code>=pod

my $object = NotGonnaHappen-&gt;new();

ignored_sub();

$wont_be_assigned = 37;

=cut</code></pre> <p>The quick-and-dirty method only works well when you don't plan to leave the commented code in the source. If a Pod parser comes along, your multiline comment is going to show up in the Pod translation. A better way hides it from Pod parsers as well.</p> <p>The <code>=begin</code> directive can mark a section for a particular purpose. If the Pod parser doesn't want to handle it, it just ignores it. Label the comments with <code>comment</code>. End the comment using <code>=end</code> with the same label. You still need the <code>=cut</code> to go back to Perl code from the Pod comment:</p> <pre data-language="perl"><code>=begin comment

my $object = NotGonnaHappen-&gt;new();

ignored_sub();

$wont_be_assigned = 37;

=end comment

=cut</code></pre> <p>For more information on Pod, check out <a href="perlpod.html">perlpod</a> and <a href="perlpodspec.html">perlpodspec</a>.</p> <h3 id="How-do-I-clear-a-package?">
How do I clear a package?</h3> <p>Use this code, provided by Mark-Jason Dominus:</p> <pre data-language="perl"><code>sub scrub_package {
    no strict 'refs';
    my $pack = shift;
    die "Shouldn't delete main package"
        if $pack eq "" || $pack eq "main";
    my $stash = *{$pack . '::'}{HASH};
    my $name;
    foreach $name (keys %$stash) {
        my $fullname = $pack . '::' . $name;
        # Get rid of everything with that name.
        undef $$fullname;
        undef @$fullname;
        undef %$fullname;
        undef &amp;$fullname;
        undef *$fullname;
    }
}</code></pre> <p>Or, if you're using a recent release of Perl, you can just use the Symbol::delete_package() function instead.</p> <h3 id="How-can-I-use-a-variable-as-a-variable-name?">
How can I use a variable as a variable name?</h3> <p>Beginners often think they want to have a variable contain the name of a variable.</p> <pre data-language="perl"><code>$fred    = 23;
$varname = "fred";
++$$varname;         # $fred now 24</code></pre> <p>This works <i>sometimes</i>, but it is a very bad idea for two reasons.</p> <p>The first reason is that this technique <i>only works on global variables</i>. That means that if $fred is a lexical variable created with my() in the above example, the code wouldn't work at all: you'd accidentally access the global and skip right over the private lexical altogether. Global variables are bad because they can easily collide accidentally and in general make for non-scalable and confusing code.</p> <p>Symbolic references are forbidden under the <code>use strict</code> pragma. They are not true references and consequently are not reference-counted or garbage-collected.</p> <p>The other reason why using a variable to hold the name of another variable is a bad idea is that the question often stems from a lack of understanding of Perl data structures, particularly hashes. By using symbolic references, you are just using the package's symbol-table hash (like <code>%main::</code>) instead of a user-defined hash. The solution is to use your own hash or a real reference instead.</p> <pre data-language="perl"><code>$USER_VARS{"fred"} = 23;
my $varname = "fred";
$USER_VARS{$varname}++;  # not $$varname++</code></pre> <p>There we're using the %USER_VARS hash instead of symbolic references. Sometimes this comes up in reading strings from the user with variable references and wanting to expand them to the values of your perl program's variables. This is also a bad idea because it conflates the program-addressable namespace and the user-addressable one. Instead of reading a string and expanding it to the actual contents of your program's own variables:</p> <pre data-language="perl"><code>$str = 'this has a $fred and $barney in it';
$str =~ s/(\$\w+)/$1/eeg;          # need double eval</code></pre> <p>it would be better to keep a hash around like %USER_VARS and have variable references actually refer to entries in that hash:</p> <pre data-language="perl"><code>$str =~ s/\$(\w+)/$USER_VARS{$1}/g;   # no /e here at all</code></pre> <p>That's faster, cleaner, and safer than the previous approach. Of course, you don't need to use a dollar sign. You could use your own scheme to make it less confusing, like bracketed percent symbols, etc.</p> <pre data-language="perl"><code>$str = 'this has a %fred% and %barney% in it';
$str =~ s/%(\w+)%/$USER_VARS{$1}/g;   # no /e here at all</code></pre> <p>Another reason that folks sometimes think they want a variable to contain the name of a variable is that they don't know how to build proper data structures using hashes. For example, let's say they wanted two hashes in their program: %fred and %barney, and that they wanted to use another scalar variable to refer to those by name.</p> <pre data-language="perl"><code>$name = "fred";
$$name{WIFE} = "wilma";     # set %fred

$name = "barney";
$$name{WIFE} = "betty";    # set %barney</code></pre> <p>This is still a symbolic reference, and is still saddled with the problems enumerated above. It would be far better to write:</p> <pre data-language="perl"><code>$folks{"fred"}{WIFE}   = "wilma";
$folks{"barney"}{WIFE} = "betty";</code></pre> <p>And just use a multilevel hash to start with.</p> <p>The only times that you absolutely <i>must</i> use symbolic references are when you really must refer to the symbol table. This may be because it's something that one can't take a real reference to, such as a format name. Doing so may also be important for method calls, since these always go through the symbol table for resolution.</p> <p>In those cases, you would turn off <code>strict 'refs'</code> temporarily so you can play around with the symbol table. For example:</p> <pre data-language="perl"><code>@colors = qw(red blue green yellow orange purple violet);
for my $name (@colors) {
    no strict 'refs';  # renege for the block
    *$name = sub { "&lt;FONT COLOR='$name'&gt;@_&lt;/FONT&gt;" };
}</code></pre> <p>All those functions (red(), blue(), green(), etc.) appear to be separate, but the real code in the closure actually was compiled only once.</p> <p>So, sometimes you might want to use symbolic references to manipulate the symbol table directly. This doesn't matter for formats, handles, and subroutines, because they are always global--you can't use my() on them. For scalars, arrays, and hashes, though--and usually for subroutines-- you probably only want to use hard references.</p> <h3 id='What-does-"bad-interpreter"-mean?'>
What does "bad interpreter" mean?</h3> <p>(contributed by brian d foy)</p> <p>The "bad interpreter" message comes from the shell, not perl. The actual message may vary depending on your platform, shell, and locale settings.</p> <p>If you see "bad interpreter - no such file or directory", the first line in your perl script (the "shebang" line) does not contain the right path to perl (or any other program capable of running scripts). Sometimes this happens when you move the script from one machine to another and each machine has a different path to perl--/usr/bin/perl versus /usr/local/bin/perl for instance. It may also indicate that the source machine has CRLF line terminators and the destination machine has LF only: the shell tries to find /usr/bin/perl&lt;CR&gt;, but can't.</p> <p>If you see "bad interpreter: Permission denied", you need to make your script executable.</p> <p>In either case, you should still be able to run the scripts with perl explicitly:</p> <pre data-language="perl"><code class="plaintext">% perl script.pl</code></pre> <p>If you get a message like "perl: command not found", perl is not in your PATH, which might also mean that the location of perl is not where you expect it so you need to adjust your shebang line.</p> <h3 id="Do-I-need-to-recompile-XS-modules-when-there-is-a-change-in-the-C-library?">
Do I need to recompile XS modules when there is a change in the C library?</h3> <p>(contributed by Alex Beamish)</p> <p>If the new version of the C library is ABI-compatible (that's Application Binary Interface compatible) with the version you're upgrading from, and if the shared library version didn't change, no re-compilation should be necessary.</p> <h2 id="AUTHOR-AND-COPYRIGHT">
AUTHOR AND COPYRIGHT</h2> <p>Copyright (c) 1997-2013 Tom Christiansen, Nathan Torkington, and other authors as noted. All rights reserved.</p> <p>This documentation is free; you can redistribute it and/or modify it under the same terms as Perl itself.</p> <p>Irrespective of its distribution, all code examples in this file are hereby placed into the public domain. You are permitted and encouraged to use this code in your own programs for fun or for profit as you see fit. A simple comment in the code giving credit would be courteous but is not required.</p><div class="_attribution">
  <p class="_attribution-p">
    &copy; 1993&ndash;2023 Larry Wall and others<br>Licensed under the GNU General Public License version 1 or later, or the Artistic License.<br>The Perl logo is a trademark of the Perl Foundation.<br>
    <a href="https://perldoc.perl.org/5.38.0/perlfaq7" class="_attribution-link">https://perldoc.perl.org/5.38.0/perlfaq7</a>
  </p>
</div>
