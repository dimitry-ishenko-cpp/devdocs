<h1 id="SECTION03080000000000000000">Circles</h1>  The <b>circles</b> style plots a circle with an explicit radius at each data point. The radius is always interpreted in the units of the plot's horizontal axis (x or x2). The scale on y and the aspect ratio of the plot are both ignored. If the radius is not given in a separate column for each point it is taken from <b>set style circle</b>. In this case the radius may use graph or screen coordinates. <p> Many combinations of per-point and previously set properties are possible. For 2D plots these include </p>
<pre>
using x:y
using x:y:radius
using x:y:color
using x:y:radius:color
using x:y:radius:arc_begin:arc_end
using x:y:radius:arc_begin:arc_end:color</pre>  <p> By default a full circle will be drawn. It is possible to instead plot arc segments by specifying a start and end angle (in degrees) in columns 4 and 5. </p>
<p> A per-circle color may be provided in the last column of the using specifier. In this case the plot command must include a corresponding variable color term such as <b>lc variable</b> or <b>fillcolor rgb variable</b>. </p>
<p> For 3D plots the using specifier must contain </p>
<pre>
splot DATA using x:y:z:radius:color</pre>  <p> where the variable color column is options. See <b><a href="set_show.html#set_style_circle">set style circle</a></b> and <b><a href="set_show.html#set_style_fill">set style fill</a></b>. </p>
<p> Examples: </p>
<pre>
# draws circles whose area is proportional to the value in column 3
set style fill transparent solid 0.2 noborder
plot 'data' using 1:2:(sqrt($3)) with circles, \
     'data' using 1:2 with linespoints</pre>  <pre>
# draws Pac-men instead of circles
plot 'data' using 1:2:(10):(40):(320) with circles</pre>  <pre>
# draw a pie chart with inline data
set xrange [-15:15]
set style fill transparent solid 0.9 noborder
plot '-' using 1:2:3:4:5:6 with circles lc var
0    0    5    0    30    1
0    0    5   30    70    2
0    0    5   70   120    3
0    0    5  120   230    4
0    0    5  230   360    5
e</pre>  <p> The result is similar to using a <b>points</b> plot with variable size points and pointstyle 7, except that the circles will scale with the x axis range. See also <b><a href="set_show.html#set_object_circle">set object circle</a></b> and <b><a href="set_show.html#fillstyle">fillstyle</a></b>. </p><div class="_attribution">
  <p class="_attribution-p">
    Copyright 1986 - 1993, 1998, 2004   Thomas Williams, Colin Kelley<br>Distributed under the <a href="https://sourceforge.net/p/gnuplot/gnuplot-main/ci/master/tree/Copyright">gnuplot license</a> (rights to distribute modified versions are withheld).<br>
    
  </p>
</div>
