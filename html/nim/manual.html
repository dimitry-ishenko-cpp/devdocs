<h1 class="title">Nim Manual</h1>  
<blockquote><p>"Complexity" seems to be a lot like "energy": you can transfer it from the end-user to one/some of the other players, but the total amount seems to remain pretty much constant for a given task. -- Ran</p></blockquote> <h2 id="about-this-document">About this document</h2>
<p><strong>Note</strong>: This document is a draft! Several of Nim's features may need more precise wording. This manual is constantly evolving into a proper specification.</p> <p><strong>Note</strong>: The experimental features of Nim are covered <a href="manual_experimental.html">here</a>.</p> <p><strong>Note</strong>: Assignments, moves, and destruction are specified in the <a href="destructors.html">destructors</a> document.</p> <p>This document describes the lexis, the syntax, and the semantics of the Nim language.</p> <p>To learn how to compile Nim programs and generate documentation see <a href="nimc.html">Compiler User Guide</a> and <a href="https://nim-lang.org/docs/docgen.html">DocGen Tools Guide</a>.</p> <p>The language constructs are explained using an extended BNF, in which <code>(a)*</code> means 0 or more <code>a</code>'s, <code>a+</code> means 1 or more <code>a</code>'s, and <code>(a)?</code> means an optional <em>a</em>. Parentheses may be used to group elements.</p> <p><code>&amp;</code> is the lookahead operator; <code>&amp;a</code> means that an <code>a</code> is expected but not consumed. It will be consumed in the following rule.</p> <p>The <code>|</code>, <code>/</code> symbols are used to mark alternatives and have the lowest precedence. <code>/</code> is the ordered choice that requires the parser to try the alternatives in the given order. <code>/</code> is often used to ensure the grammar is not ambiguous.</p> <p>Non-terminals start with a lowercase letter, abstract terminal symbols are in UPPERCASE. Verbatim terminal symbols (including keywords) are quoted with <code>'</code>. An example:</p>
<pre data-language="nim">ifStmt = 'if' expr ':' stmts ('elif' expr ':' stmts)* ('else' stmts)?</pre> <p>The binary <code>^*</code> operator is used as a shorthand for 0 or more occurrences separated by its second argument; likewise <code>^+</code> means 1 or more occurrences: <code>a ^+ b</code> is short for <code>a (b a)*</code> and <code>a ^* b</code> is short for <code>(a (b a)*)?</code>. Example:</p>
<pre data-language="nim">arrayConstructor = '[' expr ^* ',' ']'</pre> <p>Other parts of Nim, like scoping rules or runtime semantics, are described informally.</p> <h2 id="definitions">Definitions</h2>
<p>Nim code specifies a computation that acts on a memory consisting of components called <span id="locations_1">locations</span>. A variable is basically a name for a location. Each variable and location is of a certain <span id="type_1">type</span>. The variable's type is called <span id="static-type_1">static type</span>, the location's type is called <span id="dynamic-type_1">dynamic type</span>. If the static type is not the same as the dynamic type, it is a super-type or subtype of the dynamic type.</p> <p>An <span id="identifier_1">identifier</span> is a symbol declared as a name for a variable, type, procedure, etc. The region of the program over which a declaration applies is called the <span id="scope_1">scope</span> of the declaration. Scopes can be nested. The meaning of an identifier is determined by the smallest enclosing scope in which the identifier is declared unless overloading resolution rules suggest otherwise.</p> <p>An expression specifies a computation that produces a value or location. Expressions that produce locations are called <span id="lminusvalues_1">l-values</span>. An l-value can denote either a location or the value the location contains, depending on the context.</p> <p>A Nim <span id="program_1">program</span> consists of one or more text <span id="source-files_1">source files</span> containing Nim code. It is processed by a Nim <span id="compiler_1">compiler</span> into an <span id="executable_1">executable</span>. The nature of this executable depends on the compiler implementation; it may, for example, be a native binary or JavaScript source code.</p> <p>In a typical Nim program, most of the code is compiled into the executable. However, some of the code may be executed at <span id="compileminustime_1">compile-time</span>. This can include constant expressions, macro definitions, and Nim procedures used by macro definitions. Most of the Nim language is supported at compile-time, but there are some restrictions -- see <a href="#restrictions-on-compileminustime-execution">Restrictions on Compile-Time Execution</a> for details. We use the term <span id="runtime_1">runtime</span> to cover both compile-time execution and code execution in the executable.</p> <p>The compiler parses Nim source code into an internal data structure called the <span id="abstract-syntax-tree_1">abstract syntax tree</span> (<span id="ast_1">AST</span>). Then, before executing the code or compiling it into the executable, it transforms the AST through <span id="semantic-analysis_1">semantic analysis</span>. This adds semantic information such as expression types, identifier meanings, and in some cases expression values. An error detected during semantic analysis is called a <span id="static-error_1">static error</span>. Errors described in this manual are static errors when not otherwise specified.</p> <p>A <span id="panic_1">panic</span> is an error that the implementation detects and reports at runtime. The method for reporting such errors is via <em>raising exceptions</em> or <em>dying with a fatal error</em>. However, the implementation provides a means to disable these <span id="runtime-checks_1">runtime checks</span>. See the section <a href="#pragmas">pragmas</a> for details.</p> <p>Whether a panic results in an exception or in a fatal error is implementation specific. Thus the following program is invalid; even though the code purports to catch the <code>IndexDefect</code> from an out-of-bounds array access, the compiler may instead choose to allow the program to die with a fatal error.</p> <pre class="listing" data-language="nim">var a: array[0..1, char]
let i = 5
try:
  a[i] = 'N'
except IndexDefect:
  echo "invalid index"</pre>
<p>The current implementation allows to switch between these different behaviors via <code>--panics:on|off</code>. When panics are turned on, the program dies with a panic, if they are turned off the runtime errors are turned into exceptions. The benefit of <code>--panics:on</code> is that it produces smaller binary code and the compiler has more freedom to optimize the code.</p> <p>An <span id="unchecked-runtime-error_1">unchecked runtime error</span> is an error that is not guaranteed to be detected and can cause the subsequent behavior of the computation to be arbitrary. Unchecked runtime errors cannot occur if only <span id="safe_1">safe</span> language features are used and if no runtime checks are disabled.</p> <p>A <span id="constant-expression_1">constant expression</span> is an expression whose value can be computed during a semantic analysis of the code in which it appears. It is never an l-value and never has side effects. Constant expressions are not limited to the capabilities of semantic analysis, such as constant folding; they can use all Nim language features that are supported for compile-time execution. Since constant expressions can be used as an input to semantic analysis (such as for defining array bounds), this flexibility requires the compiler to interleave semantic analysis and compile-time code execution.</p> <p>It is mostly accurate to picture semantic analysis proceeding top to bottom and left to right in the source code, with compile-time code execution interleaved when necessary to compute values that are required for subsequent semantic analysis. We will see much later in this document that macro invocation not only requires this interleaving, but also creates a situation where semantic analysis does not entirely proceed top to bottom and left to right.</p> <h2 id="lexical-analysis">Lexical Analysis</h2> <h3 id="lexical-analysis-encoding">Encoding</h3>
<p>All Nim source files are in the UTF-8 encoding (or its ASCII subset). Other encodings are not supported. Any of the standard platform line termination sequences can be used - the Unix form using ASCII LF (linefeed), the Windows form using the ASCII sequence CR LF (return followed by linefeed), or the old Macintosh form using the ASCII CR (return) character. All of these forms can be used equally, regardless of the platform.</p> <h3 id="lexical-analysis-indentation">Indentation</h3>
<p>Nim's standard grammar describes an <span id="indentation-sensitive_1">indentation sensitive</span> language. This means that all the control structures are recognized by indentation. Indentation consists only of spaces; tabulators are not allowed.</p> <p>The indentation handling is implemented as follows: The lexer annotates the following token with the preceding number of spaces; indentation is not a separate token. This trick allows parsing of Nim with only 1 token of lookahead.</p> <p>The parser uses a stack of indentation levels: the stack consists of integers counting the spaces. The indentation information is queried at strategic places in the parser but ignored otherwise: The pseudo-terminal <code>IND{&gt;}</code> denotes an indentation that consists of more spaces than the entry at the top of the stack; <code>IND{=}</code> an indentation that has the same number of spaces. <code>DED</code> is another pseudo terminal that describes the <em>action</em> of popping a value from the stack, <code>IND{&gt;}</code> then implies to push onto the stack.</p> <p>With this notation we can now easily define the core of the grammar: A block of statements (simplified example):</p>
<pre data-language="nim">ifStmt = 'if' expr ':' stmt
         (IND{=} 'elif' expr ':' stmt)*
         (IND{=} 'else' ':' stmt)?

simpleStmt = ifStmt / ...

stmt = IND{&gt;} stmt ^+ IND{=} DED  # list of statements
     / simpleStmt                 # or a simple statement</pre> <h3 id="lexical-analysis-comments">Comments</h3>
<p>Comments start anywhere outside a string or character literal with the hash character <code>#</code>. Comments consist of a concatenation of <span id="comment-pieces_1">comment pieces</span>. A comment piece starts with <code>#</code> and runs until the end of the line. The end of line characters belong to the piece. If the next line only consists of a comment piece with no other tokens between it and the preceding one, it does not start a new comment:</p> <pre class="listing" data-language="nim">i = 0     # This is a single comment over multiple lines.
  # The scanner merges these two pieces.
  # The comment continues here.</pre>
<p><span id="documentation-comments_1">Documentation comments</span> are comments that start with two <code>##</code>. Documentation comments are tokens; they are only allowed at certain places in the input file as they belong to the syntax tree!</p> <h3 id="lexical-analysis-multiline-comments">Multiline comments</h3>
<p>Starting with version 0.13.0 of the language Nim supports multiline comments. They look like:</p> <pre class="listing" data-language="nim">#[Comment here.
Multiple lines
are not a problem.]#</pre>
<p>Multiline comments support nesting:</p> <pre class="listing" data-language="nim">#[  #[ Multiline comment in already
   commented out code. ]#
proc p[T](x: T) = discard
]#</pre>
<p>Multiline documentation comments also exist and support nesting too:</p> <pre class="listing" data-language="nim">proc foo =
  ##[Long documentation comment
  here.
  ]##</pre> <h3 id="lexical-analysis-identifiers-amp-keywords">Identifiers &amp; Keywords</h3>
<p>Identifiers in Nim can be any string of letters, digits and underscores, with the following restrictions:</p> <ul class="simple">
<li>begins with a letter</li> <li>does not end with an underscore <code>_</code>
</li> <li>
<p>two immediate following underscores <code>__</code> are not allowed::</p> <p>letter ::= 'A'..'Z' | 'a'..'z' | 'x80'..'xff' digit ::= '0'..'9' IDENTIFIER ::= letter ( ['_'] (letter | digit) )*</p> </li> </ul> <p>Currently, any Unicode character with an ordinal value &gt; 127 (non-ASCII) is classified as a <code>letter</code> and may thus be part of an identifier but later versions of the language may assign some Unicode characters to belong to the operator characters instead.</p> <p>The following keywords are reserved and cannot be used as identifiers:</p> <pre class="listing" data-language="nim">addr and as asm
bind block break
case cast concept const continue converter
defer discard distinct div do
elif else end enum except export
finally for from func
if import in include interface is isnot iterator
let
macro method mixin mod
nil not notin
object of or out
proc ptr
raise ref return
shl shr static
template try tuple type
using
var
when while
xor
yield</pre>
<p>Some keywords are unused; they are reserved for future developments of the language.</p> <h3 id="lexical-analysis-identifier-equality">Identifier equality</h3>
<p>Two identifiers are considered equal if the following algorithm returns true:</p> <pre class="listing" data-language="nim">proc sameIdentifier(a, b: string): bool =
  a[0] == b[0] and
    a.replace("_", "").toLowerAscii == b.replace("_", "").toLowerAscii</pre>
<p>That means only the first letters are compared in a case-sensitive manner. Other letters are compared case-insensitively within the ASCII range and underscores are ignored.</p> <p>This rather unorthodox way to do identifier comparisons is called <span id="partial-caseminusinsensitivity_1">partial case-insensitivity</span> and has some advantages over the conventional case sensitivity:</p> <p>It allows programmers to mostly use their own preferred spelling style, be it humpStyle or snake_style, and libraries written by different programmers cannot use incompatible conventions. A Nim-aware editor or IDE can show the identifiers as preferred. Another advantage is that it frees the programmer from remembering the exact spelling of an identifier. The exception with respect to the first letter allows common code like <code>var foo: Foo</code> to be parsed unambiguously.</p> <p>Note that this rule also applies to keywords, meaning that <code>notin</code> is the same as <code>notIn</code> and <code>not_in</code> (all-lowercase version (<code>notin</code>, <code>isnot</code>) is the preferred way of writing keywords).</p> <p>Historically, Nim was a fully <span id="styleminusinsensitive_1">style-insensitive</span> language. This meant that it was not case-sensitive and underscores were ignored and there was not even a distinction between <code>foo</code> and <code>Foo</code>.</p> <h3 id="lexical-analysis-stropping">Stropping</h3>
<p>If a keyword is enclosed in backticks it loses its keyword property and becomes an ordinary identifier.</p> <p>Examples</p> <pre class="listing" data-language="nim">var `var` = "Hello Stropping"</pre>
<pre class="listing" data-language="nim">type Type = object
  `int`: int

let `object` = Type(`int`: 9)
assert `object` is Type
assert `object`.`int` == 9

var `var` = 42
let `let` = 8
assert `var` + `let` == 50

const `assert` = true
assert `assert`</pre> <h3 id="lexical-analysis-string-literals">String literals</h3>
<p>Terminal symbol in the grammar: <code>STR_LIT</code>.</p> <p>String literals can be delimited by matching double quotes, and can contain the following <span id="escape-sequences_1">escape sequences</span>:</p> <table>
<tr>
<th>Escape sequence</th>
<th>Meaning</th>
</tr> <tr>
<td><code>\p</code></td>
<td>platform specific newline: CRLF on Windows, LF on Unix</td>
</tr> <tr>
<td>
<code>\r</code>, <code>\c</code>
</td>
<td><span id="carriage-return_1">carriage return</span></td>
</tr> <tr>
<td>
<code>\n</code>, <code>\l</code>
</td>
<td>
<span id="line-feed_1">line feed</span> (often called <span id="newline_1">newline</span>)</td>
</tr> <tr>
<td><code>\f</code></td>
<td><span id="form-feed_1">form feed</span></td>
</tr> <tr>
<td><code>\t</code></td>
<td><span id="tabulator_1">tabulator</span></td>
</tr> <tr>
<td><code>\v</code></td>
<td><span id="vertical-tabulator_1">vertical tabulator</span></td>
</tr> <tr>
<td><code>\\</code></td>
<td><span id="backslash_1">backslash</span></td>
</tr> <tr>
<td><code>\"</code></td>
<td><span id="quotation-mark_1">quotation mark</span></td>
</tr> <tr>
<td><code>\'</code></td>
<td><span id="apostrophe_1">apostrophe</span></td>
</tr> <tr>
<td>
<code>\</code> '0'..'9'+</td>
<td>
<span id="character-with-decimal-value-d_1">character with decimal value d</span>; all decimal digits directly following are used for the character</td>
</tr> <tr>
<td><code>\a</code></td>
<td><span id="alert_1">alert</span></td>
</tr> <tr>
<td><code>\b</code></td>
<td><span id="backspace_1">backspace</span></td>
</tr> <tr>
<td><code>\e</code></td>
<td>
<span id="escape_1">escape</span> <span id="esc_1">[ESC]</span>
</td>
</tr> <tr>
<td>
<code>\x</code> HH</td>
<td>
<span id="character-with-hex-value-hh_1">character with hex value HH</span>; exactly two hex digits are allowed</td>
</tr> <tr>
<td>
<code>\u</code> HHHH</td>
<td>
<span id="unicode-codepoint-with-hex-value-hhhh_1">unicode codepoint with hex value HHHH</span>; exactly four hex digits are allowed</td>
</tr> <tr>
<td>
<code>\u</code> {H+}</td>
<td>
<span id="unicode-codepoint_1">unicode codepoint</span>; all hex digits enclosed in <code>{}</code> are used for the codepoint</td>
</tr> </table>
<p>Strings in Nim may contain any 8-bit value, even embedded zeros. However some operations may interpret the first binary zero as a terminator.</p> <h3 id="lexical-analysis-triple-quoted-string-literals">Triple quoted string literals</h3>
<p>Terminal symbol in the grammar: <code>TRIPLESTR_LIT</code>.</p> <p>String literals can also be delimited by three double quotes <code>"""</code> ... <code>"""</code>. Literals in this form may run for several lines, may contain <code>"</code> and do not interpret any escape sequences. For convenience, when the opening <code>"""</code> is followed by a newline (there may be whitespace between the opening <code>"""</code> and the newline), the newline (and the preceding whitespace) is not included in the string. The ending of the string literal is defined by the pattern <code>"""[^"]</code>, so this:</p> <pre class="listing" data-language="nim">""""long string within quotes""""</pre>
<p>Produces:</p>
<pre data-language="nim">"long string within quotes"</pre> <h3 id="lexical-analysis-raw-string-literals">Raw string literals</h3>
<p>Terminal symbol in the grammar: <code>RSTR_LIT</code>.</p> <p>There are also raw string literals that are preceded with the letter <code>r</code> (or <code>R</code>) and are delimited by matching double quotes (just like ordinary string literals) and do not interpret the escape sequences. This is especially convenient for regular expressions or Windows paths:</p> <pre class="listing" data-language="nim">var f = openFile(r"C:\texts\text.txt") # a raw string, so ``\t`` is no tab</pre>
<p>To produce a single <code>"</code> within a raw string literal, it has to be doubled:</p> <pre class="listing" data-language="nim">r"a""b"</pre>
<p>Produces:</p>
<pre data-language="nim">a"b</pre> <p><code>r""""</code> is not possible with this notation, because the three leading quotes introduce a triple quoted string literal. <code>r"""</code> is the same as <code>"""</code> since triple quoted string literals do not interpret escape sequences either.</p> <h3 id="lexical-analysis-generalized-raw-string-literals">Generalized raw string literals</h3>
<p>Terminal symbols in the grammar: <code>GENERALIZED_STR_LIT</code>, <code>GENERALIZED_TRIPLESTR_LIT</code>.</p> <p>The construct <code>identifier"string literal"</code> (without whitespace between the identifier and the opening quotation mark) is a generalized raw string literal. It is a shortcut for the construct <code>identifier(r"string literal")</code>, so it denotes a procedure call with a raw string literal as its only argument. Generalized raw string literals are especially convenient for embedding mini languages directly into Nim (for example regular expressions).</p> <p>The construct <code>identifier"""string literal"""</code> exists too. It is a shortcut for <code>identifier("""string literal""")</code>.</p> <h3 id="lexical-analysis-character-literals">Character literals</h3>
<p>Character literals are enclosed in single quotes <code>''</code> and can contain the same escape sequences as strings - with one exception: the platform dependent <span id="newline_2">newline</span> (<code>\p</code>) is not allowed as it may be wider than one character (often it is the pair CR/LF for example). Here are the valid <span id="escape-sequences_2">escape sequences</span> for character literals:</p> <table>
<tr>
<th>Escape sequence</th>
<th>Meaning</th>
</tr> <tr>
<td>
<code>\r</code>, <code>\c</code>
</td>
<td><span id="carriage-return_2">carriage return</span></td>
</tr> <tr>
<td>
<code>\n</code>, <code>\l</code>
</td>
<td><span id="line-feed_2">line feed</span></td>
</tr> <tr>
<td><code>\f</code></td>
<td><span id="form-feed_2">form feed</span></td>
</tr> <tr>
<td><code>\t</code></td>
<td><span id="tabulator_2">tabulator</span></td>
</tr> <tr>
<td><code>\v</code></td>
<td><span id="vertical-tabulator_2">vertical tabulator</span></td>
</tr> <tr>
<td><code>\\</code></td>
<td><span id="backslash_2">backslash</span></td>
</tr> <tr>
<td><code>\"</code></td>
<td><span id="quotation-mark_2">quotation mark</span></td>
</tr> <tr>
<td><code>\'</code></td>
<td><span id="apostrophe_2">apostrophe</span></td>
</tr> <tr>
<td>
<code>\</code> '0'..'9'+</td>
<td>
<span id="character-with-decimal-value-d_2">character with decimal value d</span>; all decimal digits directly following are used for the character</td>
</tr> <tr>
<td><code>\a</code></td>
<td><span id="alert_2">alert</span></td>
</tr> <tr>
<td><code>\b</code></td>
<td><span id="backspace_2">backspace</span></td>
</tr> <tr>
<td><code>\e</code></td>
<td>
<span id="escape_2">escape</span> <span id="esc_2">[ESC]</span>
</td>
</tr> <tr>
<td>
<code>\x</code> HH</td>
<td>
<span id="character-with-hex-value-hh_2">character with hex value HH</span>; exactly two hex digits are allowed</td>
</tr> </table>
<p>A character is not a Unicode character but a single byte. The reason for this is efficiency: for the overwhelming majority of use-cases, the resulting programs will still handle UTF-8 properly as UTF-8 was specially designed for this. Another reason is that Nim can thus support <code>array[char, int]</code> or <code>set[char]</code> efficiently as many algorithms rely on this feature. The <code>Rune</code> type is used for Unicode characters, it can represent any Unicode character. <code>Rune</code> is declared in the <a href="unicode.html">unicode module</a>.</p> <h3 id="lexical-analysis-numerical-constants">Numerical constants</h3>
<p>Numerical constants are of a single type and have the form:</p>
<pre data-language="nim">hexdigit = digit | 'A'..'F' | 'a'..'f'
octdigit = '0'..'7'
bindigit = '0'..'1'
HEX_LIT = '0' ('x' | 'X' ) hexdigit ( ['_'] hexdigit )*
DEC_LIT = digit ( ['_'] digit )*
OCT_LIT = '0' 'o' octdigit ( ['_'] octdigit )*
BIN_LIT = '0' ('b' | 'B' ) bindigit ( ['_'] bindigit )*

INT_LIT = HEX_LIT
        | DEC_LIT
        | OCT_LIT
        | BIN_LIT

INT8_LIT = INT_LIT ['\''] ('i' | 'I') '8'
INT16_LIT = INT_LIT ['\''] ('i' | 'I') '16'
INT32_LIT = INT_LIT ['\''] ('i' | 'I') '32'
INT64_LIT = INT_LIT ['\''] ('i' | 'I') '64'

UINT_LIT = INT_LIT ['\''] ('u' | 'U')
UINT8_LIT = INT_LIT ['\''] ('u' | 'U') '8'
UINT16_LIT = INT_LIT ['\''] ('u' | 'U') '16'
UINT32_LIT = INT_LIT ['\''] ('u' | 'U') '32'
UINT64_LIT = INT_LIT ['\''] ('u' | 'U') '64'

exponent = ('e' | 'E' ) ['+' | '-'] digit ( ['_'] digit )*
FLOAT_LIT = digit (['_'] digit)* (('.' digit (['_'] digit)* [exponent]) |exponent)
FLOAT32_SUFFIX = ('f' | 'F') ['32']
FLOAT32_LIT = HEX_LIT '\'' FLOAT32_SUFFIX
            | (FLOAT_LIT | DEC_LIT | OCT_LIT | BIN_LIT) ['\''] FLOAT32_SUFFIX
FLOAT64_SUFFIX = ( ('f' | 'F') '64' ) | 'd' | 'D'
FLOAT64_LIT = HEX_LIT '\'' FLOAT64_SUFFIX
            | (FLOAT_LIT | DEC_LIT | OCT_LIT | BIN_LIT) ['\''] FLOAT64_SUFFIX</pre> <p>As can be seen in the productions, numerical constants can contain underscores for readability. Integer and floating-point literals may be given in decimal (no prefix), binary (prefix <code>0b</code>), octal (prefix <code>0o</code>), and hexadecimal (prefix <code>0x</code>) notation.</p> <p>There exists a literal for each numerical type that is defined. The suffix starting with an apostrophe (''') is called a <span id="type-suffix_1">type suffix</span>. Literals without a type suffix are of an integer type unless the literal contains a dot or <code>E|e</code> in which case it is of type <code>float</code>. This integer type is <code>int</code> if the literal is in the range <code>low(i32)..high(i32)</code>, otherwise it is <code>int64</code>. For notational convenience, the apostrophe of a type suffix is optional if it is not ambiguous (only hexadecimal floating-point literals with a type suffix can be ambiguous).</p> <p>The type suffixes are:</p> <table>
<tr>
<th>Type Suffix</th>
<th>Resulting type of literal</th>
</tr> <tr>
<td><code>'i8</code></td>
<td>int8</td>
</tr> <tr>
<td><code>'i16</code></td>
<td>int16</td>
</tr> <tr>
<td><code>'i32</code></td>
<td>int32</td>
</tr> <tr>
<td><code>'i64</code></td>
<td>int64</td>
</tr> <tr>
<td><code>'u</code></td>
<td>uint</td>
</tr> <tr>
<td><code>'u8</code></td>
<td>uint8</td>
</tr> <tr>
<td><code>'u16</code></td>
<td>uint16</td>
</tr> <tr>
<td><code>'u32</code></td>
<td>uint32</td>
</tr> <tr>
<td><code>'u64</code></td>
<td>uint64</td>
</tr> <tr>
<td><code>'f</code></td>
<td>float32</td>
</tr> <tr>
<td><code>'d</code></td>
<td>float64</td>
</tr> <tr>
<td><code>'f32</code></td>
<td>float32</td>
</tr> <tr>
<td><code>'f64</code></td>
<td>float64</td>
</tr> </table>
<p>Floating-point literals may also be in binary, octal or hexadecimal notation: <code>0B0_10001110100_0000101001000111101011101111111011000101001101001001'f64</code> is approximately 1.72826e35 according to the IEEE floating-point standard.</p> <p>Literals are bounds checked so that they fit the datatype. Non-base-10 literals are used mainly for flags and bit pattern representations, therefore bounds checking is done on bit width, not value range. If the literal fits in the bit width of the datatype, it is accepted. Hence: 0b10000000'u8 == 0x80'u8 == 128, but, 0b10000000'i8 == 0x80'i8 == -1 instead of causing an overflow error.</p> <h3 id="lexical-analysis-operators">Operators</h3>
<p>Nim allows user defined operators. An operator is any combination of the following characters:</p>
<pre data-language="nim">=     +     -     *     /     &lt;     &gt;
@     $     ~     &amp;     %     |
!     ?     ^     .     :     \</pre> <p>(The grammar uses the terminal OPR to refer to operator symbols as defined here.)</p> <p>These keywords are also operators: <code>and or not xor shl shr div mod in notin is isnot of as from</code>.</p> <p><span class="tok">.</span> <span class="tok">=</span>, <span class="tok">:</span>, <span class="tok">::</span> are not available as general operators; they are used for other notational purposes.</p> <p><code>*:</code> is as a special case treated as the two tokens <span class="tok">*</span> and <span class="tok">:</span> (to support <code>var v*: T</code>).</p> <p>The <code>not</code> keyword is always a unary operator, <code>a not b</code> is parsed as <code>a(not b)</code>, not as <code>(a) not (b)</code>.</p> <h3 id="lexical-analysis-other-tokens">Other tokens</h3>
<p>The following strings denote other tokens:</p>
<pre data-language="nim">`   (    )     {    }     [    ]    ,  ;   [.    .]  {.   .}  (.  .)  [:</pre> <p>The <span id="slice_1">slice</span> operator <span class="tok">..</span> takes precedence over other tokens that contain a dot: <span class="tok">{..}</span> are the three tokens <span class="tok">{</span>, <span class="tok">..</span>, <span class="tok">}</span> and not the two tokens <span class="tok">{.</span>, <span class="tok">.}</span>.</p> <h2 id="syntax">Syntax</h2>
<p>This section lists Nim's standard syntax. How the parser handles the indentation is already described in the <a href="#lexical-analysis">Lexical Analysis</a> section.</p> <p>Nim allows user-definable operators. Binary operators have 11 different levels of precedence.</p> <h3 id="syntax-associativity">Associativity</h3>
<p>Binary operators whose first character is <code>^</code> are right-associative, all other binary operators are left-associative.</p> <pre class="listing" data-language="nim">proc `^/`(x, y: float): float =
  # a right-associative division operator
  result = x / y
echo 12 ^/ 4 ^/ 8 # 24.0 (4 / 8 = 0.5, then 12 / 0.5 = 24.0)
echo 12  / 4  / 8 # 0.375 (12 / 4 = 3.0, then 3 / 8 = 0.375)</pre> <h3 id="syntax-precedence">Precedence</h3>
<p>Unary operators always bind stronger than any binary operator: <code>$a + b</code> is <code>($a) + b</code> and not <code>$(a + b)</code>.</p> <p>If an unary operator's first character is <code>@</code> it is a <span id="sigilminuslike_1">sigil-like</span> operator which binds stronger than a <code>primarySuffix</code>: <code>@x.abc</code> is parsed as <code>(@x).abc</code> whereas <code>$x.abc</code> is parsed as <code>$(x.abc)</code>.</p> <p>For binary operators that are not keywords, the precedence is determined by the following rules:</p> <p>Operators ending in either <code>-&gt;</code>, <code>~&gt;</code> or <code>=&gt;</code> are called <span id="arrow-like_1">arrow like</span>, and have the lowest precedence of all operators.</p> <p>If the operator ends with <code>=</code> and its first character is none of <code>&lt;</code>, <code>&gt;</code>, <code>!</code>, <code>=</code>, <code>~</code>, <code>?</code>, it is an <em>assignment operator</em> which has the second-lowest precedence.</p> <p>Otherwise, precedence is determined by the first character.</p> <table>
<tr>
<th>Precedence level</th>
<th>Operators</th>
<th>First character</th>
<th>Terminal symbol</th>
</tr> <tr>
<td>10 (highest)</td>
<td></td>
<td><code>$ ^</code></td>
<td>OP10</td>
</tr> <tr>
<td>9</td>
<td><code>* / div mod shl shr %</code></td>
<td><code>* % \ /</code></td>
<td>OP9</td>
</tr> <tr>
<td>8</td>
<td><code>+ -</code></td>
<td><code>+ - ~ |</code></td>
<td>OP8</td>
</tr> <tr>
<td>7</td>
<td><code>&amp;</code></td>
<td><code>&amp;</code></td>
<td>OP7</td>
</tr> <tr>
<td>6</td>
<td><code>..</code></td>
<td><code>.</code></td>
<td>OP6</td>
</tr> <tr>
<td>5</td>
<td><code>== &lt;= &lt; &gt;= &gt; != in notin is isnot not of as from</code></td>
<td><code>= &lt; &gt; !</code></td>
<td>OP5</td>
</tr> <tr>
<td>4</td>
<td><code>and</code></td>
<td></td>
<td>OP4</td>
</tr> <tr>
<td>3</td>
<td><code>or xor</code></td>
<td></td>
<td>OP3</td>
</tr> <tr>
<td>2</td>
<td></td>
<td><code>@ : ?</code></td>
<td>OP2</td>
</tr> <tr>
<td>1</td>
<td>
<em>assignment operator</em> (like <code>+=</code>, <code>*=</code>)</td>
<td></td>
<td>OP1</td>
</tr> <tr>
<td>0 (lowest)</td>
<td>
<em>arrow like operator</em> (like <code>-&gt;</code>, <code>=&gt;</code>)</td>
<td></td>
<td>OP0</td>
</tr> </table>
<p>Whether an operator is used as a prefix operator is also affected by preceding whitespace (this parsing change was introduced with version 0.13.0):</p> <pre class="listing" data-language="nim">echo $foo
# is parsed as
echo($foo)</pre>
<p>Spacing also determines whether <code>(a, b)</code> is parsed as an argument list of a call or whether it is parsed as a tuple constructor:</p> <pre class="listing" data-language="nim">echo(1, 2) # pass 1 and 2 to echo</pre>
<pre class="listing" data-language="nim">echo (1, 2) # pass the tuple (1, 2) to echo</pre> <h3 id="syntax-grammar">Grammar</h3>
<p>The grammar's start symbol is <code>module</code>.</p> <pre data-language="nim"># This file is generated by compiler/parser.nim.
module = stmt ^* (';' / IND{=})
comma = ',' COMMENT?
semicolon = ';' COMMENT?
colon = ':' COMMENT?
colcom = ':' COMMENT?
operator =  OP0 | OP1 | OP2 | OP3 | OP4 | OP5 | OP6 | OP7 | OP8 | OP9
         | 'or' | 'xor' | 'and'
         | 'is' | 'isnot' | 'in' | 'notin' | 'of' | 'as' | 'from'
         | 'div' | 'mod' | 'shl' | 'shr' | 'not' | 'static' | '..'
prefixOperator = operator
optInd = COMMENT? IND?
optPar = (IND{&gt;} | IND{=})?
simpleExpr = arrowExpr (OP0 optInd arrowExpr)* pragma?
arrowExpr = assignExpr (OP1 optInd assignExpr)*
assignExpr = orExpr (OP2 optInd orExpr)*
orExpr = andExpr (OP3 optInd andExpr)*
andExpr = cmpExpr (OP4 optInd cmpExpr)*
cmpExpr = sliceExpr (OP5 optInd sliceExpr)*
sliceExpr = ampExpr (OP6 optInd ampExpr)*
ampExpr = plusExpr (OP7 optInd plusExpr)*
plusExpr = mulExpr (OP8 optInd mulExpr)*
mulExpr = dollarExpr (OP9 optInd dollarExpr)*
dollarExpr = primary (OP10 optInd primary)*
symbol = '`' (KEYW|IDENT|literal|(operator|'('|')'|'['|']'|'{'|'}'|'=')+)+ '`'
       | IDENT | KEYW
exprColonEqExpr = expr (':'|'=' expr)?
exprList = expr ^+ comma
exprColonEqExprList = exprColonEqExpr (comma exprColonEqExpr)* (comma)?
dotExpr = expr '.' optInd (symbol | '[:' exprList ']')
explicitGenericInstantiation = '[:' exprList ']' ( '(' exprColonEqExpr ')' )?
qualifiedIdent = symbol ('.' optInd symbol)?
setOrTableConstr = '{' ((exprColonEqExpr comma)* | ':' ) '}'
castExpr = 'cast' ('[' optInd typeDesc optPar ']' '(' optInd expr optPar ')') /
parKeyw = 'discard' | 'include' | 'if' | 'while' | 'case' | 'try'
        | 'finally' | 'except' | 'for' | 'block' | 'const' | 'let'
        | 'when' | 'var' | 'mixin'
par = '(' optInd
          ( &amp;parKeyw (ifExpr \ complexOrSimpleStmt) ^+ ';'
          | ';' (ifExpr \ complexOrSimpleStmt) ^+ ';'
          | pragmaStmt
          | simpleExpr ( ('=' expr (';' (ifExpr \ complexOrSimpleStmt) ^+ ';' )? )
                       | (':' expr (',' exprColonEqExpr     ^+ ',' )? ) ) )
          optPar ')'
literal = | INT_LIT | INT8_LIT | INT16_LIT | INT32_LIT | INT64_LIT
          | UINT_LIT | UINT8_LIT | UINT16_LIT | UINT32_LIT | UINT64_LIT
          | FLOAT_LIT | FLOAT32_LIT | FLOAT64_LIT
          | STR_LIT | RSTR_LIT | TRIPLESTR_LIT
          | CHAR_LIT
          | NIL
generalizedLit = GENERALIZED_STR_LIT | GENERALIZED_TRIPLESTR_LIT
identOrLiteral = generalizedLit | symbol | literal
               | par | arrayConstr | setOrTableConstr
               | castExpr
tupleConstr = '(' optInd (exprColonEqExpr comma?)* optPar ')'
arrayConstr = '[' optInd (exprColonEqExpr comma?)* optPar ']'
primarySuffix = '(' (exprColonEqExpr comma?)* ')'
      | '.' optInd symbol generalizedLit?
      | '[' optInd exprColonEqExprList optPar ']'
      | '{' optInd exprColonEqExprList optPar '}'
      | &amp;( '`'|IDENT|literal|'cast'|'addr'|'type') expr # command syntax
pragma = '{.' optInd (exprColonEqExpr comma?)* optPar ('.}' | '}')
identVis = symbol OPR?  # postfix position
identVisDot = symbol '.' optInd symbol OPR?
identWithPragma = identVis pragma?
identWithPragmaDot = identVisDot pragma?
declColonEquals = identWithPragma (comma identWithPragma)* comma?
                  (':' optInd typeDesc)? ('=' optInd expr)?
identColonEquals = IDENT (comma IDENT)* comma?
     (':' optInd typeDesc)? ('=' optInd expr)?)
inlTupleDecl = 'tuple'
    '[' optInd  (identColonEquals (comma/semicolon)?)*  optPar ']'
extTupleDecl = 'tuple'
    COMMENT? (IND{&gt;} identColonEquals (IND{=} identColonEquals)*)?
tupleClass = 'tuple'
paramList = '(' declColonEquals ^* (comma/semicolon) ')'
paramListArrow = paramList? ('-&gt;' optInd typeDesc)?
paramListColon = paramList? (':' optInd typeDesc)?
doBlock = 'do' paramListArrow pragma? colcom stmt
procExpr = 'proc' paramListColon pragma? ('=' COMMENT? stmt)?
distinct = 'distinct' optInd typeDesc
forStmt = 'for' (identWithPragma ^+ comma) 'in' expr colcom stmt
forExpr = forStmt
expr = (blockExpr
      | ifExpr
      | whenExpr
      | caseStmt
      | forExpr
      | tryExpr)
      / simpleExpr
typeKeyw = 'var' | 'out' | 'ref' | 'ptr' | 'shared' | 'tuple'
         | 'proc' | 'iterator' | 'distinct' | 'object' | 'enum'
primary = typeKeyw optInd typeDesc
        /  prefixOperator* identOrLiteral primarySuffix*
        / 'bind' primary
typeDesc = simpleExpr ('not' expr)?
typeDefAux = simpleExpr ('not' expr)?
           | 'concept' typeClass
postExprBlocks = ':' stmt? ( IND{=} doBlock
                           | IND{=} 'of' exprList ':' stmt
                           | IND{=} 'elif' expr ':' stmt
                           | IND{=} 'except' exprList ':' stmt
                           | IND{=} 'else' ':' stmt )*
exprStmt = simpleExpr
         (( '=' optInd expr colonBody? )
         / ( expr ^+ comma
             postExprBlocks
           ))?
importStmt = 'import' optInd expr
              ((comma expr)*
              / 'except' optInd (expr ^+ comma))
exportStmt = 'export' optInd expr
              ((comma expr)*
              / 'except' optInd (expr ^+ comma))
includeStmt = 'include' optInd expr ^+ comma
fromStmt = 'from' expr 'import' optInd expr (comma expr)*
returnStmt = 'return' optInd expr?
raiseStmt = 'raise' optInd expr?
yieldStmt = 'yield' optInd expr?
discardStmt = 'discard' optInd expr?
breakStmt = 'break' optInd expr?
continueStmt = 'break' optInd expr?
condStmt = expr colcom stmt COMMENT?
           (IND{=} 'elif' expr colcom stmt)*
           (IND{=} 'else' colcom stmt)?
ifStmt = 'if' condStmt
whenStmt = 'when' condStmt
condExpr = expr colcom expr optInd
        ('elif' expr colcom expr optInd)*
         'else' colcom expr
ifExpr = 'if' condExpr
whenExpr = 'when' condExpr
whileStmt = 'while' expr colcom stmt
ofBranch = 'of' exprList colcom stmt
ofBranches = ofBranch (IND{=} ofBranch)*
                      (IND{=} 'elif' expr colcom stmt)*
                      (IND{=} 'else' colcom stmt)?
caseStmt = 'case' expr ':'? COMMENT?
            (IND{&gt;} ofBranches DED
            | IND{=} ofBranches)
tryStmt = 'try' colcom stmt &amp;(IND{=}? 'except'|'finally')
           (IND{=}? 'except' exprList colcom stmt)*
           (IND{=}? 'finally' colcom stmt)?
tryExpr = 'try' colcom stmt &amp;(optInd 'except'|'finally')
           (optInd 'except' exprList colcom stmt)*
           (optInd 'finally' colcom stmt)?
exceptBlock = 'except' colcom stmt
blockStmt = 'block' symbol? colcom stmt
blockExpr = 'block' symbol? colcom stmt
staticStmt = 'static' colcom stmt
deferStmt = 'defer' colcom stmt
asmStmt = 'asm' pragma? (STR_LIT | RSTR_LIT | TRIPLESTR_LIT)
genericParam = symbol (comma symbol)* (colon expr)? ('=' optInd expr)?
genericParamList = '[' optInd
  genericParam ^* (comma/semicolon) optPar ']'
pattern = '{' stmt '}'
indAndComment = (IND{&gt;} COMMENT)? | COMMENT?
routine = optInd identVis pattern? genericParamList?
  paramListColon pragma? ('=' COMMENT? stmt)? indAndComment
commentStmt = COMMENT
section(RULE) = COMMENT? RULE / (IND{&gt;} (RULE / COMMENT)^+IND{=} DED)
enum = 'enum' optInd (symbol pragma? optInd ('=' optInd expr COMMENT?)? comma?)+
objectWhen = 'when' expr colcom objectPart COMMENT?
            ('elif' expr colcom objectPart COMMENT?)*
            ('else' colcom objectPart COMMENT?)?
objectBranch = 'of' exprList colcom objectPart
objectBranches = objectBranch (IND{=} objectBranch)*
                      (IND{=} 'elif' expr colcom objectPart)*
                      (IND{=} 'else' colcom objectPart)?
objectCase = 'case' identWithPragma ':' typeDesc ':'? COMMENT?
            (IND{&gt;} objectBranches DED
            | IND{=} objectBranches)
objectPart = IND{&gt;} objectPart^+IND{=} DED
           / objectWhen / objectCase / 'nil' / 'discard' / declColonEquals
object = 'object' pragma? ('of' typeDesc)? COMMENT? objectPart
typeClassParam = ('var' | 'out')? symbol
typeClass = typeClassParam ^* ',' (pragma)? ('of' typeDesc ^* ',')?
              &amp;IND{&gt;} stmt
typeDef = identWithPragmaDot genericParamList? '=' optInd typeDefAux
            indAndComment? / identVisDot genericParamList? pragma '=' optInd typeDefAux
            indAndComment?
varTuple = '(' optInd identWithPragma ^+ comma optPar ')' '=' optInd expr
colonBody = colcom stmt postExprBlocks?
variable = (varTuple / identColonEquals) colonBody? indAndComment
constant = (varTuple / identWithPragma) (colon typeDesc)? '=' optInd expr indAndComment
bindStmt = 'bind' optInd qualifiedIdent ^+ comma
mixinStmt = 'mixin' optInd qualifiedIdent ^+ comma
pragmaStmt = pragma (':' COMMENT? stmt)?
simpleStmt = ((returnStmt | raiseStmt | yieldStmt | discardStmt | breakStmt
           | continueStmt | pragmaStmt | importStmt | exportStmt | fromStmt
           | includeStmt | commentStmt) / exprStmt) COMMENT?
complexOrSimpleStmt = (ifStmt | whenStmt | whileStmt
                    | tryStmt | forStmt
                    | blockStmt | staticStmt | deferStmt | asmStmt
                    | 'proc' routine
                    | 'method' routine
                    | 'func' routine
                    | 'iterator' routine
                    | 'macro' routine
                    | 'template' routine
                    | 'converter' routine
                    | 'type' section(typeDef)
                    | 'const' section(constant)
                    | ('let' | 'var' | 'using') section(variable)
                    | bindStmt | mixinStmt)
                    / simpleStmt
stmt = (IND{&gt;} complexOrSimpleStmt^+(IND{=} / ';') DED)
     / simpleStmt ^+ ';'</pre> <h2 id="order-of-evaluation">Order of evaluation</h2>
<p>Order of evaluation is strictly left-to-right, inside-out as it is typical for most others imperative programming languages:</p> <pre class="listing" data-language="nim">var s = ""

proc p(arg: int): int =
  s.add $arg
  result = arg

discard p(p(1) + p(2))

doAssert s == "123"</pre>
<p>Assignments are not special, the left-hand-side expression is evaluated before the right-hand side:</p> <pre class="listing" data-language="nim">var v = 0
proc getI(): int =
  result = v
  inc v

var a, b: array[0..2, int]

proc someCopy(a: var int; b: int) = a = b

a[getI()] = getI()

doAssert a == [1, 0, 0]

v = 0
someCopy(b[getI()], getI())

doAssert b == [1, 0, 0]</pre>
<p>Rationale: Consistency with overloaded assignment or assignment-like operations, <code>a = b</code> can be read as <code>performSomeCopy(a, b)</code>.</p> <p>However, the concept of "order of evaluation" is only applicable after the code was normalized: The normalization involves template expansions and argument reorderings that have been passed to named parameters:</p> <pre class="listing" data-language="nim">var s = ""

proc p(): int =
  s.add "p"
  result = 5

proc q(): int =
  s.add "q"
  result = 3

# Evaluation order is 'b' before 'a' due to template
# expansion's semantics.
template swapArgs(a, b): untyped =
  b + a

doAssert swapArgs(p() + q(), q() - p()) == 6
doAssert s == "qppq"

# Evaluation order is not influenced by named parameters:
proc construct(first, second: int) =
  discard

# 'p' is evaluated before 'q'!
construct(second = q(), first = p())

doAssert s == "qppqpq"</pre>
<p>Rationale: This is far easier to implement than hypothetical alternatives.</p> <h2 id="constants-and-constant-expressions">Constants and Constant Expressions</h2>
<p>A <span id="constant_1">constant</span> is a symbol that is bound to the value of a constant expression. Constant expressions are restricted to depend only on the following categories of values and operations, because these are either built into the language or declared and evaluated before semantic analysis of the constant expression:</p> <ul class="simple">
<li>literals</li> <li>built-in operators</li> <li>previously declared constants and compile-time variables</li> <li>previously declared macros and templates</li> <li>previously declared procedures that have no side effects beyond possibly modifying compile-time variables</li> </ul> <p>A constant expression can contain code blocks that may internally use all Nim features supported at compile time (as detailed in the next section below). Within such a code block, it is possible to declare variables and then later read and update them, or declare variables and pass them to procedures that modify them. However, the code in such a block must still adhere to the restrictions listed above for referencing values and operations outside the block.</p> <p>The ability to access and modify compile-time variables adds flexibility to constant expressions that may be surprising to those coming from other statically typed languages. For example, the following code echoes the beginning of the Fibonacci series <strong>at compile-time</strong>. (This is a demonstration of flexibility in defining constants, not a recommended style for solving this problem!)</p> <pre class="listing" data-language="nim">import strformat

var fib_n {.compileTime.}: int
var fib_prev {.compileTime.}: int
var fib_prev_prev {.compileTime.}: int

proc next_fib(): int =
  result = if fib_n &lt; 2:
    fib_n
  else:
    fib_prev_prev + fib_prev
  inc(fib_n)
  fib_prev_prev = fib_prev
  fib_prev = result

const f0 = next_fib()
const f1 = next_fib()

const display_fib = block:
  const f2 = next_fib()
  var result = fmt"Fibonacci sequence: {f0}, {f1}, {f2}"
  for i in 3..12:
    add(result, fmt", {next_fib()}")
  result

static:
  echo display_fib</pre> <h2 id="restrictions-on-compileminustime-execution">Restrictions on Compile-Time Execution</h2>
<p>Nim code that will be executed at compile time cannot use the following language features:</p> <ul class="simple">
<li>methods</li> <li>closure iterators</li> <li>the <code>cast</code> operator</li> <li>reference (pointer) types</li> <li>FFI</li> </ul> <p>The use of wrappers that use FFI and/or <code>cast</code> is also disallowed. Note that these wrappers include the ones in the standard libraries.</p> <p>Some or all of these restrictions are likely to be lifted over time.</p> <h2 id="types">Types</h2>
<p>All expressions have a type that is known during semantic analysis. Nim is statically typed. One can declare new types, which is in essence defining an identifier that can be used to denote this custom type.</p> <p>These are the major type classes:</p> <ul class="simple">
<li>ordinal types (consist of integer, bool, character, enumeration (and subranges thereof) types)</li> <li>floating-point types</li> <li>string type</li> <li>structured types</li> <li>reference (pointer) type</li> <li>procedural type</li> <li>generic type</li> </ul> <h3 id="types-ordinal-types">Ordinal types</h3>
<p>Ordinal types have the following characteristics:</p> <ul class="simple">
<li>Ordinal types are countable and ordered. This property allows the operation of functions as <code>inc</code>, <code>ord</code>, <code>dec</code> on ordinal types to be defined.</li> <li>Ordinal values have the smallest possible value. Trying to count further down than the smallest value produces a panic or a static error.</li> <li>Ordinal values have the largest possible value. Trying to count further than the largest value produces a panic or a static error.</li> </ul> <p>Integers, bool, characters, and enumeration types (and subranges of these types) belong to ordinal types. For reasons of simplicity of implementation the types <code>uint</code> and <code>uint64</code> are not ordinal types. (This will be changed in later versions of the language.)</p> <p>A distinct type is an ordinal type if its base type is an ordinal type.</p> <h3 id="types-preminusdefined-integer-types">Pre-defined integer types</h3>
<p>These integer types are pre-defined:</p> <dl>
<dt><code>int</code></dt> <dd>the generic signed integer type; its size is platform-dependent and has the same size as a pointer. This type should be used in general. An integer literal that has no type suffix is of this type if it is in the range <code>low(int32)..high(int32)</code> otherwise the literal's type is <code>int64</code>.</dd> <dt>intXX</dt> <dd>additional signed integer types of XX bits use this naming scheme (example: int16 is a 16-bit wide integer). The current implementation supports <code>int8</code>, <code>int16</code>, <code>int32</code>, <code>int64</code>. Literals of these types have the suffix 'iXX.</dd> <dt><code>uint</code></dt> <dd>the generic <span id="unsigned-integer_1">unsigned integer</span> type; its size is platform-dependent and has the same size as a pointer. An integer literal with the type suffix <code>'u</code> is of this type.</dd> <dt>uintXX</dt> <dd>additional unsigned integer types of XX bits use this naming scheme (example: uint16 is a 16-bit wide unsigned integer). The current implementation supports <code>uint8</code>, <code>uint16</code>, <code>uint32</code>, <code>uint64</code>. Literals of these types have the suffix 'uXX. Unsigned operations all wrap around; they cannot lead to over- or underflow errors.</dd> </dl> <p>In addition to the usual arithmetic operators for signed and unsigned integers (<code>+ - *</code> etc.) there are also operators that formally work on <em>signed</em> integers but treat their arguments as <em>unsigned</em>: They are mostly provided for backwards compatibility with older versions of the language that lacked unsigned integer types. These unsigned operations for signed integers use the <code>%</code> suffix as convention:</p> <table>
<tr>
<th>operation</th>
<th>meaning</th>
</tr> <tr>
<td><code>a +% b</code></td>
<td>unsigned integer addition</td>
</tr> <tr>
<td><code>a -% b</code></td>
<td>unsigned integer subtraction</td>
</tr> <tr>
<td><code>a *% b</code></td>
<td>unsigned integer multiplication</td>
</tr> <tr>
<td><code>a /% b</code></td>
<td>unsigned integer division</td>
</tr> <tr>
<td><code>a %% b</code></td>
<td>unsigned integer modulo operation</td>
</tr> <tr>
<td><code>a &lt;% b</code></td>
<td>treat <code>a</code> and <code>b</code> as unsigned and compare</td>
</tr> <tr>
<td><code>a &lt;=% b</code></td>
<td>treat <code>a</code> and <code>b</code> as unsigned and compare</td>
</tr> <tr>
<td><code>ze(a)</code></td>
<td>extends the bits of <code>a</code> with zeros until it has the width of the <code>int</code> type</td>
</tr> <tr>
<td><code>toU8(a)</code></td>
<td>treats <code>a</code> as unsigned and converts it to an unsigned integer of 8 bits (but still the <code>int8</code> type)</td>
</tr> <tr>
<td><code>toU16(a)</code></td>
<td>treats <code>a</code> as unsigned and converts it to an unsigned integer of 16 bits (but still the <code>int16</code> type)</td>
</tr> <tr>
<td><code>toU32(a)</code></td>
<td>treats <code>a</code> as unsigned and converts it to an unsigned integer of 32 bits (but still the <code>int32</code> type)</td>
</tr> </table>
<p><span id="automatic-type-conversion_1">Automatic type conversion</span> is performed in expressions where different kinds of integer types are used: the smaller type is converted to the larger.</p> <p>A <span id="narrowing-type-conversion_1">narrowing type conversion</span> converts a larger to a smaller type (for example <code>int32 -&gt; int16</code>. A <span id="widening-type-conversion_1">widening type conversion</span> converts a smaller type to a larger type (for example <code>int16 -&gt; int32</code>). In Nim only widening type conversions are <em>implicit</em>:</p> <pre class="listing" data-language="nim">var myInt16 = 5i16
var myInt: int
myInt16 + 34     # of type ``int16``
myInt16 + myInt  # of type ``int``
myInt16 + 2i32   # of type ``int32``</pre>
<p>However, <code>int</code> literals are implicitly convertible to a smaller integer type if the literal's value fits this smaller type and such a conversion is less expensive than other implicit conversions, so <code>myInt16 + 34</code> produces an <code>int16</code> result.</p> <p>For further details, see <a href="#type-relations-convertible-relation">Convertible relation</a>.</p> <h3 id="types-subrange-types">Subrange types</h3>
<p>A subrange type is a range of values from an ordinal or floating-point type (the base type). To define a subrange type, one must specify its limiting values -- the lowest and highest value of the type. For example:</p> <pre class="listing" data-language="nim">type
  Subrange = range[0..5]
  PositiveFloat = range[0.0..Inf]</pre>
<p><code>Subrange</code> is a subrange of an integer which can only hold the values 0 to 5. <code>PositiveFloat</code> defines a subrange of all positive floating-point values. NaN does not belong to any subrange of floating-point types. Assigning any other value to a variable of type <code>Subrange</code> is a panic (or a static error if it can be determined during semantic analysis). Assignments from the base type to one of its subrange types (and vice versa) are allowed.</p> <p>A subrange type has the same size as its base type (<code>int</code> in the Subrange example).</p> <h3 id="types-preminusdefined-floatingminuspoint-types">Pre-defined floating-point types</h3>
<p>The following floating-point types are pre-defined:</p> <dl>
<dt><code>float</code></dt> <dd>the generic floating-point type; its size used to be platform-dependent, but now it is always mapped to <code>float64</code>. This type should be used in general.</dd> <dt>floatXX</dt> <dd>an implementation may define additional floating-point types of XX bits using this naming scheme (example: float64 is a 64-bit wide float). The current implementation supports <code>float32</code> and <code>float64</code>. Literals of these types have the suffix 'fXX.</dd> </dl> <p>Automatic type conversion in expressions with different kinds of floating-point types is performed: See <a href="#type-relations-convertible-relation">Convertible relation</a> for further details. Arithmetic performed on floating-point types follows the IEEE standard. Integer types are not converted to floating-point types automatically and vice versa.</p> <p>The IEEE standard defines five types of floating-point exceptions:</p> <ul class="simple">
<li>Invalid: operations with mathematically invalid operands, for example 0.0/0.0, sqrt(-1.0), and log(-37.8).</li> <li>Division by zero: divisor is zero and dividend is a finite nonzero number, for example 1.0/0.0.</li> <li>Overflow: operation produces a result that exceeds the range of the exponent, for example MAXDOUBLE+0.0000000000001e308.</li> <li>Underflow: operation produces a result that is too small to be represented as a normal number, for example, MINDOUBLE * MINDOUBLE.</li> <li>Inexact: operation produces a result that cannot be represented with infinite precision, for example, 2.0 / 3.0, log(1.1) and 0.1 in input.</li> </ul> <p>The IEEE exceptions are either ignored during execution or mapped to the Nim exceptions: <span id="floatinvalidopdefect_1">FloatInvalidOpDefect</span>, <span id="floatdivbyzerodefect_1">FloatDivByZeroDefect</span>, <span id="floatoverflowdefect_1">FloatOverflowDefect</span>, <span id="floatunderflowdefect_1">FloatUnderflowDefect</span>, and <span id="floatinexactdefect_1">FloatInexactDefect</span>. These exceptions inherit from the <span id="floatingpointdefect_1">FloatingPointDefect</span> base class.</p> <p>Nim provides the pragmas <span id="nanchecks_1">nanChecks</span> and <span id="infchecks_1">infChecks</span> to control whether the IEEE exceptions are ignored or trap a Nim exception:</p> <pre class="listing" data-language="nim">{.nanChecks: on, infChecks: on.}
var a = 1.0
var b = 0.0
echo b / b # raises FloatInvalidOpDefect
echo a / b # raises FloatOverflowDefect</pre>
<p>In the current implementation <code>FloatDivByZeroDefect</code> and <code>FloatInexactDefect</code> are never raised. <code>FloatOverflowDefect</code> is raised instead of <code>FloatDivByZeroDefect</code>. There is also a <span id="floatchecks_1">floatChecks</span> pragma that is a short-cut for the combination of <code>nanChecks</code> and <code>infChecks</code> pragmas. <code>floatChecks</code> are turned off as default.</p> <p>The only operations that are affected by the <code>floatChecks</code> pragma are the <code>+</code>, <code>-</code>, <code>*</code>, <code>/</code> operators for floating-point types.</p> <p>An implementation should always use the maximum precision available to evaluate floating pointer values during semantic analysis; this means expressions like <code>0.09'f32 + 0.01'f32 == 0.09'f64 + 0.01'f64</code> that are evaluating during constant folding are true.</p> <h3 id="types-boolean-type">Boolean type</h3>
<p>The boolean type is named <span id="bool_1">bool</span> in Nim and can be one of the two pre-defined values <code>true</code> and <code>false</code>. Conditions in <code>while</code>, <code>if</code>, <code>elif</code>, <code>when</code>-statements need to be of type <code>bool</code>.</p> <p>This condition holds:</p>
<pre data-language="nim">ord(false) == 0 and ord(true) == 1</pre> <p>The operators <code>not, and, or, xor, &lt;, &lt;=, &gt;, &gt;=, !=, ==</code> are defined for the bool type. The <code>and</code> and <code>or</code> operators perform short-cut evaluation. Example:</p> <pre class="listing" data-language="nim">while p != nil and p.name != "xyz":
  # p.name is not evaluated if p == nil
  p = p.next</pre>
<p>The size of the bool type is one byte.</p> <h3 id="types-character-type">Character type</h3>
<p>The character type is named <code>char</code> in Nim. Its size is one byte. Thus it cannot represent a UTF-8 character, but a part of it. The reason for this is efficiency: for the overwhelming majority of use-cases, the resulting programs will still handle UTF-8 properly as UTF-8 was especially designed for this. Another reason is that Nim can support <code>array[char, int]</code> or <code>set[char]</code> efficiently as many algorithms rely on this feature. The <code>Rune</code> type is used for Unicode characters, it can represent any Unicode character. <code>Rune</code> is declared in the <a href="unicode.html">unicode module</a>.</p> <h3 id="types-enumeration-types">Enumeration types</h3>
<p>Enumeration types define a new type whose values consist of the ones specified. The values are ordered. Example:</p> <pre class="listing" data-language="nim">type
  Direction = enum
    north, east, south, west</pre>
<p>Now the following holds:</p>
<pre data-language="nim">ord(north) == 0
ord(east) == 1
ord(south) == 2
ord(west) == 3

# Also allowed:
ord(Direction.west) == 3</pre> <p>Thus, north &lt; east &lt; south &lt; west. The comparison operators can be used with enumeration types. Instead of <code>north</code> etc, the enum value can also be qualified with the enum type that it resides in, <code>Direction.north</code>.</p> <p>For better interfacing to other programming languages, the fields of enum types can be assigned an explicit ordinal value. However, the ordinal values have to be in ascending order. A field whose ordinal value is not explicitly given is assigned the value of the previous field + 1.</p> <p>An explicit ordered enum can have <em>holes</em>:</p> <pre class="listing" data-language="nim">type
  TokenType = enum
    a = 2, b = 4, c = 89 # holes are valid</pre>
<p>However, it is then not ordinal anymore, so it is not possible to use these enums as an index type for arrays. The procedures <code>inc</code>, <code>dec</code>, <code>succ</code> and <code>pred</code> are not available for them either.</p> <p>The compiler supports the built-in stringify operator <code>$</code> for enumerations. The stringify's result can be controlled by explicitly giving the string values to use:</p> <pre class="listing" data-language="nim">type
  MyEnum = enum
    valueA = (0, "my value A"),
    valueB = "value B",
    valueC = 2,
    valueD = (3, "abc")</pre>
<p>As can be seen from the example, it is possible to both specify a field's ordinal value and its string value by using a tuple. It is also possible to only specify one of them.</p> <p>An enum can be marked with the <code>pure</code> pragma so that its fields are added to a special module-specific hidden scope that is only queried as the last attempt. Only non-ambiguous symbols are added to this scope. But one can always access these via type qualification written as <code>MyEnum.value</code>:</p> <pre class="listing" data-language="nim">type
  MyEnum {.pure.} = enum
    valueA, valueB, valueC, valueD, amb
  
  OtherEnum {.pure.} = enum
    valueX, valueY, valueZ, amb


echo valueA # MyEnum.valueA
echo amb    # Error: Unclear whether it's MyEnum.amb or OtherEnum.amb
echo MyEnum.amb # OK.</pre>
<p>To implement bit fields with enums see <a href="#set-type-bit-fields">Bit fields</a></p> <h3 id="types-string-type">String type</h3>
<p>All string literals are of the type <code>string</code>. A string in Nim is very similar to a sequence of characters. However, strings in Nim are both zero-terminated and have a length field. One can retrieve the length with the builtin <code>len</code> procedure; the length never counts the terminating zero.</p> <p>The terminating zero cannot be accessed unless the string is converted to the <code>cstring</code> type first. The terminating zero assures that this conversion can be done in O(1) and without any allocations.</p> <p>The assignment operator for strings always copies the string. The <code>&amp;</code> operator concatenates strings.</p> <p>Most native Nim types support conversion to strings with the special <code>$</code> proc. When calling the <code>echo</code> proc, for example, the built-in stringify operation for the parameter is called:</p> <pre class="listing" data-language="nim">echo 3 # calls `$` for `int`</pre>
<p>Whenever a user creates a specialized object, implementation of this procedure provides for <code>string</code> representation.</p> <pre class="listing" data-language="nim">type
  Person = object
    name: string
    age: int

proc `$`(p: Person): string = # `$` always returns a string
  result = p.name &amp; " is " &amp;
          $p.age &amp; # we *need* the `$` in front of p.age which
                   # is natively an integer to convert it to
                   # a string
          " years old."</pre>
<p>While <code>$p.name</code> can also be used, the <code>$</code> operation on a string does nothing. Note that we cannot rely on automatic conversion from an <code>int</code> to a <code>string</code> like we can for the <code>echo</code> proc.</p> <p>Strings are compared by their lexicographical order. All comparison operators are available. Strings can be indexed like arrays (lower bound is 0). Unlike arrays, they can be used in case statements:</p> <pre class="listing" data-language="nim">case paramStr(i)
of "-v": incl(options, optVerbose)
of "-h", "-?": incl(options, optHelp)
else: write(stdout, "invalid command line option!\n")</pre>
<p>Per convention, all strings are UTF-8 strings, but this is not enforced. For example, when reading strings from binary files, they are merely a sequence of bytes. The index operation <code>s[i]</code> means the i-th <em>char</em> of <code>s</code>, not the i-th <em>unichar</em>. The iterator <code>runes</code> from the <a href="unicode.html">unicode module</a> can be used for iteration over all Unicode characters.</p> <h3 id="types-cstring-type">cstring type</h3>
<p>The <code>cstring</code> type meaning <code>compatible string</code> is the native representation of a string for the compilation backend. For the C backend the <code>cstring</code> type represents a pointer to a zero-terminated char array compatible with the type <code>char*</code> in Ansi C. Its primary purpose lies in easy interfacing with C. The index operation <code>s[i]</code> means the i-th <em>char</em> of <code>s</code>; however no bounds checking for <code>cstring</code> is performed making the index operation unsafe.</p> <p>A Nim <code>string</code> is implicitly convertible to <code>cstring</code> for convenience. If a Nim string is passed to a C-style variadic proc, it is implicitly converted to <code>cstring</code> too:</p> <pre class="listing" data-language="nim">proc printf(formatstr: cstring) {.importc: "printf", varargs,
                                  header: "&lt;stdio.h&gt;".}

printf("This works %s", "as expected")</pre>
<p>Even though the conversion is implicit, it is not <em>safe</em>: The garbage collector does not consider a <code>cstring</code> to be a root and may collect the underlying memory. However, in practice, this almost never happens as the GC considers stack roots conservatively. One can use the builtin procs <code>GC_ref</code> and <code>GC_unref</code> to keep the string data alive for the rare cases where it does not work.</p> <p>A <code>$</code> proc is defined for cstrings that returns a string. Thus to get a nim string from a cstring:</p> <pre class="listing" data-language="nim">var str: string = "Hello!"
var cstr: cstring = str
var newstr: string = $cstr</pre> <h3 id="types-structured-types">Structured types</h3>
<p>A variable of a structured type can hold multiple values at the same time. Structured types can be nested to unlimited levels. Arrays, sequences, tuples, objects, and sets belong to the structured types.</p> <h3 id="types-array-and-sequence-types">Array and sequence types</h3>
<p>Arrays are a homogeneous type, meaning that each element in the array has the same type. Arrays always have a fixed length specified as a constant expression (except for open arrays). They can be indexed by any ordinal type. A parameter <code>A</code> may be an <em>open array</em>, in which case it is indexed by integers from 0 to <code>len(A)-1</code>. An array expression may be constructed by the array constructor <code>[]</code>. The element type of this array expression is inferred from the type of the first element. All other elements need to be implicitly convertible to this type.</p> <p>An array type can be defined using the <code>array[size, T]</code> syntax, or using <code>array[lo..hi, T]</code> for arrays that start at an index other than zero.</p> <p>Sequences are similar to arrays but of dynamic length which may change during runtime (like strings). Sequences are implemented as growable arrays, allocating pieces of memory as items are added. A sequence <code>S</code> is always indexed by integers from 0 to <code>len(S)-1</code> and its bounds are checked. Sequences can be constructed by the array constructor <code>[]</code> in conjunction with the array to sequence operator <code>@</code>. Another way to allocate space for a sequence is to call the built-in <code>newSeq</code> procedure.</p> <p>A sequence may be passed to a parameter that is of type <em>open array</em>.</p> <p>Example:</p> <pre class="listing" data-language="nim">type
  IntArray = array[0..5, int] # an array that is indexed with 0..5
  IntSeq = seq[int] # a sequence of integers
var
  x: IntArray
  y: IntSeq
x = [1, 2, 3, 4, 5, 6]  # [] is the array constructor
y = @[1, 2, 3, 4, 5, 6] # the @ turns the array into a sequence

let z = [1.0, 2, 3, 4] # the type of z is array[0..3, float]</pre>
<p>The lower bound of an array or sequence may be received by the built-in proc <code>low()</code>, the higher bound by <code>high()</code>. The length may be received by <code>len()</code>. <code>low()</code> for a sequence or an open array always returns 0, as this is the first valid index. One can append elements to a sequence with the <code>add()</code> proc or the <code>&amp;</code> operator, and remove (and get) the last element of a sequence with the <code>pop()</code> proc.</p> <p>The notation <code>x[i]</code> can be used to access the i-th element of <code>x</code>.</p> <p>Arrays are always bounds checked (statically or at runtime). These checks can be disabled via pragmas or invoking the compiler with the <code>--boundChecks:off</code> command-line switch.</p> <p>An array constructor can have explicit indexes for readability:</p> <pre class="listing" data-language="nim">type
  Values = enum
    valA, valB, valC

const
  lookupTable = [
    valA: "A",
    valB: "B",
    valC: "C"
  ]</pre>
<p>If an index is left out, <code>succ(lastIndex)</code> is used as the index value:</p> <pre class="listing" data-language="nim">type
  Values = enum
    valA, valB, valC, valD, valE

const
  lookupTable = [
    valA: "A",
    "B",
    valC: "C",
    "D", "e"
  ]</pre> <h3 id="types-open-arrays">Open arrays</h3>
<p>Often fixed size arrays turn out to be too inflexible; procedures should be able to deal with arrays of different sizes. The <span id="openarray_1">openarray</span> type allows this; it can only be used for parameters. Openarrays are always indexed with an <code>int</code> starting at position 0. The <code>len</code>, <code>low</code> and <code>high</code> operations are available for open arrays too. Any array with a compatible base type can be passed to an openarray parameter, the index type does not matter. In addition to arrays sequences can also be passed to an open array parameter.</p> <p>The openarray type cannot be nested: multidimensional openarrays are not supported because this is seldom needed and cannot be done efficiently.</p> <pre class="listing" data-language="nim">proc testOpenArray(x: openArray[int]) = echo repr(x)

testOpenArray([1,2,3])  # array[]
testOpenArray(@[1,2,3]) # seq[]</pre> <h3 id="types-varargs">Varargs</h3>
<p>A <code>varargs</code> parameter is an openarray parameter that additionally allows to pass a variable number of arguments to a procedure. The compiler converts the list of arguments to an array implicitly:</p> <pre class="listing" data-language="nim">proc myWriteln(f: File, a: varargs[string]) =
  for s in items(a):
    write(f, s)
  write(f, "\n")

myWriteln(stdout, "abc", "def", "xyz")
# is transformed to:
myWriteln(stdout, ["abc", "def", "xyz"])</pre>
<p>This transformation is only done if the varargs parameter is the last parameter in the procedure header. It is also possible to perform type conversions in this context:</p> <pre class="listing" data-language="nim">proc myWriteln(f: File, a: varargs[string, `$`]) =
  for s in items(a):
    write(f, s)
  write(f, "\n")

myWriteln(stdout, 123, "abc", 4.0)
# is transformed to:
myWriteln(stdout, [$123, $"def", $4.0])</pre>
<p>In this example <code>$</code> is applied to any argument that is passed to the parameter <code>a</code>. (Note that <code>$</code> applied to strings is a nop.)</p> <p>Note that an explicit array constructor passed to a <code>varargs</code> parameter is not wrapped in another implicit array construction:</p> <pre class="listing" data-language="nim">proc takeV[T](a: varargs[T]) = discard

takeV([123, 2, 1]) # takeV's T is "int", not "array of int"</pre>
<p><code>varargs[typed]</code> is treated specially: It matches a variable list of arguments of arbitrary type but <em>always</em> constructs an implicit array. This is required so that the builtin <code>echo</code> proc does what is expected:</p> <pre class="listing" data-language="nim">proc echo*(x: varargs[typed, `$`]) {...}

echo @[1, 2, 3]
# prints "@[1, 2, 3]" and not "123"</pre> <h3 id="types-unchecked-arrays">Unchecked arrays</h3>
<p>The <code>UncheckedArray[T]</code> type is a special kind of <code>array</code> where its bounds are not checked. This is often useful to implement customized flexibly sized arrays. Additionally, an unchecked array is translated into a C array of undetermined size:</p> <pre class="listing" data-language="nim">type
  MySeq = object
    len, cap: int
    data: UncheckedArray[int]</pre>
<p>Produces roughly this C code:</p> <pre class="listing" data-language="nim">typedef struct {
  NI len;
  NI cap;
  NI data[];
} MySeq;</pre>
<p>The base type of the unchecked array may not contain any GC'ed memory but this is currently not checked.</p> <p><strong>Future directions</strong>: GC'ed memory should be allowed in unchecked arrays and there should be an explicit annotation of how the GC is to determine the runtime size of the array.</p> <h3 id="types-tuples-and-object-types">Tuples and object types</h3>
<p>A variable of a tuple or object type is a heterogeneous storage container. A tuple or object defines various named <em>fields</em> of a type. A tuple also defines a lexicographic <em>order</em> of the fields. Tuples are meant to be heterogeneous storage types with few abstractions. The <code>()</code> syntax can be used to construct tuples. The order of the fields in the constructor must match the order of the tuple's definition. Different tuple-types are <em>equivalent</em> if they specify the same fields of the same type in the same order. The <em>names</em> of the fields also have to be identical.</p> <p>The assignment operator for tuples copies each component. The default assignment operator for objects copies each component. Overloading of the assignment operator is described <a href="manual_experimental.html#type-bound-operations">here</a>.</p> <pre class="listing" data-language="nim">type
  Person = tuple[name: string, age: int] # type representing a person:
                                         # a person consists of a name
                                         # and an age
var
  person: Person
person = (name: "Peter", age: 30)
echo person.name
# the same, but less readable:
person = ("Peter", 30)
echo person[0]</pre>
<p>A tuple with one unnamed field can be constructed with the parentheses and a trailing comma:</p> <pre class="listing" data-language="nim">proc echoUnaryTuple(a: (int,)) =
  echo a[0]

echoUnaryTuple (1,)</pre>
<p>In fact, a trailing comma is allowed for every tuple construction.</p> <p>The implementation aligns the fields for the best access performance. The alignment is compatible with the way the C compiler does it.</p> <p>For consistency with <code>object</code> declarations, tuples in a <code>type</code> section can also be defined with indentation instead of <code>[]</code>:</p> <pre class="listing" data-language="nim">type
  Person = tuple   # type representing a person
    name: string   # a person consists of a name
    age: Natural   # and an age</pre>
<p>Objects provide many features that tuples do not. Object provide inheritance and the ability to hide fields from other modules. Objects with inheritance enabled have information about their type at runtime so that the <code>of</code> operator can be used to determine the object's type. The <code>of</code> operator is similar to the <code>instanceof</code> operator in Java.</p> <pre class="listing" data-language="nim">type
  Person = object of RootObj
    name*: string   # the * means that `name` is accessible from other modules
    age: int        # no * means that the field is hidden
  
  Student = ref object of Person # a student is a person
    id: int                      # with an id field

var
  student: Student
  person: Person
assert(student of Student) # is true
assert(student of Person) # also true</pre>
<p>Object fields that should be visible from outside the defining module, have to be marked by <code>*</code>. In contrast to tuples, different object types are never <em>equivalent</em>, they are nominal types whereas tuples are structural. Objects that have no ancestor are implicitly <code>final</code> and thus have no hidden type information. One can use the <code>inheritable</code> pragma to introduce new object roots apart from <code>system.RootObj</code>.</p> <pre class="listing" data-language="nim">type
  Person = object # example of a final object
    name*: string
    age: int
  
  Student = ref object of Person # Error: inheritance only works with non-final objects
    id: int</pre> <h3 id="types-object-construction">Object construction</h3>
<p>Objects can also be created with an <span id="object-construction-expression_1">object construction expression</span> that has the syntax <code>T(fieldA: valueA, fieldB: valueB, ...)</code> where <code>T</code> is an <code>object</code> type or a <code>ref object</code> type:</p> <pre class="listing" data-language="nim">var student = Student(name: "Anton", age: 5, id: 3)</pre>
<p>Note that, unlike tuples, objects require the field names along with their values. For a <code>ref object</code> type <code>system.new</code> is invoked implicitly.</p> <h3 id="types-object-variants">Object variants</h3>
<p>Often an object hierarchy is an overkill in certain situations where simple variant types are needed. Object variants are tagged unions discriminated via an enumerated type used for runtime type flexibility, mirroring the concepts of <em>sum types</em> and <em>algebraic data types (ADTs)</em> as found in other languages.</p> <p>An example:</p> <pre class="listing" data-language="nim"># This is an example how an abstract syntax tree could be modelled in Nim
type
  NodeKind = enum  # the different node types
    nkInt,          # a leaf with an integer value
    nkFloat,        # a leaf with a float value
    nkString,       # a leaf with a string value
    nkAdd,          # an addition
    nkSub,          # a subtraction
    nkIf            # an if statement
  Node = ref NodeObj
  NodeObj = object
    case kind: NodeKind  # the ``kind`` field is the discriminator
    of nkInt: intVal: int
    of nkFloat: floatVal: float
    of nkString: strVal: string
    of nkAdd, nkSub:
      leftOp, rightOp: Node
    of nkIf:
      condition, thenPart, elsePart: Node

# create a new case object:
var n = Node(kind: nkIf, condition: nil)
# accessing n.thenPart is valid because the ``nkIf`` branch is active:
n.thenPart = Node(kind: nkFloat, floatVal: 2.0)

# the following statement raises an `FieldDefect` exception, because
# n.kind's value does not fit and the ``nkString`` branch is not active:
n.strVal = ""

# invalid: would change the active object branch:
n.kind = nkInt

var x = Node(kind: nkAdd, leftOp: Node(kind: nkInt, intVal: 4),
                          rightOp: Node(kind: nkInt, intVal: 2))
# valid: does not change the active object branch:
x.kind = nkSub</pre>
<p>As can be seen from the example, an advantage to an object hierarchy is that no casting between different object types is needed. Yet, access to invalid object fields raises an exception.</p> <p>The syntax of <code>case</code> in an object declaration follows closely the syntax of the <code>case</code> statement: The branches in a <code>case</code> section may be indented too.</p> <p>In the example, the <code>kind</code> field is called the <span id="discriminator_1">discriminator</span>: For safety its address cannot be taken and assignments to it are restricted: The new value must not lead to a change of the active object branch. Also, when the fields of a particular branch are specified during object construction, the corresponding discriminator value must be specified as a constant expression.</p> <p>Instead of changing the active object branch, replace the old object in memory with a new one completely:</p> <pre class="listing" data-language="nim">var x = Node(kind: nkAdd, leftOp: Node(kind: nkInt, intVal: 4),
                          rightOp: Node(kind: nkInt, intVal: 2))
# change the node's contents:
x[] = NodeObj(kind: nkString, strVal: "abc")</pre>
<p>Starting with version 0.20 <code>system.reset</code> cannot be used anymore to support object branch changes as this never was completely memory safe.</p> <p>As a special rule, the discriminator kind can also be bounded using a <code>case</code> statement. If possible values of the discriminator variable in a <code>case</code> statement branch are a subset of discriminator values for the selected object branch, the initialization is considered valid. This analysis only works for immutable discriminators of an ordinal type and disregards <code>elif</code> branches. For discriminator values with a <code>range</code> type, the compiler checks if the entire range of possible values for the discriminator value is valid for the chosen object branch.</p> <p>A small example:</p> <pre class="listing" data-language="nim">let unknownKind = nkSub

# invalid: unsafe initialization because the kind field is not statically known:
var y = Node(kind: unknownKind, strVal: "y")

var z = Node()
case unknownKind
of nkAdd, nkSub:
  # valid: possible values of this branch are a subset of nkAdd/nkSub object branch:
  z = Node(kind: unknownKind, leftOp: Node(), rightOp: Node())
else:
  echo "ignoring: ", unknownKind

# also valid, since unknownKindBounded can only contain the values nkAdd or nkSub
let unknownKindBounded = range[nkAdd..nkSub](unknownKind)
z = Node(kind: unknownKindBounded, leftOp: Node(), rightOp: Node())</pre> <h3 id="types-set-type">Set type</h3>The set type models the mathematical notion of a set. The set's basetype can only be an ordinal type of a certain size, namely:<ul class="simple">
<li>
<code>int8</code>-<code>int16</code>
</li> <li>
<code>uint8</code>/<code>byte</code>-<code>uint16</code>
</li> <li><code>char</code></li> <li><code>enum</code></li> </ul> <p>or equivalent. For signed integers the set's base type is defined to be in the range <code>0 .. MaxSetElements-1</code> where <code>MaxSetElements</code> is currently always 2^16.</p> <p>The reason is that sets are implemented as high performance bit vectors. Attempting to declare a set with a larger type will result in an error:</p> <pre class="listing" data-language="nim">var s: set[int64] # Error: set is too large</pre>
<p>Sets can be constructed via the set constructor: <code>{}</code> is the empty set. The empty set is type compatible with any concrete set type. The constructor can also be used to include elements (and ranges of elements):</p> <pre class="listing" data-language="nim">type
  CharSet = set[char]
var
  x: CharSet
x = {'a'..'z', '0'..'9'} # This constructs a set that contains the
                         # letters from 'a' to 'z' and the digits
                         # from '0' to '9'</pre>
<p>These operations are supported by sets:</p> <table>
<tr>
<th>operation</th>
<th>meaning</th>
</tr> <tr>
<td><code>A + B</code></td>
<td>union of two sets</td>
</tr> <tr>
<td><code>A * B</code></td>
<td>intersection of two sets</td>
</tr> <tr>
<td><code>A - B</code></td>
<td>difference of two sets (A without B's elements)</td>
</tr> <tr>
<td><code>A == B</code></td>
<td>set equality</td>
</tr> <tr>
<td><code>A &lt;= B</code></td>
<td>subset relation (A is subset of B or equal to B)</td>
</tr> <tr>
<td><code>A &lt; B</code></td>
<td>strict subset relation (A is a proper subset of B)</td>
</tr> <tr>
<td><code>e in A</code></td>
<td>set membership (A contains element e)</td>
</tr> <tr>
<td><code>e notin A</code></td>
<td>A does not contain element e</td>
</tr> <tr>
<td><code>contains(A, e)</code></td>
<td>A contains element e</td>
</tr> <tr>
<td><code>card(A)</code></td>
<td>the cardinality of A (number of elements in A)</td>
</tr> <tr>
<td><code>incl(A, elem)</code></td>
<td>same as <code>A = A + {elem}</code>
</td>
</tr> <tr>
<td><code>excl(A, elem)</code></td>
<td>same as <code>A = A - {elem}</code>
</td>
</tr> </table> <h4 id="set-type-bit-fields">Bit fields</h4>
<p>Sets are often used to define a type for the <em>flags</em> of a procedure. This is a cleaner (and type safe) solution than defining integer constants that have to be <code>or</code>'ed together.</p> <p>Enum, sets and casting can be used together as in:</p> <pre class="listing" data-language="nim">type
  MyFlag* {.size: sizeof(cint).} = enum
    A
    B
    C
    D
  MyFlags = set[MyFlag]

proc toNum(f: MyFlags): int = cast[cint](f)
proc toFlags(v: int): MyFlags = cast[MyFlags](v)

assert toNum({}) == 0
assert toNum({A}) == 1
assert toNum({D}) == 8
assert toNum({A, C}) == 5
assert toFlags(0) == {}
assert toFlags(7) == {A, B, C}</pre>
<p>Note how the set turns enum values into powers of 2.</p> <p>If using enums and sets with C, use distinct cint.</p> <p>For interoperability with C see also the <a href="manual.html#implementation-specific-pragmas-bitsize-pragma">bitsize pragma</a>.</p> <h3 id="types-reference-and-pointer-types">Reference and pointer types</h3>
<p>References (similar to pointers in other programming languages) are a way to introduce many-to-one relationships. This means different references can point to and modify the same location in memory (also called <span id="aliasing_1">aliasing</span>).</p> <p>Nim distinguishes between <span id="traced_1">traced</span> and <span id="untraced_1">untraced</span> references. Untraced references are also called <em>pointers</em>. Traced references point to objects of a garbage-collected heap, untraced references point to manually allocated objects or objects somewhere else in memory. Thus untraced references are <em>unsafe</em>. However, for certain low-level operations (accessing the hardware) untraced references are unavoidable.</p> <p>Traced references are declared with the <strong>ref</strong> keyword, untraced references are declared with the <strong>ptr</strong> keyword. In general, a <code>ptr T</code> is implicitly convertible to the <code>pointer</code> type.</p> <p>An empty subscript <code>[]</code> notation can be used to de-refer a reference, the <code>addr</code> procedure returns the address of an item. An address is always an untraced reference. Thus the usage of <code>addr</code> is an <em>unsafe</em> feature.</p> <p>The <code>.</code> (access a tuple/object field operator) and <code>[]</code> (array/string/sequence index operator) operators perform implicit dereferencing operations for reference types:</p> <pre class="listing" data-language="nim">type
  Node = ref NodeObj
  NodeObj = object
    le, ri: Node
    data: int

var
  n: Node
new(n)
n.data = 9
# no need to write n[].data; in fact n[].data is highly discouraged!</pre>
<p>Automatic dereferencing can be performed for the first argument of a routine call, but this is an experimental feature and is described <a href="manual_experimental.html#type-bound-operations">here</a>.</p> <p>In order to simplify structural type checking, recursive tuples are not valid:</p> <pre class="listing" data-language="nim"># invalid recursion
type MyTuple = tuple[a: ref MyTuple]</pre>
<p>Likewise <code>T = ref T</code> is an invalid type.</p> <p>As a syntactical extension <code>object</code> types can be anonymous if declared in a type section via the <code>ref object</code> or <code>ptr object</code> notations. This feature is useful if an object should only gain reference semantics:</p> <pre class="listing" data-language="nim">type
  Node = ref object
    le, ri: Node
    data: int</pre>
<p>To allocate a new traced object, the built-in procedure <code>new</code> has to be used. To deal with untraced memory, the procedures <code>alloc</code>, <code>dealloc</code> and <code>realloc</code> can be used. The documentation of the system module contains further information.</p> <p>Nil ---</p> <p>If a reference points to <em>nothing</em>, it has the value <code>nil</code>. <code>nil</code> is the default value for all <code>ref</code> and <code>ptr</code> types. The <code>nil</code> value can also be used like any other literal value. For example, it can be used in an assignment like <code>myRef = nil</code>.</p> <p>Dereferencing <code>nil</code> is an unrecoverable fatal runtime error (and not a panic).</p> <p>A successful dereferencing operation <code>p[]</code> implies that <code>p</code> is not nil. This can be exploited by the implementation to optimize code like:</p> <pre class="listing" data-language="nim">p[].field = 3
if p != nil:
  # if p were nil, ``p[]`` would have caused a crash already,
  # so we know ``p`` is always not nil here.
  action()</pre>
<p>Into:</p> <pre class="listing" data-language="nim">p[].field = 3
action()</pre>
<p><em>Note</em>: This is not comparable to C's "undefined behavior" for dereferencing NULL pointers.</p> <h3 id="types-mixing-gc-ed-memory-with-ptr">Mixing GC'ed memory with <code>ptr</code>
</h3>
<p>Special care has to be taken if an untraced object contains traced objects like traced references, strings, or sequences: in order to free everything properly, the built-in procedure <code>reset</code> has to be called before freeing the untraced memory manually:</p> <pre class="listing" data-language="nim">type
  Data = tuple[x, y: int, s: string]

# allocate memory for Data on the heap:
var d = cast[ptr Data](alloc0(sizeof(Data)))

# create a new string on the garbage collected heap:
d.s = "abc"

# tell the GC that the string is not needed anymore:
reset(d.s)

# free the memory:
dealloc(d)</pre>
<p>Without the <code>reset</code> call the memory allocated for the <code>d.s</code> string would never be freed. The example also demonstrates two important features for low-level programming: the <code>sizeof</code> proc returns the size of a type or value in bytes. The <code>cast</code> operator can circumvent the type system: the compiler is forced to treat the result of the <code>alloc0</code> call (which returns an untyped pointer) as if it would have the type <code>ptr Data</code>. Casting should only be done if it is unavoidable: it breaks type safety and bugs can lead to mysterious crashes.</p> <p><strong>Note</strong>: The example only works because the memory is initialized to zero (<code>alloc0</code> instead of <code>alloc</code> does this): <code>d.s</code> is thus initialized to binary zero which the string assignment can handle. One needs to know low-level details like this when mixing garbage-collected data with unmanaged memory.</p> <h3 id="types-procedural-type">Procedural type</h3>
<p>A procedural type is internally a pointer to a procedure. <code>nil</code> is an allowed value for variables of a procedural type. Nim uses procedural types to achieve <span id="functional_1">functional</span> programming techniques.</p> <p>Examples:</p> <pre class="listing" data-language="nim">proc printItem(x: int) = ...

proc forEach(c: proc (x: int) {.cdecl.}) =
  ...

forEach(printItem)  # this will NOT compile because calling conventions differ</pre>
<pre class="listing" data-language="nim">type
  OnMouseMove = proc (x, y: int) {.closure.}

proc onMouseMove(mouseX, mouseY: int) =
  # has default calling convention
  echo "x: ", mouseX, " y: ", mouseY

proc setOnMouseMove(mouseMoveEvent: OnMouseMove) = discard

# ok, 'onMouseMove' has the default calling convention, which is compatible
# to 'closure':
setOnMouseMove(onMouseMove)</pre>
<p>A subtle issue with procedural types is that the calling convention of the procedure influences the type compatibility: procedural types are only compatible if they have the same calling convention. As a special extension, a procedure of the calling convention <code>nimcall</code> can be passed to a parameter that expects a proc of the calling convention <code>closure</code>.</p> <p>Nim supports these <span id="calling-conventions_1">calling conventions</span>:</p> <dl>
<dt><span id="nimcall_1">nimcall</span></dt> <dd>is the default convention used for a Nim <strong>proc</strong>. It is the same as <code>fastcall</code>, but only for C compilers that support <code>fastcall</code>.</dd> <dt><span id="closure_1">closure</span></dt> <dd>is the default calling convention for a <strong>procedural type</strong> that lacks any pragma annotations. It indicates that the procedure has a hidden implicit parameter (an <em>environment</em>). Proc vars that have the calling convention <code>closure</code> take up two machine words: One for the proc pointer and another one for the pointer to implicitly passed environment.</dd> <dt><span id="stdcall_1">stdcall</span></dt> <dd>This is the stdcall convention as specified by Microsoft. The generated C procedure is declared with the <code>__stdcall</code> keyword.</dd> <dt><span id="cdecl_1">cdecl</span></dt> <dd>The cdecl convention means that a procedure shall use the same convention as the C compiler. Under Windows the generated C procedure is declared with the <code>__cdecl</code> keyword.</dd> <dt><span id="safecall_1">safecall</span></dt> <dd>This is the safecall convention as specified by Microsoft. The generated C procedure is declared with the <code>__safecall</code> keyword. The word <em>safe</em> refers to the fact that all hardware registers shall be pushed to the hardware stack.</dd> <dt><span id="inline_1">inline</span></dt> <dd>The inline convention means the the caller should not call the procedure, but inline its code directly. Note that Nim does not inline, but leaves this to the C compiler; it generates <code>__inline</code> procedures. This is only a hint for the compiler: it may completely ignore it and it may inline procedures that are not marked as <code>inline</code>.</dd> <dt><span id="fastcall_1">fastcall</span></dt> <dd>Fastcall means different things to different C compilers. One gets whatever the C <code>__fastcall</code> means.</dd> <dt><span id="thiscall_1">thiscall</span></dt> <dd>This is thiscall calling convention as specified by Microsoft, used on C++ class member functions on the x86 architecture</dd> <dt><span id="syscall_1">syscall</span></dt> <dd>The syscall convention is the same as <code>__syscall</code> in C. It is used for interrupts.</dd> <dt><span id="noconv_1">noconv</span></dt> <dd>The generated C code will not have any explicit calling convention and thus use the C compiler's default calling convention. This is needed because Nim's default calling convention for procedures is <code>fastcall</code> to improve speed.</dd> </dl> <p>Most calling conventions exist only for the Windows 32-bit platform.</p> <p>The default calling convention is <code>nimcall</code>, unless it is an inner proc (a proc inside of a proc). For an inner proc an analysis is performed whether it accesses its environment. If it does so, it has the calling convention <code>closure</code>, otherwise it has the calling convention <code>nimcall</code>.</p> <h3 id="types-distinct-type">Distinct type</h3>
<p>A <code>distinct</code> type is a new type derived from a <span id="base-type_1">base type</span> that is incompatible with its base type. In particular, it is an essential property of a distinct type that it <strong>does not</strong> imply a subtype relation between it and its base type. Explicit type conversions from a distinct type to its base type and vice versa are allowed. See also <code>distinctBase</code> to get the reverse operation.</p> <p>A distinct type is an ordinal type if its base type is an ordinal type.</p> <h4 id="distinct-type-modeling-currencies">Modeling currencies</h4>
<p>A distinct type can be used to model different physical <span id="units_1">units</span> with a numerical base type, for example. The following example models currencies.</p> <p>Different currencies should not be mixed in monetary calculations. Distinct types are a perfect tool to model different currencies:</p> <pre class="listing" data-language="nim">type
  Dollar = distinct int
  Euro = distinct int

var
  d: Dollar
  e: Euro

echo d + 12
# Error: cannot add a number with no unit and a ``Dollar``</pre>
<p>Unfortunately, <code>d + 12.Dollar</code> is not allowed either, because <code>+</code> is defined for <code>int</code> (among others), not for <code>Dollar</code>. So a <code>+</code> for dollars needs to be defined:</p> <pre class="listing" data-language="nim">proc `+` (x, y: Dollar): Dollar =
  result = Dollar(int(x) + int(y))</pre>
<p>It does not make sense to multiply a dollar with a dollar, but with a number without unit; and the same holds for division:</p> <pre class="listing" data-language="nim">proc `*` (x: Dollar, y: int): Dollar =
  result = Dollar(int(x) * y)

proc `*` (x: int, y: Dollar): Dollar =
  result = Dollar(x * int(y))

proc `div` ...</pre>
<p>This quickly gets tedious. The implementations are trivial and the compiler should not generate all this code only to optimize it away later - after all <code>+</code> for dollars should produce the same binary code as <code>+</code> for ints. The pragma <span id="borrow_1">borrow</span> has been designed to solve this problem; in principle, it generates the above trivial implementations:</p> <pre class="listing" data-language="nim">proc `*` (x: Dollar, y: int): Dollar {.borrow.}
proc `*` (x: int, y: Dollar): Dollar {.borrow.}
proc `div` (x: Dollar, y: int): Dollar {.borrow.}</pre>
<p>The <code>borrow</code> pragma makes the compiler use the same implementation as the proc that deals with the distinct type's base type, so no code is generated.</p> <p>But it seems all this boilerplate code needs to be repeated for the <code>Euro</code> currency. This can be solved with <a href="#templates">templates</a>.</p> <pre class="listing" data-language="nim">template additive(typ: typedesc) =
  proc `+` *(x, y: typ): typ {.borrow.}
  proc `-` *(x, y: typ): typ {.borrow.}
  
  # unary operators:
  proc `+` *(x: typ): typ {.borrow.}
  proc `-` *(x: typ): typ {.borrow.}

template multiplicative(typ, base: typedesc) =
  proc `*` *(x: typ, y: base): typ {.borrow.}
  proc `*` *(x: base, y: typ): typ {.borrow.}
  proc `div` *(x: typ, y: base): typ {.borrow.}
  proc `mod` *(x: typ, y: base): typ {.borrow.}

template comparable(typ: typedesc) =
  proc `&lt;` * (x, y: typ): bool {.borrow.}
  proc `&lt;=` * (x, y: typ): bool {.borrow.}
  proc `==` * (x, y: typ): bool {.borrow.}

template defineCurrency(typ, base: untyped) =
  type
    typ* = distinct base
  additive(typ)
  multiplicative(typ, base)
  comparable(typ)

defineCurrency(Dollar, int)
defineCurrency(Euro, int)</pre>
<p>The borrow pragma can also be used to annotate the distinct type to allow certain builtin operations to be lifted:</p> <pre class="listing" data-language="nim">type
  Foo = object
    a, b: int
    s: string
  
  Bar {.borrow: `.`.} = distinct Foo

var bb: ref Bar
new bb
# field access now valid
bb.a = 90
bb.s = "abc"</pre>
<p>Currently, only the dot accessor can be borrowed in this way.</p> <h4 id="distinct-type-avoiding-sql-injection-attacks">Avoiding SQL injection attacks</h4>
<p>An SQL statement that is passed from Nim to an SQL database might be modeled as a string. However, using string templates and filling in the values is vulnerable to the famous <span id="sql-injection-attack_1">SQL injection attack</span>:</p> <pre class="listing" data-language="nim">import strutils

proc query(db: DbHandle, statement: string) = ...

var
  username: string

db.query("SELECT FROM users WHERE name = '$1'" % username)
# Horrible security hole, but the compiler does not mind!</pre>
<p>This can be avoided by distinguishing strings that contain SQL from strings that don't. Distinct types provide a means to introduce a new string type <code>SQL</code> that is incompatible with <code>string</code>:</p> <pre class="listing" data-language="nim">type
  SQL = distinct string

proc query(db: DbHandle, statement: SQL) = ...

var
  username: string

db.query("SELECT FROM users WHERE name = '$1'" % username)
# Static error: `query` expects an SQL string!</pre>
<p>It is an essential property of abstract types that they <strong>do not</strong> imply a subtype relation between the abstract type and its base type. Explicit type conversions from <code>string</code> to <code>SQL</code> are allowed:</p> <pre class="listing" data-language="nim">import strutils, sequtils

proc properQuote(s: string): SQL =
  # quotes a string properly for an SQL statement
  return SQL(s)

proc `%` (frmt: SQL, values: openarray[string]): SQL =
  # quote each argument:
  let v = values.mapIt(SQL, properQuote(it))
  # we need a temporary type for the type conversion :-(
  type StrSeq = seq[string]
  # call strutils.`%`:
  result = SQL(string(frmt) % StrSeq(v))

db.query("SELECT FROM users WHERE name = '$1'".SQL % [username])</pre>
<p>Now we have compile-time checking against SQL injection attacks. Since <code>"".SQL</code> is transformed to <code>SQL("")</code> no new syntax is needed for nice looking <code>SQL</code> string literals. The hypothetical <code>SQL</code> type actually exists in the library as the <a href="db_common.html#SqlQuery">SqlQuery type</a> of modules like <a href="db_sqlite.html">db_sqlite</a>.</p> <h3 id="types-auto-type">Auto type</h3>
<p>The <code>auto</code> type can only be used for return types and parameters. For return types it causes the compiler to infer the type from the routine body:</p> <pre class="listing" data-language="nim">proc returnsInt(): auto = 1984</pre>
<p>For parameters it currently creates implicitly generic routines:</p> <pre class="listing" data-language="nim">proc foo(a, b: auto) = discard</pre>
<p>Is the same as:</p> <pre class="listing" data-language="nim">proc foo[T1, T2](a: T1, b: T2) = discard</pre>
<p>However, later versions of the language might change this to mean "infer the parameters' types from the body". Then the above <code>foo</code> would be rejected as the parameters' types can not be inferred from an empty <code>discard</code> statement.</p> <h2 id="type-relations">Type relations</h2>
<p>The following section defines several relations on types that are needed to describe the type checking done by the compiler.</p> <h3 id="type-relations-type-equality">Type equality</h3>
<p>Nim uses structural type equivalence for most types. Only for objects, enumerations and distinct types name equivalence is used. The following algorithm, <em>in pseudo-code</em>, determines type equality:</p> <pre class="listing" data-language="nim">proc typeEqualsAux(a, b: PType,
                   s: var HashSet[(PType, PType)]): bool =
  if (a,b) in s: return true
  incl(s, (a,b))
  if a.kind == b.kind:
    case a.kind
    of int, intXX, float, floatXX, char, string, cstring, pointer,
        bool, nil, void:
      # leaf type: kinds identical; nothing more to check
      result = true
    of ref, ptr, var, set, seq, openarray:
      result = typeEqualsAux(a.baseType, b.baseType, s)
    of range:
      result = typeEqualsAux(a.baseType, b.baseType, s) and
        (a.rangeA == b.rangeA) and (a.rangeB == b.rangeB)
    of array:
      result = typeEqualsAux(a.baseType, b.baseType, s) and
               typeEqualsAux(a.indexType, b.indexType, s)
    of tuple:
      if a.tupleLen == b.tupleLen:
        for i in 0..a.tupleLen-1:
          if not typeEqualsAux(a[i], b[i], s): return false
        result = true
    of object, enum, distinct:
      result = a == b
    of proc:
      result = typeEqualsAux(a.parameterTuple, b.parameterTuple, s) and
               typeEqualsAux(a.resultType, b.resultType, s) and
               a.callingConvention == b.callingConvention

proc typeEquals(a, b: PType): bool =
  var s: HashSet[(PType, PType)] = {}
  result = typeEqualsAux(a, b, s)</pre>
<p>Since types are graphs which can have cycles, the above algorithm needs an auxiliary set <code>s</code> to detect this case.</p> <h3 id="type-relations-type-equality-modulo-type-distinction">Type equality modulo type distinction</h3>
<p>The following algorithm (in pseudo-code) determines whether two types are equal with no respect to <code>distinct</code> types. For brevity the cycle check with an auxiliary set <code>s</code> is omitted:</p> <pre class="listing" data-language="nim">proc typeEqualsOrDistinct(a, b: PType): bool =
  if a.kind == b.kind:
    case a.kind
    of int, intXX, float, floatXX, char, string, cstring, pointer,
        bool, nil, void:
      # leaf type: kinds identical; nothing more to check
      result = true
    of ref, ptr, var, set, seq, openarray:
      result = typeEqualsOrDistinct(a.baseType, b.baseType)
    of range:
      result = typeEqualsOrDistinct(a.baseType, b.baseType) and
        (a.rangeA == b.rangeA) and (a.rangeB == b.rangeB)
    of array:
      result = typeEqualsOrDistinct(a.baseType, b.baseType) and
               typeEqualsOrDistinct(a.indexType, b.indexType)
    of tuple:
      if a.tupleLen == b.tupleLen:
        for i in 0..a.tupleLen-1:
          if not typeEqualsOrDistinct(a[i], b[i]): return false
        result = true
    of distinct:
      result = typeEqualsOrDistinct(a.baseType, b.baseType)
    of object, enum:
      result = a == b
    of proc:
      result = typeEqualsOrDistinct(a.parameterTuple, b.parameterTuple) and
               typeEqualsOrDistinct(a.resultType, b.resultType) and
               a.callingConvention == b.callingConvention
  elif a.kind == distinct:
    result = typeEqualsOrDistinct(a.baseType, b)
  elif b.kind == distinct:
    result = typeEqualsOrDistinct(a, b.baseType)</pre> <h3 id="type-relations-subtype-relation">Subtype relation</h3>
<p>If object <code>a</code> inherits from <code>b</code>, <code>a</code> is a subtype of <code>b</code>. This subtype relation is extended to the types <code>var</code>, <code>ref</code>, <code>ptr</code>:</p> <pre class="listing" data-language="nim">proc isSubtype(a, b: PType): bool =
  if a.kind == b.kind:
    case a.kind
    of object:
      var aa = a.baseType
      while aa != nil and aa != b: aa = aa.baseType
      result = aa == b
    of var, ref, ptr:
      result = isSubtype(a.baseType, b.baseType)</pre> <h3 id="type-relations-convertible-relation">Convertible relation</h3>
<p>A type <code>a</code> is <strong>implicitly</strong> convertible to type <code>b</code> iff the following algorithm returns true:</p> <pre class="listing" data-language="nim">proc isImplicitlyConvertible(a, b: PType): bool =
  if isSubtype(a, b) or isCovariant(a, b):
    return true
  if isIntLiteral(a):
    return b in {int8, int16, int32, int64, int, uint, uint8, uint16,
                 uint32, uint64, float32, float64}
  case a.kind
  of int:     result = b in {int32, int64}
  of int8:    result = b in {int16, int32, int64, int}
  of int16:   result = b in {int32, int64, int}
  of int32:   result = b in {int64, int}
  of uint:    result = b in {uint32, uint64}
  of uint8:   result = b in {uint16, uint32, uint64}
  of uint16:  result = b in {uint32, uint64}
  of uint32:  result = b in {uint64}
  of float32: result = b in {float64}
  of float64: result = b in {float32}
  of seq:
    result = b == openArray and typeEquals(a.baseType, b.baseType)
  of array:
    result = b == openArray and typeEquals(a.baseType, b.baseType)
    if a.baseType == char and a.indexType.rangeA == 0:
      result = b == cstring
  of cstring, ptr:
    result = b == pointer
  of string:
    result = b == cstring</pre>
<p>Implicit conversions are also performed for Nim's <code>range</code> type constructor.</p> <p>Let <code>a0</code>, <code>b0</code> of type <code>T</code>.</p> <p>Let <code>A = range[a0..b0]</code> be the argument's type, <code>F</code> the formal parameter's type. Then an implicit conversion from <code>A</code> to <code>F</code> exists if <code>a0 &gt;= low(F) and b0 &lt;= high(F)</code> and both <code>T</code> and <code>F</code> are signed integers or if both are unsigned integers.</p> <p>A type <code>a</code> is <strong>explicitly</strong> convertible to type <code>b</code> iff the following algorithm returns true:</p> <pre class="listing" data-language="nim">proc isIntegralType(t: PType): bool =
  result = isOrdinal(t) or t.kind in {float, float32, float64}

proc isExplicitlyConvertible(a, b: PType): bool =
  result = false
  if isImplicitlyConvertible(a, b): return true
  if typeEqualsOrDistinct(a, b): return true
  if isIntegralType(a) and isIntegralType(b): return true
  if isSubtype(a, b) or isSubtype(b, a): return true</pre>
<p>The convertible relation can be relaxed by a user-defined type <span id="converter_1">converter</span>.</p> <pre class="listing" data-language="nim">converter toInt(x: char): int = result = ord(x)

var
  x: int
  chr: char = 'a'

# implicit conversion magic happens here
x = chr
echo x # =&gt; 97
# one can use the explicit form too
x = chr.toInt
echo x # =&gt; 97</pre>
<p>The type conversion <code>T(a)</code> is an L-value if <code>a</code> is an L-value and <code>typeEqualsOrDistinct(T, typeof(a))</code> holds.</p> <h3 id="type-relations-assignment-compatibility">Assignment compatibility</h3>
<p>An expression <code>b</code> can be assigned to an expression <code>a</code> iff <code>a</code> is an <code>l-value</code> and <code>isImplicitlyConvertible(b.typ, a.typ)</code> holds.</p> <h2 id="overloading-resolution">Overloading resolution</h2>
<p>In a call <code>p(args)</code> the routine <code>p</code> that matches best is selected. If multiple routines match equally well, the ambiguity is reported during semantic analysis.</p> <p>Every arg in args needs to match. There are multiple different categories how an argument can match. Let <code>f</code> be the formal parameter's type and <code>a</code> the type of the argument.</p> <ol class="simple">
<li>Exact match: <code>a</code> and <code>f</code> are of the same type.</li> <li>Literal match: <code>a</code> is an integer literal of value <code>v</code> and <code>f</code> is a signed or unsigned integer type and <code>v</code> is in <code>f</code>'s range. Or: <code>a</code> is a floating-point literal of value <code>v</code> and <code>f</code> is a floating-point type and <code>v</code> is in <code>f</code>'s range.</li> <li>Generic match: <code>f</code> is a generic type and <code>a</code> matches, for instance <code>a</code> is <code>int</code> and <code>f</code> is a generic (constrained) parameter type (like in <code>[T]</code> or <code>[T: int|char]</code>.</li> <li>Subrange or subtype match: <code>a</code> is a <code>range[T]</code> and <code>T</code> matches <code>f</code> exactly. Or: <code>a</code> is a subtype of <code>f</code>.</li> <li>Integral conversion match: <code>a</code> is convertible to <code>f</code> and <code>f</code> and <code>a</code> is some integer or floating-point type.</li> <li>Conversion match: <code>a</code> is convertible to <code>f</code>, possibly via a user defined <code>converter</code>.</li> </ol> <p>These matching categories have a priority: An exact match is better than a literal match and that is better than a generic match etc. In the following <code>count(p, m)</code> counts the number of matches of the matching category <code>m</code> for the routine <code>p</code>.</p> <p>A routine <code>p</code> matches better than a routine <code>q</code> if the following algorithm returns true:</p>
<pre data-language="nim">for each matching category m in ["exact match", "literal match",
                                "generic match", "subtype match",
                                "integral match", "conversion match"]:
  if count(p, m) &gt; count(q, m): return true
  elif count(p, m) == count(q, m):
    discard "continue with next category m"
  else:
    return false
return "ambiguous"</pre> <p>Some examples:</p> <pre class="listing" data-language="nim">proc takesInt(x: int) = echo "int"
proc takesInt[T](x: T) = echo "T"
proc takesInt(x: int16) = echo "int16"

takesInt(4) # "int"
var x: int32
takesInt(x) # "T"
var y: int16
takesInt(y) # "int16"
var z: range[0..4] = 0
takesInt(z) # "T"</pre>
<p>If this algorithm returns "ambiguous" further disambiguation is performed: If the argument <code>a</code> matches both the parameter type <code>f</code> of <code>p</code> and <code>g</code> of <code>q</code> via a subtyping relation, the inheritance depth is taken into account:</p> <pre class="listing" data-language="nim">type
  A = object of RootObj
  B = object of A
  C = object of B

proc p(obj: A) =
  echo "A"

proc p(obj: B) =
  echo "B"

var c = C()
# not ambiguous, calls 'B', not 'A' since B is a subtype of A
# but not vice versa:
p(c)

proc pp(obj: A, obj2: B) = echo "A B"
proc pp(obj: B, obj2: A) = echo "B A"

# but this is ambiguous:
pp(c, c)</pre>
<p>Likewise for generic matches the most specialized generic type (that still matches) is preferred:</p> <pre class="listing" data-language="nim">proc gen[T](x: ref ref T) = echo "ref ref T"
proc gen[T](x: ref T) = echo "ref T"
proc gen[T](x: T) = echo "T"

var ri: ref int
gen(ri) # "ref T"</pre> <h3 id="overloading-resolution-overloading-based-on-var-t">Overloading based on 'var T'</h3>
<p>If the formal parameter <code>f</code> is of type <code>var T</code> in addition to the ordinary type checking, the argument is checked to be an <span id="lminusvalue_1">l-value</span>. <code>var T</code> matches better than just <code>T</code> then.</p> <pre class="listing" data-language="nim">proc sayHi(x: int): string =
  # matches a non-var int
  result = $x
proc sayHi(x: var int): string =
  # matches a var int
  result = $(x + 10)

proc sayHello(x: int) =
  var m = x # a mutable version of x
  echo sayHi(x) # matches the non-var version of sayHi
  echo sayHi(m) # matches the var version of sayHi

sayHello(3) # 3
            # 13</pre> <h3 id="overloading-resolution-lazy-type-resolution-for-untyped">Lazy type resolution for untyped</h3>
<p><strong>Note</strong>: An <span id="unresolved_1">unresolved</span> expression is an expression for which no symbol lookups and no type checking have been performed.</p> <p>Since templates and macros that are not declared as <code>immediate</code> participate in overloading resolution, it's essential to have a way to pass unresolved expressions to a template or macro. This is what the meta-type <code>untyped</code> accomplishes:</p> <pre class="listing" data-language="nim">template rem(x: untyped) = discard

rem unresolvedExpression(undeclaredIdentifier)</pre>
<p>A parameter of type <code>untyped</code> always matches any argument (as long as there is any argument passed to it).</p> <p>But one has to watch out because other overloads might trigger the argument's resolution:</p> <pre class="listing" data-language="nim">template rem(x: untyped) = discard
proc rem[T](x: T) = discard

# undeclared identifier: 'unresolvedExpression'
rem unresolvedExpression(undeclaredIdentifier)</pre>
<p><code>untyped</code> and <code>varargs[untyped]</code> are the only metatype that are lazy in this sense, the other metatypes <code>typed</code> and <code>typedesc</code> are not lazy.</p> <h3 id="overloading-resolution-varargs-matching">Varargs matching</h3>
<p>See <a href="#types-varargs">Varargs</a>.</p> <h2 id="statements-and-expressions">Statements and expressions</h2>
<p>Nim uses the common statement/expression paradigm: Statements do not produce a value in contrast to expressions. However, some expressions are statements.</p> <p>Statements are separated into <span id="simple-statements_1">simple statements</span> and <span id="complex-statements_1">complex statements</span>. Simple statements are statements that cannot contain other statements like assignments, calls, or the <code>return</code> statement; complex statements can contain other statements. To avoid the <span id="dangling-else-problem_1">dangling else problem</span>, complex statements always have to be indented. The details can be found in the grammar.</p> <h3 id="statements-and-expressions-statement-list-expression">Statement list expression</h3>
<p>Statements can also occur in an expression context that looks like <code>(stmt1; stmt2; ...; ex)</code>. This is called a statement list expression or <code>(;)</code>. The type of <code>(stmt1; stmt2; ...; ex)</code> is the type of <code>ex</code>. All the other statements must be of type <code>void</code>. (One can use <code>discard</code> to produce a <code>void</code> type.) <code>(;)</code> does not introduce a new scope.</p> <h3 id="statements-and-expressions-discard-statement">Discard statement</h3>
<p>Example:</p> <pre class="listing" data-language="nim">proc p(x, y: int): int =
  result = x + y

discard p(3, 4) # discard the return value of `p`</pre>
<p>The <code>discard</code> statement evaluates its expression for side-effects and throws the expression's resulting value away, and should only be used when ignoring this value is known not to cause problems.</p> <p>Ignoring the return value of a procedure without using a discard statement is a static error.</p> <p>The return value can be ignored implicitly if the called proc/iterator has been declared with the <span id="discardable_1">discardable</span> pragma:</p> <pre class="listing" data-language="nim">proc p(x, y: int): int {.discardable.} =
  result = x + y

p(3, 4) # now valid</pre>
<p>however the discardable pragma does not work on templates as templates substitute the AST in place. For example:</p> <pre class="listing" data-language="nim">{.push discardable .}
template example(): string = "https://nim-lang.org"
{.pop.}

example()</pre>
<p>This template will resolve into "<a href="https://nim-lang.org">https://nim-lang.org</a>" which is a string literal and since {.discardable.} doesn't apply to literals, the compiler will error.</p> <p>An empty <code>discard</code> statement is often used as a null statement:</p> <pre class="listing" data-language="nim">proc classify(s: string) =
  case s[0]
  of SymChars, '_': echo "an identifier"
  of '0'..'9': echo "a number"
  else: discard</pre> <h3 id="statements-and-expressions-void-context">Void context</h3>
<p>In a list of statements every expression except the last one needs to have the type <code>void</code>. In addition to this rule an assignment to the builtin <code>result</code> symbol also triggers a mandatory <code>void</code> context for the subsequent expressions:</p> <pre class="listing" data-language="nim">proc invalid*(): string =
  result = "foo"
  "invalid"  # Error: value of type 'string' has to be discarded</pre>
<pre class="listing" data-language="nim">proc valid*(): string =
  let x = 317
  "valid"</pre> <h3 id="statements-and-expressions-var-statement">Var statement</h3>
<p>Var statements declare new local and global variables and initialize them. A comma-separated list of variables can be used to specify variables of the same type:</p> <pre class="listing" data-language="nim">var
  a: int = 0
  x, y, z: int</pre>
<p>If an initializer is given the type can be omitted: the variable is then of the same type as the initializing expression. Variables are always initialized with a default value if there is no initializing expression. The default value depends on the type and is always a zero in binary.</p> <table>
<tr>
<th>Type</th>
<th>default value</th>
</tr> <tr>
<td>any integer type</td>
<td>0</td>
</tr> <tr>
<td>any float</td>
<td>0.0</td>
</tr> <tr>
<td>char</td>
<td>'\0'</td>
</tr> <tr>
<td>bool</td>
<td>false</td>
</tr> <tr>
<td>ref or pointer type</td>
<td>nil</td>
</tr> <tr>
<td>procedural type</td>
<td>nil</td>
</tr> <tr>
<td>sequence</td>
<td><code>@[]</code></td>
</tr> <tr>
<td>string</td>
<td><code>""</code></td>
</tr> <tr>
<td>tuple[x: A, y: B, ...]</td>
<td>(default(A), default(B), ...) (analogous for objects)</td>
</tr> <tr>
<td>array[0..., T]</td>
<td>[default(T), ...]</td>
</tr> <tr>
<td>range[T]</td>
<td>default(T); this may be out of the valid range</td>
</tr> <tr>
<td>T = enum</td>
<td>cast[T](0); this may be an invalid value</td>
</tr> </table>
<p>The implicit initialization can be avoided for optimization reasons with the <span id="noinit_1">noinit</span> pragma:</p> <pre class="listing" data-language="nim">var
  a {.noInit.}: array[0..1023, char]</pre>
<p>If a proc is annotated with the <code>noinit</code> pragma this refers to its implicit <code>result</code> variable:</p> <pre class="listing" data-language="nim">proc returnUndefinedValue: int {.noinit.} = discard</pre>
<p>The implicit initialization can be also prevented by the <span id="requiresinit_1">requiresInit</span> type pragma. The compiler requires an explicit initialization for the object and all of its fields. However, it does a <span id="control-flow-analysis_1">control flow analysis</span> to prove the variable has been initialized and does not rely on syntactic properties:</p> <pre class="listing" data-language="nim">type
  MyObject = object {.requiresInit.}

proc p() =
  # the following is valid:
  var x: MyObject
  if someCondition():
    x = a()
  else:
    x = a()
  # use x</pre> <h3 id="statements-and-expressions-let-statement">Let statement</h3>
<p>A <code>let</code> statement declares new local and global <span id="single-assignment_1">single assignment</span> variables and binds a value to them. The syntax is the same as that of the <code>var</code> statement, except that the keyword <code>var</code> is replaced by the keyword <code>let</code>. Let variables are not l-values and can thus not be passed to <code>var</code> parameters nor can their address be taken. They cannot be assigned new values.</p> <p>For let variables, the same pragmas are available as for ordinary variables.</p> <p>As <code>let</code> statements are immutable after creation they need to define a value when they are declared. The only exception to this is if the <code>{.importc.}</code> pragma (or any of the other <code>importX</code> pragmas) is applied, in this case the value is expected to come from native code, typically a C/C++ <code>const</code>.</p> <h3 id="statements-and-expressions-tuple-unpacking">Tuple unpacking</h3>
<p>In a <code>var</code> or <code>let</code> statement tuple unpacking can be performed. The special identifier <code>_</code> can be used to ignore some parts of the tuple:</p> <pre class="listing" data-language="nim">proc returnsTuple(): (int, int, int) = (4, 2, 3)

let (x, _, z) = returnsTuple()</pre> <h3 id="statements-and-expressions-const-section">Const section</h3>
<p>A const section declares constants whose values are constant expressions:</p> <pre class="listing" data-language="nim">import strutils
const
  roundPi = 3.1415
  constEval = contains("abc", 'b') # computed at compile time!</pre>
<p>Once declared, a constant's symbol can be used as a constant expression.</p> <p>See <a href="#constants-and-constant-expressions">Constants and Constant Expressions</a> for details.</p> <h3 id="statements-and-expressions-static-statementslashexpression">Static statement/expression</h3>
<p>A static statement/expression explicitly requires compile-time execution. Even some code that has side effects is permitted in a static block:</p> <pre class="listing" data-language="nim">static:
  echo "echo at compile time"</pre>
<p>There are limitations on what Nim code can be executed at compile time; see <a href="#restrictions-on-compileminustime-execution">Restrictions on Compile-Time Execution</a> for details. It's a static error if the compiler cannot execute the block at compile time.</p> <h3 id="statements-and-expressions-if-statement">If statement</h3>
<p>Example:</p> <pre class="listing" data-language="nim">var name = readLine(stdin)

if name == "Andreas":
  echo "What a nice name!"
elif name == "":
  echo "Don't you have a name?"
else:
  echo "Boring name..."</pre>
<p>The <code>if</code> statement is a simple way to make a branch in the control flow: The expression after the keyword <code>if</code> is evaluated, if it is true the corresponding statements after the <code>:</code> are executed. Otherwise the expression after the <code>elif</code> is evaluated (if there is an <code>elif</code> branch), if it is true the corresponding statements after the <code>:</code> are executed. This goes on until the last <code>elif</code>. If all conditions fail, the <code>else</code> part is executed. If there is no <code>else</code> part, execution continues with the next statement.</p> <p>In <code>if</code> statements new scopes begin immediately after the <code>if</code>/<code>elif</code>/<code>else</code> keywords and ends after the corresponding <em>then</em> block. For visualization purposes the scopes have been enclosed in <code>{| |}</code> in the following example:</p> <pre class="listing" data-language="nim">if {| (let m = input =~ re"(\w+)=\w+"; m.isMatch):
  echo "key ", m[0], " value ", m[1]  |}
elif {| (let m = input =~ re""; m.isMatch):
  echo "new m in this scope"  |}
else: {|
  echo "m not declared here"  |}</pre> <h3 id="statements-and-expressions-case-statement">Case statement</h3>
<p>Example:</p> <pre class="listing" data-language="nim">case readline(stdin)
of "delete-everything", "restart-computer":
  echo "permission denied"
of "go-for-a-walk":     echo "please yourself"
else:                   echo "unknown command"

# indentation of the branches is also allowed; and so is an optional colon
# after the selecting expression:
case readline(stdin):
  of "delete-everything", "restart-computer":
    echo "permission denied"
  of "go-for-a-walk":     echo "please yourself"
  else:                   echo "unknown command"</pre>
<p>The <code>case</code> statement is similar to the if statement, but it represents a multi-branch selection. The expression after the keyword <code>case</code> is evaluated and if its value is in a <em>slicelist</em> the corresponding statements (after the <code>of</code> keyword) are executed. If the value is not in any given <em>slicelist</em> the <code>else</code> part is executed. If there is no <code>else</code> part and not all possible values that <code>expr</code> can hold occur in a <code>slicelist</code>, a static error occurs. This holds only for expressions of ordinal types. "All possible values" of <code>expr</code> are determined by <code>expr</code>'s type. To suppress the static error an <code>else</code> part with an empty <code>discard</code> statement should be used.</p> <p>For non-ordinal types, it is not possible to list every possible value and so these always require an <code>else</code> part.</p> <p>Because case statements are checked for exhaustiveness during semantic analysis, the value in every <code>of</code> branch must be a constant expression. This restriction also allows the compiler to generate more performant code.</p> <p>As a special semantic extension, an expression in an <code>of</code> branch of a case statement may evaluate to a set or array constructor; the set or array is then expanded into a list of its elements:</p> <pre class="listing" data-language="nim">const
  SymChars: set[char] = {'a'..'z', 'A'..'Z', '\x80'..'\xFF'}

proc classify(s: string) =
  case s[0]
  of SymChars, '_': echo "an identifier"
  of '0'..'9': echo "a number"
  else: echo "other"

# is equivalent to:
proc classify(s: string) =
  case s[0]
  of 'a'..'z', 'A'..'Z', '\x80'..'\xFF', '_': echo "an identifier"
  of '0'..'9': echo "a number"
  else: echo "other"</pre>
<p>The <code>case</code> statement doesn't produce an l-value, so the following example won't work:</p> <pre class="listing" data-language="nim">type
  Foo = ref object
    x: seq[string]

proc get_x(x: Foo): var seq[string] =
  # doesn't work
  case true
  of true:
    x.x
  else:
    x.x

var foo = Foo(x: @[])
foo.get_x().add("asd")</pre>
<p>This can be fixed by explicitly using <code>return</code>:</p> <pre class="listing" data-language="nim">proc get_x(x: Foo): var seq[string] =
  case true
  of true:
    return x.x
  else:
    return x.x</pre> <h3 id="statements-and-expressions-when-statement">When statement</h3>
<p>Example:</p> <pre class="listing" data-language="nim">when sizeof(int) == 2:
  echo "running on a 16 bit system!"
elif sizeof(int) == 4:
  echo "running on a 32 bit system!"
elif sizeof(int) == 8:
  echo "running on a 64 bit system!"
else:
  echo "cannot happen!"</pre>
<p>The <code>when</code> statement is almost identical to the <code>if</code> statement with some exceptions:</p> <ul class="simple">
<li>Each condition (<code>expr</code>) has to be a constant expression (of type <code>bool</code>).</li> <li>The statements do not open a new scope.</li> <li>The statements that belong to the expression that evaluated to true are translated by the compiler, the other statements are not checked for semantics! However, each condition is checked for semantics.</li> </ul> <p>The <code>when</code> statement enables conditional compilation techniques. As a special syntactic extension, the <code>when</code> construct is also available within <code>object</code> definitions.</p> <h3 id="statements-and-expressions-when-nimvm-statement">When nimvm statement</h3>
<p><code>nimvm</code> is a special symbol, that may be used as an expression of <code>when nimvm</code> statement to differentiate execution path between compile-time and the executable.</p> <p>Example:</p> <pre class="listing" data-language="nim">proc someProcThatMayRunInCompileTime(): bool =
  when nimvm:
    # This branch is taken at compile time.
    result = true
  else:
    # This branch is taken in the executable.
    result = false
const ctValue = someProcThatMayRunInCompileTime()
let rtValue = someProcThatMayRunInCompileTime()
assert(ctValue == true)
assert(rtValue == false)</pre>
<p><code>when nimvm</code> statement must meet the following requirements:</p> <ul class="simple">
<li>Its expression must always be <code>nimvm</code>. More complex expressions are not allowed.</li> <li>It must not contain <code>elif</code> branches.</li> <li>It must contain <code>else</code> branch.</li> <li>Code in branches must not affect semantics of the code that follows the <code>when nimvm</code> statement. E.g. it must not define symbols that are used in the following code.</li> </ul> <h3 id="statements-and-expressions-return-statement">Return statement</h3>
<p>Example:</p> <pre class="listing" data-language="nim">return 40+2</pre>
<p>The <code>return</code> statement ends the execution of the current procedure. It is only allowed in procedures. If there is an <code>expr</code>, this is syntactic sugar for:</p> <pre class="listing" data-language="nim">result = expr
return result</pre>
<p><code>return</code> without an expression is a short notation for <code>return result</code> if the proc has a return type. The <span id="result_1">result</span> variable is always the return value of the procedure. It is automatically declared by the compiler. As all variables, <code>result</code> is initialized to (binary) zero:</p> <pre class="listing" data-language="nim">proc returnZero(): int =
  # implicitly returns 0</pre> <h3 id="statements-and-expressions-yield-statement">Yield statement</h3>
<p>Example:</p> <pre class="listing" data-language="nim">yield (1, 2, 3)</pre>
<p>The <code>yield</code> statement is used instead of the <code>return</code> statement in iterators. It is only valid in iterators. Execution is returned to the body of the for loop that called the iterator. Yield does not end the iteration process, but the execution is passed back to the iterator if the next iteration starts. See the section about iterators (<a href="#iterators-and-the-for-statement">Iterators and the for statement</a>) for further information.</p> <h3 id="statements-and-expressions-block-statement">Block statement</h3>
<p>Example:</p> <pre class="listing" data-language="nim">var found = false
block myblock:
  for i in 0..3:
    for j in 0..3:
      if a[j][i] == 7:
        found = true
        break myblock # leave the block, in this case both for-loops
echo found</pre>
<p>The block statement is a means to group statements to a (named) <code>block</code>. Inside the block, the <code>break</code> statement is allowed to leave the block immediately. A <code>break</code> statement can contain a name of a surrounding block to specify which block is to leave.</p> <h3 id="statements-and-expressions-break-statement">Break statement</h3>
<p>Example:</p> <pre class="listing">break</pre>
<p>The <code>break</code> statement is used to leave a block immediately. If <code>symbol</code> is given, it is the name of the enclosing block that is to leave. If it is absent, the innermost block is left.</p> <h3 id="statements-and-expressions-while-statement">While statement</h3>
<p>Example:</p> <pre class="listing" data-language="nim">echo "Please tell me your password:"
var pw = readLine(stdin)
while pw != "12345":
  echo "Wrong password! Next try:"
  pw = readLine(stdin)</pre>
<p>The <code>while</code> statement is executed until the <code>expr</code> evaluates to false. Endless loops are no error. <code>while</code> statements open an <code>implicit block</code>, so that they can be left with a <code>break</code> statement.</p> <h3 id="statements-and-expressions-continue-statement">Continue statement</h3>
<p>A <code>continue</code> statement leads to the immediate next iteration of the surrounding loop construct. It is only allowed within a loop. A continue statement is syntactic sugar for a nested block:</p> <pre class="listing" data-language="nim">while expr1:
  stmt1
  continue
  stmt2</pre>
<p>Is equivalent to:</p> <pre class="listing" data-language="nim">while expr1:
  block myBlockName:
    stmt1
    break myBlockName
    stmt2</pre> <h3 id="statements-and-expressions-assembler-statement">Assembler statement</h3>
<p>The direct embedding of assembler code into Nim code is supported by the unsafe <code>asm</code> statement. Identifiers in the assembler code that refer to Nim identifiers shall be enclosed in a special character which can be specified in the statement's pragmas. The default special character is <code>'`'</code>:</p> <pre class="listing" data-language="nim">{.push stackTrace:off.}
proc addInt(a, b: int): int =
  # a in eax, and b in edx
  asm """
      mov eax, `a`
      add eax, `b`
      jno theEnd
      call `raiseOverflow`
    theEnd:
  """
{.pop.}</pre>
<p>If the GNU assembler is used, quotes and newlines are inserted automatically:</p> <pre class="listing" data-language="nim">proc addInt(a, b: int): int =
  asm """
    addl %%ecx, %%eax
    jno 1
    call `raiseOverflow`
    1:
    :"=a"(`result`)
    :"a"(`a`), "c"(`b`)
  """</pre>
<p>Instead of:</p> <pre class="listing" data-language="nim">proc addInt(a, b: int): int =
  asm """
    "addl %%ecx, %%eax\n"
    "jno 1\n"
    "call `raiseOverflow`\n"
    "1: \n"
    :"=a"(`result`)
    :"a"(`a`), "c"(`b`)
  """</pre> <h3 id="statements-and-expressions-using-statement">Using statement</h3>
<p>The using statement provides syntactic convenience in modules where the same parameter names and types are used over and over. Instead of:</p> <pre class="listing" data-language="nim">proc foo(c: Context; n: Node) = ...
proc bar(c: Context; n: Node, counter: int) = ...
proc baz(c: Context; n: Node) = ...</pre>
<p>One can tell the compiler about the convention that a parameter of name <code>c</code> should default to type <code>Context</code>, <code>n</code> should default to <code>Node</code> etc.:</p> <pre class="listing" data-language="nim">using
  c: Context
  n: Node
  counter: int

proc foo(c, n) = ...
proc bar(c, n, counter) = ...
proc baz(c, n) = ...

proc mixedMode(c, n; x, y: int) =
  # 'c' is inferred to be of the type 'Context'
  # 'n' is inferred to be of the type 'Node'
  # But 'x' and 'y' are of type 'int'.</pre>
<p>The <code>using</code> section uses the same indentation based grouping syntax as a <code>var</code> or <code>let</code> section.</p> <p>Note that <code>using</code> is not applied for <code>template</code> since the untyped template parameters default to the type <code>system.untyped</code>.</p> <p>Mixing parameters that should use the <code>using</code> declaration with parameters that are explicitly typed is possible and requires a semicolon between them.</p> <h3 id="statements-and-expressions-if-expression">If expression</h3>
<p>An <code>if expression</code> is almost like an if statement, but it is an expression. This feature is similar to <code>ternary operators</code> in other languages. Example:</p> <pre class="listing" data-language="nim">var y = if x &gt; 8: 9 else: 10</pre>
<p>An if expression always results in a value, so the <code>else</code> part is required. <code>Elif</code> parts are also allowed.</p> <h3 id="statements-and-expressions-when-expression">When expression</h3>
<p>Just like an <code>if expression</code>, but corresponding to the when statement.</p> <h3 id="statements-and-expressions-case-expression">Case expression</h3>
<p>The <code>case expression</code> is again very similar to the case statement:</p> <pre class="listing" data-language="nim">var favoriteFood = case animal
  of "dog": "bones"
  of "cat": "mice"
  elif animal.endsWith"whale": "plankton"
  else:
    echo "I'm not sure what to serve, but everybody loves ice cream"
    "ice cream"</pre>
<p>As seen in the above example, the case expression can also introduce side effects. When multiple statements are given for a branch, Nim will use the last expression as the result value.</p> <h3 id="statements-and-expressions-block-expression">Block expression</h3>
<p>A <code>block expression</code> is almost like a block statement, but it is an expression that uses the last expression under the block as the value. It is similar to the statement list expression, but the statement list expression does not open a new block scope.</p> <pre class="listing" data-language="nim">let a = block:
  var fib = @[0, 1]
  for i in 0..10:
    fib.add fib[^1] + fib[^2]
  fib</pre> <h3 id="statements-and-expressions-table-constructor">Table constructor</h3>
<p>A table constructor is syntactic sugar for an array constructor:</p> <pre class="listing" data-language="nim">{"key1": "value1", "key2", "key3": "value2"}

# is the same as:
[("key1", "value1"), ("key2", "value2"), ("key3", "value2")]</pre>
<p>The empty table can be written <code>{:}</code> (in contrast to the empty set which is <code>{}</code>) which is thus another way to write as the empty array constructor <code>[]</code>. This slightly unusual way of supporting tables has lots of advantages:</p> <ul class="simple">
<li>The order of the (key,value)-pairs is preserved, thus it is easy to support ordered dicts with for example <code>{key: val}.newOrderedTable</code>.</li> <li>A table literal can be put into a <code>const</code> section and the compiler can easily put it into the executable's data section just like it can for arrays and the generated data section requires a minimal amount of memory.</li> <li>Every table implementation is treated equally syntactically.</li> <li>Apart from the minimal syntactic sugar the language core does not need to know about tables.</li> </ul> <h3 id="statements-and-expressions-type-conversions">Type conversions</h3>
<p>Syntactically a <em>type conversion</em> is like a procedure call, but a type name replaces the procedure name. A type conversion is always safe in the sense that a failure to convert a type to another results in an exception (if it cannot be determined statically).</p> <p>Ordinary procs are often preferred over type conversions in Nim: For instance, <code>$</code> is the <code>toString</code> operator by convention and <code>toFloat</code> and <code>toInt</code> can be used to convert from floating-point to integer or vice versa.</p> <p>Type conversion can also be used to disambiguate overloaded routines:</p> <pre class="listing" data-language="nim">proc p(x: int) = echo "int"
proc p(x: string) = echo "string"

let procVar = (proc(x: string))(p)
procVar("a")</pre>
<p>Since operations on unsigned numbers wrap around and are unchecked so are type conversion to unsigned integers and between unsigned integers. The rationale for this is mostly better interoperability with the C Programming language when algorithms are ported from C to Nim.</p> <p>Exception: Values that are converted to an unsigned type at compile time are checked so that code like <code>byte(-1)</code> does not compile.</p> <p><strong>Note</strong>: Historically the operations were unchecked and the conversions were sometimes checked but starting with the revision 1.0.4 of this document and the language implementation the conversions too are now <em>always unchecked</em>.</p> <h3 id="statements-and-expressions-type-casts">Type casts</h3>
<p><em>Type casts</em> are a crude mechanism to interpret the bit pattern of an expression as if it would be of another type. Type casts are only needed for low-level programming and are inherently unsafe.</p> <pre class="listing" data-language="nim">cast[int](x)</pre>
<p>The target type of a cast must be a concrete type, for instance, a target type that is a type class (which is non-concrete) would be invalid:</p> <pre class="listing" data-language="nim">type Foo = int or float
var x = cast[Foo](1) # Error: cannot cast to a non concrete type: 'Foo'</pre>
<p>Type casts should not be confused with <em>type conversions,</em> as mentioned in the prior section. Unlike type conversions, a type cast cannot change the underlying bit pattern of the data being casted (aside from that the size of the target type may differ from the source type). Casting resembles <em>type punning</em> in other languages or C++'s <code>reinterpret_cast</code> and <code>bit_cast</code> features.</p> <h3 id="statements-and-expressions-the-addr-operator">The addr operator</h3>
<p>The <code>addr</code> operator returns the address of an l-value. If the type of the location is <code>T</code>, the <code>addr</code> operator result is of the type <code>ptr T</code>. An address is always an untraced reference. Taking the address of an object that resides on the stack is <strong>unsafe</strong>, as the pointer may live longer than the object on the stack and can thus reference a non-existing object. One can get the address of variables, but one can't use it on variables declared through <code>let</code> statements:</p> <pre class="listing" data-language="nim">let t1 = "Hello"
var
  t2 = t1
  t3 : pointer = addr(t2)
echo repr(addr(t2))
# --&gt; ref 0x7fff6b71b670 --&gt; 0x10bb81050"Hello"
echo cast[ptr string](t3)[]
# --&gt; Hello
# The following line doesn't compile:
echo repr(addr(t1))
# Error: expression has no address</pre> <h3 id="statements-and-expressions-the-unsafeaddr-operator">The unsafeAddr operator</h3>
<p>For easier interoperability with other compiled languages such as C, retrieving the address of a <code>let</code> variable, a parameter or a <code>for</code> loop variable, the <code>unsafeAddr</code> operation can be used:</p> <pre class="listing" data-language="nim">let myArray = [1, 2, 3]
foreignProcThatTakesAnAddr(unsafeAddr myArray)</pre> <h2 id="procedures">Procedures</h2>
<p>What most programming languages call <span id="methods_1">methods</span> or <span id="functions_1">functions</span> are called <span id="procedures_1">procedures</span> in Nim. A procedure declaration consists of an identifier, zero or more formal parameters, a return value type and a block of code. Formal parameters are declared as a list of identifiers separated by either comma or semicolon. A parameter is given a type by <code>: typename</code>. The type applies to all parameters immediately before it, until either the beginning of the parameter list, a semicolon separator, or an already typed parameter, is reached. The semicolon can be used to make separation of types and subsequent identifiers more distinct.</p> <pre class="listing" data-language="nim"># Using only commas
proc foo(a, b: int, c, d: bool): int

# Using semicolon for visual distinction
proc foo(a, b: int; c, d: bool): int

# Will fail: a is untyped since ';' stops type propagation.
proc foo(a; b: int; c, d: bool): int</pre>
<p>A parameter may be declared with a default value which is used if the caller does not provide a value for the argument.</p> <pre class="listing" data-language="nim"># b is optional with 47 as its default value
proc foo(a: int, b: int = 47): int</pre>
<p>Parameters can be declared mutable and so allow the proc to modify those arguments, by using the type modifier <code>var</code>.</p> <pre class="listing" data-language="nim"># "returning" a value to the caller through the 2nd argument
# Notice that the function uses no actual return value at all (ie void)
proc foo(inp: int, outp: var int) =
  outp = inp + 47</pre>
<p>If the proc declaration has no body, it is a <span id="forward_1">forward</span> declaration. If the proc returns a value, the procedure body can access an implicitly declared variable named <span id="result_2">result</span> that represents the return value. Procs can be overloaded. The overloading resolution algorithm determines which proc is the best match for the arguments. Example:</p> <pre class="listing" data-language="nim">proc toLower(c: char): char = # toLower for characters
  if c in {'A'..'Z'}:
    result = chr(ord(c) + (ord('a') - ord('A')))
  else:
    result = c

proc toLower(s: string): string = # toLower for strings
  result = newString(len(s))
  for i in 0..len(s) - 1:
    result[i] = toLower(s[i]) # calls toLower for characters; no recursion!</pre>
<p>Calling a procedure can be done in many different ways:</p> <pre class="listing" data-language="nim">proc callme(x, y: int, s: string = "", c: char, b: bool = false) = ...

# call with positional arguments      # parameter bindings:
callme(0, 1, "abc", '\t', true)       # (x=0, y=1, s="abc", c='\t', b=true)
# call with named and positional arguments:
callme(y=1, x=0, "abd", '\t')         # (x=0, y=1, s="abd", c='\t', b=false)
# call with named arguments (order is not relevant):
callme(c='\t', y=1, x=0)              # (x=0, y=1, s="", c='\t', b=false)
# call as a command statement: no () needed:
callme 0, 1, "abc", '\t'              # (x=0, y=1, s="abc", c='\t', b=false)</pre>
<p>A procedure may call itself recursively.</p> <p><span id="operators_1">Operators</span> are procedures with a special operator symbol as identifier:</p> <pre class="listing" data-language="nim">proc `$` (x: int): string =
  # converts an integer to a string; this is a prefix operator.
  result = intToStr(x)</pre>
<p>Operators with one parameter are prefix operators, operators with two parameters are infix operators. (However, the parser distinguishes these from the operator's position within an expression.) There is no way to declare postfix operators: all postfix operators are built-in and handled by the grammar explicitly.</p> <p>Any operator can be called like an ordinary proc with the '<code>opr</code>' notation. (Thus an operator can have more than two parameters):</p> <pre class="listing" data-language="nim">proc `*+` (a, b, c: int): int =
  # Multiply and add
  result = a * b + c

assert `*+`(3, 4, 6) == `+`(`*`(a, b), c)</pre> <h3 id="procedures-export-marker">Export marker</h3>
<p>If a declared symbol is marked with an <span id="asterisk_1">asterisk</span> it is exported from the current module:</p> <pre class="listing" data-language="nim">proc exportedEcho*(s: string) = echo s
proc `*`*(a: string; b: int): string =
  result = newStringOfCap(a.len * b)
  for i in 1..b: result.add a

var exportedVar*: int
const exportedConst* = 78
type
  ExportedType* = object
    exportedField*: int</pre> <h3 id="procedures-method-call-syntax">Method call syntax</h3>
<p>For object-oriented programming, the syntax <code>obj.method(args)</code> can be used instead of <code>method(obj, args)</code>. The parentheses can be omitted if there are no remaining arguments: <code>obj.len</code> (instead of <code>len(obj)</code>).</p> <p>This method call syntax is not restricted to objects, it can be used to supply any type of first argument for procedures:</p> <pre class="listing" data-language="nim">echo "abc".len # is the same as echo len "abc"
echo "abc".toUpper()
echo {'a', 'b', 'c'}.card
stdout.writeLine("Hallo") # the same as writeLine(stdout, "Hallo")</pre>
<p>Another way to look at the method call syntax is that it provides the missing postfix notation.</p> <p>The method call syntax conflicts with explicit generic instantiations: <code>p[T](x)</code> cannot be written as <code>x.p[T]</code> because <code>x.p[T]</code> is always parsed as <code>(x.p)[T]</code>.</p> <p>See also: <a href="#templates-limitations-of-the-method-call-syntax">Limitations of the method call syntax</a>.</p> <p>The <code>[: ]</code> notation has been designed to mitigate this issue: <code>x.p[:T]</code> is rewritten by the parser to <code>p[T](x)</code>, <code>x.p[:T](y)</code> is rewritten to <code>p[T](x, y)</code>. Note that <code>[: ]</code> has no AST representation, the rewrite is performed directly in the parsing step.</p> <h3 id="procedures-properties">Properties</h3>
<p>Nim has no need for <em>get-properties</em>: Ordinary get-procedures that are called with the <em>method call syntax</em> achieve the same. But setting a value is different; for this, a special setter syntax is needed:</p> <pre class="listing" data-language="nim"># Module asocket
type
  Socket* = ref object of RootObj
    host: int # cannot be accessed from the outside of the module

proc `host=`*(s: var Socket, value: int) {.inline.} =
  ## setter of hostAddr.
  ## This accesses the 'host' field and is not a recursive call to
  ## ``host=`` because the builtin dot access is preferred if it is
  ## available:
  s.host = value

proc host*(s: Socket): int {.inline.} =
  ## getter of hostAddr
  ## This accesses the 'host' field and is not a recursive call to
  ## ``host`` because the builtin dot access is preferred if it is
  ## available:
  s.host</pre>
<pre class="listing" data-language="nim"># module B
import asocket
var s: Socket
new s
s.host = 34  # same as `host=`(s, 34)</pre>
<p>A proc defined as <code>f=</code> (with the trailing <code>=</code>) is called a <span id="setter_1">setter</span>. A setter can be called explicitly via the common backticks notation:</p> <pre class="listing" data-language="nim">proc `f=`(x: MyObject; value: string) =
  discard

`f=`(myObject, "value")</pre>
<p><code>f=</code> can be called implicitly in the pattern <code>x.f = value</code> if and only if the type of <code>x</code> does not have a field named <code>f</code> or if <code>f</code> is not visible in the current module. These rules ensure that object fields and accessors can have the same name. Within the module <code>x.f</code> is then always interpreted as field access and outside the module it is interpreted as an accessor proc call.</p> <h3 id="procedures-command-invocation-syntax">Command invocation syntax</h3>
<p>Routines can be invoked without the <code>()</code> if the call is syntactically a statement. This command invocation syntax also works for expressions, but then only a single argument may follow. This restriction means <code>echo f 1, f 2</code> is parsed as <code>echo(f(1), f(2))</code> and not as <code>echo(f(1, f(2)))</code>. The method call syntax may be used to provide one more argument in this case:</p> <pre class="listing" data-language="nim">proc optarg(x: int, y: int = 0): int = x + y
proc singlearg(x: int): int = 20*x

echo optarg 1, " ", singlearg 2  # prints "1 40"

let fail = optarg 1, optarg 8   # Wrong. Too many arguments for a command call
let x = optarg(1, optarg 8)  # traditional procedure call with 2 arguments
let y = 1.optarg optarg 8    # same thing as above, w/o the parenthesis
assert x == y</pre>
<p>The command invocation syntax also can't have complex expressions as arguments. For example: (<a href="#procedures-anonymous-procs">anonymous procs</a>), <code>if</code>, <code>case</code> or <code>try</code>. Function calls with no arguments still need () to distinguish between a call and the function itself as a first-class value.</p> <h3 id="procedures-closures">Closures</h3>
<p>Procedures can appear at the top level in a module as well as inside other scopes, in which case they are called nested procs. A nested proc can access local variables from its enclosing scope and if it does so it becomes a closure. Any captured variables are stored in a hidden additional argument to the closure (its environment) and they are accessed by reference by both the closure and its enclosing scope (i.e. any modifications made to them are visible in both places). The closure environment may be allocated on the heap or on the stack if the compiler determines that this would be safe.</p> <h4 id="closures-creating-closures-in-loops">Creating closures in loops</h4>
<p>Since closures capture local variables by reference it is often not wanted behavior inside loop bodies. See <a href="system.html#closureScope.t,untyped">closureScope</a> and <a href="sugar.html#capture.m,openArray%5Btyped%5D,untyped">capture</a> for details on how to change this behavior.</p> <h3 id="procedures-anonymous-procs">Anonymous Procs</h3>
<p>Unnamed procedures can be used as lambda expressions to pass into other procedures:</p> <pre class="listing" data-language="nim">var cities = @["Frankfurt", "Tokyo", "New York", "Kyiv"]

cities.sort(proc (x,y: string): int =
    cmp(x.len, y.len))</pre>
<p>Procs as expressions can appear both as nested procs and inside top-level executable code. The <a href="sugar.html">sugar</a> module contains the <code>=&gt;</code> macro which enables a more succinct syntax for anonymous procedures resembling lambdas as they are in languages like JavaScript, C#, etc.</p> <h3 id="procedures-func">Func</h3>
<p>The <code>func</code> keyword introduces a shortcut for a <span id="nosideeffect_1">noSideEffect</span> proc.</p> <pre class="listing" data-language="nim">func binarySearch[T](a: openArray[T]; elem: T): int</pre>
<p>Is short for:</p> <pre class="listing" data-language="nim">proc binarySearch[T](a: openArray[T]; elem: T): int {.noSideEffect.}</pre> <h3 id="procedures-nonoverloadable-builtins">Nonoverloadable builtins</h3>
<p>The following built-in procs cannot be overloaded for reasons of implementation simplicity (they require specialized semantic checking):</p>
<pre data-language="nim">declared, defined, definedInScope, compiles, sizeof,
is, shallowCopy, getAst, astToStr, spawn, procCall</pre> <p>Thus they act more like keywords than like ordinary identifiers; unlike a keyword however, a redefinition may <span id="shadow_1">shadow</span> the definition in the <code>system</code> module. From this list the following should not be written in dot notation <code>x.f</code> since <code>x</code> cannot be type-checked before it gets passed to <code>f</code>:</p>
<pre data-language="nim">declared, defined, definedInScope, compiles, getAst, astToStr</pre> <h3 id="procedures-var-parameters">Var parameters</h3>
<p>The type of a parameter may be prefixed with the <code>var</code> keyword:</p> <pre class="listing" data-language="nim">proc divmod(a, b: int; res, remainder: var int) =
  res = a div b
  remainder = a mod b

var
  x, y: int

divmod(8, 5, x, y) # modifies x and y
assert x == 1
assert y == 3</pre>
<p>In the example, <code>res</code> and <code>remainder</code> are <code>var parameters</code>. Var parameters can be modified by the procedure and the changes are visible to the caller. The argument passed to a var parameter has to be an l-value. Var parameters are implemented as hidden pointers. The above example is equivalent to:</p> <pre class="listing" data-language="nim">proc divmod(a, b: int; res, remainder: ptr int) =
  res[] = a div b
  remainder[] = a mod b

var
  x, y: int
divmod(8, 5, addr(x), addr(y))
assert x == 1
assert y == 3</pre>
<p>In the examples, var parameters or pointers are used to provide two return values. This can be done in a cleaner way by returning a tuple:</p> <pre class="listing" data-language="nim">proc divmod(a, b: int): tuple[res, remainder: int] =
  (a div b, a mod b)

var t = divmod(8, 5)

assert t.res == 1
assert t.remainder == 3</pre>
<p>One can use <span id="tuple-unpacking_1">tuple unpacking</span> to access the tuple's fields:</p> <pre class="listing" data-language="nim">var (x, y) = divmod(8, 5) # tuple unpacking
assert x == 1
assert y == 3</pre>
<p><strong>Note</strong>: <code>var</code> parameters are never necessary for efficient parameter passing. Since non-var parameters cannot be modified the compiler is always free to pass arguments by reference if it considers it can speed up execution.</p> <h3 id="procedures-var-return-type">Var return type</h3>
<p>A proc, converter, or iterator may return a <code>var</code> type which means that the returned value is an l-value and can be modified by the caller:</p> <pre class="listing" data-language="nim">var g = 0

proc writeAccessToG(): var int =
  result = g

writeAccessToG() = 6
assert g == 6</pre>
<p>It is a static error if the implicitly introduced pointer could be used to access a location beyond its lifetime:</p> <pre class="listing" data-language="nim">proc writeAccessToG(): var int =
  var g = 0
  result = g # Error!</pre>
<p>For iterators, a component of a tuple return type can have a <code>var</code> type too:</p> <pre class="listing" data-language="nim">iterator mpairs(a: var seq[string]): tuple[key: int, val: var string] =
  for i in 0..a.high:
    yield (i, a[i])</pre>
<p>In the standard library every name of a routine that returns a <code>var</code> type starts with the prefix <code>m</code> per convention.</p> Memory safety for returning by <code>var T</code> is ensured by a simple borrowing rule: If <code>result</code> does not refer to a location pointing to the heap (that is in <code>result = X</code> the <code>X</code> involves a <code>ptr</code> or <code>ref</code> access) then it has to be derived from the routine's first parameter:<pre class="listing" data-language="nim">proc forward[T](x: var T): var T =
  result = x # ok, derived from the first parameter.

proc p(param: var int): var int =
  var x: int
  # we know 'forward' provides a view into the location derived from
  # its first argument 'x'.
  result = forward(x) # Error: location is derived from ``x``
                      # which is not p's first parameter and lives
                      # on the stack.</pre>
<p>In other words, the lifetime of what <code>result</code> points to is attached to the lifetime of the first parameter and that is enough knowledge to verify memory safety at the call site.</p> <h4 id="var-return-type-future-directions">Future directions</h4>
<p>Later versions of Nim can be more precise about the borrowing rule with a syntax like:</p> <pre class="listing" data-language="nim">proc foo(other: Y; container: var X): var T from container</pre>
<p>Here <code>var T from container</code> explicitly exposes that the location is derived from the second parameter (called 'container' in this case). The syntax <code>var T from p</code> specifies a type <code>varTy[T, 2]</code> which is incompatible with <code>varTy[T, 1]</code>.</p> <h3 id="procedures-nrvo">NRVO</h3>
<p><strong>Note</strong>: This section describes the current implementation. This part of the language specification will be changed. See <a href="https://github.com/nim-lang/RFCs/issues/230">https://github.com/nim-lang/RFCs/issues/230</a> for more information.</p> <p>The return value is represented inside the body of a routine as the special <span id="result_3">result</span> variable. This allows for a mechanism much like C++'s "named return value optimization" (<span id="nrvo_1">NRVO</span>). NRVO means that the stores to <code>result</code> inside <code>p</code> directly affect the destination <code>dest</code> in <code>let/var dest = p(args)</code> (definition of <code>dest</code>) and also in <code>dest = p(args)</code> (assignment to <code>dest</code>). This is achieved by rewriting <code>dest = p(args)</code> to <code>p'(args, dest)</code> where <code>p'</code> is a variation of <code>p</code> that returns <code>void</code> and receives a hidden mutable parameter representing <code>result</code>.</p> <p>Informally:</p> <pre class="listing" data-language="nim">proc p(): BigT = ...

var x = p()
x = p()

# is roughly turned into:

proc p(result: var BigT) = ...

var x; p(x)
p(x)</pre>
<p>Let <code>T</code>'s be <code>p</code>'s return type. NRVO applies for <code>T</code> if <code>sizeof(T) &gt;= N</code> (where <code>N</code> is implementation dependent), in other words, it applies for "big" structures.</p> <p>If <code>p</code> can raise an exception, NRVO applies regardless. This can produce observable differences in behavior:</p> <pre class="listing" data-language="nim">type
  BigT = array[16, int]

proc p(raiseAt: int): BigT =
  for i in 0..high(result):
    if i == raiseAt: raise newException(ValueError, "interception")
    result[i] = i

proc main =
  var x: BigT
  try:
    x = p(8)
  except ValueError:
    doAssert x == [0, 1, 2, 3, 4, 5, 6, 7, 0, 0, 0, 0, 0, 0, 0, 0]

main()</pre>
<p>However, the current implementation produces a warning in these cases. There are different ways to deal with this warning:</p> <ol class="simple">
<li>Disable the warning via <code>{.push warning[ObservableStores]: off.}</code> ... <code>{.pop.}</code>. Then one may need to ensure that <code>p</code> only raises <em>before</em> any stores to <code>result</code> happen.</li> <li>One can use a temporary helper variable, for example instead of <code>x = p(8)</code> use <code>let tmp = p(8); x = tmp</code>.</li> </ol> <h3 id="procedures-overloading-of-the-subscript-operator">Overloading of the subscript operator</h3>
<p>The <code>[]</code> subscript operator for arrays/openarrays/sequences can be overloaded.</p> <h2 id="methods">Methods</h2>
<p>Procedures always use static dispatch. Methods use dynamic dispatch. For dynamic dispatch to work on an object it should be a reference type.</p> <pre class="listing" data-language="nim">type
  Expression = ref object of RootObj ## abstract base class for an expression
  Literal = ref object of Expression
    x: int
  PlusExpr = ref object of Expression
    a, b: Expression

method eval(e: Expression): int {.base.} =
  # override this base method
  raise newException(CatchableError, "Method without implementation override")

method eval(e: Literal): int = return e.x

method eval(e: PlusExpr): int =
  # watch out: relies on dynamic binding
  result = eval(e.a) + eval(e.b)

proc newLit(x: int): Literal =
  new(result)
  result.x = x

proc newPlus(a, b: Expression): PlusExpr =
  new(result)
  result.a = a
  result.b = b

echo eval(newPlus(newPlus(newLit(1), newLit(2)), newLit(4)))</pre>
<p>In the example the constructors <code>newLit</code> and <code>newPlus</code> are procs because they should use static binding, but <code>eval</code> is a method because it requires dynamic binding.</p> <p>As can be seen in the example, base methods have to be annotated with the <span id="base_1">base</span> pragma. The <code>base</code> pragma also acts as a reminder for the programmer that a base method <code>m</code> is used as the foundation to determine all the effects that a call to <code>m</code> might cause.</p> <p><strong>Note</strong>: Compile-time execution is not (yet) supported for methods.</p> <p><strong>Note</strong>: Starting from Nim 0.20, generic methods are deprecated.</p> <h3 id="methods-multiminusmethods">Multi-methods</h3>
<p><strong>Note:</strong> Starting from Nim 0.20, to use multi-methods one must explicitly pass <code>--multimethods:on</code> when compiling.</p> <p>In a multi-method all parameters that have an object type are used for the dispatching:</p> <pre class="listing" data-language="nim">type
  Thing = ref object of RootObj
  Unit = ref object of Thing
    x: int

method collide(a, b: Thing) {.inline.} =
  quit "to override!"

method collide(a: Thing, b: Unit) {.inline.} =
  echo "1"

method collide(a: Unit, b: Thing) {.inline.} =
  echo "2"

var a, b: Unit
new a
new b
collide(a, b) # output: 2</pre> <h3 id="methods-inhibit-dynamic-method-resolution-via-proccall">Inhibit dynamic method resolution via procCall</h3>
<p>Dynamic method resolution can be inhibited via the builtin <span id="systemdotproccall_1">system.procCall</span>. This is somewhat comparable to the <span id="super_1">super</span> keyword that traditional OOP languages offer.</p> <pre class="listing" data-language="nim">type
  Thing = ref object of RootObj
  Unit = ref object of Thing
    x: int

method m(a: Thing) {.base.} =
  echo "base"

method m(a: Unit) =
  # Call the base method:
  procCall m(Thing(a))
  echo "1"</pre> <h2 id="iterators-and-the-for-statement">Iterators and the for statement</h2>
<p>The <span id="for_1">for</span> statement is an abstract mechanism to iterate over the elements of a container. It relies on an <span id="iterator_1">iterator</span> to do so. Like <code>while</code> statements, <code>for</code> statements open an <span id="implicit-block_1">implicit block</span>, so that they can be left with a <code>break</code> statement.</p> <p>The <code>for</code> loop declares iteration variables - their scope reaches until the end of the loop body. The iteration variables' types are inferred by the return type of the iterator.</p> <p>An iterator is similar to a procedure, except that it can be called in the context of a <code>for</code> loop. Iterators provide a way to specify the iteration over an abstract type. A key role in the execution of a <code>for</code> loop plays the <code>yield</code> statement in the called iterator. Whenever a <code>yield</code> statement is reached the data is bound to the <code>for</code> loop variables and control continues in the body of the <code>for</code> loop. The iterator's local variables and execution state are automatically saved between calls. Example:</p> <pre class="listing" data-language="nim"># this definition exists in the system module
iterator items*(a: string): char {.inline.} =
  var i = 0
  while i &lt; len(a):
    yield a[i]
    inc(i)

for ch in items("hello world"): # `ch` is an iteration variable
  echo ch</pre>
<p>The compiler generates code as if the programmer would have written this:</p> <pre class="listing" data-language="nim">var i = 0
while i &lt; len(a):
  var ch = a[i]
  echo ch
  inc(i)</pre>
<p>If the iterator yields a tuple, there can be as many iteration variables as there are components in the tuple. The i'th iteration variable's type is the type of the i'th component. In other words, implicit tuple unpacking in a for loop context is supported.</p> <h3 id="iterators-and-the-for-statement-implicit-itemsslashpairs-invocations">Implicit items/pairs invocations</h3>
<p>If the for loop expression <code>e</code> does not denote an iterator and the for loop has exactly 1 variable, the for loop expression is rewritten to <code>items(e)</code>; ie. an <code>items</code> iterator is implicitly invoked:</p> <pre class="listing" data-language="nim">for x in [1,2,3]: echo x</pre>
<p>If the for loop has exactly 2 variables, a <code>pairs</code> iterator is implicitly invoked.</p> <p>Symbol lookup of the identifiers <code>items</code>/<code>pairs</code> is performed after the rewriting step, so that all overloads of <code>items</code>/<code>pairs</code> are taken into account.</p> <h3 id="iterators-and-the-for-statement-firstminusclass-iterators">First-class iterators</h3>
<p>There are 2 kinds of iterators in Nim: <em>inline</em> and <em>closure</em> iterators. An <span id="inline-iterator_1">inline iterator</span> is an iterator that's always inlined by the compiler leading to zero overhead for the abstraction, but may result in a heavy increase in code size.</p> <p>Caution: the body of a for loop over an inline iterator is inlined into each <code>yield</code> statement appearing in the iterator code, so ideally the code should be refactored to contain a single yield when possible to avoid code bloat.</p> <p>Inline iterators are second class citizens; They can be passed as parameters only to other inlining code facilities like templates, macros, and other inline iterators.</p> <p>In contrast to that, a <span id="closure-iterator_1">closure iterator</span> can be passed around more freely:</p> <pre class="listing" data-language="nim">iterator count0(): int {.closure.} =
  yield 0

iterator count2(): int {.closure.} =
  var x = 1
  yield x
  inc x
  yield x

proc invoke(iter: iterator(): int {.closure.}) =
  for x in iter(): echo x

invoke(count0)
invoke(count2)</pre>
<p>Closure iterators and inline iterators have some restrictions:</p> <ol class="simple">
<li>For now, a closure iterator cannot be executed at compile time.</li> <li>
<code>return</code> is allowed in a closure iterator but not in an inline iterator (but rarely useful) and ends the iteration.</li> <li>Neither inline nor closure iterators can be (directly)* recursive.</li> <li>Neither inline nor closure iterators have the special <code>result</code> variable.</li> <li>Closure iterators are not supported by the js backend.</li> </ol> <p>(*) Closure iterators can be co-recursive with a factory proc which results in similar syntax to a recursive iterator. More details follow.</p> <p>Iterators that are neither marked <code>{.closure.}</code> nor <code>{.inline.}</code> explicitly default to being inline, but this may change in future versions of the implementation.</p> <p>The <code>iterator</code> type is always of the calling convention <code>closure</code> implicitly; the following example shows how to use iterators to implement a <span id="collaborative-tasking_1">collaborative tasking</span> system:</p> <pre class="listing" data-language="nim"># simple tasking:
type
  Task = iterator (ticker: int)

iterator a1(ticker: int) {.closure.} =
  echo "a1: A"
  yield
  echo "a1: B"
  yield
  echo "a1: C"
  yield
  echo "a1: D"

iterator a2(ticker: int) {.closure.} =
  echo "a2: A"
  yield
  echo "a2: B"
  yield
  echo "a2: C"

proc runTasks(t: varargs[Task]) =
  var ticker = 0
  while true:
    let x = t[ticker mod t.len]
    if finished(x): break
    x(ticker)
    inc ticker

runTasks(a1, a2)</pre>
<p>The builtin <code>system.finished</code> can be used to determine if an iterator has finished its operation; no exception is raised on an attempt to invoke an iterator that has already finished its work.</p> <p>Note that <code>system.finished</code> is error prone to use because it only returns <code>true</code> one iteration after the iterator has finished:</p> <pre class="listing" data-language="nim">iterator mycount(a, b: int): int {.closure.} =
  var x = a
  while x &lt;= b:
    yield x
    inc x

var c = mycount # instantiate the iterator
while not finished(c):
  echo c(1, 3)

# Produces
1
2
3
0</pre>
<p>Instead this code has to be used:</p> <pre class="listing" data-language="nim">var c = mycount # instantiate the iterator
while true:
  let value = c(1, 3)
  if finished(c): break # and discard 'value'!
  echo value</pre>
<p>It helps to think that the iterator actually returns a pair <code>(value, done)</code> and <code>finished</code> is used to access the hidden <code>done</code> field.</p> <p>Closure iterators are <em>resumable functions</em> and so one has to provide the arguments to every call. To get around this limitation one can capture parameters of an outer factory proc:</p> <pre class="listing" data-language="nim">proc mycount(a, b: int): iterator (): int =
  result = iterator (): int =
    var x = a
    while x &lt;= b:
      yield x
      inc x

let foo = mycount(1, 4)

for f in foo():
  echo f</pre>
<p>The call can be made more like an inline iterator with a for loop macro:</p> <pre class="listing" data-language="nim">import macros
macro toItr(x: ForLoopStmt): untyped =
  let expr = x[0]
  let call = x[1][1] # Get foo out of toItr(foo)
  let body = x[2]
  result = quote do:
    block:
      let itr = `call`
      for `expr` in itr():
          `body`

for f in toItr(mycount(1, 4)): # using early `proc mycount`
  echo f</pre>
<p>Because of full backend function call aparatus involvment, closure iterator invocation is typically higher cost than inline iterators. Adornment by a macro wrapper at the call site like this is a possibly useful reminder.</p> <p>The factory <code>proc</code>, as an ordinary procedure, can be recursive. The above macro allows such recursion to look much like a recursive iterator would. For example:</p> <pre class="listing" data-language="nim">proc recCountDown(n: int): iterator(): int =
  result = iterator(): int =
    if n &gt; 0:
      yield n
      for e in toItr(recCountDown(n - 1)):
          yield e

for i in toItr(recCountDown(6)): # Emits: 6 5 4 3 2 1
  echo i</pre> <h2 id="converters">Converters</h2>
<p>A converter is like an ordinary proc except that it enhances the "implicitly convertible" type relation (see <a href="#type-relations-convertible-relation">Convertible relation</a>):</p> <pre class="listing" data-language="nim"># bad style ahead: Nim is not C.
converter toBool(x: int): bool = x != 0

if 4:
  echo "compiles"</pre>
<p>A converter can also be explicitly invoked for improved readability. Note that implicit converter chaining is not supported: If there is a converter from type A to type B and from type B to type C the implicit conversion from A to C is not provided.</p> <h2 id="type-sections">Type sections</h2>
<p>Example:</p> <pre class="listing" data-language="nim">type # example demonstrating mutually recursive types
  Node = ref object  # an object managed by the garbage collector (ref)
    le, ri: Node     # left and right subtrees
    sym: ref Sym     # leaves contain a reference to a Sym
  
  Sym = object       # a symbol
    name: string     # the symbol's name
    line: int        # the line the symbol was declared in
    code: Node       # the symbol's abstract syntax tree</pre>
<p>A type section begins with the <code>type</code> keyword. It contains multiple type definitions. A type definition binds a type to a name. Type definitions can be recursive or even mutually recursive. Mutually recursive types are only possible within a single <code>type</code> section. Nominal types like <code>objects</code> or <code>enums</code> can only be defined in a <code>type</code> section.</p> <h2 id="exception-handling">Exception handling</h2> <h3 id="exception-handling-try-statement">Try statement</h3>
<p>Example:</p> <pre class="listing" data-language="nim"># read the first two lines of a text file that should contain numbers
# and tries to add them
var
  f: File
if open(f, "numbers.txt"):
  try:
    var a = readLine(f)
    var b = readLine(f)
    echo "sum: " &amp; $(parseInt(a) + parseInt(b))
  except OverflowDefect:
    echo "overflow!"
  except ValueError:
    echo "could not convert string to integer"
  except IOError:
    echo "IO error!"
  except:
    echo "Unknown exception!"
  finally:
    close(f)</pre>
<p>The statements after the <code>try</code> are executed in sequential order unless an exception <code>e</code> is raised. If the exception type of <code>e</code> matches any listed in an <code>except</code> clause the corresponding statements are executed. The statements following the <code>except</code> clauses are called <span id="exception-handlers_1">exception handlers</span>.</p> <p>The empty <span id="except_1">except</span> clause is executed if there is an exception that is not listed otherwise. It is similar to an <code>else</code> clause in <code>if</code> statements.</p> <p>If there is a <span id="finally_1">finally</span> clause, it is always executed after the exception handlers.</p> <p>The exception is <em>consumed</em> in an exception handler. However, an exception handler may raise another exception. If the exception is not handled, it is propagated through the call stack. This means that often the rest of the procedure - that is not within a <code>finally</code> clause - is not executed (if an exception occurs).</p> <h3 id="exception-handling-try-expression">Try expression</h3>
<p>Try can also be used as an expression; the type of the <code>try</code> branch then needs to fit the types of <code>except</code> branches, but the type of the <code>finally</code> branch always has to be <code>void</code>:</p> <pre class="listing" data-language="nim">from strutils import parseInt

let x = try: parseInt("133a")
        except: -1
        finally: echo "hi"</pre>
<p>To prevent confusing code there is a parsing limitation; if the <code>try</code> follows a <code>(</code> it has to be written as a one liner:</p> <pre class="listing" data-language="nim">let x = (try: parseInt("133a") except: -1)</pre> <h3 id="exception-handling-except-clauses">Except clauses</h3>
<p>Within an <code>except</code> clause it is possible to access the current exception using the following syntax:</p> <pre class="listing" data-language="nim">try:
  # ...
except IOError as e:
  # Now use "e"
  echo "I/O error: " &amp; e.msg</pre>
<p>Alternatively, it is possible to use <code>getCurrentException</code> to retrieve the exception that has been raised:</p> <pre class="listing" data-language="nim">try:
  # ...
except IOError:
  let e = getCurrentException()
  # Now use "e"</pre>
<p>Note that <code>getCurrentException</code> always returns a <code>ref Exception</code> type. If a variable of the proper type is needed (in the example above, <code>IOError</code>), one must convert it explicitly:</p> <pre class="listing" data-language="nim">try:
  # ...
except IOError:
  let e = (ref IOError)(getCurrentException())
  # "e" is now of the proper type</pre>
<p>However, this is seldom needed. The most common case is to extract an error message from <code>e</code>, and for such situations, it is enough to use <code>getCurrentExceptionMsg</code>:</p> <pre class="listing" data-language="nim">try:
  # ...
except:
  echo getCurrentExceptionMsg()</pre> <h3 id="exception-handling-custom-exceptions">Custom exceptions</h3>
<p>Is it possible to create custom exceptions. A custom exception is a custom type:</p> <pre class="listing" data-language="nim">type
  LoadError* = object of Exception</pre>
<p>Ending the custom exception's name with <code>Error</code> is recommended.</p> <p>Custom exceptions can be raised like any others, e.g.:</p> <pre class="listing" data-language="nim">raise newException(LoadError, "Failed to load data")</pre> <h3 id="exception-handling-defer-statement">Defer statement</h3>
<p>Instead of a <code>try finally</code> statement a <code>defer</code> statement can be used, which avoids lexical nesting and offers more flexibility in terms of scoping as shown below.</p> <p>Any statements following the <code>defer</code> in the current block will be considered to be in an implicit try block:</p> <pre class="listing" data-language="nim">proc main =
  var f = open("numbers.txt", fmWrite)
  defer: close(f)
  f.write "abc"
  f.write "def"</pre>
<p>Is rewritten to:</p> <pre class="listing" data-language="nim">proc main =
  var f = open("numbers.txt")
  try:
    f.write "abc"
    f.write "def"
  finally:
    close(f)</pre>
<p>When <code>defer</code> is at the outermost scope of a template/macro, its scope extends to the block where the template is called from:</p> <pre class="listing" data-language="nim">template safeOpenDefer(f, path) =
  var f = open(path, fmWrite)
  defer: close(f)

template safeOpenFinally(f, path, body) =
  var f = open(path, fmWrite)
  try: body # without `defer`, `body` must be specified as parameter
  finally: close(f)

block:
  safeOpenDefer(f, "/tmp/z01.txt")
  f.write "abc"
block:
  safeOpenFinally(f, "/tmp/z01.txt"):
    f.write "abc" # adds a lexical scope
block:
  var f = open("/tmp/z01.txt", fmWrite)
  try:
    f.write "abc" # adds a lexical scope
  finally: close(f)</pre>
<p>Top-level <code>defer</code> statements are not supported since it's unclear what such a statement should refer to.</p> <h3 id="exception-handling-raise-statement">Raise statement</h3>
<p>Example:</p> <pre class="listing" data-language="nim">raise newException(IOError, "IO failed")</pre>
<p>Apart from built-in operations like array indexing, memory allocation, etc. the <code>raise</code> statement is the only way to raise an exception.</p> <p>If no exception name is given, the current exception is <span id="reminusraised_1">re-raised</span>. The <span id="reraisedefect_1">ReraiseDefect</span> exception is raised if there is no exception to re-raise. It follows that the <code>raise</code> statement <em>always</em> raises an exception.</p> <h3 id="exception-handling-exception-hierarchy">Exception hierarchy</h3>
<p>The exception tree is defined in the <a href="system.html">system</a> module. Every exception inherits from <code>system.Exception</code>. Exceptions that indicate programming bugs inherit from <code>system.Defect</code> (which is a subtype of <code>Exception</code>) and are strictly speaking not catchable as they can also be mapped to an operation that terminates the whole process. If panics are turned into exceptions, these exceptions inherit from <code>Defect</code>.</p> <p>Exceptions that indicate any other runtime error that can be caught inherit from <code>system.CatchableError</code> (which is a subtype of <code>Exception</code>).</p> <h3 id="exception-handling-imported-exceptions">Imported exceptions</h3>
<p>It is possible to raise/catch imported C++ exceptions. Types imported using <code>importcpp</code> can be raised or caught. Exceptions are raised by value and caught by reference. Example:</p> <pre class="listing" data-language="nim">type
  CStdException {.importcpp: "std::exception", header: "&lt;exception&gt;", inheritable.} = object
    ## does not inherit from `RootObj`, so we use `inheritable` instead
  CRuntimeError {.requiresInit, importcpp: "std::runtime_error", header: "&lt;stdexcept&gt;".} = object of CStdException
    ## `CRuntimeError` has no default constructor =&gt; `requiresInit`
proc what(s: CStdException): cstring {.importcpp: "((char *)#.what())".}
proc initRuntimeError(a: cstring): CRuntimeError {.importcpp: "std::runtime_error(@)", constructor.}
proc initStdException(): CStdException {.importcpp: "std::exception()", constructor.}

proc fn() =
  let a = initRuntimeError("foo")
  doAssert $a.what == "foo"
  var b: cstring
  try: raise initRuntimeError("foo2")
  except CStdException as e:
    doAssert e is CStdException
    b = e.what()
  doAssert $b == "foo2"
  
  try: raise initStdException()
  except CStdException: discard
  
  try: raise initRuntimeError("foo3")
  except CRuntimeError as e:
    b = e.what()
  except CStdException:
    doAssert false
  doAssert $b == "foo3"

fn()</pre>
<p><strong>Note:</strong> <code>getCurrentException()</code> and <code>getCurrentExceptionMsg()</code> are not available for imported exceptions from C++. One needs to use the <code>except ImportedException as x:</code> syntax and rely on functionality of the <code>x</code> object to get exception details.</p> <h2 id="effect-system">Effect system</h2> <h3 id="effect-system-exception-tracking">Exception tracking</h3>
<p>Nim supports exception tracking. The <span id="raises_1">raises</span> pragma can be used to explicitly define which exceptions a proc/iterator/method/converter is allowed to raise. The compiler verifies this:</p> <pre class="listing" data-language="nim">proc p(what: bool) {.raises: [IOError, OSError].} =
  if what: raise newException(IOError, "IO")
  else: raise newException(OSError, "OS")</pre>
<p>An empty <code>raises</code> list (<code>raises: []</code>) means that no exception may be raised:</p> <pre class="listing" data-language="nim">proc p(): bool {.raises: [].} =
  try:
    unsafeCall()
    result = true
  except:
    result = false</pre>
<p>A <code>raises</code> list can also be attached to a proc type. This affects type compatibility:</p> <pre class="listing" data-language="nim">type
  Callback = proc (s: string) {.raises: [IOError].}
var
  c: Callback

proc p(x: string) =
  raise newException(OSError, "OS")

c = p # type error</pre>
<p>For a routine <code>p</code> the compiler uses inference rules to determine the set of possibly raised exceptions; the algorithm operates on <code>p</code>'s call graph:</p> <ol class="simple">
<li>Every indirect call via some proc type <code>T</code> is assumed to raise <code>system.Exception</code> (the base type of the exception hierarchy) and thus any exception unless <code>T</code> has an explicit <code>raises</code> list. However, if the call is of the form <code>f(...)</code> where <code>f</code> is a parameter of the currently analyzed routine it is ignored. The call is optimistically assumed to have no effect. Rule 2 compensates for this case.</li> <li>Every expression of some proc type within a call that is not a call itself (and not nil) is assumed to be called indirectly somehow and thus its raises list is added to <code>p</code>'s raises list.</li> <li>Every call to a proc <code>q</code> which has an unknown body (due to a forward declaration or an <code>importc</code> pragma) is assumed to raise <code>system.Exception</code> unless <code>q</code> has an explicit <code>raises</code> list.</li> <li>Every call to a method <code>m</code> is assumed to raise <code>system.Exception</code> unless <code>m</code> has an explicit <code>raises</code> list.</li> <li>For every other call the analysis can determine an exact <code>raises</code> list.</li> <li>For determining a <code>raises</code> list, the <code>raise</code> and <code>try</code> statements of <code>p</code> are taken into consideration.</li> </ol> <p>Rules 1-2 ensure the following works:</p> <pre class="listing" data-language="nim">proc noRaise(x: proc()) {.raises: [].} =
  # unknown call that might raise anything, but valid:
  x()

proc doRaise() {.raises: [IOError].} =
  raise newException(IOError, "IO")

proc use() {.raises: [].} =
  # doesn't compile! Can raise IOError!
  noRaise(doRaise)</pre>
<p>So in many cases a callback does not cause the compiler to be overly conservative in its effect analysis.</p> <p>Exceptions inheriting from <code>system.Defect</code> are not tracked with the <code>.raises: []</code> exception tracking mechanism. This is more consistent with the built-in operations. The following code is valid::</p> <pre class="listing" data-language="nim">proc mydiv(a, b): int {.raises: [].} =
  a div b # can raise an DivByZeroDefect</pre>
<p>And so is::</p> <pre class="listing" data-language="nim">proc mydiv(a, b): int {.raises: [].} =
  if b == 0: raise newException(DivByZeroDefect, "division by zero")
  else: result = a div b</pre>
<p>The reason for this is that <code>DivByZeroDefect</code> inherits from <code>Defect</code> and with <code>--panics:on</code> Defects become unrecoverable errors. (Since version 1.4 of the language.)</p> <h3 id="effect-system-tag-tracking">Tag tracking</h3>
<p>The exception tracking is part of Nim's <span id="effect-system_1">effect system</span>. Raising an exception is an <em>effect</em>. Other effects can also be defined. A user defined effect is a means to <em>tag</em> a routine and to perform checks against this tag:</p> <pre class="listing" data-language="nim">type IO = object ## input/output effect
proc readLine(): string {.tags: [IO].} = discard

proc no_IO_please() {.tags: [].} =
  # the compiler prevents this:
  let x = readLine()</pre>
<p>A tag has to be a type name. A <code>tags</code> list - like a <code>raises</code> list - can also be attached to a proc type. This affects type compatibility.</p> <p>The inference for tag tracking is analogous to the inference for exception tracking.</p> <h3 id="effect-system-effects-pragma">Effects pragma</h3>
<p>The <code>effects</code> pragma has been designed to assist the programmer with the effects analysis. It is a statement that makes the compiler output all inferred effects up to the <code>effects</code>'s position:</p> <pre class="listing" data-language="nim">proc p(what: bool) =
  if what:
    raise newException(IOError, "IO")
    {.effects.}
  else:
    raise newException(OSError, "OS")</pre>
<p>The compiler produces a hint message that <code>IOError</code> can be raised. <code>OSError</code> is not listed as it cannot be raised in the branch the <code>effects</code> pragma appears in.</p> <h2 id="generics">Generics</h2>
<p>Generics are Nim's means to parametrize procs, iterators or types with <span id="type-parameters_1">type parameters</span>. Depending on the context, the brackets are used either to introduce type parameters or to instantiate a generic proc, iterator, or type.</p> <p>The following example shows a generic binary tree can be modeled:</p> <pre class="listing" data-language="nim">type
  BinaryTree*[T] = ref object # BinaryTree is a generic type with
                              # generic param ``T``
    le, ri: BinaryTree[T]     # left and right subtrees; may be nil
    data: T                   # the data stored in a node

proc newNode*[T](data: T): BinaryTree[T] =
  # constructor for a node
  result = BinaryTree[T](le: nil, ri: nil, data: data)

proc add*[T](root: var BinaryTree[T], n: BinaryTree[T]) =
  # insert a node into the tree
  if root == nil:
    root = n
  else:
    var it = root
    while it != nil:
      # compare the data items; uses the generic ``cmp`` proc
      # that works for any type that has a ``==`` and ``&lt;`` operator
      var c = cmp(it.data, n.data)
      if c &lt; 0:
        if it.le == nil:
          it.le = n
          return
        it = it.le
      else:
        if it.ri == nil:
          it.ri = n
          return
        it = it.ri

proc add*[T](root: var BinaryTree[T], data: T) =
  # convenience proc:
  add(root, newNode(data))

iterator preorder*[T](root: BinaryTree[T]): T =
  # Preorder traversal of a binary tree.
  # This uses an explicit stack (which is more efficient than
  # a recursive iterator factory).
  var stack: seq[BinaryTree[T]] = @[root]
  while stack.len &gt; 0:
    var n = stack.pop()
    while n != nil:
      yield n.data
      add(stack, n.ri)  # push right subtree onto the stack
      n = n.le          # and follow the left pointer

var
  root: BinaryTree[string] # instantiate a BinaryTree with ``string``
add(root, newNode("hello")) # instantiates ``newNode`` and ``add``
add(root, "world")          # instantiates the second ``add`` proc
for str in preorder(root):
  stdout.writeLine(str)</pre>
<p>The <code>T</code> is called a <span id="generic-type-parameter_1">generic type parameter</span> or a <span id="type-variable_1">type variable</span>.</p> <h3 id="generics-is-operator">Is operator</h3>
<p>The <code>is</code> operator is evaluated during semantic analysis to check for type equivalence. It is therefore very useful for type specialization within generic code:</p> <pre class="listing" data-language="nim">type
  Table[Key, Value] = object
    keys: seq[Key]
    values: seq[Value]
    when not (Key is string): # empty value for strings used for optimization
      deletedKeys: seq[bool]</pre> <h3 id="generics-type-classes">Type Classes</h3>
<p>A type class is a special pseudo-type that can be used to match against types in the context of overload resolution or the <code>is</code> operator. Nim supports the following built-in type classes:</p> <table>
<tr>
<th>type class</th>
<th>matches</th>
</tr> <tr>
<td><code>object</code></td>
<td>any object type</td>
</tr> <tr>
<td><code>tuple</code></td>
<td>any tuple type</td>
</tr> <tr>
<td></td>
<td></td>
</tr> <tr>
<td><code>enum</code></td>
<td>any enumeration</td>
</tr> <tr>
<td><code>proc</code></td>
<td>any proc type</td>
</tr> <tr>
<td><code>ref</code></td>
<td>any <code>ref</code> type</td>
</tr> <tr>
<td><code>ptr</code></td>
<td>any <code>ptr</code> type</td>
</tr> <tr>
<td><code>var</code></td>
<td>any <code>var</code> type</td>
</tr> <tr>
<td><code>distinct</code></td>
<td>any distinct type</td>
</tr> <tr>
<td><code>array</code></td>
<td>any array type</td>
</tr> <tr>
<td><code>set</code></td>
<td>any set type</td>
</tr> <tr>
<td><code>seq</code></td>
<td>any seq type</td>
</tr> <tr>
<td><code>auto</code></td>
<td>any type</td>
</tr> <tr>
<td><code>any</code></td>
<td>distinct auto (see below)</td>
</tr> </table>
<p>Furthermore, every generic type automatically creates a type class of the same name that will match any instantiation of the generic type.</p> <p>Type classes can be combined using the standard boolean operators to form more complex type classes:</p> <pre class="listing" data-language="nim"># create a type class that will match all tuple and object types
type RecordType = tuple or object

proc printFields[T: RecordType](rec: T) =
  for key, value in fieldPairs(rec):
    echo key, " = ", value</pre>
<p>Whilst the syntax of type classes appears to resemble that of ADTs/algebraic data types in ML-like languages, it should be understood that type classes are static constraints to be enforced at type instantiations. Type classes are not really types in themselves but are instead a system of providing generic "checks" that ultimately <em>resolve</em> to some singular type. Type classes do not allow for runtime type dynamism, unlike object variants or methods.</p> <p>As an example, the following would not compile:</p> <pre class="listing" data-language="nim">type TypeClass = int | string
var foo: TypeClass = 2 # foo's type is resolved to an int here
foo = "this will fail" # error here, because foo is an int</pre>
<p>Nim allows for type classes and regular types to be specified as <span id="type-constraints_1">type constraints</span> of the generic type parameter:</p> <pre class="listing" data-language="nim">proc onlyIntOrString[T: int|string](x, y: T) = discard

onlyIntOrString(450, 616) # valid
onlyIntOrString(5.0, 0.0) # type mismatch
onlyIntOrString("xy", 50) # invalid as 'T' cannot be both at the same time</pre> <h3 id="generics-implicit-generics">Implicit generics</h3>
<p>A type class can be used directly as the parameter's type.</p> <pre class="listing" data-language="nim"># create a type class that will match all tuple and object types
type RecordType = tuple or object

proc printFields(rec: RecordType) =
  for key, value in fieldPairs(rec):
    echo key, " = ", value</pre>
<p>Procedures utilizing type classes in such a manner are considered to be <span id="implicitly-generic_1">implicitly generic</span>. They will be instantiated once for each unique combination of param types used within the program.</p> <p>By default, during overload resolution, each named type class will bind to exactly one concrete type. We call such type classes <span id="bind-once_1">bind once</span> types. Here is an example taken directly from the system module to illustrate this:</p> <pre class="listing" data-language="nim">proc `==`*(x, y: tuple): bool =
  ## requires `x` and `y` to be of the same tuple type
  ## generic ``==`` operator for tuples that is lifted from the components
  ## of `x` and `y`.
  result = true
  for a, b in fields(x, y):
    if a != b: result = false</pre>
<p>Alternatively, the <code>distinct</code> type modifier can be applied to the type class to allow each param matching the type class to bind to a different type. Such type classes are called <span id="bind-many_1">bind many</span> types.</p> <p>Procs written with the implicitly generic style will often need to refer to the type parameters of the matched generic type. They can be easily accessed using the dot syntax:</p> <pre class="listing" data-language="nim">type Matrix[T, Rows, Columns] = object
  ...

proc `[]`(m: Matrix, row, col: int): Matrix.T =
  m.data[col * high(Matrix.Columns) + row]</pre>
<p>Here are more examples that illustrate implicit generics:</p> <pre class="listing" data-language="nim">proc p(t: Table; k: Table.Key): Table.Value

# is roughly the same as:

proc p[Key, Value](t: Table[Key, Value]; k: Key): Value</pre>
<pre class="listing" data-language="nim">proc p(a: Table, b: Table)

# is roughly the same as:

proc p[Key, Value](a, b: Table[Key, Value])</pre>
<pre class="listing" data-language="nim">proc p(a: Table, b: distinct Table)

# is roughly the same as:

proc p[Key, Value, KeyB, ValueB](a: Table[Key, Value], b: Table[KeyB, ValueB])</pre>
<p><code>typedesc</code> used as a parameter type also introduces an implicit generic. <code>typedesc</code> has its own set of rules:</p> <pre class="listing" data-language="nim">proc p(a: typedesc)

# is roughly the same as:

proc p[T](a: typedesc[T])</pre>
<p><code>typedesc</code> is a "bind many" type class:</p> <pre class="listing" data-language="nim">proc p(a, b: typedesc)

# is roughly the same as:

proc p[T, T2](a: typedesc[T], b: typedesc[T2])</pre>
<p>A parameter of type <code>typedesc</code> is itself usable as a type. If it is used as a type, it's the underlying type. (In other words, one level of "typedesc"-ness is stripped off:</p> <pre class="listing" data-language="nim">proc p(a: typedesc; b: a) = discard

# is roughly the same as:
proc p[T](a: typedesc[T]; b: T) = discard

# hence this is a valid call:
p(int, 4)
# as parameter 'a' requires a type, but 'b' requires a value.</pre> <h3 id="generics-generic-inference-restrictions">Generic inference restrictions</h3>
<p>The types <code>var T</code> and <code>typedesc[T]</code> cannot be inferred in a generic instantiation. The following is not allowed:</p> <pre class="listing" data-language="nim">proc g[T](f: proc(x: T); x: T) =
  f(x)

proc c(y: int) = echo y
proc v(y: var int) =
  y += 100
var i: int

# allowed: infers 'T' to be of type 'int'
g(c, 42)

# not valid: 'T' is not inferred to be of type 'var int'
g(v, i)

# also not allowed: explicit instantiation via 'var int'
g[var int](v, i)</pre> <h3 id="generics-symbol-lookup-in-generics">Symbol lookup in generics</h3> <h4 id="symbol-lookup-in-generics-open-and-closed-symbols">Open and Closed symbols</h4>
<p>The symbol binding rules in generics are slightly subtle: There are "open" and "closed" symbols. A "closed" symbol cannot be re-bound in the instantiation context, an "open" symbol can. Per default overloaded symbols are open and every other symbol is closed.</p> <p>Open symbols are looked up in two different contexts: Both the context at definition and the context at instantiation are considered:</p> <pre class="listing" data-language="nim">type
  Index = distinct int

proc `==` (a, b: Index): bool {.borrow.}

var a = (0, 0.Index)
var b = (0, 0.Index)

echo a == b # works!</pre>
<p>In the example, the generic <code>==</code> for tuples (as defined in the system module) uses the <code>==</code> operators of the tuple's components. However, the <code>==</code> for the <code>Index</code> type is defined <em>after</em> the <code>==</code> for tuples; yet the example compiles as the instantiation takes the currently defined symbols into account too.</p> <h3 id="generics-mixin-statement">Mixin statement</h3>
<p>A symbol can be forced to be open by a <span id="mixin_1">mixin</span> declaration:</p> <pre class="listing" data-language="nim">proc create*[T](): ref T =
  # there is no overloaded 'init' here, so we need to state that it's an
  # open symbol explicitly:
  mixin init
  new result
  init result</pre>
<p><code>mixin</code> statements only make sense in templates and generics.</p> <h3 id="generics-bind-statement">Bind statement</h3>
<p>The <code>bind</code> statement is the counterpart to the <code>mixin</code> statement. It can be used to explicitly declare identifiers that should be bound early (i.e. the identifiers should be looked up in the scope of the template/generic definition):</p> <pre class="listing" data-language="nim"># Module A
var
  lastId = 0

template genId*: untyped =
  bind lastId
  inc(lastId)
  lastId</pre>
<pre class="listing" data-language="nim"># Module B
import A

echo genId()</pre>
<p>But a <code>bind</code> is rarely useful because symbol binding from the definition scope is the default.</p> <p><code>bind</code> statements only make sense in templates and generics.</p> <h3 id="generics-delegating-bind-statements">Delegating bind statements</h3>
<p>The following example outlines a problem that can arise when generic instantiations cross multiple different modules:</p> <pre class="listing" data-language="nim"># module A
proc genericA*[T](x: T) =
  mixin init
  init(x)</pre>
<pre class="listing" data-language="nim">import C

# module B
proc genericB*[T](x: T) =
  # Without the `bind init` statement C's init proc is
  # not available when `genericB` is instantiated:
  bind init
  genericA(x)</pre>
<pre class="listing" data-language="nim"># module C
type O = object
proc init*(x: var O) = discard</pre>
<pre class="listing" data-language="nim"># module main
import B, C

genericB O()</pre>
<p>In module B has an <code>init</code> proc from module C in its scope that is not taken into account when <code>genericB</code> is instantiated which leads to the instantiation of <code>genericA</code>. The solution is to <code>forward</code>:idx these symbols by a <code>bind</code> statement inside <code>genericB</code>.</p> <h2 id="templates">Templates</h2>
<p>A template is a simple form of a macro: It is a simple substitution mechanism that operates on Nim's abstract syntax trees. It is processed in the semantic pass of the compiler.</p> <p>The syntax to <em>invoke</em> a template is the same as calling a procedure.</p> <p>Example:</p> <pre class="listing" data-language="nim">template `!=` (a, b: untyped): untyped =
  # this definition exists in the System module
  not (a == b)

assert(5 != 6) # the compiler rewrites that to: assert(not (5 == 6))</pre>
<p>The <code>!=</code>, <code>&gt;</code>, <code>&gt;=</code>, <code>in</code>, <code>notin</code>, <code>isnot</code> operators are in fact templates:</p> <p><code>a &gt; b</code> is transformed into <code>b &lt; a</code>.<br><code>a in b</code> is transformed into <code>contains(b, a)</code>.<br><code>notin</code> and <code>isnot</code> have the obvious meanings.<br></p>
<p>The "types" of templates can be the symbols <code>untyped</code>, <code>typed</code> or <code>typedesc</code>. These are "meta types", they can only be used in certain contexts. Regular types can be used too; this implies that <code>typed</code> expressions are expected.</p> <h3 id="templates-typed-vs-untyped-parameters">Typed vs untyped parameters</h3>
<p>An <code>untyped</code> parameter means that symbol lookups and type resolution is not performed before the expression is passed to the template. This means that for example <em>undeclared</em> identifiers can be passed to the template:</p> <pre class="listing" data-language="nim">template declareInt(x: untyped) =
  var x: int

declareInt(x) # valid
x = 3</pre>
<pre class="listing" data-language="nim">template declareInt(x: typed) =
  var x: int

declareInt(x) # invalid, because x has not been declared and so it has no type</pre>
<p>A template where every parameter is <code>untyped</code> is called an <span id="immediate_1">immediate</span> template. For historical reasons templates can be explicitly annotated with an <code>immediate</code> pragma and then these templates do not take part in overloading resolution and the parameters' types are <em>ignored</em> by the compiler. Explicit immediate templates are now deprecated.</p> <p><strong>Note</strong>: For historical reasons <code>stmt</code> was an alias for <code>typed</code> and <code>expr</code> was an alias for <code>untyped</code>, but they are removed.</p> <h3 id="templates-passing-a-code-block-to-a-template">Passing a code block to a template</h3>
<p>One can pass a block of statements as the last argument to a template following the special <code>:</code> syntax:</p> <pre class="listing" data-language="nim">template withFile(f, fn, mode, actions: untyped): untyped =
  var f: File
  if open(f, fn, mode):
    try:
      actions
    finally:
      close(f)
  else:
    quit("cannot open: " &amp; fn)

withFile(txt, "ttempl3.txt", fmWrite):  # special colon
  txt.writeLine("line 1")
  txt.writeLine("line 2")</pre>
<p>In the example, the two <code>writeLine</code> statements are bound to the <code>actions</code> parameter.</p> <p>Usually to pass a block of code to a template the parameter that accepts the block needs to be of type <code>untyped</code>. Because symbol lookups are then delayed until template instantiation time:</p> <pre class="listing" data-language="nim">template t(body: typed) =
  proc p = echo "hey"
  block:
    body

t:
  p()  # fails with 'undeclared identifier: p'</pre>
<p>The above code fails with the error message that <code>p</code> is not declared. The reason for this is that the <code>p()</code> body is type-checked before getting passed to the <code>body</code> parameter and type checking in Nim implies symbol lookups. The same code works with <code>untyped</code> as the passed body is not required to be type-checked:</p> <pre class="listing" data-language="nim">template t(body: untyped) =
  proc p = echo "hey"
  block:
    body

t:
  p()  # compiles</pre> <h3 id="templates-varargs-of-untyped">Varargs of untyped</h3>
<p>In addition to the <code>untyped</code> meta-type that prevents type checking there is also <code>varargs[untyped]</code> so that not even the number of parameters is fixed:</p> <pre class="listing" data-language="nim">template hideIdentifiers(x: varargs[untyped]) = discard

hideIdentifiers(undeclared1, undeclared2)</pre>
<p>However, since a template cannot iterate over varargs, this feature is generally much more useful for macros.</p> <h3 id="templates-symbol-binding-in-templates">Symbol binding in templates</h3>
<p>A template is a <span id="hygienic_1">hygienic</span> macro and so opens a new scope. Most symbols are bound from the definition scope of the template:</p> <pre class="listing" data-language="nim"># Module A
var
  lastId = 0

template genId*: untyped =
  inc(lastId)
  lastId</pre>
<pre class="listing" data-language="nim"># Module B
import A

echo genId() # Works as 'lastId' has been bound in 'genId's defining scope</pre>
<p>As in generics symbol binding can be influenced via <code>mixin</code> or <code>bind</code> statements.</p> <h3 id="templates-identifier-construction">Identifier construction</h3>
<p>In templates identifiers can be constructed with the backticks notation:</p> <pre class="listing" data-language="nim">template typedef(name: untyped, typ: typedesc) =
  type
    `T name`* {.inject.} = typ
    `P name`* {.inject.} = ref `T name`

typedef(myint, int)
var x: PMyInt</pre>
<p>In the example <code>name</code> is instantiated with <code>myint</code>, so `T name` becomes <code>Tmyint</code>.</p> <h3 id="templates-lookup-rules-for-template-parameters">Lookup rules for template parameters</h3>
<p>A parameter <code>p</code> in a template is even substituted in the expression <code>x.p</code>. Thus template arguments can be used as field names and a global symbol can be shadowed by the same argument name even when fully qualified:</p> <pre class="listing" data-language="nim"># module 'm'

type
  Lev = enum
    levA, levB

var abclev = levB

template tstLev(abclev: Lev) =
  echo abclev, " ", m.abclev

tstLev(levA)
# produces: 'levA levA'</pre>
<p>But the global symbol can properly be captured by a <code>bind</code> statement:</p> <pre class="listing" data-language="nim"># module 'm'

type
  Lev = enum
    levA, levB

var abclev = levB

template tstLev(abclev: Lev) =
  bind m.abclev
  echo abclev, " ", m.abclev

tstLev(levA)
# produces: 'levA levB'</pre> <h3 id="templates-hygiene-in-templates">Hygiene in templates</h3>
<p>Per default templates are <span id="hygienic_2">hygienic</span>: Local identifiers declared in a template cannot be accessed in the instantiation context:</p> <pre class="listing" data-language="nim">template newException*(exceptn: typedesc, message: string): untyped =
  var
    e: ref exceptn  # e is implicitly gensym'ed here
  new(e)
  e.msg = message
  e

# so this works:
let e = "message"
raise newException(IoError, e)</pre>
<p>Whether a symbol that is declared in a template is exposed to the instantiation scope is controlled by the <span id="inject_1">inject</span> and <span id="gensym_1">gensym</span> pragmas: gensym'ed symbols are not exposed but inject'ed are.</p> <p>The default for symbols of entity <code>type</code>, <code>var</code>, <code>let</code> and <code>const</code> is <code>gensym</code> and for <code>proc</code>, <code>iterator</code>, <code>converter</code>, <code>template</code>, <code>macro</code> is <code>inject</code>. However, if the name of the entity is passed as a template parameter, it is an inject'ed symbol:</p> <pre class="listing" data-language="nim">template withFile(f, fn, mode: untyped, actions: untyped): untyped =
  block:
    var f: File  # since 'f' is a template param, it's injected implicitly
    ...

withFile(txt, "ttempl3.txt", fmWrite):
  txt.writeLine("line 1")
  txt.writeLine("line 2")</pre>
<p>The <code>inject</code> and <code>gensym</code> pragmas are second class annotations; they have no semantics outside of a template definition and cannot be abstracted over:</p> <pre class="listing" data-language="nim">{.pragma myInject: inject.}

template t() =
  var x {.myInject.}: int # does NOT work</pre>
<p>To get rid of hygiene in templates, one can use the <span id="dirty_1">dirty</span> pragma for a template. <code>inject</code> and <code>gensym</code> have no effect in <code>dirty</code> templates.</p> <p><code>gensym</code>'ed symbols cannot be used as <code>field</code> in the <code>x.field</code> syntax. Nor can they be used in the <code>ObjectConstruction(field: value)</code> and <code>namedParameterCall(field = value)</code> syntactic constructs.</p> <p>The reason for this is that code like</p> <pre class="listing" data-language="nim">type
  T = object
    f: int

template tmp(x: T) =
  let f = 34
  echo x.f, T(f: 4)</pre>
<p>should work as expected.</p> <p>However, this means that the method call syntax is not available for <code>gensym</code>'ed symbols:</p> <pre class="listing" data-language="nim">template tmp(x) =
  type
    T {.gensym.} = int
  
  echo x.T # invalid: instead use:  'echo T(x)'.

tmp(12)</pre>
<p><strong>Note</strong>: The Nim compiler prior to version 1 was more lenient about this requirement. Use the <code>--useVersion:0.19</code> switch for a transition period.</p> <h3 id="templates-limitations-of-the-method-call-syntax">Limitations of the method call syntax</h3>
<p>The expression <code>x</code> in <code>x.f</code> needs to be semantically checked (that means symbol lookup and type checking) before it can be decided that it needs to be rewritten to <code>f(x)</code>. Therefore the dot syntax has some limitations when it is used to invoke templates/macros:</p> <pre class="listing" data-language="nim">template declareVar(name: untyped) =
  const name {.inject.} = 45

# Doesn't compile:
unknownIdentifier.declareVar</pre>
<p>Another common example is this:</p> <pre class="listing" data-language="nim">from sequtils import toSeq

iterator something: string =
  yield "Hello"
  yield "World"

var info = something().toSeq</pre>
<p>The problem here is that the compiler already decided that <code>something()</code> as an iterator is not callable in this context before <code>toSeq</code> gets its chance to convert it into a sequence.</p> <p>It is also not possible to use fully qualified identifiers with module symbol in method call syntax. The order in which the dot operator binds to symbols prohibits this.</p> <pre class="listing" data-language="nim">import sequtils

var myItems = @[1,3,3,7]
let N1 = count(myItems, 3) # OK
let N2 = sequtils.count(myItems, 3) # fully qualified, OK
let N3 = myItems.count(3) # OK
let N4 = myItems.sequtils.count(3) # illegal, `myItems.sequtils` can't be resolved</pre>
<p>This means that when for some reason a procedure needs a disambiguation through the module name, the call needs to be written in function call syntax.</p> <h2 id="macros">Macros</h2>
<p>A macro is a special function that is executed at compile time. Normally the input for a macro is an abstract syntax tree (AST) of the code that is passed to it. The macro can then do transformations on it and return the transformed AST. This can be used to add custom language features and implement <span id="domainminusspecific-languages_1">domain-specific languages</span>.</p> <p>Macro invocation is a case where semantic analysis does <strong>not</strong> entirely proceed top to bottom and left to right. Instead, semantic analysis happens at least twice:</p> <ul class="simple">
<li>Semantic analysis recognizes and resolves the macro invocation.</li> <li>The compiler executes the macro body (which may invoke other procs).</li> <li>It replaces the AST of the macro invocation with the AST returned by the macro.</li> <li>It repeats semantic analysis of that region of the code.</li> <li>If the AST returned by the macro contains other macro invocations, this process iterates.</li> </ul> <p>While macros enable advanced compile-time code transformations, they cannot change Nim's syntax.</p> <h3 id="macros-debug-example">Debug Example</h3>
<p>The following example implements a powerful <code>debug</code> command that accepts a variable number of arguments:</p> <pre class="listing" data-language="nim"># to work with Nim syntax trees, we need an API that is defined in the
# ``macros`` module:
import macros

macro debug(args: varargs[untyped]): untyped =
  # `args` is a collection of `NimNode` values that each contain the
  # AST for an argument of the macro. A macro always has to
  # return a `NimNode`. A node of kind `nnkStmtList` is suitable for
  # this use case.
  result = nnkStmtList.newTree()
  # iterate over any argument that is passed to this macro:
  for n in args:
    # add a call to the statement list that writes the expression;
    # `toStrLit` converts an AST to its string representation:
    result.add newCall("write", newIdentNode("stdout"), newLit(n.repr))
    # add a call to the statement list that writes ": "
    result.add newCall("write", newIdentNode("stdout"), newLit(": "))
    # add a call to the statement list that writes the expressions value:
    result.add newCall("writeLine", newIdentNode("stdout"), n)

var
  a: array[0..10, int]
  x = "some string"
a[0] = 42
a[1] = 45

debug(a[0], a[1], x)</pre>
<p>The macro call expands to:</p> <pre class="listing" data-language="nim">write(stdout, "a[0]")
write(stdout, ": ")
writeLine(stdout, a[0])

write(stdout, "a[1]")
write(stdout, ": ")
writeLine(stdout, a[1])

write(stdout, "x")
write(stdout, ": ")
writeLine(stdout, x)</pre>
<p>Arguments that are passed to a <code>varargs</code> parameter are wrapped in an array constructor expression. This is why <code>debug</code> iterates over all of <code>n</code>'s children.</p> <h3 id="macros-bindsym">BindSym</h3>
<p>The above <code>debug</code> macro relies on the fact that <code>write</code>, <code>writeLine</code> and <code>stdout</code> are declared in the system module and thus visible in the instantiating context. There is a way to use bound identifiers (aka <span id="symbols_1">symbols</span>) instead of using unbound identifiers. The <code>bindSym</code> builtin can be used for that:</p> <pre class="listing" data-language="nim">import macros

macro debug(n: varargs[typed]): untyped =
  result = newNimNode(nnkStmtList, n)
  for x in n:
    # we can bind symbols in scope via 'bindSym':
    add(result, newCall(bindSym"write", bindSym"stdout", toStrLit(x)))
    add(result, newCall(bindSym"write", bindSym"stdout", newStrLitNode(": ")))
    add(result, newCall(bindSym"writeLine", bindSym"stdout", x))

var
  a: array[0..10, int]
  x = "some string"
a[0] = 42
a[1] = 45

debug(a[0], a[1], x)</pre>
<p>The macro call expands to:</p> <pre class="listing" data-language="nim">write(stdout, "a[0]")
write(stdout, ": ")
writeLine(stdout, a[0])

write(stdout, "a[1]")
write(stdout, ": ")
writeLine(stdout, a[1])

write(stdout, "x")
write(stdout, ": ")
writeLine(stdout, x)</pre>
<p>However, the symbols <code>write</code>, <code>writeLine</code> and <code>stdout</code> are already bound and are not looked up again. As the example shows, <code>bindSym</code> does work with overloaded symbols implicitly.</p> <h3 id="macros-caseminusof-macro">Case-Of Macro</h3>
<p>In Nim it is possible to have a macro with the syntax of a <em>case-of</em> expression just with the difference that all of branches are passed to and processed by the macro implementation. It is then up the macro implementation to transform the <em>of-branches</em> into a valid Nim statement. The following example should show how this feature could be used for a lexical analyzer.</p> <pre class="listing" data-language="nim">import macros

macro case_token(args: varargs[untyped]): untyped =
  echo args.treeRepr
  # creates a lexical analyzer from regular expressions
  # ... (implementation is an exercise for the reader ;-)
  discard

case_token: # this colon tells the parser it is a macro statement
of r"[A-Za-z_]+[A-Za-z_0-9]*":
  return tkIdentifier
of r"0-9+":
  return tkInteger
of r"[\+\-\*\?]+":
  return tkOperator
else:
  return tkUnknown</pre>
<p><strong>Style note</strong>: For code readability, it is the best idea to use the least powerful programming construct that still suffices. So the "check list" is:</p> <ol class="simple">
<li>Use an ordinary proc/iterator, if possible.</li> <li>Else: Use a generic proc/iterator, if possible.</li> <li>Else: Use a template, if possible.</li> <li>Else: Use a macro.</li> </ol> <h3 id="macros-for-loop-macro">For loop macro</h3>
<p>A macro that takes as its only input parameter an expression of the special type <code>system.ForLoopStmt</code> can rewrite the entirety of a <code>for</code> loop:</p> <pre class="listing" data-language="nim">import macros

macro enumerate(x: ForLoopStmt): untyped =
  expectKind x, nnkForStmt
  # check if the starting count is specified:
  var countStart = if x[^2].len == 2: newLit(0) else: x[^2][1]
  result = newStmtList()
  # we strip off the first for loop variable and use it as an integer counter:
  result.add newVarStmt(x[0], countStart)
  var body = x[^1]
  if body.kind != nnkStmtList:
    body = newTree(nnkStmtList, body)
  body.add newCall(bindSym"inc", x[0])
  var newFor = newTree(nnkForStmt)
  for i in 1..x.len-3:
    newFor.add x[i]
  # transform enumerate(X) to 'X'
  newFor.add x[^2][^1]
  newFor.add body
  result.add newFor
  # now wrap the whole macro in a block to create a new scope
  result = quote do:
    block: `result`

for a, b in enumerate(items([1, 2, 3])):
  echo a, " ", b

# without wrapping the macro in a block, we'd need to choose different
# names for `a` and `b` here to avoid redefinition errors
for a, b in enumerate(10, [1, 2, 3, 5]):
  echo a, " ", b</pre> <h2 id="special-types">Special Types</h2> <h3 id="special-types-static-t">static[T]</h3>
<p>As their name suggests, static parameters must be constant expressions:</p> <pre class="listing" data-language="nim">proc precompiledRegex(pattern: static string): RegEx =
  var res {.global.} = re(pattern)
  return res

precompiledRegex("/d+") # Replaces the call with a precompiled
                        # regex, stored in a global variable

precompiledRegex(paramStr(1)) # Error, command-line options
                              # are not constant expressions</pre>
<p>For the purposes of code generation, all static params are treated as generic params - the proc will be compiled separately for each unique supplied value (or combination of values).</p> <p>Static params can also appear in the signatures of generic types:</p> <pre class="listing" data-language="nim">type
  Matrix[M,N: static int; T: Number] = array[0..(M*N - 1), T]
    # Note how `Number` is just a type constraint here, while
    # `static int` requires us to supply an int value
  
  AffineTransform2D[T] = Matrix[3, 3, T]
  AffineTransform3D[T] = Matrix[4, 4, T]

var m1: AffineTransform3D[float]  # OK
var m2: AffineTransform2D[string] # Error, `string` is not a `Number`</pre>
<p>Please note that <code>static T</code> is just a syntactic convenience for the underlying generic type <code>static[T]</code>. The type param can be omitted to obtain the type class of all constant expressions. A more specific type class can be created by instantiating <code>static</code> with another type class.</p> <p>One can force an expression to be evaluated at compile time as a constant expression by coercing it to a corresponding <code>static</code> type:</p> <pre class="listing" data-language="nim">import math

echo static(fac(5)), " ", static[bool](16.isPowerOfTwo)</pre>
<p>The compiler will report any failure to evaluate the expression or a possible type mismatch error.</p> <h3 id="special-types-typedesc-t">typedesc[T]</h3>
<p>In many contexts, Nim treats the names of types as regular values. These values exist only during the compilation phase, but since all values must have a type, <code>typedesc</code> is considered their special type.</p> <p><code>typedesc</code> acts as a generic type. For instance, the type of the symbol <code>int</code> is <code>typedesc[int]</code>. Just like with regular generic types, when the generic param is omitted, <code>typedesc</code> denotes the type class of all types. As a syntactic convenience, one can also use <code>typedesc</code> as a modifier.</p> <p>Procs featuring <code>typedesc</code> params are considered implicitly generic. They will be instantiated for each unique combination of supplied types and within the body of the proc, the name of each param will refer to the bound concrete type:</p> <pre class="listing" data-language="nim">proc new(T: typedesc): ref T =
  echo "allocating ", T.name
  new(result)

var n = Node.new
var tree = new(BinaryTree[int])</pre>
<p>When multiple type params are present, they will bind freely to different types. To force a bind-once behavior one can use an explicit generic param:</p> <pre class="listing" data-language="nim">proc acceptOnlyTypePairs[T, U](A, B: typedesc[T]; C, D: typedesc[U])</pre>
<p>Once bound, type params can appear in the rest of the proc signature:</p> <pre class="listing" data-language="nim">template declareVariableWithType(T: typedesc, value: T) =
  var x: T = value

declareVariableWithType int, 42</pre>
<p>Overload resolution can be further influenced by constraining the set of types that will match the type param. This works in practice by attaching attributes to types via templates. The constraint can be a concrete type or a type class.</p> <pre class="listing" data-language="nim">template maxval(T: typedesc[int]): int = high(int)
template maxval(T: typedesc[float]): float = Inf

var i = int.maxval
var f = float.maxval
when false:
  var s = string.maxval # error, maxval is not implemented for string

template isNumber(t: typedesc[object]): string = "Don't think so."
template isNumber(t: typedesc[SomeInteger]): string = "Yes!"
template isNumber(t: typedesc[SomeFloat]): string = "Maybe, could be NaN."

echo "is int a number? ", isNumber(int)
echo "is float a number? ", isNumber(float)
echo "is RootObj a number? ", isNumber(RootObj)</pre>
<p>Passing <code>typedesc</code> almost identical, just with the differences that the macro is not instantiated generically. The type expression is simply passed as a <code>NimNode</code> to the macro, like everything else.</p> <pre class="listing" data-language="nim">import macros

macro forwardType(arg: typedesc): typedesc =
  # ``arg`` is of type ``NimNode``
  let tmp: NimNode = arg
  result = tmp

var tmp: forwardType(int)</pre> <h3 id="special-types-typeof-operator">typeof operator</h3>
<p><strong>Note</strong>: <code>typeof(x)</code> can for historical reasons also be written as <code>type(x)</code> but <code>type(x)</code> is discouraged.</p> <p>One can obtain the type of a given expression by constructing a <code>typeof</code> value from it (in many other languages this is known as the <span id="typeof_1">typeof</span> operator):</p> <pre class="listing" data-language="nim">var x = 0
var y: typeof(x) # y has type int</pre>
<p>If <code>typeof</code> is used to determine the result type of a proc/iterator/converter call <code>c(X)</code> (where <code>X</code> stands for a possibly empty list of arguments), the interpretation, where <code>c</code> is an iterator, is preferred over the other interpretations, but this behavior can be changed by passing <code>typeOfProc</code> as the second argument to <code>typeof</code>:</p> <pre class="listing" data-language="nim">iterator split(s: string): string = discard
proc split(s: string): seq[string] = discard

# since an iterator is the preferred interpretation, `y` has the type ``string``:
assert typeof("a b c".split) is string

assert typeof("a b c".split, typeOfProc) is seq[string]</pre> <h2 id="modules">Modules</h2>
<p>Nim supports splitting a program into pieces by a module concept. Each module needs to be in its own file and has its own <span id="namespace_1">namespace</span>. Modules enable <span id="information-hiding_1">information hiding</span> and <span id="separate-compilation_1">separate compilation</span>. A module may gain access to symbols of another module by the <span id="import_1">import</span> statement. <span id="recursive-module-dependencies_1">Recursive module dependencies</span> are allowed, but slightly subtle. Only top-level symbols that are marked with an asterisk (<code>*</code>) are exported. A valid module name can only be a valid Nim identifier (and thus its filename is <code>identifier.nim</code>).</p> <p>The algorithm for compiling modules is:</p> <ul class="simple">
<li>compile the whole module as usual, following import statements recursively</li> <li>if there is a cycle only import the already parsed symbols (that are exported); if an unknown identifier occurs then abort</li> </ul> <p>This is best illustrated by an example:</p> <pre class="listing" data-language="nim"># Module A
type
  T1* = int  # Module A exports the type ``T1``
import B     # the compiler starts parsing B

proc main() =
  var i = p(3) # works because B has been parsed completely here

main()</pre>
<pre class="listing" data-language="nim"># Module B
import A  # A is not parsed here! Only the already known symbols
          # of A are imported.

proc p*(x: A.T1): A.T1 =
  # this works because the compiler has already
  # added T1 to A's interface symbol table
  result = x + 1</pre> <h4 id="modules-import-statement">Import statement</h4>
<p>After the <code>import</code> statement a list of module names can follow or a single module name followed by an <code>except</code> list to prevent some symbols to be imported:</p> <pre class="listing" data-language="nim">import strutils except `%`, toUpperAscii

# doesn't work then:
echo "$1" % "abc".toUpperAscii</pre>
<p>It is not checked that the <code>except</code> list is really exported from the module. This feature allows us to compile against an older version of the module that does not export these identifiers.</p> <p>The <code>import</code> statement is only allowed at the top level.</p> <h4 id="modules-include-statement">Include statement</h4>
<p>The <code>include</code> statement does something fundamentally different than importing a module: it merely includes the contents of a file. The <code>include</code> statement is useful to split up a large module into several files:</p> <pre class="listing" data-language="nim">include fileA, fileB, fileC</pre>
<p>The <code>include</code> statement can be used outside of the top level, as such:</p> <pre class="listing" data-language="nim"># Module A
echo "Hello World!"</pre>
<pre class="listing" data-language="nim"># Module B
proc main() =
  include A

main() # =&gt; Hello World!</pre> <h4 id="modules-module-names-in-imports">Module names in imports</h4>
<p>A module alias can be introduced via the <code>as</code> keyword:</p> <pre class="listing" data-language="nim">import strutils as su, sequtils as qu

echo su.format("$1", "lalelu")</pre>
<p>The original module name is then not accessible. The notations <code>path/to/module</code> or <code>"path/to/module"</code> can be used to refer to a module in subdirectories:</p> <pre class="listing" data-language="nim">import lib/pure/os, "lib/pure/times"</pre>
<p>Note that the module name is still <code>strutils</code> and not <code>lib/pure/strutils</code> and so one <strong>cannot</strong> do:</p> <pre class="listing" data-language="nim">import lib/pure/strutils
echo lib/pure/strutils.toUpperAscii("abc")</pre>
<p>Likewise, the following does not make sense as the name is <code>strutils</code> already:</p> <pre class="listing" data-language="nim">import lib/pure/strutils as strutils</pre> <h4 id="modules-collective-imports-from-a-directory">Collective imports from a directory</h4>
<p>The syntax <code>import dir / [moduleA, moduleB]</code> can be used to import multiple modules from the same directory.</p> <p>Path names are syntactically either Nim identifiers or string literals. If the path name is not a valid Nim identifier it needs to be a string literal:</p> <pre class="listing" data-language="nim">import "gfx/3d/somemodule" # in quotes because '3d' is not a valid Nim identifier</pre> <h4 id="modules-pseudo-importslashinclude-paths">Pseudo import/include paths</h4>
<p>A directory can also be a so-called "pseudo directory". They can be used to avoid ambiguity when there are multiple modules with the same path.</p> <p>There are two pseudo directories:</p> <ol class="simple">
<li>
<code>std</code>: The <code>std</code> pseudo directory is the abstract location of Nim's standard library. For example, the syntax <code>import std / strutils</code> is used to unambiguously refer to the standard library's <code>strutils</code> module.</li> <li>
<code>pkg</code>: The <code>pkg</code> pseudo directory is used to unambiguously refer to a Nimble package. However, for technical details that lie outside of the scope of this document its semantics are: <em>Use the search path to look for module name but ignore the standard library locations</em>. In other words, it is the opposite of <code>std</code>.</li> </ol> <h4 id="modules-from-import-statement">From import statement</h4>
<p>After the <code>from</code> statement, a module name follows followed by an <code>import</code> to list the symbols one likes to use without explicit full qualification:</p> <pre class="listing" data-language="nim">from strutils import `%`

echo "$1" % "abc"
# always possible: full qualification:
echo strutils.replace("abc", "a", "z")</pre>
<p>It's also possible to use <code>from module import nil</code> if one wants to import the module but wants to enforce fully qualified access to every symbol in <code>module</code>.</p> <h4 id="modules-export-statement">Export statement</h4>
<p>An <code>export</code> statement can be used for symbol forwarding so that client modules don't need to import a module's dependencies:</p> <pre class="listing" data-language="nim"># module B
type MyObject* = object</pre>
<pre class="listing" data-language="nim"># module A
import B
export B.MyObject

proc `$`*(x: MyObject): string = "my object"</pre>
<pre class="listing" data-language="nim"># module C
import A

# B.MyObject has been imported implicitly here:
var x: MyObject
echo $x</pre>
<p>When the exported symbol is another module, all of its definitions will be forwarded. One can use an <code>except</code> list to exclude some of the symbols.</p> <p>Notice that when exporting, one needs to specify only the module name:</p> <pre class="listing" data-language="nim">import foo/bar/baz
export baz</pre> <h3 id="modules-scope-rules">Scope rules</h3>
<p>Identifiers are valid from the point of their declaration until the end of the block in which the declaration occurred. The range where the identifier is known is the scope of the identifier. The exact scope of an identifier depends on the way it was declared.</p> <h4 id="scope-rules-block-scope">Block scope</h4>
<p>The <em>scope</em> of a variable declared in the declaration part of a block is valid from the point of declaration until the end of the block. If a block contains a second block, in which the identifier is redeclared, then inside this block, the second declaration will be valid. Upon leaving the inner block, the first declaration is valid again. An identifier cannot be redefined in the same block, except if valid for procedure or iterator overloading purposes.</p> <h4 id="scope-rules-tuple-or-object-scope">Tuple or object scope</h4>
<p>The field identifiers inside a tuple or object definition are valid in the following places:</p> <ul class="simple">
<li>To the end of the tuple/object definition.</li> <li>Field designators of a variable of the given tuple/object type.</li> <li>In all descendant types of the object type.</li> </ul> <h4 id="scope-rules-module-scope">Module scope</h4>
<p>All identifiers of a module are valid from the point of declaration until the end of the module. Identifiers from indirectly dependent modules are <em>not</em> available. The <span id="system_1">system</span> module is automatically imported in every module.</p> <p>If a module imports an identifier by two different modules, each occurrence of the identifier has to be qualified unless it is an overloaded procedure or iterator in which case the overloading resolution takes place:</p> <pre class="listing" data-language="nim"># Module A
var x*: string</pre>
<pre class="listing" data-language="nim"># Module B
var x*: int</pre>
<pre class="listing" data-language="nim"># Module C
import A, B
write(stdout, x) # error: x is ambiguous
write(stdout, A.x) # no error: qualifier used

var x = 4
write(stdout, x) # not ambiguous: uses the module C's x</pre> <h2 id="compiler-messages">Compiler Messages</h2>
<p>The Nim compiler emits different kinds of messages: <span id="hint_1">hint</span>, <span id="warning_1">warning</span>, and <span id="error_1">error</span> messages. An <em>error</em> message is emitted if the compiler encounters any static error.</p> <h2 id="pragmas">Pragmas</h2>
<p>Pragmas are Nim's method to give the compiler additional information / commands without introducing a massive number of new keywords. Pragmas are processed on the fly during semantic checking. Pragmas are enclosed in the special <code>{.</code> and <code>.}</code> curly brackets. Pragmas are also often used as a first implementation to play with a language feature before a nicer syntax to access the feature becomes available.</p> <h3 id="pragmas-deprecated-pragma">deprecated pragma</h3>
<p>The deprecated pragma is used to mark a symbol as deprecated:</p> <pre class="listing" data-language="nim">proc p() {.deprecated.}
var x {.deprecated.}: char</pre>
<p>This pragma can also take in an optional warning string to relay to developers.</p> <pre class="listing" data-language="nim">proc thing(x: bool) {.deprecated: "use thong instead".}</pre> <h3 id="pragmas-nosideeffect-pragma">noSideEffect pragma</h3>
<p>The <code>noSideEffect</code> pragma is used to mark a proc/iterator to have no side effects. This means that the proc/iterator only changes locations that are reachable from its parameters and the return value only depends on the arguments. If none of its parameters have the type <code>var T</code> or <code>ref T</code> or <code>ptr T</code> this means no locations are modified. It is a static error to mark a proc/iterator to have no side effect if the compiler cannot verify this.</p> <p>As a special semantic rule, the built-in <a href="system.html#debugEcho,varargs%5Btyped,%5D">debugEcho</a> pretends to be free of side effects, so that it can be used for debugging routines marked as <code>noSideEffect</code>.</p> <p><code>func</code> is syntactic sugar for a proc with no side effects:</p> <pre class="listing" data-language="nim">func `+` (x, y: int): int</pre>
<p>To override the compiler's side effect analysis a <code>{.noSideEffect.}</code> <code>cast</code> pragma block can be used:</p> <pre class="listing" data-language="nim">func f() =
  {.cast(noSideEffect).}:
    echo "test"</pre> <h3 id="pragmas-compiletime-pragma">compileTime pragma</h3>
<p>The <code>compileTime</code> pragma is used to mark a proc or variable to be used only during compile-time execution. No code will be generated for it. Compile-time procs are useful as helpers for macros. Since version 0.12.0 of the language, a proc that uses <code>system.NimNode</code> within its parameter types is implicitly declared <code>compileTime</code>:</p> <pre class="listing" data-language="nim">proc astHelper(n: NimNode): NimNode =
  result = n</pre>
<p>Is the same as:</p> <pre class="listing" data-language="nim">proc astHelper(n: NimNode): NimNode {.compileTime.} =
  result = n</pre>
<p><code>compileTime</code> variables are available at runtime too. This simplifies certain idioms where variables are filled at compile-time (for example, lookup tables) but accessed at runtime:</p> <pre class="listing" data-language="nim">import macros

var nameToProc {.compileTime.}: seq[(string, proc (): string {.nimcall.})]

macro registerProc(p: untyped): untyped =
  result = newTree(nnkStmtList, p)
  
  let procName = p[0]
  let procNameAsStr = $p[0]
  result.add quote do:
    nameToProc.add((`procNameAsStr`, `procName`))

proc foo: string {.registerProc.} = "foo"
proc bar: string {.registerProc.} = "bar"
proc baz: string {.registerProc.} = "baz"

doAssert nameToProc[2][1]() == "baz"</pre> <h3 id="pragmas-noreturn-pragma">noReturn pragma</h3>
<p>The <code>noreturn</code> pragma is used to mark a proc that never returns.</p> <h3 id="pragmas-acyclic-pragma">acyclic pragma</h3>
<p>The <code>acyclic</code> pragma can be used for object types to mark them as acyclic even though they seem to be cyclic. This is an <strong>optimization</strong> for the garbage collector to not consider objects of this type as part of a cycle:</p> <pre class="listing" data-language="nim">type
  Node = ref NodeObj
  NodeObj {.acyclic.} = object
    left, right: Node
    data: string</pre>
<p>Or if we directly use a ref object:</p> <pre class="listing" data-language="nim">type
  Node {.acyclic.} = ref object
    left, right: Node
    data: string</pre>
<p>In the example, a tree structure is declared with the <code>Node</code> type. Note that the type definition is recursive and the GC has to assume that objects of this type may form a cyclic graph. The <code>acyclic</code> pragma passes the information that this cannot happen to the GC. If the programmer uses the <code>acyclic</code> pragma for data types that are in reality cyclic, the memory leaks can be the result, but memory safety is preserved.</p> <h3 id="pragmas-final-pragma">final pragma</h3>
<p>The <code>final</code> pragma can be used for an object type to specify that it cannot be inherited from. Note that inheritance is only available for objects that inherit from an existing object (via the <code>object of SuperType</code> syntax) or that have been marked as <code>inheritable</code>.</p> <h3 id="pragmas-shallow-pragma">shallow pragma</h3>
<p>The <code>shallow</code> pragma affects the semantics of a type: The compiler is allowed to make a shallow copy. This can cause serious semantic issues and break memory safety! However, it can speed up assignments considerably, because the semantics of Nim require deep copying of sequences and strings. This can be expensive, especially if sequences are used to build a tree structure:</p> <pre class="listing" data-language="nim">type
  NodeKind = enum nkLeaf, nkInner
  Node {.shallow.} = object
    case kind: NodeKind
    of nkLeaf:
      strVal: string
    of nkInner:
      children: seq[Node]</pre> <h3 id="pragmas-pure-pragma">pure pragma</h3>
<p>An object type can be marked with the <code>pure</code> pragma so that its type field which is used for runtime type identification is omitted. This used to be necessary for binary compatibility with other compiled languages.</p> <p>An enum type can be marked as <code>pure</code>. Then access of its fields always requires full qualification.</p> <h3 id="pragmas-asmnostackframe-pragma">asmNoStackFrame pragma</h3>
<p>A proc can be marked with the <code>asmNoStackFrame</code> pragma to tell the compiler it should not generate a stack frame for the proc. There are also no exit statements like <code>return result;</code> generated and the generated C function is declared as <code>__declspec(naked)</code> or <code>__attribute__((naked))</code> (depending on the used C compiler).</p> <p><strong>Note</strong>: This pragma should only be used by procs which consist solely of assembler statements.</p> <h3 id="pragmas-error-pragma">error pragma</h3>
<p>The <code>error</code> pragma is used to make the compiler output an error message with the given content. The compilation does not necessarily abort after an error though.</p> <p>The <code>error</code> pragma can also be used to annotate a symbol (like an iterator or proc). The <em>usage</em> of the symbol then triggers a static error. This is especially useful to rule out that some operation is valid due to overloading and type conversions:</p> <pre class="listing" data-language="nim">## check that underlying int values are compared and not the pointers:
proc `==`(x, y: ptr int): bool {.error.}</pre> <h3 id="pragmas-fatal-pragma">fatal pragma</h3>
<p>The <code>fatal</code> pragma is used to make the compiler output an error message with the given content. In contrast to the <code>error</code> pragma, the compilation is guaranteed to be aborted by this pragma. Example:</p> <pre class="listing" data-language="nim">when not defined(objc):
  {.fatal: "Compile this program with the objc command!".}</pre> <h3 id="pragmas-warning-pragma">warning pragma</h3>
<p>The <code>warning</code> pragma is used to make the compiler output a warning message with the given content. Compilation continues after the warning.</p> <h3 id="pragmas-hint-pragma">hint pragma</h3>
<p>The <code>hint</code> pragma is used to make the compiler output a hint message with the given content. Compilation continues after the hint.</p> <h3 id="pragmas-line-pragma">line pragma</h3>
<p>The <code>line</code> pragma can be used to affect line information of the annotated statement, as seen in stack backtraces:</p> <pre class="listing" data-language="nim">template myassert*(cond: untyped, msg = "") =
  if not cond:
    # change run-time line information of the 'raise' statement:
    {.line: instantiationInfo().}:
      raise newException(EAssertionFailed, msg)</pre>
<p>If the <code>line</code> pragma is used with a parameter, the parameter needs be a <code>tuple[filename: string, line: int]</code>. If it is used without a parameter, <code>system.InstantiationInfo()</code> is used.</p> <h3 id="pragmas-linearscanend-pragma">linearScanEnd pragma</h3>
<p>The <code>linearScanEnd</code> pragma can be used to tell the compiler how to compile a Nim <span id="case_1">case</span> statement. Syntactically it has to be used as a statement:</p> <pre class="listing" data-language="nim">case myInt
of 0:
  echo "most common case"
of 1:
  {.linearScanEnd.}
  echo "second most common case"
of 2: echo "unlikely: use branch table"
else: echo "unlikely too: use branch table for ", myInt</pre>
<p>In the example, the case branches <code>0</code> and <code>1</code> are much more common than the other cases. Therefore the generated assembler code should test for these values first so that the CPU's branch predictor has a good chance to succeed (avoiding an expensive CPU pipeline stall). The other cases might be put into a jump table for O(1) overhead but at the cost of a (very likely) pipeline stall.</p> <p>The <code>linearScanEnd</code> pragma should be put into the last branch that should be tested against via linear scanning. If put into the last branch of the whole <code>case</code> statement, the whole <code>case</code> statement uses linear scanning.</p> <h3 id="pragmas-computedgoto-pragma">computedGoto pragma</h3>
<p>The <code>computedGoto</code> pragma can be used to tell the compiler how to compile a Nim <span id="case_2">case</span> in a <code>while true</code> statement. Syntactically it has to be used as a statement inside the loop:</p> <pre class="listing" data-language="nim">type
  MyEnum = enum
    enumA, enumB, enumC, enumD, enumE

proc vm() =
  var instructions: array[0..100, MyEnum]
  instructions[2] = enumC
  instructions[3] = enumD
  instructions[4] = enumA
  instructions[5] = enumD
  instructions[6] = enumC
  instructions[7] = enumA
  instructions[8] = enumB
  
  instructions[12] = enumE
  var pc = 0
  while true:
    {.computedGoto.}
    let instr = instructions[pc]
    case instr
    of enumA:
      echo "yeah A"
    of enumC, enumD:
      echo "yeah CD"
    of enumB:
      echo "yeah B"
    of enumE:
      break
    inc(pc)

vm()</pre>
<p>As the example shows <code>computedGoto</code> is mostly useful for interpreters. If the underlying backend (C compiler) does not support the computed goto extension the pragma is simply ignored.</p> <h3 id="pragmas-immediate-pragma">immediate pragma</h3>
<p>The immediate pragma is obsolete. See <a href="#templates-typed-vs-untyped-parameters">Typed vs untyped parameters</a>.</p> <h3 id="pragmas-compilation-option-pragmas">compilation option pragmas</h3>
<p>The listed pragmas here can be used to override the code generation options for a proc/method/converter.</p> <p>The implementation currently provides the following possible options (various others may be added later).</p> <table>
<tr>
<th>pragma</th>
<th>allowed values</th>
<th>description</th>
</tr> <tr>
<td>checks</td>
<td>on|off </td>
<td>Turns the code generation for all runtime checks on or off.</td>
</tr> <tr>
<td>boundChecks</td>
<td>on|off </td>
<td>Turns the code generation for array bound checks on or off.</td>
</tr> <tr>
<td>overflowChecks</td>
<td>on|off </td>
<td>Turns the code generation for over- or underflow checks on or off.</td>
</tr> <tr>
<td>nilChecks</td>
<td>on|off </td>
<td>Turns the code generation for nil pointer checks on or off.</td>
</tr> <tr>
<td>assertions</td>
<td>on|off </td>
<td>Turns the code generation for assertions on or off.</td>
</tr> <tr>
<td>warnings</td>
<td>on|off </td>
<td>Turns the warning messages of the compiler on or off.</td>
</tr> <tr>
<td>hints</td>
<td>on|off </td>
<td>Turns the hint messages of the compiler on or off.</td>
</tr> <tr>
<td>optimization</td>
<td>none|speed|size </td>
<td>Optimize the code for speed or size, or disable optimization.</td>
</tr> <tr>
<td>patterns</td>
<td>on|off </td>
<td>Turns the term rewriting templates/macros on or off.</td>
</tr> <tr>
<td>callconv</td>
<td>cdecl|...</td>
<td>Specifies the default calling convention for all procedures (and procedure types) that follow.</td>
</tr> </table>
<p>Example:</p> <pre class="listing" data-language="nim">{.checks: off, optimization: speed.}
# compile without runtime checks and optimize for speed</pre> <h3 id="pragmas-push-and-pop-pragmas">push and pop pragmas</h3>
<p>The <span id="pushslashpop_1">push/pop</span> pragmas are very similar to the option directive, but are used to override the settings temporarily. Example:</p> <pre class="listing" data-language="nim">{.push checks: off.}
# compile this section without runtime checks as it is
# speed critical
# ... some code ...
{.pop.} # restore old settings</pre>
<p><span id="pushslashpop_2">push/pop</span> can switch on/off some standard library pragmas, example:</p> <pre class="listing" data-language="nim">{.push inline.}
proc thisIsInlined(): int = 42
func willBeInlined(): float = 42.0
{.pop.}
proc notInlined(): int = 9

{.push discardable, boundChecks: off, compileTime, noSideEffect, experimental.}
template example(): string = "https://nim-lang.org"
{.pop.}

{.push deprecated, hint[LineTooLong]: off, used, stackTrace: off.}
proc sample(): bool = true
{.pop.}</pre>
<p>For third party pragmas, it depends on its implementation but uses the same syntax.</p> <h3 id="pragmas-register-pragma">register pragma</h3>
<p>The <code>register</code> pragma is for variables only. It declares the variable as <code>register</code>, giving the compiler a hint that the variable should be placed in a hardware register for faster access. C compilers usually ignore this though and for good reasons: Often they do a better job without it anyway.</p> <p>In highly specific cases (a dispatch loop of a bytecode interpreter for example) it may provide benefits, though.</p> <h3 id="pragmas-global-pragma">global pragma</h3>
<p>The <code>global</code> pragma can be applied to a variable within a proc to instruct the compiler to store it in a global location and initialize it once at program startup.</p> <pre class="listing" data-language="nim">proc isHexNumber(s: string): bool =
  var pattern {.global.} = re"[0-9a-fA-F]+"
  result = s.match(pattern)</pre>
<p>When used within a generic proc, a separate unique global variable will be created for each instantiation of the proc. The order of initialization of the created global variables within a module is not defined, but all of them will be initialized after any top-level variables in their originating module and before any variable in a module that imports it.</p> <h3 id="pragmas-disabling-certain-messages">Disabling certain messages</h3>
<p>Nim generates some warnings and hints ("line too long") that may annoy the user. A mechanism for disabling certain messages is provided: Each hint and warning message contains a symbol in brackets. This is the message's identifier that can be used to enable or disable it:</p> <pre class="listing" data-language="nim">{.hint[LineTooLong]: off.} # turn off the hint about too long lines</pre>
<p>This is often better than disabling all warnings at once.</p> <h3 id="pragmas-used-pragma">used pragma</h3>
<p>Nim produces a warning for symbols that are not exported and not used either. The <code>used</code> pragma can be attached to a symbol to suppress this warning. This is particularly useful when the symbol was generated by a macro:</p> <pre class="listing" data-language="nim">template implementArithOps(T) =
  proc echoAdd(a, b: T) {.used.} =
    echo a + b
  proc echoSub(a, b: T) {.used.} =
    echo a - b

# no warning produced for the unused 'echoSub'
implementArithOps(int)
echoAdd 3, 5</pre>
<p><code>used</code> can also be used as a top-level statement to mark a module as "used". This prevents the "Unused import" warning:</p> <pre class="listing" data-language="nim"># module: debughelper.nim
when defined(nimHasUsed):
  # 'import debughelper' is so useful for debugging
  # that Nim shouldn't produce a warning for that import,
  # even if currently unused:
  {.used.}</pre> <h3 id="pragmas-experimental-pragma">experimental pragma</h3>
<p>The <code>experimental</code> pragma enables experimental language features. Depending on the concrete feature, this means that the feature is either considered too unstable for an otherwise stable release or that the future of the feature is uncertain (it may be removed at any time).</p> <p>Example:</p> <pre class="listing" data-language="nim">import threadpool
{.experimental: "parallel".}

proc threadedEcho(s: string, i: int) =
  echo(s, " ", $i)

proc useParallel() =
  parallel:
    for i in 0..4:
      spawn threadedEcho("echo in parallel", i)

useParallel()</pre>
<p>As a top-level statement, the experimental pragma enables a feature for the rest of the module it's enabled in. This is problematic for macro and generic instantiations that cross a module scope. Currently, these usages have to be put into a <code>.push/pop</code> environment:</p> <pre class="listing" data-language="nim"># client.nim
proc useParallel*[T](unused: T) =
  # use a generic T here to show the problem.
  {.push experimental: "parallel".}
  parallel:
    for i in 0..4:
      echo "echo in parallel"
  
  {.pop.}</pre>
<pre class="listing" data-language="nim">import client
useParallel(1)</pre> <h2 id="implementation-specific-pragmas">Implementation Specific Pragmas</h2>
<p>This section describes additional pragmas that the current Nim implementation supports but which should not be seen as part of the language specification.</p> <h3 id="implementation-specific-pragmas-bitsize-pragma">Bitsize pragma</h3>
<p>The <code>bitsize</code> pragma is for object field members. It declares the field as a bitfield in C/C++.</p> <pre class="listing" data-language="nim">type
  mybitfield = object
    flag {.bitsize:1.}: cuint</pre>
<p>generates:</p> <pre class="listing" data-language="nim">struct mybitfield {
  unsigned int flag:1;
};</pre> <h3 id="implementation-specific-pragmas-align-pragma">Align pragma</h3>
<p>The <span id="align_1">align</span> pragma is for variables and object field members. It modifies the alignment requirement of the entity being declared. The argument must be a constant power of 2. Valid non-zero alignments that are weaker than other align pragmas on the same declaration are ignored. Alignments that are weaker than the alignment requirement of the type are ignored.</p> <pre class="listing" data-language="nim">type
  sseType = object
    sseData {.align(16).}: array[4, float32]
  
  # every object will be aligned to 128-byte boundary
  Data = object
    x: char
    cacheline {.align(128).}: array[128, char] # over-aligned array of char,

proc main() =
  echo "sizeof(Data) = ", sizeof(Data), " (1 byte + 127 bytes padding + 128-byte array)"
  # output: sizeof(Data) = 256 (1 byte + 127 bytes padding + 128-byte array)
  echo "alignment of sseType is ", alignof(sseType)
  # output: alignment of sseType is 16
  var d {.align(2048).}: Data # this instance of data is aligned even stricter

main()</pre>
<p>This pragma has no effect on the JS backend.</p> <h3 id="implementation-specific-pragmas-volatile-pragma">Volatile pragma</h3>
<p>The <code>volatile</code> pragma is for variables only. It declares the variable as <code>volatile</code>, whatever that means in C/C++ (its semantics are not well defined in C/C++).</p> <p><strong>Note</strong>: This pragma will not exist for the LLVM backend.</p> <h3 id="implementation-specific-pragmas-nodecl-pragma">NoDecl pragma</h3>
<p>The <code>noDecl</code> pragma can be applied to almost any symbol (variable, proc, type, etc.) and is sometimes useful for interoperability with C: It tells Nim that it should not generate a declaration for the symbol in the C code. For example:</p> <pre class="listing" data-language="nim">var
  EACCES {.importc, noDecl.}: cint # pretend EACCES was a variable, as
                                   # Nim does not know its value</pre>
<p>However, the <code>header</code> pragma is often the better alternative.</p> <p><strong>Note</strong>: This will not work for the LLVM backend.</p> <h3 id="implementation-specific-pragmas-header-pragma">Header pragma</h3>
<p>The <code>header</code> pragma is very similar to the <code>noDecl</code> pragma: It can be applied to almost any symbol and specifies that it should not be declared and instead, the generated code should contain an <code>#include</code>:</p> <pre class="listing" data-language="nim">type
  PFile {.importc: "FILE*", header: "&lt;stdio.h&gt;".} = distinct pointer
    # import C's FILE* type; Nim will treat it as a new pointer type</pre>
<p>The <code>header</code> pragma always expects a string constant. The string constant contains the header file: As usual for C, a system header file is enclosed in angle brackets: <code>&lt;&gt;</code>. If no angle brackets are given, Nim encloses the header file in <code>""</code> in the generated C code.</p> <p><strong>Note</strong>: This will not work for the LLVM backend.</p> <h3 id="implementation-specific-pragmas-incompletestruct-pragma">IncompleteStruct pragma</h3>
<p>The <code>incompleteStruct</code> pragma tells the compiler to not use the underlying C <code>struct</code> in a <code>sizeof</code> expression:</p> <pre class="listing" data-language="nim">type
  DIR* {.importc: "DIR", header: "&lt;dirent.h&gt;",
         pure, incompleteStruct.} = object</pre> <h3 id="implementation-specific-pragmas-compile-pragma">Compile pragma</h3>
<p>The <code>compile</code> pragma can be used to compile and link a C/C++ source file with the project:</p> <pre class="listing" data-language="nim">{.compile: "myfile.cpp".}</pre>
<p><strong>Note</strong>: Nim computes a SHA1 checksum and only recompiles the file if it has changed. One can use the <code>-f</code> command-line option to force the recompilation of the file.</p> <p>Since 1.4 the <code>compile</code> pragma is also available with this syntax:</p> <pre class="listing" data-language="nim">{.compile("myfile.cpp", "--custom flags here").}</pre>
<p>As can be seen in the example, this new variant allows for custom flags that are passed to the C compiler when the file is recompiled.</p> <h3 id="implementation-specific-pragmas-link-pragma">Link pragma</h3>
<p>The <code>link</code> pragma can be used to link an additional file with the project:</p> <pre class="listing" data-language="nim">{.link: "myfile.o".}</pre> <h3 id="implementation-specific-pragmas-passc-pragma">PassC pragma</h3>
<p>The <code>passc</code> pragma can be used to pass additional parameters to the C compiler like one would using the command-line switch <code>--passc</code>:</p> <pre class="listing" data-language="nim">{.passc: "-Wall -Werror".}</pre>
<p>Note that one can use <code>gorge</code> from the <a href="system.html">system module</a> to embed parameters from an external command that will be executed during semantic analysis:</p> <pre class="listing" data-language="nim">{.passc: gorge("pkg-config --cflags sdl").}</pre> <h3 id="implementation-specific-pragmas-localpassc-pragma">LocalPassc pragma</h3>
<p>The <code>localPassc</code> pragma can be used to pass additional parameters to the C compiler, but only for the C/C++ file that is produced from the Nim module the pragma resides in:</p> <pre class="listing" data-language="nim"># Module A.nim
# Produces: A.nim.cpp
{.localPassc: "-Wall -Werror".} # Passed when compiling A.nim.cpp</pre> <h3 id="implementation-specific-pragmas-passl-pragma">PassL pragma</h3>
<p>The <code>passL</code> pragma can be used to pass additional parameters to the linker like one would be using the command-line switch <code>--passL</code>:</p> <pre class="listing" data-language="nim">{.passL: "-lSDLmain -lSDL".}</pre>
<p>Note that one can use <code>gorge</code> from the <a href="system.html">system module</a> to embed parameters from an external command that will be executed during semantic analysis:</p> <pre class="listing" data-language="nim">{.passL: gorge("pkg-config --libs sdl").}</pre> <h3 id="implementation-specific-pragmas-emit-pragma">Emit pragma</h3>
<p>The <code>emit</code> pragma can be used to directly affect the output of the compiler's code generator. The code is then unportable to other code generators/backends. Its usage is highly discouraged! However, it can be extremely useful for interfacing with <span id="cplusplus_1">C++</span> or <span id="objective-c_1">Objective C</span> code.</p> <p>Example:</p> <pre class="listing" data-language="nim">{.emit: """
static int cvariable = 420;
""".}

{.push stackTrace:off.}
proc embedsC() =
  var nimVar = 89
  # access Nim symbols within an emit section outside of string literals:
  {.emit: ["""fprintf(stdout, "%d\n", cvariable + (int)""", nimVar, ");"].}
{.pop.}

embedsC()</pre>
<p><code>nimbase.h</code> defines <code>NIM_EXTERNC</code> C macro that can be used for <code>extern "C"</code> code to work with both <code>nim c</code> and <code>nim cpp</code>, e.g.:</p> <pre class="listing" data-language="nim">proc foobar() {.importc:"$1".}
{.emit: """
#include &lt;stdio.h&gt;
NIM_EXTERNC
void fun(){}
""".}</pre>
<p>For backward compatibility, if the argument to the <code>emit</code> statement is a single string literal, Nim symbols can be referred to via backticks. This usage is however deprecated.</p> <p>For a toplevel emit statement the section where in the generated C/C++ file the code should be emitted can be influenced via the prefixes <code>/*TYPESECTION*/</code> or <code>/*VARSECTION*/</code> or <code>/*INCLUDESECTION*/</code>:</p> <pre class="listing" data-language="nim">{.emit: """/*TYPESECTION*/
struct Vector3 {
public:
  Vector3(): x(5) {}
  Vector3(float x_): x(x_) {}
  float x;
};
""".}

type Vector3 {.importcpp: "Vector3", nodecl} = object
  x: cfloat

proc constructVector3(a: cfloat): Vector3 {.importcpp: "Vector3(@)", nodecl}</pre> <h3 id="implementation-specific-pragmas-importcpp-pragma">ImportCpp pragma</h3>
<p><strong>Note</strong>: <a href="https://github.com/nim-lang/c2nim/blob/master/doc/c2nim.rst">c2nim</a> can parse a large subset of C++ and knows about the <code>importcpp</code> pragma pattern language. It is not necessary to know all the details described here.</p> <p>Similar to the <a href="#foreign-function-interface-importc-pragma">importc pragma for C</a>, the <code>importcpp</code> pragma can be used to import <span id="cplusplus_2">C++</span> methods or C++ symbols in general. The generated code then uses the C++ method calling syntax: <code>obj-&gt;method(arg)</code>. In combination with the <code>header</code> and <code>emit</code> pragmas this allows <em>sloppy</em> interfacing with libraries written in C++:</p> <pre class="listing" data-language="nim"># Horrible example of how to interface with a C++ engine ... ;-)

{.link: "/usr/lib/libIrrlicht.so".}

{.emit: """
using namespace irr;
using namespace core;
using namespace scene;
using namespace video;
using namespace io;
using namespace gui;
""".}

const
  irr = "&lt;irrlicht/irrlicht.h&gt;"

type
  IrrlichtDeviceObj {.header: irr,
                      importcpp: "IrrlichtDevice".} = object
  IrrlichtDevice = ptr IrrlichtDeviceObj

proc createDevice(): IrrlichtDevice {.
  header: irr, importcpp: "createDevice(@)".}
proc run(device: IrrlichtDevice): bool {.
  header: irr, importcpp: "#.run(@)".}</pre>
<p>The compiler needs to be told to generate C++ (command <code>cpp</code>) for this to work. The conditional symbol <code>cpp</code> is defined when the compiler emits C++ code.</p> <h4 id="importcpp-pragma-namespaces">Namespaces</h4>
<p>The <em>sloppy interfacing</em> example uses <code>.emit</code> to produce <code>using namespace</code> declarations. It is usually much better to instead refer to the imported name via the <code>namespace::identifier</code> notation:</p> <pre class="listing" data-language="nim">type
  IrrlichtDeviceObj {.header: irr,
                      importcpp: "irr::IrrlichtDevice".} = object</pre> <h4 id="importcpp-pragma-importcpp-for-enums">Importcpp for enums</h4>
<p>When <code>importcpp</code> is applied to an enum type the numerical enum values are annotated with the C++ enum type, like in this example: <code>((TheCppEnum)(3))</code>. (This turned out to be the simplest way to implement it.)</p> <h4 id="importcpp-pragma-importcpp-for-procs">Importcpp for procs</h4>
<p>Note that the <code>importcpp</code> variant for procs uses a somewhat cryptic pattern language for maximum flexibility:</p> <ul class="simple">
<li>A hash <code>#</code> symbol is replaced by the first or next argument.</li> <li>A dot following the hash <code>#.</code> indicates that the call should use C++'s dot or arrow notation.</li> <li>An at symbol <code>@</code> is replaced by the remaining arguments, separated by commas.</li> </ul> <p>For example:</p> <pre class="listing" data-language="nim">proc cppMethod(this: CppObj, a, b, c: cint) {.importcpp: "#.CppMethod(@)".}
var x: ptr CppObj
cppMethod(x[], 1, 2, 3)</pre>
<p>Produces:</p> <pre class="listing" data-language="nim">x-&gt;CppMethod(1, 2, 3)</pre>
<p>As a special rule to keep backward compatibility with older versions of the <code>importcpp</code> pragma, if there is no special pattern character (any of <code># ' @</code>) at all, C++'s dot or arrow notation is assumed, so the above example can also be written as:</p> <pre class="listing" data-language="nim">proc cppMethod(this: CppObj, a, b, c: cint) {.importcpp: "CppMethod".}</pre>
<p>Note that the pattern language naturally also covers C++'s operator overloading capabilities:</p> <pre class="listing" data-language="nim">proc vectorAddition(a, b: Vec3): Vec3 {.importcpp: "# + #".}
proc dictLookup(a: Dict, k: Key): Value {.importcpp: "#[#]".}</pre>
<ul class="simple">
<li>An apostrophe <code>'</code> followed by an integer <code>i</code> in the range 0..9 is replaced by the i'th parameter <em>type</em>. The 0th position is the result type. This can be used to pass types to C++ function templates. Between the <code>'</code> and the digit, an asterisk can be used to get to the base type of the type. (So it "takes away a star" from the type; <code>T*</code> becomes <code>T</code>.) Two stars can be used to get to the element type of the element type etc.</li> </ul> <p>For example:</p> <pre class="listing" data-language="nim">type Input {.importcpp: "System::Input".} = object
proc getSubsystem*[T](): ptr T {.importcpp: "SystemManager::getSubsystem&lt;'*0&gt;()", nodecl.}

let x: ptr Input = getSubsystem[Input]()</pre>
<p>Produces:</p> <pre class="listing" data-language="nim">x = SystemManager::getSubsystem&lt;System::Input&gt;()</pre>
<ul class="simple">
<li>
<code>#@</code> is a special case to support a <code>cnew</code> operation. It is required so that the call expression is inlined directly, without going through a temporary location. This is only required to circumvent a limitation of the current code generator.</li> </ul> <p>For example C++'s <code>new</code> operator can be "imported" like this:</p> <pre class="listing" data-language="nim">proc cnew*[T](x: T): ptr T {.importcpp: "(new '*0#@)", nodecl.}

# constructor of 'Foo':
proc constructFoo(a, b: cint): Foo {.importcpp: "Foo(@)".}

let x = cnew constructFoo(3, 4)</pre>
<p>Produces:</p> <pre class="listing" data-language="nim">x = new Foo(3, 4)</pre>
<p>However, depending on the use case <code>new Foo</code> can also be wrapped like this instead:</p> <pre class="listing" data-language="nim">proc newFoo(a, b: cint): ptr Foo {.importcpp: "new Foo(@)".}

let x = newFoo(3, 4)</pre> <h4 id="importcpp-pragma-wrapping-constructors">Wrapping constructors</h4>
<p>Sometimes a C++ class has a private copy constructor and so code like <code>Class c = Class(1,2);</code> must not be generated but instead <code>Class c(1,2);</code>. For this purpose the Nim proc that wraps a C++ constructor needs to be annotated with the <span id="constructor_1">constructor</span> pragma. This pragma also helps to generate faster C++ code since construction then doesn't invoke the copy constructor:</p> <pre class="listing" data-language="nim"># a better constructor of 'Foo':
proc constructFoo(a, b: cint): Foo {.importcpp: "Foo(@)", constructor.}</pre> <h4 id="importcpp-pragma-wrapping-destructors">Wrapping destructors</h4>
<p>Since Nim generates C++ directly, any destructor is called implicitly by the C++ compiler at the scope exits. This means that often one can get away with not wrapping the destructor at all! However, when it needs to be invoked explicitly, it needs to be wrapped. The pattern language provides everything that is required:</p> <pre class="listing" data-language="nim">proc destroyFoo(this: var Foo) {.importcpp: "#.~Foo()".}</pre> <h4 id="importcpp-pragma-importcpp-for-objects">Importcpp for objects</h4>
<p>Generic <code>importcpp</code>'ed objects are mapped to C++ templates. This means that one can import C++'s templates rather easily without the need for a pattern language for object types:</p> <pre class="listing" data-language="nim">type
  StdMap {.importcpp: "std::map", header: "&lt;map&gt;".} [K, V] = object
proc `[]=`[K, V](this: var StdMap[K, V]; key: K; val: V) {.
  importcpp: "#[#] = #", header: "&lt;map&gt;".}

var x: StdMap[cint, cdouble]
x[6] = 91.4</pre>
<p>Produces:</p> <pre class="listing" data-language="nim">std::map&lt;int, double&gt; x;
x[6] = 91.4;</pre>
<ul class="simple">
<li>If more precise control is needed, the apostrophe <code>'</code> can be used in the supplied pattern to denote the concrete type parameters of the generic type. See the usage of the apostrophe operator in proc patterns for more details.</li> </ul> <pre class="listing" data-language="nim">type
  VectorIterator {.importcpp: "std::vector&lt;'0&gt;::iterator".} [T] = object

var x: VectorIterator[cint]</pre>
<p>Produces:</p> <pre class="listing" data-language="nim">std::vector&lt;int&gt;::iterator x;</pre> <h3 id="implementation-specific-pragmas-importjs-pragma">ImportJs pragma</h3>
<p>Similar to the <a href="#implementation-specific-pragmas-importcpp-pragma">importcpp pragma for C++</a>, the <code>importjs</code> pragma can be used to import Javascript methods or symbols in general. The generated code then uses the Javascript method calling syntax: <code>obj.method(arg)</code>.</p> <h3 id="implementation-specific-pragmas-importobjc-pragma">ImportObjC pragma</h3>
<p>Similar to the <a href="#foreign-function-interface-importc-pragma">importc pragma for C</a>, the <code>importobjc</code> pragma can be used to import <span id="objective-c_2">Objective C</span> methods. The generated code then uses the Objective C method calling syntax: <code>[obj method param1: arg]</code>. In addition with the <code>header</code> and <code>emit</code> pragmas this allows <em>sloppy</em> interfacing with libraries written in Objective C:</p> <pre class="listing" data-language="nim"># horrible example of how to interface with GNUStep ...

{.passL: "-lobjc".}
{.emit: """
#include &lt;objc/Object.h&gt;
@interface Greeter:Object
{
}

- (void)greet:(long)x y:(long)dummy;
@end

#include &lt;stdio.h&gt;
@implementation Greeter

- (void)greet:(long)x y:(long)dummy
{
  printf("Hello, World!\n");
}
@end

#include &lt;stdlib.h&gt;
""".}

type
  Id {.importc: "id", header: "&lt;objc/Object.h&gt;", final.} = distinct int

proc newGreeter: Id {.importobjc: "Greeter new", nodecl.}
proc greet(self: Id, x, y: int) {.importobjc: "greet", nodecl.}
proc free(self: Id) {.importobjc: "free", nodecl.}

var g = newGreeter()
g.greet(12, 34)
g.free()</pre>
<p>The compiler needs to be told to generate Objective C (command <code>objc</code>) for this to work. The conditional symbol <code>objc</code> is defined when the compiler emits Objective C code.</p> <h3 id="implementation-specific-pragmas-codegendecl-pragma">CodegenDecl pragma</h3>
<p>The <code>codegenDecl</code> pragma can be used to directly influence Nim's code generator. It receives a format string that determines how the variable or proc is declared in the generated code.</p> <p>For variables, $1 in the format string represents the type of the variable and $2 is the name of the variable.</p> <p>The following Nim code:</p> <pre class="listing" data-language="nim">var
  a {.codegenDecl: "$# progmem $#".}: int</pre>
<p>will generate this C code:</p> <pre class="listing">int progmem a</pre>
<p>For procedures $1 is the return type of the procedure, $2 is the name of the procedure and $3 is the parameter list.</p> <p>The following nim code:</p> <pre class="listing" data-language="nim">proc myinterrupt() {.codegenDecl: "__interrupt $# $#$#".} =
  echo "realistic interrupt handler"</pre>
<p>will generate this code:</p> <pre class="listing" data-language="nim">__interrupt void myinterrupt()</pre> <h3 id="implementation-specific-pragmas-injectstmt-pragma">InjectStmt pragma</h3>
<p>The <code>injectStmt</code> pragma can be used to inject a statement before every other statement in the current module. It is only supposed to be used for debugging:</p> <pre class="listing" data-language="nim">{.injectStmt: gcInvariants().}

# ... complex code here that produces crashes ...</pre> <h3 id="implementation-specific-pragmas-compileminustime-define-pragmas">compile-time define pragmas</h3>
<p>The pragmas listed here can be used to optionally accept values from the -d/--define option at compile time.</p> <p>The implementation currently provides the following possible options (various others may be added later).</p> <table>
<tr>
<th>pragma</th>
<th>description</th>
</tr> <tr>
<td><span id="intdefine_1">intdefine</span></td>
<td>Reads in a build-time define as an integer</td>
</tr> <tr>
<td><span id="strdefine_1">strdefine</span></td>
<td>Reads in a build-time define as a string</td>
</tr> <tr>
<td><span id="booldefine_1">booldefine</span></td>
<td>Reads in a build-time define as a bool</td>
</tr> </table>
<pre class="listing" data-language="nim">const FooBar {.intdefine.}: int = 5
echo FooBar</pre>
<pre data-language="nim">nim c -d:FooBar=42 foobar.nim</pre> <p>In the above example, providing the -d flag causes the symbol <code>FooBar</code> to be overwritten at compile-time, printing out 42. If the <code>-d:FooBar=42</code> were to be omitted, the default value of 5 would be used. To see if a value was provided, <code>defined(FooBar)</code> can be used.</p> <p>The syntax <code>-d:flag</code> is actually just a shortcut for <code>-d:flag=true</code>.</p> <h2 id="userminusdefined-pragmas">User-defined pragmas</h2> <h3 id="userminusdefined-pragmas-pragma-pragma">pragma pragma</h3>
<p>The <code>pragma</code> pragma can be used to declare user-defined pragmas. This is useful because Nim's templates and macros do not affect pragmas. User-defined pragmas are in a different module-wide scope than all other symbols. They cannot be imported from a module.</p> <p>Example:</p> <pre class="listing" data-language="nim">when appType == "lib":
  {.pragma: rtl, exportc, dynlib, cdecl.}
else:
  {.pragma: rtl, importc, dynlib: "client.dll", cdecl.}

proc p*(a, b: int): int {.rtl.} =
  result = a+b</pre>
<p>In the example, a new pragma named <code>rtl</code> is introduced that either imports a symbol from a dynamic library or exports the symbol for dynamic library generation.</p> <h3 id="userminusdefined-pragmas-custom-annotations">Custom annotations</h3>
<p>It is possible to define custom typed pragmas. Custom pragmas do not affect code generation directly, but their presence can be detected by macros. Custom pragmas are defined using templates annotated with pragma <code>pragma</code>:</p> <pre class="listing" data-language="nim">template dbTable(name: string, table_space: string = "") {.pragma.}
template dbKey(name: string = "", primary_key: bool = false) {.pragma.}
template dbForeignKey(t: typedesc) {.pragma.}
template dbIgnore {.pragma.}</pre>
<p>Consider stylized example of possible Object Relation Mapping (ORM) implementation:</p> <pre class="listing" data-language="nim">const tblspace {.strdefine.} = "dev" # switch for dev, test and prod environments

type
  User {.dbTable("users", tblspace).} = object
    id {.dbKey(primary_key = true).}: int
    name {.dbKey"full_name".}: string
    is_cached {.dbIgnore.}: bool
    age: int
  
  UserProfile {.dbTable("profiles", tblspace).} = object
    id {.dbKey(primary_key = true).}: int
    user_id {.dbForeignKey: User.}: int
    read_access: bool
    write_access: bool
    admin_acess: bool</pre>
<p>In this example, custom pragmas are used to describe how Nim objects are mapped to the schema of the relational database. Custom pragmas can have zero or more arguments. In order to pass multiple arguments use one of template call syntaxes. All arguments are typed and follow standard overload resolution rules for templates. Therefore, it is possible to have default values for arguments, pass by name, varargs, etc.</p> <p>Custom pragmas can be used in all locations where ordinary pragmas can be specified. It is possible to annotate procs, templates, type and variable definitions, statements, etc.</p> <p>Macros module includes helpers which can be used to simplify custom pragma access <code>hasCustomPragma</code>, <code>getCustomPragmaVal</code>. Please consult the macros module documentation for details. These macros are not magic, everything they do can also be achieved by walking the AST of the object representation.</p> <p>More examples with custom pragmas:</p> <ul class="simple">
<li>Better serialization/deserialization control:</li> </ul> <pre class="listing" data-language="nim">type MyObj = object
  a {.dontSerialize.}: int
  b {.defaultDeserialize: 5.}: int
  c {.serializationKey: "_c".}: string</pre>
<ul class="simple">
<li>Adopting type for gui inspector in a game engine:</li> </ul> <pre class="listing" data-language="nim">type MyComponent = object
  position {.editable, animatable.}: Vector3
  alpha {.editRange: [0.0..1.0], animatable.}: float32</pre> <h3 id="userminusdefined-pragmas-macro-pragmas">Macro pragmas</h3>
<p>All macros and templates can also be used as pragmas. They can be attached to routines (procs, iterators, etc), type names, or type expressions. The compiler will perform the following simple syntactic transformations:</p> <pre class="listing" data-language="nim">template command(name: string, def: untyped) = discard

proc p() {.command("print").} = discard</pre>
<p>This is translated to:</p> <pre class="listing" data-language="nim">command("print"):
  proc p() = discard</pre>
<hr> <pre class="listing" data-language="nim">type
  AsyncEventHandler = proc (x: Event) {.async.}</pre>
<p>This is translated to:</p> <pre class="listing" data-language="nim">type
  AsyncEventHandler = async(proc (x: Event))</pre>
<hr> <pre class="listing" data-language="nim">type
  MyObject {.schema: "schema.protobuf".} = object</pre>
<p>This is translated to a call to the <code>schema</code> macro with a <code>nnkTypeDef</code> AST node capturing both the left-hand side and right-hand side of the definition. The macro can return a potentially modified <code>nnkTypeDef</code> tree which will replace the original row in the type section.</p> <p>When multiple macro pragmas are applied to the same definition, the compiler will apply them consequently from left to right. Each macro will receive as input the output of the previous one.</p> <h2 id="foreign-function-interface">Foreign function interface</h2>
<p>Nim's <span id="ffi_1">FFI</span> (foreign function interface) is extensive and only the parts that scale to other future backends (like the LLVM/JavaScript backends) are documented here.</p> <h3 id="foreign-function-interface-importc-pragma">Importc pragma</h3>
<p>The <code>importc</code> pragma provides a means to import a proc or a variable from C. The optional argument is a string containing the C identifier. If the argument is missing, the C name is the Nim identifier <em>exactly as spelled</em>:</p> <pre class="listing" data-language="nim">proc printf(formatstr: cstring) {.header: "&lt;stdio.h&gt;", importc: "printf", varargs.}</pre>
<p>When <code>importc</code> is applied to a <code>let</code> statement it can omit its value which will then be expected to come from C. This can be used to import a C <code>const</code>:</p> <pre class="listing" data-language="nim">{.emit: "const int cconst = 42;".}

let cconst {.importc, nodecl.}: cint

assert cconst == 42</pre>
<p>Note that this pragma has been abused in the past to also work in the js backend for js objects and functions. : Other backends do provide the same feature under the same name. Also, when the target language is not set to C, other pragmas are available:</p> <blockquote>
<ul class="simple">
<li><a href="manual.html#implementation-specific-pragmas-importcpp-pragma">importcpp</a></li> <li><a href="manual.html#implementation-specific-pragmas-importobjc-pragma">importobjc</a></li> <li><a href="manual.html#implementation-specific-pragmas-importjs-pragma">importjs</a></li> </ul> </blockquote> <pre class="listing" data-language="nim">proc p(s: cstring) {.importc: "prefix$1".}</pre>
<p>In the example, the external name of <code>p</code> is set to <code>prefixp</code>. Only <code>$1</code> is available and a literal dollar sign must be written as <code>$$</code>.</p> <h3 id="foreign-function-interface-exportc-pragma">Exportc pragma</h3>
<p>The <code>exportc</code> pragma provides a means to export a type, a variable, or a procedure to C. Enums and constants can't be exported. The optional argument is a string containing the C identifier. If the argument is missing, the C name is the Nim identifier <em>exactly as spelled</em>:</p> <pre class="listing" data-language="nim">proc callme(formatstr: cstring) {.exportc: "callMe", varargs.}</pre>
<p>Note that this pragma is somewhat of a misnomer: Other backends do provide the same feature under the same name.</p> <p>The string literal passed to <code>exportc</code> can be a format string:</p> <pre class="listing" data-language="nim">proc p(s: string) {.exportc: "prefix$1".} =
  echo s</pre>
<p>In the example the external name of <code>p</code> is set to <code>prefixp</code>. Only <code>$1</code> is available and a literal dollar sign must be written as <code>$$</code>.</p> <p>If the symbol should also be exported to a dynamic library, the <code>dynlib</code> pragma should be used in addition to the <code>exportc</code> pragma. See <a href="#foreign-function-interface-dynlib-pragma-for-export">Dynlib pragma for export</a>.</p> <h3 id="foreign-function-interface-extern-pragma">Extern pragma</h3>
<p>Like <code>exportc</code> or <code>importc</code>, the <code>extern</code> pragma affects name mangling. The string literal passed to <code>extern</code> can be a format string:</p> <pre class="listing" data-language="nim">proc p(s: string) {.extern: "prefix$1".} =
  echo s</pre>
<p>In the example, the external name of <code>p</code> is set to <code>prefixp</code>. Only <code>$1</code> is available and a literal dollar sign must be written as <code>$$</code>.</p> <h3 id="foreign-function-interface-bycopy-pragma">Bycopy pragma</h3>
<p>The <code>bycopy</code> pragma can be applied to an object or tuple type and instructs the compiler to pass the type by value to procs:</p> <pre class="listing" data-language="nim">type
  Vector {.bycopy.} = object
    x, y, z: float</pre> <h3 id="foreign-function-interface-byref-pragma">Byref pragma</h3>
<p>The <code>byref</code> pragma can be applied to an object or tuple type and instructs the compiler to pass the type by reference (hidden pointer) to procs.</p> <h3 id="foreign-function-interface-varargs-pragma">Varargs pragma</h3>
<p>The <code>varargs</code> pragma can be applied to procedures only (and procedure types). It tells Nim that the proc can take a variable number of parameters after the last specified parameter. Nim string values will be converted to C strings automatically:</p> <pre class="listing" data-language="nim">proc printf(formatstr: cstring) {.nodecl, varargs.}

printf("hallo %s", "world") # "world" will be passed as C string</pre> <h3 id="foreign-function-interface-union-pragma">Union pragma</h3>
<p>The <code>union</code> pragma can be applied to any <code>object</code> type. It means all of the object's fields are overlaid in memory. This produces a <code>union</code> instead of a <code>struct</code> in the generated C/C++ code. The object declaration then must not use inheritance or any GC'ed memory but this is currently not checked.</p> <p><strong>Future directions</strong>: GC'ed memory should be allowed in unions and the GC should scan unions conservatively.</p> <h3 id="foreign-function-interface-packed-pragma">Packed pragma</h3>
<p>The <code>packed</code> pragma can be applied to any <code>object</code> type. It ensures that the fields of an object are packed back-to-back in memory. It is useful to store packets or messages from/to network or hardware drivers, and for interoperability with C. Combining packed pragma with inheritance is not defined, and it should not be used with GC'ed memory (ref's).</p> <p><strong>Future directions</strong>: Using GC'ed memory in packed pragma will result in a static error. Usage with inheritance should be defined and documented.</p> <h3 id="foreign-function-interface-dynlib-pragma-for-import">Dynlib pragma for import</h3>
<p>With the <code>dynlib</code> pragma a procedure or a variable can be imported from a dynamic library (<code>.dll</code> files for Windows, <code>lib*.so</code> files for UNIX). The non-optional argument has to be the name of the dynamic library:</p> <pre class="listing" data-language="nim">proc gtk_image_new(): PGtkWidget
  {.cdecl, dynlib: "libgtk-x11-2.0.so", importc.}</pre>
<p>In general, importing a dynamic library does not require any special linker options or linking with import libraries. This also implies that no <em>devel</em> packages need to be installed.</p> <p>The <code>dynlib</code> import mechanism supports a versioning scheme:</p> <pre class="listing" data-language="nim">proc Tcl_Eval(interp: pTcl_Interp, script: cstring): int {.cdecl,
  importc, dynlib: "libtcl(|8.5|8.4|8.3).so.(1|0)".}</pre>
<p>At runtime the dynamic library is searched for (in this order):</p>
<pre data-language="nim">libtcl.so.1
libtcl.so.0
libtcl8.5.so.1
libtcl8.5.so.0
libtcl8.4.so.1
libtcl8.4.so.0
libtcl8.3.so.1
libtcl8.3.so.0</pre> <p>The <code>dynlib</code> pragma supports not only constant strings as an argument but also string expressions in general:</p> <pre class="listing" data-language="nim">import os

proc getDllName: string =
  result = "mylib.dll"
  if fileExists(result): return
  result = "mylib2.dll"
  if fileExists(result): return
  quit("could not load dynamic library")

proc myImport(s: cstring) {.cdecl, importc, dynlib: getDllName().}</pre>
<p><strong>Note</strong>: Patterns like <code>libtcl(|8.5|8.4).so</code> are only supported in constant strings, because they are precompiled.</p> <p><strong>Note</strong>: Passing variables to the <code>dynlib</code> pragma will fail at runtime because of order of initialization problems.</p> <p><strong>Note</strong>: A <code>dynlib</code> import can be overridden with the <code>--dynlibOverride:name</code> command-line option. The Compiler User Guide contains further information.</p> <h3 id="foreign-function-interface-dynlib-pragma-for-export">Dynlib pragma for export</h3>
<p>With the <code>dynlib</code> pragma a procedure can also be exported to a dynamic library. The pragma then has no argument and has to be used in conjunction with the <code>exportc</code> pragma:</p> <pre class="listing" data-language="nim">proc exportme(): int {.cdecl, exportc, dynlib.}</pre>
<p>This is only useful if the program is compiled as a dynamic library via the <code>--app:lib</code> command-line option.</p> <h2 id="threads">Threads</h2>
<p>To enable thread support the <code>--threads:on</code> command-line switch needs to be used. The <code>system</code> module then contains several threading primitives. See the <a href="threads.html">threads</a> and <a href="channels.html">channels</a> modules for the low-level thread API. There are also high-level parallelism constructs available. See <a href="manual_experimental.html#parallel-amp-spawn">spawn</a> for further details.</p> <p>Nim's memory model for threads is quite different than that of other common programming languages (C, Pascal, Java): Each thread has its own (garbage collected) heap, and sharing of memory is restricted to global variables. This helps to prevent race conditions. GC efficiency is improved quite a lot, because the GC never has to stop other threads and see what they reference.</p> <h3 id="threads-thread-pragma">Thread pragma</h3>
<p>A proc that is executed as a new thread of execution should be marked by the <code>thread</code> pragma for reasons of readability. The compiler checks for violations of the <span id="no-heap-sharing-restriction_1">no heap sharing restriction</span>: This restriction implies that it is invalid to construct a data structure that consists of memory allocated from different (thread-local) heaps.</p> <p>A thread proc is passed to <code>createThread</code> or <code>spawn</code> and invoked indirectly; so the <code>thread</code> pragma implies <code>procvar</code>.</p> <h3 id="threads-gc-safety">GC safety</h3>
<p>We call a proc <code>p</code> <span id="gc-safe_1">GC safe</span> when it doesn't access any global variable that contains GC'ed memory (<code>string</code>, <code>seq</code>, <code>ref</code> or a closure) either directly or indirectly through a call to a GC unsafe proc.</p> <p>The <span id="gcsafe_1">gcsafe</span> annotation can be used to mark a proc to be gcsafe, otherwise this property is inferred by the compiler. Note that <code>noSideEffect</code> implies <code>gcsafe</code>. The only way to create a thread is via <code>spawn</code> or <code>createThread</code>. The invoked proc must not use <code>var</code> parameters nor must any of its parameters contain a <code>ref</code> or <code>closure</code> type. This enforces the <em>no heap sharing restriction</em>.</p> <p>Routines that are imported from C are always assumed to be <code>gcsafe</code>. To disable the GC-safety checking the <code>--threadAnalysis:off</code> command-line switch can be used. This is a temporary workaround to ease the porting effort from old code to the new threading model.</p> <p>To override the compiler's gcsafety analysis a <code>{.cast(gcsafe).}</code> pragma block can be used:</p> <pre class="listing" data-language="nim">var
  someGlobal: string = "some string here"
  perThread {.threadvar.}: string

proc setPerThread() =
  {.cast(gcsafe).}:
    deepCopy(perThread, someGlobal)</pre>
<p>See also:</p> <ul class="simple">
<li>
<a href="gc.html">Shared heap memory management.</a>.</li> </ul> <h3 id="threads-threadvar-pragma">Threadvar pragma</h3>
<p>A variable can be marked with the <code>threadvar</code> pragma, which makes it a <span id="threadminuslocal_1">thread-local</span> variable; Additionally, this implies all the effects of the <code>global</code> pragma.</p> <pre class="listing" data-language="nim">var checkpoints* {.threadvar.}: seq[string]</pre>
<p>Due to implementation restrictions thread-local variables cannot be initialized within the <code>var</code> section. (Every thread-local variable needs to be replicated at thread creation.)</p> <h3 id="threads-threads-and-exceptions">Threads and exceptions</h3>
<p>The interaction between threads and exceptions is simple: A <em>handled</em> exception in one thread cannot affect any other thread. However, an <em>unhandled</em> exception in one thread terminates the whole <em>process</em>! </p><div class="_attribution">
  <p class="_attribution-p">
    &copy; 2006&ndash;2021 Andreas Rumpf<br>Licensed under the MIT License.<br>
    <a href="https://nim-lang.org/docs/manual.html" class="_attribution-link">https://nim-lang.org/docs/manual.html</a>
  </p>
</div>
