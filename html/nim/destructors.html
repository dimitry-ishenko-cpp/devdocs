<h1 class="title">Nim Destructors and Move Semantics</h1>   <h2 id="about-this-document">About this document</h2>
<p>This document describes the upcoming Nim runtime which does not use classical GC algorithms anymore but is based on destructors and move semantics. The new runtime's advantages are that Nim programs become oblivious to the involved heap sizes and programs are easier to write to make effective use of multi-core machines. As a nice bonus, files and sockets and the like will not require manual <code>close</code> calls anymore.</p> <p>This document aims to be a precise specification about how move semantics and destructors work in Nim.</p> <h2 id="motivating-example">Motivating example</h2>
<p>With the language mechanisms described here, a custom seq could be written as:</p> <pre class="listing" data-language="nim">type
  myseq*[T] = object
    len, cap: int
    data: ptr UncheckedArray[T]

proc `=destroy`*[T](x: var myseq[T]) =
  if x.data != nil:
    for i in 0..&lt;x.len: `=destroy`(x[i])
    dealloc(x.data)

proc `=copy`*[T](a: var myseq[T]; b: myseq[T]) =
  # do nothing for self-assignments:
  if a.data == b.data: return
  `=destroy`(a)
  wasMoved(a)
  a.len = b.len
  a.cap = b.cap
  if b.data != nil:
    a.data = cast[typeof(a.data)](alloc(a.cap * sizeof(T)))
    for i in 0..&lt;a.len:
      a.data[i] = b.data[i]

proc `=sink`*[T](a: var myseq[T]; b: myseq[T]) =
  # move assignment, optional.
  # Compiler is using `=destroy` and `copyMem` when not provided
  `=destroy`(a)
  wasMoved(a)
  a.len = b.len
  a.cap = b.cap
  a.data = b.data

proc add*[T](x: var myseq[T]; y: sink T) =
  if x.len &gt;= x.cap: resize(x)
  x.data[x.len] = y
  inc x.len

proc `[]`*[T](x: myseq[T]; i: Natural): lent T =
  assert i &lt; x.len
  x.data[i]

proc `[]=`*[T](x: var myseq[T]; i: Natural; y: sink T) =
  assert i &lt; x.len
  x.data[i] = y

proc createSeq*[T](elems: varargs[T]): myseq[T] =
  result.cap = elems.len
  result.len = elems.len
  result.data = cast[typeof(result.data)](alloc(result.cap * sizeof(T)))
  for i in 0..&lt;result.len: result.data[i] = elems[i]

proc len*[T](x: myseq[T]): int {.inline.} = x.len</pre> <h2 id="lifetimeminustracking-hooks">Lifetime-tracking hooks</h2>
<p>The memory management for Nim's standard <code>string</code> and <code>seq</code> types as well as other standard collections is performed via so-called "Lifetime-tracking hooks" or "type-bound operators". There are 3 different hooks for each (generic or concrete) object type <code>T</code> (<code>T</code> can also be a <code>distinct</code> type) that are called implicitly by the compiler.</p> <p>(Note: The word "hook" here does not imply any kind of dynamic binding or runtime indirections, the implicit calls are statically bound and potentially inlined.)</p> <h3 id="lifetimeminustracking-hooks-eqdestroy-hook">
<code>=destroy</code> hook</h3>
<p>A <code>=destroy</code> hook frees the object's associated memory and releases other associated resources. Variables are destroyed via this hook when they go out of scope or when the routine they were declared in is about to return.</p> <p>The prototype of this hook for a type <code>T</code> needs to be:</p> <pre class="listing" data-language="nim">proc `=destroy`(x: var T)</pre>
<p>The general pattern in <code>=destroy</code> looks like:</p> <pre class="listing" data-language="nim">proc `=destroy`(x: var T) =
  # first check if 'x' was moved to somewhere else:
  if x.field != nil:
    freeResource(x.field)</pre> <h3 id="lifetimeminustracking-hooks-eqsink-hook">
<code>=sink</code> hook</h3>
<p>A <code>=sink</code> hook moves an object around, the resources are stolen from the source and passed to the destination. It is ensured that the source's destructor does not free the resources afterward by setting the object to its default value (the value the object's state started in). Setting an object <code>x</code> back to its default value is written as <code>wasMoved(x)</code>. When not provided the compiler is using a combination of <code>=destroy</code> and <code>copyMem</code> instead. This is efficient hence users rarely need to implement their own <code>=sink</code> operator, it is enough to provide <code>=destroy</code> and <code>=copy</code>, compiler will take care of the rest.</p> <p>The prototype of this hook for a type <code>T</code> needs to be:</p> <pre class="listing" data-language="nim">proc `=sink`(dest: var T; source: T)</pre>
<p>The general pattern in <code>=sink</code> looks like:</p> <pre class="listing" data-language="nim">proc `=sink`(dest: var T; source: T) =
  `=destroy`(dest)
  wasMoved(dest)
  dest.field = source.field</pre>
<p><strong>Note</strong>: <code>=sink</code> does not need to check for self-assignments. How self-assignments are handled is explained later in this document.</p> <h3 id="lifetimeminustracking-hooks-eqcopy-hook">
<code>=copy</code> hook</h3>
<p>The ordinary assignment in Nim conceptually copies the values. The <code>=copy</code> hook is called for assignments that couldn't be transformed into <code>=sink</code> operations.</p> <p>The prototype of this hook for a type <code>T</code> needs to be:</p> <pre class="listing" data-language="nim">proc `=copy`(dest: var T; source: T)</pre>
<p>The general pattern in <code>=copy</code> looks like:</p> <pre class="listing" data-language="nim">proc `=copy`(dest: var T; source: T) =
  # protect against self-assignments:
  if dest.field != source.field:
    `=destroy`(dest)
    wasMoved(dest)
    dest.field = duplicateResource(source.field)</pre>
<p>The <code>=copy</code> proc can be marked with the <code>{.error.}</code> pragma. Then any assignment that otherwise would lead to a copy is prevented at compile-time. This looks like:</p> <pre class="listing" data-language="nim">proc `=copy`(dest: var T; source: T) {.error.}</pre>
<p>but a custom error message (e.g., <code>{.error: "custom error".}</code>) will not be emitted by the compiler. Notice that there is no <code>=</code> before the <code>{.error.}</code> pragma.</p> <h2 id="move-semantics">Move semantics</h2>
<p>A "move" can be regarded as an optimized copy operation. If the source of the copy operation is not used afterward, the copy can be replaced by a move. This document uses the notation <code>lastReadOf(x)</code> to describe that <code>x</code> is not used afterwards. This property is computed by a static control flow analysis but can also be enforced by using <code>system.move</code> explicitly.</p> <h2 id="swap">Swap</h2>
<p>The need to check for self-assignments and also the need to destroy previous objects inside <code>=copy</code> and <code>=sink</code> is a strong indicator to treat <code>system.swap</code> as a builtin primitive of its own that simply swaps every field in the involved objects via <code>copyMem</code> or a comparable mechanism. In other words, <code>swap(a, b)</code> is <strong>not</strong> implemented as <code>let tmp = move(b); b = move(a); a = move(tmp)</code>.</p> <p>This has further consequences:</p> <ul class="simple">
<li>Objects that contain pointers that point to the same object are not supported by Nim's model. Otherwise swapped objects would end up in an inconsistent state.</li> <li>Seqs can use <code>realloc</code> in the implementation.</li> </ul> <h2 id="sink-parameters">Sink parameters</h2>
<p>To move a variable into a collection usually <code>sink</code> parameters are involved. A location that is passed to a <code>sink</code> parameter should not be used afterward. This is ensured by a static analysis over a control flow graph. If it cannot be proven to be the last usage of the location, a copy is done instead and this copy is then passed to the sink parameter.</p> <p>A sink parameter <em>may</em> be consumed once in the proc's body but doesn't have to be consumed at all. The reason for this is that signatures like <code>proc put(t: var Table; k: sink Key, v: sink Value)</code> should be possible without any further overloads and <code>put</code> might not take ownership of <code>k</code> if <code>k</code> already exists in the table. Sink parameters enable an affine type system, not a linear type system.</p> <p>The employed static analysis is limited and only concerned with local variables; however, object and tuple fields are treated as separate entities:</p> <pre class="listing" data-language="nim">proc consume(x: sink Obj) = discard "no implementation"

proc main =
  let tup = (Obj(), Obj())
  consume tup[0]
  # ok, only tup[0] was consumed, tup[1] is still alive:
  echo tup[1]</pre>
<p>Sometimes it is required to explicitly <code>move</code> a value into its final position:</p> <pre class="listing" data-language="nim">proc main =
  var dest, src: array[10, string]
  # ...
  for i in 0..high(dest): dest[i] = move(src[i])</pre>
<p>An implementation is allowed, but not required to implement even more move optimizations (and the current implementation does not).</p> <h2 id="sink-parameter-inference">Sink parameter inference</h2>
<p>The current implementation can do a limited form of sink parameter inference. But it has to be enabled via <code>--sinkInference:on</code>, either on the command line or via a <code>push</code> pragma.</p> <p>To enable it for a section of code, one can use <code>{.push sinkInference: on.}</code>...`{.pop.}`.</p> <p>The <span id="dotnosinks_1">.nosinks</span> pragma can be used to disable this inference for a single routine:</p> <pre class="listing" data-language="nim">proc addX(x: T; child: T) {.nosinks.} =
  x.s.add child</pre>
<p>The details of the inference algorithm are currently undocumented.</p> <h2 id="rewrite-rules">Rewrite rules</h2>
<p><strong>Note</strong>: There are two different allowed implementation strategies:</p> <ol class="simple">
<li>The produced <code>finally</code> section can be a single section that is wrapped around the complete routine body.</li> <li>The produced <code>finally</code> section is wrapped around the enclosing scope.</li> </ol> <p>The current implementation follows strategy (2). This means that resources are destroyed at the scope exit.</p> <pre data-language="nim">var x: T; stmts
---------------             (destroy-var)
var x: T; try stmts
finally: `=destroy`(x)


g(f(...))
------------------------    (nested-function-call)
g(let tmp;
bitwiseCopy tmp, f(...);
tmp)
finally: `=destroy`(tmp)


x = f(...)
------------------------    (function-sink)
`=sink`(x, f(...))


x = lastReadOf z
------------------          (move-optimization)
`=sink`(x, z)
wasMoved(z)


v = v
------------------   (self-assignment-removal)
discard "nop"


x = y
------------------          (copy)
`=copy`(x, y)


f_sink(g())
-----------------------     (call-to-sink)
f_sink(g())


f_sink(notLastReadOf y)
--------------------------     (copy-to-sink)
(let tmp; `=copy`(tmp, y);
f_sink(tmp))


f_sink(lastReadOf y)
-----------------------     (move-to-sink)
f_sink(y)
wasMoved(y)</pre> <h2 id="object-and-array-construction">Object and array construction</h2>
<p>Object and array construction is treated as a function call where the function has <code>sink</code> parameters.</p> <h2 id="destructor-removal">Destructor removal</h2>
<p><code>wasMoved(x);</code> followed by a <code>=destroy(x)</code> operation cancel each other out. An implementation is encouraged to exploit this in order to improve efficiency and code sizes. The current implementation does perform this optimization.</p> <h2 id="self-assignments">Self assignments</h2>
<p><code>=sink</code> in combination with <code>wasMoved</code> can handle self-assignments but it's subtle.</p> <p>The simple case of <code>x = x</code> cannot be turned into <code>=sink(x, x); wasMoved(x)</code> because that would lose <code>x</code>'s value. The solution is that simple self-assignments are simply transformed into an empty statement that does nothing.</p> <p>The complex case looks like a variant of <code>x = f(x)</code>, we consider <code>x = select(rand() &lt; 0.5, x, y)</code> here:</p> <pre class="listing" data-language="nim">proc select(cond: bool; a, b: sink string): string =
  if cond:
    result = a # moves a into result
  else:
    result = b # moves b into result

proc main =
  var x = "abc"
  var y = "xyz"
  # possible self-assignment:
  x = select(true, x, y)</pre>
<p>Is transformed into:</p> <pre class="listing" data-language="nim">proc select(cond: bool; a, b: sink string): string =
  try:
    if cond:
      `=sink`(result, a)
      wasMoved(a)
    else:
      `=sink`(result, b)
      wasMoved(b)
  finally:
    `=destroy`(b)
    `=destroy`(a)

proc main =
  var
    x: string
    y: string
  try:
    `=sink`(x, "abc")
    `=sink`(y, "xyz")
    `=sink`(x, select(true,
      let blitTmp = x
      wasMoved(x)
      blitTmp,
      let blitTmp = y
      wasMoved(y)
      blitTmp))
    echo [x]
  finally:
    `=destroy`(y)
    `=destroy`(x)</pre>
<p>As can be manually verified, this transformation is correct for self-assignments.</p> <h2 id="lent-type">Lent type</h2>
<p><code>proc p(x: sink T)</code> means that the proc <code>p</code> takes ownership of <code>x</code>. To eliminate even more creation/copy &lt;-&gt; destruction pairs, a proc's return type can be annotated as <code>lent T</code>. This is useful for "getter" accessors that seek to allow an immutable view into a container.</p> <p>The <code>sink</code> and <code>lent</code> annotations allow us to remove most (if not all) superfluous copies and destructions.</p> <p><code>lent T</code> is like <code>var T</code> a hidden pointer. It is proven by the compiler that the pointer does not outlive its origin. No destructor call is injected for expressions of type <code>lent T</code> or of type <code>var T</code>.</p> <pre class="listing" data-language="nim">type
  Tree = object
    kids: seq[Tree]

proc construct(kids: sink seq[Tree]): Tree =
  result = Tree(kids: kids)
  # converted into:
  `=sink`(result.kids, kids); wasMoved(kids)
  `=destroy`(kids)

proc `[]`*(x: Tree; i: int): lent Tree =
  result = x.kids[i]
  # borrows from 'x', this is transformed into:
  result = addr x.kids[i]
  # This means 'lent' is like 'var T' a hidden pointer.
  # Unlike 'var' this hidden pointer cannot be used to mutate the object.

iterator children*(t: Tree): lent Tree =
  for x in t.kids: yield x

proc main =
  # everything turned into moves:
  let t = construct(@[construct(@[]), construct(@[])])
  echo t[0] # accessor does not copy the element!</pre> <h2 id="the-dotcursor-annotation">The .cursor annotation</h2>
<p>Under the <code>--gc:arc|orc</code> modes Nim's <code>ref</code> type is implemented via the same runtime "hooks" and thus via reference counting. This means that cyclic structures cannot be freed immediately (<code>--gc:orc</code> ships with a cycle collector). With the <code>.cursor</code> annotation one can break up cycles declaratively:</p> <pre class="listing" data-language="nim">type
  Node = ref object
    left: Node # owning ref
    right {.cursor.}: Node # non-owning ref</pre>
<p>But please notice that this is not C++'s weak_ptr, it means the right field is not involved in the reference counting, it is a raw pointer without runtime checks.</p> <p>Automatic reference counting also has the disadvantage that it introduces overhead when iterating over linked structures. The <code>.cursor</code> annotation can also be used to avoid this overhead:</p> <pre class="listing" data-language="nim">var it {.cursor.} = listRoot
while it != nil:
  use(it)
  it = it.next</pre>
<p>In fact, <code>.cursor</code> more generally prevents object construction/destruction pairs and so can also be useful in other contexts. The alternative solution would be to use raw pointers (<code>ptr</code>) instead which is more cumbersome and also more dangerous for Nim's evolution: Later on, the compiler can try to prove <code>.cursor</code> annotations to be safe, but for <code>ptr</code> the compiler has to remain silent about possible problems.</p> <h2 id="cursor-inference-slash-copy-elision">Cursor inference / copy elision</h2>
<p>The current implementation also performs <code>.cursor</code> inference. Cursor inference is a form of copy elision.</p> <p>To see how and when we can do that, think about this question: In <code>dest = src</code> when do we really have to <em>materialize</em> the full copy? - Only if <code>dest</code> or <code>src</code> are mutated afterward. If <code>dest</code> is a local variable that is simple to analyze. And if <code>src</code> is a location derived from a formal parameter, we also know it is not mutated! In other words, we do a compile-time copy-on-write analysis.</p> <p>This means that "borrowed" views can be written naturally and without explicit pointer indirections:</p> <pre class="listing" data-language="nim">proc main(tab: Table[string, string]) =
  let v = tab["key"] # inferred as .cursor because 'tab' is not mutated.
  # no copy into 'v', no destruction of 'v'.
  use(v)
  useItAgain(v)</pre> <h2 id="hook-lifting">Hook lifting</h2>
<p>The hooks of a tuple type <code>(A, B, ...)</code> are generated by lifting the hooks of the involved types <code>A</code>, <code>B</code>, ... to the tuple type. In other words, a copy <code>x = y</code> is implemented as <code>x[0] = y[0]; x[1] = y[1]; ...</code>, likewise for <code>=sink</code> and <code>=destroy</code>.</p> <p>Other value-based compound types like <code>object</code> and <code>array</code> are handled correspondingly. For <code>object</code> however, the compiler-generated hooks can be overridden. This can also be important to use an alternative traversal of the involved data structure that is more efficient or in order to avoid deep recursions.</p> <h2 id="hook-generation">Hook generation</h2>
<p>The ability to override a hook leads to a phase ordering problem:</p> <pre class="listing" data-language="nim">type
  Foo[T] = object

proc main =
  var f: Foo[int]
  # error: destructor for 'f' called here before
  # it was seen in this module.

proc `=destroy`[T](f: var Foo[T]) =
  discard</pre>
<p>The solution is to define <code>proc `=destroy`[T](f: var Foo[T])</code> before it is used. The compiler generates implicit hooks for all types in <em>strategic places</em> so that an explicitly provided hook that comes too "late" can be detected reliably. These <em>strategic places</em> have been derived from the rewrite rules and are as follows:</p> <ul class="simple">
<li>In the construct <code>let/var x = ...</code> (var/let binding) hooks are generated for <code>typeof(x)</code>.</li> <li>In <code>x = ...</code> (assignment) hooks are generated for <code>typeof(x)</code>.</li> <li>In <code>f(...)</code> (function call) hooks are generated for <code>typeof(f(...))</code>.</li> <li>For every sink parameter <code>x: sink T</code> the hooks are generated for <code>typeof(x)</code>.</li> </ul> <h2 id="nodestroy-pragma">nodestroy pragma</h2>
<p>The experimental <span id="nodestroy_1">nodestroy</span> pragma inhibits hook injections. This can be used to specialize the object traversal in order to avoid deep recursions:</p> <pre class="listing" data-language="nim">type Node = ref object
  x, y: int32
  left, right: Node

type Tree = object
  root: Node

proc `=destroy`(t: var Tree) {.nodestroy.} =
  # use an explicit stack so that we do not get stack overflows:
  var s: seq[Node] = @[t.root]
  while s.len &gt; 0:
    let x = s.pop
    if x.left != nil: s.add(x.left)
    if x.right != nil: s.add(x.right)
    # free the memory explicit:
    dispose(x)
  # notice how even the destructor for 's' is not called implicitly
  # anymore thanks to .nodestroy, so we have to call it on our own:
  `=destroy`(s)</pre>
<p>As can be seen from the example, this solution is hardly sufficient and should eventually be replaced by a better solution. </p><div class="_attribution">
  <p class="_attribution-p">
    &copy; 2006&ndash;2021 Andreas Rumpf<br>Licensed under the MIT License.<br>
    <a href="https://nim-lang.org/docs/destructors.html" class="_attribution-link">https://nim-lang.org/docs/destructors.html</a>
  </p>
</div>
