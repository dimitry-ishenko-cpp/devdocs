<h1>Enabling TurboModule on iOS</h1>
caution<p>This documentation is still <strong>experimental</strong> and details are subject to changes as we iterate. Feel free to share your feedback on the <a href="https://github.com/reactwg/react-native-new-architecture/discussions/8" rel="noopener noreferrer">discussion inside the working group</a> for this page.</p>
<p>Moreover, it contains several <strong>manual steps</strong>. Please note that this won't be representative of the final developer experience once the New Architecture is stable. We're working on tools, templates and libraries to help you get started fast on the New Architecture, without having to go through the whole setup.</p>
<p>Make sure your application meets all the <a href="new-architecture-app-intro.html">prerequisites</a>.</p>
<h2 id="1-provide-a-turbomodulemanager-delegate">1. Provide a TurboModuleManager Delegate</h2>
<p>Add the following imports at the top of your bridge delegate (e.g. <code>AppDelegate.mm</code>):</p>
<pre tabindex="0" data-language="jsx">#import &lt;ReactCommon/RCTTurboModuleManager.h&gt;
#import &lt;React/CoreModulesPlugins.h&gt;</pre>

<p>You will also need to declare that your AppDelegate conforms to the <code>RCTTurboModuleManagerDelegate</code> protocol, as well as create an instance variable for our Turbo Module manager:</p>
<pre tabindex="0" data-language="jsx">@interface AppDelegate () &lt;RCTCxxBridgeDelegate, RCTTurboModuleManagerDelegate&gt; {
  // ...
  RCTTurboModuleManager *_turboModuleManager;
}
@end</pre>

<p>To conform to the <code>RCTTurboModuleManagerDelegate</code> protocol, you will implement these three methods:</p>
<ul>
<li>
<code>getModuleClassFromName:</code> - This method should return the Class for a native module. You may use the <code>RCTCoreModulesClassProvider()</code> method to handle the default, core modules.</li>
<li>
<code>getTurboModule:jsInvoker:</code> - This should return <code>nullptr</code>. This method may be used later to support C++ TurboModules.</li>
<li>
<code>getModuleInstanceFromClass:moduleClass:</code> - This method allows you to perform any side-effects when your TurboModules are initialized. This is the TurboModule analogue to your bridge delegate’s <code>extraModulesForBridge</code> method. At this time, you’ll need to initialize the default RCTNetworking and RCTImageLoader modules as indicated below.</li>
</ul>
<h4 id="turbomodulemanagerdelegate-example">TurboModuleManagerDelegate Example</h4>
<p>Take note of <code>getModuleInstanceFromClass:</code> in the following example, as it includes some necessary instantiation of several core modules that you will need to include in your application. Eventually, this may not be required.</p>AppDelegate.mm<pre tabindex="0" data-language="jsx">// ...

#import &lt;React/RCTDataRequestHandler.h&gt;
#import &lt;React/RCTHTTPRequestHandler.h&gt;
#import &lt;React/RCTFileRequestHandler.h&gt;
#import &lt;React/RCTNetworking.h&gt;
#import &lt;React/RCTImageLoader.h&gt;
#import &lt;React/RCTGIFImageDecoder.h&gt;
#import &lt;React/RCTLocalAssetImageLoader.h&gt;

#import &lt;React/CoreModulesPlugins.h&gt;

#import &lt;ReactCommon/RCTTurboModuleManager.h&gt;

// ...

#pragma mark RCTTurboModuleManagerDelegate

- (Class)getModuleClassFromName:(const char *)name
{
  return RCTCoreModulesClassProvider(name);
}

- (std::shared_ptr&lt;facebook::react::TurboModule&gt;)
    getTurboModule:(const std::string &amp;)name
         jsInvoker:(std::shared_ptr&lt;facebook::react::CallInvoker&gt;)jsInvoker {
  return nullptr;
}

- (id&lt;RCTTurboModule&gt;)getModuleInstanceFromClass:(Class)moduleClass
{
  // Set up the default RCTImageLoader and RCTNetworking modules.
  if (moduleClass == RCTImageLoader.class) {
    return [[moduleClass alloc] initWithRedirectDelegate:nil
        loadersProvider:^NSArray&lt;id&lt;RCTImageURLLoader&gt;&gt; *(RCTModuleRegistry * moduleRegistry) {
          return @ [[RCTLocalAssetImageLoader new]];
        }
        decodersProvider:^NSArray&lt;id&lt;RCTImageDataDecoder&gt;&gt; *(RCTModuleRegistry * moduleRegistry) {
          return @ [[RCTGIFImageDecoder new]];
        }];
  } else if (moduleClass == RCTNetworking.class) {
     return [[moduleClass alloc]
        initWithHandlersProvider:^NSArray&lt;id&lt;RCTURLRequestHandler&gt;&gt; *(
            RCTModuleRegistry *moduleRegistry) {
          return @[
            [RCTHTTPRequestHandler new],
            [RCTDataRequestHandler new],
            [RCTFileRequestHandler new],
          ];
        }];
  }
  // No custom initializer here.
  return [moduleClass new];
}</pre>

<h2 id="2-install-turbomodulemanager-javascript-bindings">2. Install TurboModuleManager JavaScript Bindings</h2>
<p>Next, you will create a <code>RCTTurboModuleManager</code> in your bridge delegate’s <code>jsExecutorFactoryForBridge:</code> method, and install the JavaScript bindings:</p>
<pre tabindex="0" data-language="jsx">#pragma mark - RCTCxxBridgeDelegate

- (std::unique_ptr&lt;facebook::react::JSExecutorFactory&gt;)jsExecutorFactoryForBridge:(RCTBridge *)bridge
{
  // Add these lines to create a TurboModuleManager
  if (RCTTurboModuleEnabled()) {
    _turboModuleManager =
        [[RCTTurboModuleManager alloc] initWithBridge:bridge
                                             delegate:self
                                            jsInvoker:bridge.jsCallInvoker];

    // Necessary to allow NativeModules to lookup TurboModules
    [bridge setRCTTurboModuleRegistry:_turboModuleManager];

    if (!RCTTurboModuleEagerInitEnabled()) {
      /**
       * Instantiating DevMenu has the side-effect of registering
       * shortcuts for CMD + d, CMD + i,  and CMD + n via RCTDevMenu.
       * Therefore, when TurboModules are enabled, we must manually create this
       * NativeModule.
       */
       [_turboModuleManager moduleForName:"DevMenu"];
    }
  }

  // Add this line...
  __weak __typeof(self) weakSelf = self;

  // If you want to use the `JSCExecutorFactory`, remember to add the `#import &lt;React/JSCExecutorFactory.h&gt;`
  // import statement on top.
  return std::make_unique&lt;facebook::react::HermesExecutorFactory&gt;(
    facebook::react::RCTJSIExecutorRuntimeInstaller([weakSelf, bridge](facebook::jsi::Runtime &amp;runtime) {
      if (!bridge) {
        return;
      }

      // And add these lines to install the bindings...
      __typeof(self) strongSelf = weakSelf;
      if (strongSelf) {
        facebook::react::RuntimeExecutor syncRuntimeExecutor =
            [&amp;](std::function&lt;void(facebook::jsi::Runtime &amp; runtime_)&gt; &amp;&amp;callback) { callback(runtime); };
        [strongSelf-&gt;_turboModuleManager installJSBindingWithRuntimeExecutor:syncRuntimeExecutor];
      }
    }));
}</pre>

<h2 id="3-enable-turbomodule-system">3. Enable TurboModule System</h2>
<p>Finally, enable TurboModules in your app by executing the following statement before React Native is initialized in your app delegate (e.g. within <code>didFinishLaunchingWithOptions:</code>):</p>
<pre tabindex="0" data-language="jsx">RCTEnableTurboModule(YES);</pre>

<h4 id="example">Example</h4>
<pre tabindex="0" data-language="jsx">@implementation AppDelegate
- (BOOL)application:(UIApplication *)application didFinishLaunchingWithOptions:(NSDictionary *)launchOptions
{
  RCTEnableTurboModule(YES);

  RCTBridge *bridge = [[RCTBridge alloc] initWithDelegate:self
                                            launchOptions:launchOptions];

  // ...

  return YES;
}</pre>
<div class="_attribution">
  <p class="_attribution-p">
    &copy; 2022 Facebook Inc.<br>Licensed under the Creative Commons Attribution 4.0 International Public License.<br>
    <a href="https://reactnative.dev/docs/new-architecture-app-modules-ios" class="_attribution-link">https://reactnative.dev/docs/new-architecture-app-modules-ios</a>
  </p>
</div>
