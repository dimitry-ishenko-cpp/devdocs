<div class="innertube"> <h1 id="Concurrent Programming">3 Concurrent Programming</h1>  <h2 id="processes" class="title-link"> <div class="title-name">3.1 Processes</div>  </h2> <p>One of the main reasons for using Erlang instead of other functional languages is Erlang's ability to handle concurrency and distributed programming. By concurrency is meant programs that can handle several threads of execution at the same time. For example, modern operating systems allow you to use a word processor, a spreadsheet, a mail client, and a print job all running at the same time. Each processor (CPU) in the system is probably only handling one thread (or job) at a time, but it swaps between the jobs at such a rate that it gives the illusion of running them all at the same time. It is easy to create parallel threads of execution in an Erlang program and to allow these threads to communicate with each other. In Erlang, each thread of execution is called a <strong>process</strong>.</p> <p>(Aside: the term "process" is usually used when the threads of execution share no data with each other and the term "thread" when they share data in some way. Threads of execution in Erlang share no data, that is why they are called processes).</p> <p>The Erlang BIF <code>spawn</code> is used to create a new process: <code>spawn(Module, Exported_Function, List of Arguments)</code>. Consider the following module:</p> <pre data-language="erlang">-module(tut14).

-export([start/0, say_something/2]).

say_something(What, 0) -&gt;
    done;
say_something(What, Times) -&gt;
    io:format("~p~n", [What]),
    say_something(What, Times - 1).

start() -&gt;
    spawn(tut14, say_something, [hello, 3]),
    spawn(tut14, say_something, [goodbye, 3]).</pre> <pre data-language="erlang">
5&gt; <code>c(tut14).</code>
{ok,tut14}
6&gt; <code>tut14:say_something(hello, 3).</code>
hello
hello
hello
done</pre> <p>As shown, the function <code>say_something</code> writes its first argument the number of times specified by second argument. The function <code>start</code> starts two Erlang processes, one that writes "hello" three times and one that writes "goodbye" three times. Both processes use the function <code>say_something</code>. Notice that a function used in this way by <code>spawn</code>, to start a process, must be exported from the module (that is, in the <code>-export</code> at the start of the module).</p> <pre data-language="erlang">
9&gt; <code>tut14:start().</code>
hello
goodbye
&lt;0.63.0&gt;
hello
goodbye
hello
goodbye</pre> <p>Notice that it did not write "hello" three times and then "goodbye" three times. Instead, the first process wrote a "hello", the second a "goodbye", the first another "hello" and so forth. But where did the &lt;0.63.0&gt; come from? The return value of a function is the return value of the last "thing" in the function. The last thing in the function <code>start</code> is</p> <pre data-language="erlang">spawn(tut14, say_something, [goodbye, 3]).</pre> <p><code>spawn</code> returns a <strong>process identifier</strong>, or <strong>pid</strong>, which uniquely identifies the process. So &lt;0.63.0&gt; is the pid of the <code>spawn</code> function call above. The next example shows how to use pids.</p> <p>Notice also that ~p is used instead of ~w in <code>io:format</code>. To quote the manual: "~p Writes the data with standard syntax in the same way as ~w, but breaks terms whose printed representation is longer than one line into many lines and indents each line sensibly. It also tries to detect lists of printable characters and to output these as strings".</p> <h2 id="message-passing" class="title-link"> <div class="title-name">3.2 Message Passing</div>  </h2> <p>In the following example two processes are created and they send messages to each other a number of times.</p> <pre data-language="erlang">-module(tut15).

-export([start/0, ping/2, pong/0]).

ping(0, Pong_PID) -&gt;
    Pong_PID ! finished,
    io:format("ping finished~n", []);

ping(N, Pong_PID) -&gt;
    Pong_PID ! {ping, self()},
    receive
        pong -&gt;
            io:format("Ping received pong~n", [])
    end,
    ping(N - 1, Pong_PID).

pong() -&gt;
    receive
        finished -&gt;
            io:format("Pong finished~n", []);
        {ping, Ping_PID} -&gt;
            io:format("Pong received ping~n", []),
            Ping_PID ! pong,
            pong()
    end.

start() -&gt;
    Pong_PID = spawn(tut15, pong, []),
    spawn(tut15, ping, [3, Pong_PID]).</pre> <pre data-language="erlang">
1&gt; <code>c(tut15).</code>
{ok,tut15}
2&gt; <code>tut15: start().</code>
&lt;0.36.0&gt;
Pong received ping
Ping received pong
Pong received ping
Ping received pong
Pong received ping
Ping received pong
ping finished
Pong finished</pre> <p>The function <code>start</code> first creates a process, let us call it "pong":</p> <pre data-language="erlang">Pong_PID = spawn(tut15, pong, [])</pre> <p>This process executes <code>tut15:pong()</code>. <code>Pong_PID</code> is the process identity of the "pong" process. The function <code>start</code> now creates another process "ping":</p> <pre data-language="erlang">spawn(tut15, ping, [3, Pong_PID]),</pre> <p>This process executes:</p> <pre data-language="erlang">tut15:ping(3, Pong_PID)</pre> <p>&lt;0.36.0&gt; is the return value from the <code>start</code> function.</p> <p>The process "pong" now does:</p> <pre data-language="erlang">receive
    finished -&gt;
        io:format("Pong finished~n", []);
    {ping, Ping_PID} -&gt;
        io:format("Pong received ping~n", []),
        Ping_PID ! pong,
        pong()
end.</pre> <p>The <code>receive</code> construct is used to allow processes to wait for messages from other processes. It has the following format:</p> <pre data-language="erlang">receive
   pattern1 -&gt;
       actions1;
   pattern2 -&gt;
       actions2;
   ....
   patternN
       actionsN
end.</pre> <p>Notice there is no ";" before the <code>end</code>.</p> <p>Messages between Erlang processes are simply valid Erlang terms. That is, they can be lists, tuples, integers, atoms, pids, and so on.</p> <p>Each process has its own input queue for messages it receives. New messages received are put at the end of the queue. When a process executes a <code>receive</code>, the first message in the queue is matched against the first pattern in the <code>receive</code>. If this matches, the message is removed from the queue and the actions corresponding to the pattern are executed.</p> <p>However, if the first pattern does not match, the second pattern is tested. If this matches, the message is removed from the queue and the actions corresponding to the second pattern are executed. If the second pattern does not match, the third is tried and so on until there are no more patterns to test. If there are no more patterns to test, the first message is kept in the queue and the second message is tried instead. If this matches any pattern, the appropriate actions are executed and the second message is removed from the queue (keeping the first message and any other messages in the queue). If the second message does not match, the third message is tried, and so on, until the end of the queue is reached. If the end of the queue is reached, the process blocks (stops execution) and waits until a new message is received and this procedure is repeated.</p> <p>The Erlang implementation is "clever" and minimizes the number of times each message is tested against the patterns in each <code>receive</code>.</p> <p>Now back to the ping pong example.</p> <p>"Pong" is waiting for messages. If the atom <code>finished</code> is received, "pong" writes "Pong finished" to the output and, as it has nothing more to do, terminates. If it receives a message with the format:</p> <pre data-language="erlang">{ping, Ping_PID}</pre> <p>it writes "Pong received ping" to the output and sends the atom <code>pong</code> to the process "ping":</p> <pre data-language="erlang">Ping_PID ! pong</pre> <p>Notice how the operator "!" is used to send messages. The syntax of "!" is:</p> <pre data-language="erlang">Pid ! Message</pre> <p>That is, <code>Message</code> (any Erlang term) is sent to the process with identity <code>Pid</code>.</p> <p>After sending the message <code>pong</code> to the process "ping", "pong" calls the <code>pong</code> function again, which causes it to get back to the <code>receive</code> again and wait for another message.</p> <p>Now let us look at the process "ping". Recall that it was started by executing:</p> <pre data-language="erlang">tut15:ping(3, Pong_PID)</pre> <p>Looking at the function <code>ping/2</code>, the second clause of <code>ping/2</code> is executed since the value of the first argument is 3 (not 0) (first clause head is <code>ping(0,Pong_PID)</code>, second clause head is <code>ping(N,Pong_PID)</code>, so <code>N</code> becomes 3).</p> <p>The second clause sends a message to "pong":</p> <pre data-language="erlang">Pong_PID ! {ping, self()},</pre> <p><code>self()</code> returns the pid of the process that executes <code>self()</code>, in this case the pid of "ping". (Recall the code for "pong", this lands up in the variable <code>Ping_PID</code> in the <code>receive</code> previously explained.)</p> <p>"Ping" now waits for a reply from "pong":</p> <pre data-language="erlang">receive
    pong -&gt;
        io:format("Ping received pong~n", [])
end,</pre> <p>It writes "Ping received pong" when this reply arrives, after which "ping" calls the <code>ping</code> function again.</p> <pre data-language="erlang">ping(N - 1, Pong_PID)</pre> <p><code>N-1</code> causes the first argument to be decremented until it becomes 0. When this occurs, the first clause of <code>ping/2</code> is executed:</p> <pre data-language="erlang">ping(0, Pong_PID) -&gt;
    Pong_PID !  finished,
    io:format("ping finished~n", []);</pre> <p>The atom <code>finished</code> is sent to "pong" (causing it to terminate as described above) and "ping finished" is written to the output. "Ping" then terminates as it has nothing left to do.</p> <h2 id="registered-process-names" class="title-link"> <div class="title-name">3.3 Registered Process Names</div>  </h2> <p>In the above example, "pong" was first created to be able to give the identity of "pong" when "ping" was started. That is, in some way "ping" must be able to know the identity of "pong" to be able to send a message to it. Sometimes processes which need to know each other's identities are started independently of each other. Erlang thus provides a mechanism for processes to be given names so that these names can be used as identities instead of pids. This is done by using the <code>register</code> BIF:</p> <pre data-language="erlang">register(some_atom, Pid)</pre> <p>Let us now rewrite the ping pong example using this and give the name <code>pong</code> to the "pong" process:</p> <pre data-language="erlang">-module(tut16).

-export([start/0, ping/1, pong/0]).

ping(0) -&gt;
    pong ! finished,
    io:format("ping finished~n", []);

ping(N) -&gt;
    pong ! {ping, self()},
    receive
        pong -&gt;
            io:format("Ping received pong~n", [])
    end,
    ping(N - 1).

pong() -&gt;
    receive
        finished -&gt;
            io:format("Pong finished~n", []);
        {ping, Ping_PID} -&gt;
            io:format("Pong received ping~n", []),
            Ping_PID ! pong,
            pong()
    end.

start() -&gt;
    register(pong, spawn(tut16, pong, [])),
    spawn(tut16, ping, [3]).</pre> <pre data-language="erlang">
2&gt; <code>c(tut16).</code>
{ok, tut16}
3&gt; <code>tut16:start().</code>
&lt;0.38.0&gt;
Pong received ping
Ping received pong
Pong received ping
Ping received pong
Pong received ping
Ping received pong
ping finished
Pong finished</pre> <p>Here the <code>start/0</code> function,</p> <pre data-language="erlang">register(pong, spawn(tut16, pong, [])),</pre> <p>both spawns the "pong" process and gives it the name <code>pong</code>. In the "ping" process, messages can be sent to <code>pong</code> by:</p> <pre data-language="erlang">pong ! {ping, self()},</pre> <p><code>ping/2</code> now becomes <code>ping/1</code> as the argument <code>Pong_PID</code> is not needed.</p> <h2 id="distributed-programming" class="title-link"> <div class="title-name">3.4 Distributed Programming</div>  </h2> <p>Let us rewrite the ping pong program with "ping" and "pong" on different computers. First a few things are needed to set up to get this to work. The distributed Erlang implementation provides a very basic authentication mechanism to prevent unintentional access to an Erlang system on another computer. Erlang systems which talk to each other must have the same <strong>magic cookie</strong>. The easiest way to achieve this is by having a file called <code>.erlang.cookie</code> in your home directory on all machines on which you are going to run Erlang systems communicating with each other: </p> <ul> <li>On Windows systems the home directory is the directory pointed out by the environment variable $HOME - you may need to set this.</li> <li> On Linux or UNIX you can safely ignore this and simply create a file called <code>.erlang.cookie</code> in the directory you get to after executing the command <code>cd</code> without any argument.</li> </ul> <p>The <code>.erlang.cookie</code> file is to contain a line with the same atom. For example, on Linux or UNIX, in the OS shell:</p> <pre data-language="erlang">
$ <code>cd</code>
$ <code>cat &gt; .erlang.cookie</code>
this_is_very_secret
$ <code>chmod 400 .erlang.cookie</code></pre> <p>The <code>chmod</code> above makes the <code>.erlang.cookie</code> file accessible only by the owner of the file. This is a requirement.</p> <p>When you start an Erlang system that is going to talk to other Erlang systems, you must give it a name, for example:</p> <pre data-language="erlang">
$ <code>erl -sname my_name</code></pre> <p>We will see more details of this later. If you want to experiment with distributed Erlang, but you only have one computer to work on, you can start two separate Erlang systems on the same computer but give them different names. Each Erlang system running on a computer is called an <strong>Erlang node</strong>.</p> <p>(Note: <code>erl -sname</code> assumes that all nodes are in the same IP domain and we can use only the first component of the IP address, if we want to use nodes in different domains we use <code>-name</code> instead, but then all IP address must be given in full.)</p> <p>Here is the ping pong example modified to run on two separate nodes:</p> <pre data-language="erlang">-module(tut17).

-export([start_ping/1, start_pong/0,  ping/2, pong/0]).

ping(0, Pong_Node) -&gt;
    {pong, Pong_Node} ! finished,
    io:format("ping finished~n", []);

ping(N, Pong_Node) -&gt;
    {pong, Pong_Node} ! {ping, self()},
    receive
        pong -&gt;
            io:format("Ping received pong~n", [])
    end,
    ping(N - 1, Pong_Node).

pong() -&gt;
    receive
        finished -&gt;
            io:format("Pong finished~n", []);
        {ping, Ping_PID} -&gt;
            io:format("Pong received ping~n", []),
            Ping_PID ! pong,
            pong()
    end.

start_pong() -&gt;
    register(pong, spawn(tut17, pong, [])).

start_ping(Pong_Node) -&gt;
    spawn(tut17, ping, [3, Pong_Node]).</pre> <p>Let us assume there are two computers called gollum and kosken. First a node is started on kosken, called ping, and then a node on gollum, called pong.</p> <p>On kosken (on a Linux/UNIX system):</p> <pre data-language="erlang">
kosken&gt; <code>erl -sname ping</code>
Erlang (BEAM) emulator version 5.2.3.7 [hipe] [threads:0]

Eshell V5.2.3.7  (abort with ^G)
(ping@kosken)1&gt;</pre> <p>On gollum:</p> <pre data-language="erlang">
gollum&gt; <code>erl -sname pong</code>
Erlang (BEAM) emulator version 5.2.3.7 [hipe] [threads:0]

Eshell V5.2.3.7  (abort with ^G)
(pong@gollum)1&gt;</pre> <p>Now the "pong" process on gollum is started:</p> <pre data-language="erlang">
(pong@gollum)1&gt; <code>tut17:start_pong().</code>
true</pre> <p>And the "ping" process on kosken is started (from the code above you can see that a parameter of the <code>start_ping</code> function is the node name of the Erlang system where "pong" is running):</p> <pre data-language="erlang">
(ping@kosken)1&gt; <code>tut17:start_ping(pong@gollum).</code>
&lt;0.37.0&gt;
Ping received pong
Ping received pong 
Ping received pong
ping finished</pre> <p>As shown, the ping pong program has run. On the "pong" side:</p> <pre data-language="erlang">
(pong@gollum)2&gt;
Pong received ping                 
Pong received ping                 
Pong received ping                 
Pong finished                      
(pong@gollum)2&gt;</pre> <p>Looking at the <code>tut17</code> code, you see that the <code>pong</code> function itself is unchanged, the following lines work in the same way irrespective of on which node the "ping" process is executes:</p> <pre data-language="erlang">{ping, Ping_PID} -&gt;
    io:format("Pong received ping~n", []),
    Ping_PID ! pong,</pre> <p>Thus, Erlang pids contain information about where the process executes. So if you know the pid of a process, the "!" operator can be used to send it a message disregarding if the process is on the same node or on a different node.</p> <p>A difference is how messages are sent to a registered process on another node:</p> <pre data-language="erlang">{pong, Pong_Node} ! {ping, self()},</pre> <p>A tuple <code>{registered_name,node_name}</code> is used instead of just the <code>registered_name</code>.</p> <p>In the previous example, "ping" and "pong" were started from the shells of two separate Erlang nodes. <code>spawn</code> can also be used to start processes in other nodes.</p> <p>The next example is the ping pong program, yet again, but this time "ping" is started in another node:</p> <pre data-language="erlang">-module(tut18).

-export([start/1,  ping/2, pong/0]).

ping(0, Pong_Node) -&gt;
    {pong, Pong_Node} ! finished,
    io:format("ping finished~n", []);

ping(N, Pong_Node) -&gt;
    {pong, Pong_Node} ! {ping, self()},
    receive
        pong -&gt;
            io:format("Ping received pong~n", [])
    end,
    ping(N - 1, Pong_Node).

pong() -&gt;
    receive
        finished -&gt;
            io:format("Pong finished~n", []);
        {ping, Ping_PID} -&gt;
            io:format("Pong received ping~n", []),
            Ping_PID ! pong,
            pong()
    end.

start(Ping_Node) -&gt;
    register(pong, spawn(tut18, pong, [])),
    spawn(Ping_Node, tut18, ping, [3, node()]).</pre> <p>Assuming an Erlang system called ping (but not the "ping" process) has already been started on kosken, then on gollum this is done:</p> <pre data-language="erlang">
(pong@gollum)1&gt; <code>tut18:start(ping@kosken).</code>
&lt;3934.39.0&gt;
Pong received ping
Ping received pong
Pong received ping
Ping received pong
Pong received ping
Ping received pong
Pong finished
ping finished</pre> <p>Notice that all the output is received on gollum. This is because the I/O system finds out where the process is spawned from and sends all output there.</p> <h2 id="a-larger-example" class="title-link"> <div class="title-name">3.5 A Larger Example</div>  </h2> <p>Now for a larger example with a simple "messenger". The messenger is a program that allows users to log in on different nodes and send simple messages to each other.</p> <p>Before starting, notice the following:</p> <ul> <li> <p>This example only shows the message passing logic - no attempt has been made to provide a nice graphical user interface, although this can also be done in Erlang.</p> </li> <li> <p>This sort of problem can be solved easier by use of the facilities in OTP, which also provide methods for updating code on the fly and so on (see <code> OTP Design Principles</code>).</p> </li> <li> <p>The first program contains some inadequacies regarding handling of nodes which disappear. These are corrected in a later version of the program.</p> </li> </ul> <p>The messenger is set up by allowing "clients" to connect to a central server and say who and where they are. That is, a user does not need to know the name of the Erlang node where another user is located to send a message.</p> <p>File <code>messenger.erl</code>:</p>  <pre id="ex" data-language="erlang">%%% Message passing utility.  
%%% User interface:
%%% logon(Name)
%%%     One user at a time can log in from each Erlang node in the
%%%     system messenger: and choose a suitable Name. If the Name
%%%     is already logged in at another node or if someone else is
%%%     already logged in at the same node, login will be rejected
%%%     with a suitable error message.
%%% logoff()
%%%     Logs off anybody at that node
%%% message(ToName, Message)
%%%     sends Message to ToName. Error messages if the user of this 
%%%     function is not logged on or if ToName is not logged on at
%%%     any node.
%%%
%%% One node in the network of Erlang nodes runs a server which maintains
%%% data about the logged on users. The server is registered as "messenger"
%%% Each node where there is a user logged on runs a client process registered
%%% as "mess_client" 
%%%
%%% Protocol between the client processes and the server
%%% ----------------------------------------------------
%%% 
%%% To server: {ClientPid, logon, UserName}
%%% Reply {messenger, stop, user_exists_at_other_node} stops the client
%%% Reply {messenger, logged_on} logon was successful
%%%
%%% To server: {ClientPid, logoff}
%%% Reply: {messenger, logged_off}
%%%
%%% To server: {ClientPid, logoff}
%%% Reply: no reply
%%%
%%% To server: {ClientPid, message_to, ToName, Message} send a message
%%% Reply: {messenger, stop, you_are_not_logged_on} stops the client
%%% Reply: {messenger, receiver_not_found} no user with this name logged on
%%% Reply: {messenger, sent} Message has been sent (but no guarantee)
%%%
%%% To client: {message_from, Name, Message},
%%%
%%% Protocol between the "commands" and the client
%%% ----------------------------------------------
%%%
%%% Started: messenger:client(Server_Node, Name)
%%% To client: logoff
%%% To client: {message_to, ToName, Message}
%%%
%%% Configuration: change the server_node() function to return the
%%% name of the node where the messenger server runs

-module(messenger).
-export([start_server/0, server/1, logon/1, logoff/0, message/2, client/2]).

%%% Change the function below to return the name of the node where the
%%% messenger server runs
server_node() -&gt;
    messenger@super.

%%% This is the server process for the "messenger"
%%% the user list has the format [{ClientPid1, Name1},{ClientPid22, Name2},...]
server(User_List) -&gt;
    receive
        {From, logon, Name} -&gt;
            New_User_List = server_logon(From, Name, User_List),
            server(New_User_List);
        {From, logoff} -&gt;
            New_User_List = server_logoff(From, User_List),
            server(New_User_List);
        {From, message_to, To, Message} -&gt;
            server_transfer(From, To, Message, User_List),
            io:format("list is now: ~p~n", [User_List]),
            server(User_List)
    end.

%%% Start the server
start_server() -&gt;
    register(messenger, spawn(messenger, server, [[]])).


%%% Server adds a new user to the user list
server_logon(From, Name, User_List) -&gt;
    %% check if logged on anywhere else
    case lists:keymember(Name, 2, User_List) of
        true -&gt;
            From ! {messenger, stop, user_exists_at_other_node},  %reject logon
            User_List;
        false -&gt;
            From ! {messenger, logged_on},
            [{From, Name} | User_List]        %add user to the list
    end.

%%% Server deletes a user from the user list
server_logoff(From, User_List) -&gt;
    lists:keydelete(From, 1, User_List).


%%% Server transfers a message between user
server_transfer(From, To, Message, User_List) -&gt;
    %% check that the user is logged on and who he is
    case lists:keysearch(From, 1, User_List) of
        false -&gt;
            From ! {messenger, stop, you_are_not_logged_on};
        {value, {From, Name}} -&gt;
            server_transfer(From, Name, To, Message, User_List)
    end.
%%% If the user exists, send the message
server_transfer(From, Name, To, Message, User_List) -&gt;
    %% Find the receiver and send the message
    case lists:keysearch(To, 2, User_List) of
        false -&gt;
            From ! {messenger, receiver_not_found};
        {value, {ToPid, To}} -&gt;
            ToPid ! {message_from, Name, Message}, 
            From ! {messenger, sent} 
    end.


%%% User Commands
logon(Name) -&gt;
    case whereis(mess_client) of 
        undefined -&gt;
            register(mess_client, 
                     spawn(messenger, client, [server_node(), Name]));
        _ -&gt; already_logged_on
    end.

logoff() -&gt;
    mess_client ! logoff.

message(ToName, Message) -&gt;
    case whereis(mess_client) of % Test if the client is running
        undefined -&gt;
            not_logged_on;
        _ -&gt; mess_client ! {message_to, ToName, Message},
             ok
end.


%%% The client process which runs on each server node
client(Server_Node, Name) -&gt;
    {messenger, Server_Node} ! {self(), logon, Name},
    await_result(),
    client(Server_Node).

client(Server_Node) -&gt;
    receive
        logoff -&gt;
            {messenger, Server_Node} ! {self(), logoff},
            exit(normal);
        {message_to, ToName, Message} -&gt;
            {messenger, Server_Node} ! {self(), message_to, ToName, Message},
            await_result();
        {message_from, FromName, Message} -&gt;
            io:format("Message from ~p: ~p~n", [FromName, Message])
    end,
    client(Server_Node).

%%% wait for a response from the server
await_result() -&gt;
    receive
        {messenger, stop, Why} -&gt; % Stop the client 
            io:format("~p~n", [Why]),
            exit(normal);
        {messenger, What} -&gt;  % Normal response
            io:format("~p~n", [What])
    end.</pre> <p>To use this program, you need to:</p> <ul> <li>Configure the <code>server_node()</code> function.</li> <li>Copy the compiled code (<code>messenger.beam</code>) to the directory on each computer where you start Erlang.</li> </ul> <p>In the following example using this program, nodes are started on four different computers. If you do not have that many machines available on your network, you can start several nodes on the same machine.</p> <p>Four Erlang nodes are started up: messenger@super, c1@bilbo, c2@kosken, c3@gollum.</p> <p>First the server at messenger@super is started up:</p> <pre data-language="erlang">
(messenger@super)1&gt; <code>messenger:start_server().</code>
true</pre> <p>Now Peter logs on at c1@bilbo:</p> <pre data-language="erlang">
(c1@bilbo)1&gt; <code>messenger:logon(peter).</code>
true
logged_on</pre> <p>James logs on at c2@kosken:</p> <pre data-language="erlang">
(c2@kosken)1&gt; <code>messenger:logon(james).</code>
true
logged_on</pre> <p>And Fred logs on at c3@gollum:</p> <pre data-language="erlang">
(c3@gollum)1&gt; <code>messenger:logon(fred).</code>
true
logged_on</pre> <p>Now Peter sends Fred a message:</p> <pre data-language="erlang">
(c1@bilbo)2&gt; <code>messenger:message(fred, "hello").</code>
ok
sent</pre> <p>Fred receives the message and sends a message to Peter and logs off:</p> <pre data-language="erlang">
Message from peter: "hello"
(c3@gollum)2&gt; <code>messenger:message(peter, "go away, I'm busy").</code>
ok
sent
(c3@gollum)3&gt; <code>messenger:logoff().</code>
logoff</pre> <p>James now tries to send a message to Fred:</p> <pre data-language="erlang">
(c2@kosken)2&gt; <code>messenger:message(fred, "peter doesn't like you").</code>
ok
receiver_not_found</pre> <p>But this fails as Fred has already logged off.</p> <p>First let us look at some of the new concepts that have been introduced.</p> <p>There are two versions of the <code>server_transfer</code> function: one with four arguments (<code>server_transfer/4</code>) and one with five (<code>server_transfer/5</code>). These are regarded by Erlang as two separate functions.</p> <p>Notice how to write the <code>server</code> function so that it calls itself, through <code>server(User_List)</code>, and thus creates a loop. The Erlang compiler is "clever" and optimizes the code so that this really is a sort of loop and not a proper function call. But this only works if there is no code after the call. Otherwise, the compiler expects the call to return and make a proper function call. This would result in the process getting bigger and bigger for every loop.</p> <p>Functions in the <code>lists</code> module are used. This is a very useful module and a study of the manual page is recommended (<code>erl -man lists</code>). <code>lists:keymember(Key,Position,Lists)</code> looks through a list of tuples and looks at <code>Position</code> in each tuple to see if it is the same as <code>Key</code>. The first element is position 1. If it finds a tuple where the element at <code>Position</code> is the same as <code>Key</code>, it returns <code>true</code>, otherwise <code>false</code>.</p> <pre data-language="erlang">
3&gt; <code>lists:keymember(a, 2, [{x,y,z},{b,b,b},{b,a,c},{q,r,s}]).</code>
true
4&gt; <code>lists:keymember(p, 2, [{x,y,z},{b,b,b},{b,a,c},{q,r,s}]).</code>
false</pre> <p><code>lists:keydelete</code> works in the same way but deletes the first tuple found (if any) and returns the remaining list:</p> <pre data-language="erlang">
5&gt; <code>lists:keydelete(a, 2, [{x,y,z},{b,b,b},{b,a,c},{q,r,s}]).</code>
[{x,y,z},{b,b,b},{q,r,s}]</pre> <p><code>lists:keysearch</code> is like <code>lists:keymember</code>, but it returns <code>{value,Tuple_Found}</code> or the atom <code>false</code>.</p> <p>There are many very useful functions in the <code>lists</code> module.</p> <p>An Erlang process (conceptually) runs until it does a <code>receive</code> and there is no message which it wants to receive in the message queue. "conceptually" is used here because the Erlang system shares the CPU time between the active processes in the system.</p> <p>A process terminates when there is nothing more for it to do, that is, the last function it calls simply returns and does not call another function. Another way for a process to terminate is for it to call <code>exit/1</code>. The argument to <code>exit/1</code> has a special meaning, which is discussed later. In this example, <code>exit(normal)</code> is done, which has the same effect as a process running out of functions to call.</p> <p>The BIF <code>whereis(RegisteredName)</code> checks if a registered process of name <code>RegisteredName</code> exists. If it exists, the pid of that process is returned. If it does not exist, the atom <code>undefined</code> is returned.</p> <p>You should by now be able to understand most of the code in the messenger-module. Let us study one case in detail: a message is sent from one user to another.</p> <p>The first user "sends" the message in the example above by:</p> <pre data-language="erlang">messenger:message(fred, "hello")</pre> <p>After testing that the client process exists:</p> <pre data-language="erlang">whereis(mess_client) </pre> <p>And a message is sent to <code>mess_client</code>:</p> <pre data-language="erlang">mess_client ! {message_to, fred, "hello"}</pre> <p>The client sends the message to the server by:</p> <pre data-language="erlang">{messenger, messenger@super} ! {self(), message_to, fred, "hello"},</pre> <p>And waits for a reply from the server.</p> <p>The server receives this message and calls:</p> <pre data-language="erlang">server_transfer(From, fred, "hello", User_List),</pre> <p>This checks that the pid <code>From</code> is in the <code>User_List</code>:</p> <pre data-language="erlang">lists:keysearch(From, 1, User_List) </pre> <p>If <code>keysearch</code> returns the atom <code>false</code>, some error has occurred and the server sends back the message:</p> <pre data-language="erlang">From ! {messenger, stop, you_are_not_logged_on}</pre> <p>This is received by the client, which in turn does <code>exit(normal)</code> and terminates. If <code>keysearch</code> returns <code>{value,{From,Name}}</code> it is certain that the user is logged on and that his name (peter) is in variable <code>Name</code>.</p> <p>Let us now call:</p> <pre data-language="erlang">server_transfer(From, peter, fred, "hello", User_List)</pre> <p>Notice that as this is <code>server_transfer/5</code>, it is not the same as the previous function <code>server_transfer/4</code>. Another <code>keysearch</code> is done on <code>User_List</code> to find the pid of the client corresponding to fred:</p> <pre data-language="erlang">lists:keysearch(fred, 2, User_List)</pre> <p>This time argument 2 is used, which is the second element in the tuple. If this returns the atom <code>false</code>, fred is not logged on and the following message is sent:</p> <pre data-language="erlang">From ! {messenger, receiver_not_found};</pre> <p>This is received by the client.</p> <p> If <code>keysearch</code> returns:</p> <pre data-language="erlang">{value, {ToPid, fred}}</pre> <p>The following message is sent to fred's client:</p> <pre data-language="erlang">ToPid ! {message_from, peter, "hello"}, </pre> <p>The following message is sent to peter's client:</p> <pre data-language="erlang">From ! {messenger, sent} </pre> <p>Fred's client receives the message and prints it:</p> <pre data-language="erlang">{message_from, peter, "hello"} -&gt;
    io:format("Message from ~p: ~p~n", [peter, "hello"])</pre> <p>Peter's client receives the message in the <code>await_result</code> function.</p> </div><div class="_attribution">
  <p class="_attribution-p">
    &copy; 2010&ndash;2023 Ericsson AB<br>Licensed under the Apache License, Version 2.0.<br>
    
  </p>
</div>
