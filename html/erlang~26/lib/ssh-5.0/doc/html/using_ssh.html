<div class="innertube"> <h1 id="Getting Started">2 Getting Started</h1> <h2 id="general-information" class="title-link"> <div class="title-name">2.1 General Information</div>  </h2> <p>The following examples use the utility function <code><a href="ssh.html#start-0"> ssh:start/0</a></code> to start all needed applications (<code>crypto</code>, <code>public_key</code>, and <code>ssh</code>). All examples are run in an Erlang shell, or in a bash shell, using <strong>openssh</strong> to illustrate how the <code>ssh</code> application can be used. The examples are run as the user <code>otptest</code> on a local network where the user is authorized to log in over <code>ssh</code> to the host <strong>tarlop</strong>. </p> <p>If nothing else is stated, it is presumed that the <code>otptest</code> user has an entry in the <strong>authorized_keys</strong> file of <strong>tarlop</strong> (allowed to log in over <code>ssh</code> without entering a password). Also, <strong>tarlop</strong> is a known host in the <code>known_hosts</code> file of the user <code>otptest</code>. This means that host-verification can be done without user-interaction. </p> <h2 id="using-the-erlang-ssh-terminal-client" class="title-link"> <div class="title-name">2.2 Using the Erlang ssh Terminal Client</div>  </h2> <p>The user <code>otptest</code>, which has bash as default shell, uses the <code>ssh:shell/1</code> client to connect to the <strong>openssh</strong> daemon running on a host called <strong>tarlop</strong>:</p> <pre data-language="erlang">
1&gt; <code>ssh:start().</code>
ok
2&gt; <code>{ok, S} = ssh:shell("tarlop").</code>
otptest@tarlop:&gt; <code>pwd</code>
/home/otptest
otptest@tarlop:&gt; <code>exit</code>
logout
3&gt; </pre> <h2 id="running-an-erlang-ssh-daemon" class="title-link"> <div class="title-name">2.3 Running an Erlang ssh Daemon</div>  </h2> <p>The <code><a href="ssh_file.html#type-system_dir_daemon_option">system_dir</a></code> option must be a directory containing a host key file and it defaults to <code>/etc/ssh</code>. For details, see Section Configuration Files in <code><span>ssh(6)</span></code>. </p> <div class="note"> <div class="label">Note</div> <div class="content">

<p>Normally, the <code>/etc/ssh</code> directory is only readable by root.</p> </div> </div> <p>The option <code><a href="ssh_file.html#type-user_dir_common_option">user_dir</a></code> defaults to directory <code>users ~/.ssh</code>.</p> <p><strong>Step 1.</strong> To run the example without root privileges, generate new keys and host keys:</p> <pre data-language="erlang">
$bash&gt; <code>ssh-keygen -t rsa -f /tmp/ssh_daemon/ssh_host_rsa_key</code>
[...]
$bash&gt; <code>ssh-keygen -t rsa -f /tmp/otptest_user/.ssh/id_rsa</code>
[...]  </pre> <p><strong>Step 2.</strong> Create the file <code>/tmp/otptest_user/.ssh/authorized_keys</code> and add the content of <code>/tmp/otptest_user/.ssh/id_rsa.pub</code>.</p>  <p id="start-daemon-step3"><strong>Step 3.</strong> Start the Erlang <code>ssh</code> daemon:</p> <pre data-language="erlang">
1&gt; <code>ssh:start().</code>
ok
2&gt; <code>{ok, Sshd} = ssh:daemon(8989, [{system_dir, "/tmp/ssh_daemon"}, {user_dir, "/tmp/otptest_user/.ssh"}]).</code>
{ok,&lt;0.54.0&gt;}
3&gt; </pre> <p><strong>Step 4.</strong> Use the <strong>openssh</strong> client from a shell to connect to the Erlang <code>ssh</code> daemon:</p> <pre data-language="erlang">
$bash&gt; <code>ssh tarlop -p 8989 -i /tmp/otptest_user/.ssh/id_rsa \ -o UserKnownHostsFile=/tmp/otptest_user/.ssh/known_hosts</code>
The authenticity of host 'tarlop' can't be established.
RSA key fingerprint is 14:81:80:50:b1:1f:57:dd:93:a8:2d:2f:dd:90:ae:a8.
Are you sure you want to continue connecting (yes/no)? <code>yes</code>
Warning: Permanently added 'tarlop' (RSA) to the list of known hosts.
Eshell V5.10  (abort with ^G)
1&gt;   </pre> <p>There are two ways of shutting down an <code>ssh</code> daemon, see <strong>Step 5a</strong> and <strong>Step 5b</strong>.</p> <p><strong>Step 5a.</strong> Shut down the Erlang <code>ssh</code> daemon so that it stops the listener but leaves existing connections, started by the listener, operational:</p> <pre data-language="erlang">
3&gt; <code>ssh:stop_listener(Sshd).</code>
ok
4&gt; </pre> <p><strong>Step 5b.</strong> Shut down the Erlang <code>ssh</code> daemon so that it stops the listener and all connections started by the listener:</p> <pre data-language="erlang">
3&gt; <code>ssh:stop_daemon(Sshd).</code>
ok
4&gt; </pre> <h2 id="one-time-execution" class="title-link"> <div class="title-name">2.4 One-Time Execution</div>  </h2> <h3 id="erlang-client-contacting-os-standard-ssh-server" class="title-link"> <div class="title-name">Erlang client contacting OS standard ssh server</div>  </h3> <p>In the following example, the Erlang shell is the client process that receives the channel replies as Erlang messages.</p> <p>Do an one-time execution of a remote OS command ("pwd") over <code>ssh</code> to the ssh server of the OS at the host "tarlop":</p> <pre data-language="erlang">
1&gt; <code>ssh:start().</code>
ok
2&gt; <code>{ok, ConnectionRef} = ssh:connect("tarlop", 22, []).</code>
{ok,&lt;0.57.0&gt;}
3&gt; <code>{ok, ChannelId} = ssh_connection:session_channel(ConnectionRef, infinity).</code>
{ok,0}
4&gt; <code>success = ssh_connection:exec(ConnectionRef, ChannelId, "pwd", infinity).</code>
5&gt; <code>flush().</code> % Get all pending messages. NOTE: ordering may vary!
Shell got {ssh_cm,&lt;0.57.0&gt;,{data,0,0,&lt;&lt;"/home/otptest\n"&gt;&gt;}}
Shell got {ssh_cm,&lt;0.57.0&gt;,{eof,0}}
Shell got {ssh_cm,&lt;0.57.0&gt;,{exit_status,0,0}}
Shell got {ssh_cm,&lt;0.57.0&gt;,{closed,0}}
ok
6&gt; <code>ssh:connection_info(ConnectionRef, channels).</code>
{channels,[]}
7&gt;</pre> <p>See <code><a href="ssh_connection.html#description">ssh_connection</a></code> and <code><a href="ssh_connection.html#exec-4">ssh_connection:exec/4</a></code> for finding documentation of the channel messages.</p> <p>To collect the channel messages in a program, use <code>receive...end</code> instead of <code>flush/1</code>:</p> <pre data-language="erlang">
5&gt; <code>receive</code>
5&gt; <code>{ssh_cm, ConnectionRef, {data, ChannelId, Type, Result}} when Type == 0 -&gt;</code>
5&gt; <code>{ok,Result}</code>
5&gt; <code>{ssh_cm, ConnectionRef, {data, ChannelId, Type, Result}} when Type == 1 -&gt;</code>
5&gt; <code>{error,Result}</code>
5&gt; <code>end.</code>
{ok,&lt;&lt;"/home/otptest\n"&gt;&gt;}
6&gt;</pre> <p>Note that only the exec channel is closed after the one-time execution. The connection is still up and can handle previously opened channels. It is also possible to open a new channel:</p> <pre data-language="erlang">
% try to open a new channel to check if the ConnectionRef is still open
7&gt; <code>{ok, NewChannelId} = ssh_connection:session_channel(ConnectionRef, infinity).</code>
{ok,1}
8&gt;</pre> <p>To close the connection, call the function <code><a href="ssh.html#close-1">ssh:close(ConnectionRef)</a></code>. As an alternative, set the option <code><a href="ssh.html#type-max_idle_time_common_option">{idle_time, 1}</a></code> when opening the connection. This will cause the connection to be closed automatically when there are no channels open for the specified time period, in this case 1 ms. </p> <h3 id="os-standard-client-and-erlang-daemon--server-" class="title-link"> <div class="title-name">OS standard client and Erlang daemon (server)</div>  </h3> <p>An Erlang SSH daemon could be called for one-time execution of a "command". The "command" must be as if entered into the erlang shell, that is a sequence of Erlang <code>expressions</code> ended by a period (.). Variables bound in that sequence will keep their bindings throughout the expression sequence. The bindings are disposed when the result is returned.</p> <p>Here is an example of a suitable expression sequence:</p> <pre data-language="erlang">
A=1, B=2, 3 == (A + B).</pre> <p>It evaluates to <code>true</code> if submitted to the Erlang daemon started in <code><a href="#start-daemon-step3">Step 3</a></code> above:</p> <pre data-language="erlang">
$bash&gt; <code>ssh tarlop -p 8989 "A=1, B=2, 3 == (A + B)."</code>
true
$bash&gt;</pre> <p>The same example but now using the Erlang ssh client to contact the Erlang server:</p> <pre data-language="erlang">
1&gt; <code>{ok, ConnectionRef} = ssh:connect("tarlop", 8989, []).</code>
{ok,&lt;0.216.0&gt;}
2&gt; <code>{ok, ChannelId} = ssh_connection:session_channel(ConnectionRef, infinity).</code>
{ok,0}
3&gt; <code>success = ssh_connection:exec(ConnectionRef, ChannelId, "A=1, B=2, 3 == (A + B).", infinity).</code>
success
4&gt; <code>flush().</code>
Shell got {ssh_cm,&lt;0.216.0&gt;,{data,0,0,&lt;&lt;"true"&gt;&gt;}}
Shell got {ssh_cm,&lt;0.216.0&gt;,{exit_status,0,0}}
Shell got {ssh_cm,&lt;0.216.0&gt;,{eof,0}}
Shell got {ssh_cm,&lt;0.216.0&gt;,{closed,0}}
ok
5&gt;</pre> <p>Note that Erlang shell specific functions and control sequences like for example <code>h().</code> are not supported. </p> <h3 id="i-o-from-a-function-called-in-an-erlang-ssh-daemon" class="title-link"> <div class="title-name">I/O from a function called in an Erlang ssh daemon</div>  </h3> <p>Output to stdout on the server side is also displayed as well as the resulting term from the function call:</p> <pre data-language="erlang">
$bash&gt; <code>ssh tarlop -p 8989 'io:format("Hello!~n~nHow are ~p?~n",[you]).'</code>
Hello!
 
How are you?
ok
$bash&gt;</pre> <p>And similar for reading from stdin. As an example we use <code>io:read/1</code> which displays the argument as a prompt on stdout, reads a term from stdin and returns it in an ok-tuple:</p> <pre data-language="erlang">
$bash&gt; <code>ssh tarlop -p 8989 'io:read("write something: ").'</code>
write something: <code>[a,b,c].</code>
{ok,[a,b,c]}
$bash&gt;</pre> <p>The same example but using the Erlang ssh client:</p> <pre data-language="erlang">	
Eshell V10.5.2  (abort with ^G)
1&gt; <code>ssh:start().</code>
ok
2&gt; <code>{ok, ConnectionRef} = ssh:connect(loopback, 8989, []).</code>
{ok,&lt;0.92.0&gt;}
3&gt; <code>{ok, ChannelId} = ssh_connection:session_channel(ConnectionRef, infinity).</code>
{ok,0}
4&gt; <code>success = ssh_connection:exec(ConnectionRef, ChannelId, "io:read(\"write something: \").", infinity).</code>
success
5&gt; <code>flush().</code>
Shell got {ssh_cm,&lt;0.92.0&gt;,{data,0,0,&lt;&lt;"write something: "&gt;&gt;}}
ok
% All data is sent as binaries with string contents:
6&gt; <code>ok = ssh_connection:send(ConnectionRef, ChannelId, &lt;&lt;"[a,b,c]."&gt;&gt;).</code>
ok
7&gt; <code>flush().</code>
ok
%% Nothing is received, because the io:read/1
%% requires the input line to end with a newline.

%% Send a newline (it could have been included in the last send):
8&gt; <code>ssh_connection:send(ConnectionRef, ChannelId, &lt;&lt;"\n"&gt;&gt;).</code>
ok
9&gt; <code>flush().</code>
Shell got {ssh_cm,&lt;0.92.0&gt;,{data,0,0,&lt;&lt;"{ok,[a,b,c]}"&gt;&gt;}}
Shell got {ssh_cm,&lt;0.92.0&gt;,{exit_status,0,0}}
Shell got {ssh_cm,&lt;0.92.0&gt;,{eof,0}}
Shell got {ssh_cm,&lt;0.92.0&gt;,{closed,0}}
ok
10&gt;</pre> <h3 id="configuring-the-server's--daemon's--command-execution" class="title-link"> <div class="title-name">Configuring the server's (daemon's) command execution</div>  </h3> <p>Every time a daemon <code><a href="#running-an-erlang-ssh-daemon">is started</a></code>, it enables one-time execution of commands as described in the <code><a href="#simple-client-example">previous section</a></code> unless explicitly disabled. </p> <p>There is often a need to configure some other exec evaluator to tailor the input language or restrict the possible functions to call. There are two ways of doing this which will be shown with examples below. See <code><a href="ssh.html#daemon-2">ssh:daemon/2,3</a></code> and <code><a href="ssh.html#type-exec_daemon_option">exec_daemon_option()</a></code>) for details.</p> <p>Examples of the two ways to configure the exec evaluator:</p> <ol> <li>Disable one-time execution.<br> To modify the daemon start example above to reject one-time execution requests, we change <code><a href="#start-daemon-step3">Step 3</a></code> by adding the option <code>{exec, disabled}</code> to: <pre data-language="erlang">
1&gt; <code>ssh:start().</code>
ok
2&gt; <code>{ok, Sshd} = ssh:daemon(8989, [{system_dir, "/tmp/ssh_daemon"}, {user_dir, "/tmp/otptest_user/.ssh"}, {exec, disabled} ]).</code>
{ok,&lt;0.54.0&gt;}
3&gt;	</pre> <p>A call to that daemon will return the text "Prohibited." on stderr (depending on the client and OS), and the exit status 255:</p> <pre data-language="erlang">
$bash&gt; <code>ssh tarlop -p 8989 "test."</code>
Prohibited.
$bash&gt; <code>echo $?</code>
255
$bash&gt;	</pre> <p>And the Erlang client library also returns the text "Prohibited." on data type 1 instead of the normal 0 and exit status 255:</p> <pre data-language="erlang">
2&gt; <code>{ok, ConnectionRef} = ssh:connect(loopback, 8989, []).</code>
{ok,&lt;0.92.0&gt;}
3&gt; <code>{ok, ChannelId} = ssh_connection:session_channel(ConnectionRef, infinity).</code>
{ok,0}
4&gt; <code>success = ssh_connection:exec(ConnectionRef, ChannelId, "test."</code>
success
5&gt; <code>flush().</code>
Shell got {ssh_cm,&lt;0.106.0&gt;,{data,0,1,&lt;&lt;"Prohibited."&gt;&gt;}}
Shell got {ssh_cm,&lt;0.106.0&gt;,{exit_status,0,255}}
Shell got {ssh_cm,&lt;0.106.0&gt;,{eof,0}}
Shell got {ssh_cm,&lt;0.106.0&gt;,{closed,0}}
ok
6&gt;</pre> </li> <li>Install an alternative evaluator.<br> Start the damon with a reference to a <code>fun()</code> that handles the evaluation: <pre data-language="erlang">
1&gt; <code>ssh:start().</code>
ok
2&gt; <code>MyEvaluator = fun("1") -&gt; {ok, some_value}; ("2") -&gt; {ok, some_other_value}; ("3") -&gt; {ok, V} = io:read("input erlang term&gt;&gt; "), {ok, V}; (Err) -&gt; {error,{bad_input,Err}} end.</code>
3&gt; <code>{ok, Sshd} = ssh:daemon(1234, [{system_dir, "/tmp/ssh_daemon"}, {user_dir, "/tmp/otptest_user/.ssh"}, {exec, {direct,MyEvaluator}} ]).</code>
{ok,&lt;0.275.0&gt;}
4&gt;</pre> and call it: <pre data-language="erlang">
$bash&gt; <code>ssh localhost -p 1234 1</code>
some_value
$bash&gt; <code>ssh localhost -p 1234 2</code>
some_other_value
# I/O works:
$bash&gt; <code>ssh localhost -p 1234 3</code>
input erlang term&gt;&gt; <code>abc.</code>
abc
# Check that Erlang evaluation is disabled:
$bash&gt; <code>ssh localhost -p 1234 1+ 2.</code>
**Error** {bad_input,"1+ 2."}
$bash&gt; 	</pre> Note that spaces are preserved and that no point (.) is needed at the end - that was required by the default evaluator. <p>The error return in the Erlang client (The text as data type 1 and exit_status 255):</p> <pre data-language="erlang">
2&gt; <code>{ok, ConnectionRef} = ssh:connect(loopback, 1234, []).</code>
{ok,&lt;0.92.0&gt;}
3&gt; <code>{ok, ChannelId} = ssh_connection:session_channel(ConnectionRef, infinity).</code>
{ok,0}
4&gt; <code>success = ssh_connection:exec(ConnectionRef, ChannelId, "1+ 2.").</code>
success
5&gt; <code>flush().</code>
Shell got {ssh_cm,&lt;0.106.0&gt;,{data,0,1,&lt;&lt;"**Error** {bad_input,\"1+ 2.\"}"&gt;&gt;}}
Shell got {ssh_cm,&lt;0.106.0&gt;,{exit_status,0,255}}
Shell got {ssh_cm,&lt;0.106.0&gt;,{eof,0}}
Shell got {ssh_cm,&lt;0.106.0&gt;,{closed,0}}
ok
6&gt;</pre> <p>The <code>fun()</code> in the exec option could take up to three arguments (<code>Cmd</code>, <code>User</code> and <code>ClientAddress</code>). See the <code><a href="ssh.html#type-exec_daemon_option">exec_daemon_option()</a></code> for the details.</p> </li> </ol> <div class="note"> <div class="label">Note</div> <div class="content">

<p>An old, discouraged and undocumented way of installing an alternative evaluator exists.</p> <p>It still works, but lacks for example I/O possibility. It is because of that compatibility we need the <code>{direct,...}</code> construction. </p> </div> </div> <h2 id="sftp-server" class="title-link"> <div class="title-name">2.5 SFTP Server</div>  </h2> <p>Start the Erlang <code>ssh</code> daemon with the SFTP subsystem:</p> <pre data-language="erlang">
1&gt; <code>ssh:start().</code>
ok
2&gt; <code>ssh:daemon(8989, [{system_dir, "/tmp/ssh_daemon"}, {user_dir, "/tmp/otptest_user/.ssh"}, {subsystems, [ssh_sftpd:subsystem_spec( [{cwd, "/tmp/sftp/example"}]) ]}]).</code>
{ok,&lt;0.54.0&gt;}
3&gt; </pre> <p>Run the OpenSSH SFTP client:</p> <pre data-language="erlang">
$bash&gt; <code>sftp -oPort=8989 -o IdentityFile=/tmp/otptest_user/.ssh/id_rsa \ -o UserKnownHostsFile=/tmp/otptest_user/.ssh/known_hosts tarlop</code>
Connecting to tarlop...
sftp&gt; <code>pwd</code>
Remote working directory: /tmp/sftp/example
sftp&gt; </pre> <h2 id="sftp-client" class="title-link"> <div class="title-name">2.6 SFTP Client</div>  </h2> <p>Fetch a file with the Erlang SFTP client:</p> <pre data-language="erlang">
1&gt; <code>ssh:start().</code>
ok
2&gt; <code>{ok, ChannelPid, Connection} = ssh_sftp:start_channel("tarlop", []).</code>
{ok,&lt;0.57.0&gt;,&lt;0.51.0&gt;}
3&gt; <code>ssh_sftp:read_file(ChannelPid, "/home/otptest/test.txt").</code>
{ok,&lt;&lt;"This is a test file\n"&gt;&gt;} </pre> <h2 id="sftp-client-with-tar-compression" class="title-link"> <div class="title-name">2.7 SFTP Client with TAR Compression</div>  </h2> <h3 id="basic-example" class="title-link"> <div class="title-name">Basic example</div>  </h3> <p>This is an example of writing and then reading a tar file:</p> <pre data-language="erlang">{ok,HandleWrite} = ssh_sftp:open_tar(ChannelPid, ?tar_file_name, [write]),
ok = erl_tar:add(HandleWrite, .... ),
ok = erl_tar:add(HandleWrite, .... ),
...
ok = erl_tar:add(HandleWrite, .... ),
ok = erl_tar:close(HandleWrite),

%% And for reading
{ok,HandleRead} = ssh_sftp:open_tar(ChannelPid, ?tar_file_name, [read]),
{ok,NameValueList} = erl_tar:extract(HandleRead,[memory]),
ok = erl_tar:close(HandleRead),</pre> <h3 id="example-with-encryption" class="title-link"> <div class="title-name">Example with encryption</div>  </h3> <p>The previous <code><a href="using_ssh.html#basic-example">Basic example</a></code> can be extended with encryption and decryption as follows:</p> <pre data-language="erlang">%% First three parameters depending on which crypto type we select:
Key = &lt;&lt;"This is a 256 bit key. abcdefghi"&gt;&gt;,
Ivec0 = crypto:strong_rand_bytes(16),
DataSize = 1024,  % DataSize rem 16 = 0 for aes_cbc

%% Initialization of the CryptoState, in this case it is the Ivector.
InitFun = fun() -&gt; {ok, Ivec0, DataSize} end,

%% How to encrypt:
EncryptFun =
    fun(PlainBin,Ivec) -&gt;
        EncryptedBin = crypto:block_encrypt(aes_cbc256, Key, Ivec, PlainBin),
        {ok, EncryptedBin, crypto:next_iv(aes_cbc,EncryptedBin)}
    end,

%% What to do with the very last block:
CloseFun =
    fun(PlainBin, Ivec) -&gt;
        EncryptedBin = crypto:block_encrypt(aes_cbc256, Key, Ivec,
                                            pad(16,PlainBin) %% Last chunk
                                           ),
       {ok, EncryptedBin}
    end,

Cw = {InitFun,EncryptFun,CloseFun},
{ok,HandleWrite} = ssh_sftp:open_tar(ChannelPid, ?tar_file_name, [write,{crypto,Cw}]),
ok = erl_tar:add(HandleWrite, .... ),
ok = erl_tar:add(HandleWrite, .... ),
...
ok = erl_tar:add(HandleWrite, .... ),
ok = erl_tar:close(HandleWrite),

%% And for decryption (in this crypto example we could use the same InitFun
%% as for encryption):
DecryptFun =
    fun(EncryptedBin,Ivec) -&gt;
        PlainBin = crypto:block_decrypt(aes_cbc256, Key, Ivec, EncryptedBin),
       {ok, PlainBin, crypto:next_iv(aes_cbc,EncryptedBin)}
    end,

Cr = {InitFun,DecryptFun},
{ok,HandleRead} = ssh_sftp:open_tar(ChannelPid, ?tar_file_name, [read,{crypto,Cw}]),
{ok,NameValueList} = erl_tar:extract(HandleRead,[memory]),
ok = erl_tar:close(HandleRead),</pre> <h2 id="creating-a-subsystem" class="title-link"> <div class="title-name">2.8 Creating a Subsystem</div>  </h2> <p>A small <code>ssh</code> subsystem that echoes N bytes can be implemented as shown in the following example:</p> <pre data-language="erlang">-module(ssh_echo_server).
-behaviour(ssh_server_channel). % replaces ssh_daemon_channel
-record(state, {
	  n,
	  id,
	  cm
	 }).
-export([init/1, handle_msg/2, handle_ssh_msg/2, terminate/2]).

init([N]) -&gt;
    {ok, #state{n = N}}.

handle_msg({ssh_channel_up, ChannelId, ConnectionManager}, State) -&gt;
    {ok, State#state{id = ChannelId,
		     cm = ConnectionManager}}.

handle_ssh_msg({ssh_cm, CM, {data, ChannelId, 0, Data}}, #state{n = N} = State) -&gt;
    M = N - size(Data),
    case M &gt; 0 of
	true -&gt;
	   ssh_connection:send(CM, ChannelId, Data),
	   {ok, State#state{n = M}};
	false -&gt;
	   &lt;&lt;SendData:N/binary, _/binary&gt;&gt; = Data,
           ssh_connection:send(CM, ChannelId, SendData),
           ssh_connection:send_eof(CM, ChannelId),
	   {stop, ChannelId, State}
    end;
handle_ssh_msg({ssh_cm, _ConnectionManager,
		{data, _ChannelId, 1, Data}}, State) -&gt;
    error_logger:format(standard_error, " ~p~n", [binary_to_list(Data)]),
    {ok, State};

handle_ssh_msg({ssh_cm, _ConnectionManager, {eof, _ChannelId}}, State) -&gt;
    {ok, State};

handle_ssh_msg({ssh_cm, _, {signal, _, _}}, State) -&gt;
    %% Ignore signals according to RFC 4254 section 6.9.
    {ok, State};

handle_ssh_msg({ssh_cm, _, {exit_signal, ChannelId, _, _Error, _}},
	       State) -&gt;
    {stop, ChannelId,  State};

handle_ssh_msg({ssh_cm, _, {exit_status, ChannelId, _Status}}, State) -&gt;
    {stop, ChannelId, State}.

terminate(_Reason, _State) -&gt;
    ok.</pre> <p>The subsystem can be run on the host <strong>tarlop</strong> with the generated keys, as described in Section <code><a href="#Running%20an%20Erlang%20ssh%20Daemon"> Running an Erlang ssh Daemon</a></code>:</p> <pre data-language="erlang">
1&gt; <code>ssh:start().</code>
ok
2&gt; <code>ssh:daemon(8989, [{system_dir, "/tmp/ssh_daemon"}, {user_dir, "/tmp/otptest_user/.ssh"} {subsystems, [{"echo_n", {ssh_echo_server, [10]}}]}]).</code>
{ok,&lt;0.54.0&gt;}
3&gt; </pre> <pre data-language="erlang">
1&gt; <code>ssh:start().</code>
ok
2&gt; <code>{ok, ConnectionRef} = ssh:connect("tarlop", 8989, [{user_dir, "/tmp/otptest_user/.ssh"}]).</code>
 {ok,&lt;0.57.0&gt;}
3&gt; <code>{ok, ChannelId} = ssh_connection:session_channel(ConnectionRef, infinity).</code>
4&gt; <code>success = ssh_connection:subsystem(ConnectionRef, ChannelId, "echo_n", infinity).</code>
5&gt; <code>ok = ssh_connection:send(ConnectionRef, ChannelId, "0123456789", infinity).</code>
6&gt; <code>flush().</code>
{ssh_msg, &lt;0.57.0&gt;, {data, 0, 1, "0123456789"}}
{ssh_msg, &lt;0.57.0&gt;, {eof, 0}}
{ssh_msg, &lt;0.57.0&gt;, {closed, 0}}
7&gt; <code>{error, closed} = ssh_connection:send(ConnectionRef, ChannelId, "10", infinity).</code> </pre> <p>See also <code><a href="ssh_client_channel.html">ssh_client_channel(3)</a></code> (replaces ssh_channel(3)).</p> </div><div class="_attribution">
  <p class="_attribution-p">
    &copy; 2010&ndash;2023 Ericsson AB<br>Licensed under the Apache License, Version 2.0.<br>
    
  </p>
</div>
