<div class="innertube"> <h1 id="Using SSL application API">3 Using SSL application API</h1> <p>To see relevant version information for ssl, call <code>ssl:versions/0</code> .</p> <p>To see all supported cipher suites, call <code>ssl:cipher_suites(all, 'tlsv1.3') </code>. The available cipher suites for a connection depend on the TLS version and pre TLS-1.3 also on the certificate. To see the default cipher suite list change <code>all</code> to <code>default</code>. Note that TLS 1.3 and previous versions do not have any cipher suites in common, for listing cipher suites for a specific version use <code>ssl:cipher_suites(exclusive, 'tlsv1.3') </code>. Specific cipher suites that you want your connection to use can also be specified. Default is to use the strongest available.</p> <p>The following sections shows small examples of how to set up client/server connections using the Erlang shell. The returned value of the <code>sslsocket</code> is abbreviated with <code>[...]</code> as it can be fairly large and is opaque to the user except for the purpose of pattern matching.</p> <div class="note"> <div class="label">Note</div> <div class="content">

<p>Note that client certificate verification is optional for the server and needs additional conguration on both sides to work. The Certificate and keys, in the examples, are provided using the <code>certs_keys</code> option introduced in OTP-25. </p> </div> </div> <h2 id="basic-client" class="title-link"> <div class="title-name">3.1 Basic Client</div>  </h2> <pre data-language="erlang">1 &gt; ssl:start(), ssl:connect("google.com", 443, [{verify, verify_peer},
   {cacerts, public_key:cacerts_get()}]).
  {ok,{sslsocket, [...]}}</pre> <h2 id="basic-connection" class="title-link"> <div class="title-name">3.2 Basic Connection</div>  </h2> <p><strong>Step 1:</strong> Start the server side:</p> <pre data-language="erlang">1 server&gt; ssl:start().
ok</pre> <p><strong>Step 2:</strong> with alternative certificates, in this example the EDDSA certificate will be preferred if TLS-1.3 is negotiated and the RSA certificate will always be used for TLS-1.2 as it does not support the EDDSA algorithm: </p> <pre data-language="erlang">2 server&gt; {ok, ListenSocket} =
ssl:listen(9999, [{certs_keys, [#{certfile =&gt; "eddsacert.pem",
                                  keyfile =&gt; "eddsakey.pem"},
			        #{certfile =&gt; "rsacert.pem",
                                  keyfile =&gt; "rsakey.pem",
			          password =&gt; "foobar"}
			       ]},{reuseaddr, true}]).
{ok,{sslsocket, [...]}}</pre> <p><strong>Step 3:</strong> Do a transport accept on the TLS listen socket:</p> <pre data-language="erlang">3 server&gt; {ok, TLSTransportSocket} = ssl:transport_accept(ListenSocket).
{ok,{sslsocket, [...]}}</pre> <div class="note"> <div class="label">Note</div> <div class="content">

<p> ssl:transport_accept/1 and ssl:handshake/2 are separate functions so that the handshake part can be called in a new erlang process dedicated to handling the connection</p> </div> </div> <p><strong>Step 4:</strong> Start the client side: </p> <pre data-language="erlang">1 client&gt; ssl:start().
ok</pre> <p> Be sure to configure trusted certificates to use for server certificate verification.</p> <pre data-language="erlang">2 client&gt; {ok, Socket} = ssl:connect("localhost", 9999,
      [{verify, verify_peer},
      {cacertfile, "cacerts.pem"}, {active, once}], infinity).
{ok,{sslsocket, [...]}}</pre> <p><strong>Step 5:</strong> Do the TLS handshake:</p> <pre data-language="erlang">4 server&gt; {ok, Socket} = ssl:handshake(TLSTransportSocket).
{ok,{sslsocket, [...]}}</pre> <div class="note"> <div class="label">Note</div> <div class="content">

<p> A real server should use ssl:handshake/2 that has a timeout to avoid DoS attacks. In the example the timeout defaults to infinty.</p> </div> </div> <p><strong>Step 6:</strong> Send a message over TLS:</p> <pre data-language="erlang">5 server&gt; ssl:send(Socket, "foo").
ok</pre> <p><strong>Step 7:</strong> Flush the shell message queue to see that the message sent on the server side is recived by the client side:</p> <pre data-language="erlang">3 client&gt; flush().
Shell got {ssl,{sslsocket,[...]},"foo"}
ok</pre> <h2 id="upgrade-example---tls-only" class="title-link"> <div class="title-name">3.3 Upgrade Example - TLS only</div>  </h2> <p>Upgrading a a TCP/IP connection to a TLS connections is mostly used when there is a desire have unencrypted communication first and then later secure the communication channel by using TLS. Note that the client and server need to agree to do the upgrade in the protocol doing the communication. This is concept is often referenced as <code>STARTLS</code> and used in many protocols such as <code>SMTP</code>, <code>FTPS</code> and <code>HTTPS</code> via a proxy. </p> <div class="warning"> <div class="label">Warning</div> <div class="content">

<p>Maximum security recommendations are however moving away from such solutions.</p>
</div> </div> <p>To upgrade to a TLS connection:</p> <p><strong>Step 1:</strong> Start the server side:</p> <pre data-language="erlang">1 server&gt; ssl:start().
  ok</pre> <p><strong>Step 2:</strong> Create a normal TCP listen socket and ensure <code>active</code> is set to <code>false</code> and not set to any active mode otherwise TLS handshake messages can be delivered to the wrong process. </p> <pre data-language="erlang">2 server&gt; {ok, ListenSocket} = gen_tcp:listen(9999, [{reuseaddr, true},
  {active, false}]).
  {ok, #Port&lt;0.475&gt;}</pre> <p><strong>Step 3:</strong> Accept client connection:</p> <pre data-language="erlang">3 server&gt; {ok, Socket} = gen_tcp:accept(ListenSocket).
  {ok, #Port&lt;0.476&gt;}</pre> <p><strong>Step 4:</strong> Start the client side:</p> <pre data-language="erlang">1 client&gt; ssl:start().
  ok</pre> <pre data-language="erlang">2 client&gt; {ok, Socket} = gen_tcp:connect("localhost", 9999,  [], infinity).</pre> <p><strong>Step 5:</strong> Do the TLS handshake:</p> <pre data-language="erlang">4 server&gt; {ok, TLSSocket} = ssl:handshake(Socket, [{verify, verify_peer},
  {fail_if_no_peer_cert, true},
  {cacertfile, "cacerts.pem"},
  {certs_keys, [#{certfile =&gt; "cert.pem", keyfile =&gt; "key.pem"}]}]).
  {ok,{sslsocket,[...]}}</pre> <p><strong>Step 6:</strong> Upgrade to a TLS connection. The client and server must agree upon the upgrade. The server must be prepared to be a TLS server before the client can do a successful connect.</p> <pre data-language="erlang">3 client&gt;{ok, TLSSocket} = ssl:connect(Socket, [{verify, verify_peer},
  {cacertfile, "cacerts.pem"},
  {certs_keys, [#{certfile =&gt; "cert.pem", keyfile =&gt; "key.pem"}]}], infinity).
{ok,{sslsocket,[...]}}</pre> <p><strong>Step 7:</strong> Send a message over TLS:</p> <pre data-language="erlang">4 client&gt; ssl:send(TLSSocket, "foo").
      ok</pre> <p><strong>Step 8:</strong> Set <code>active once</code> on the TLS socket:</p> <pre data-language="erlang">5 server&gt; ssl:setopts(TLSSocket, [{active, once}]).
      ok</pre> <p><strong>Step 9:</strong> Flush the shell message queue to see that the message sent on the client side is recived by the server side:</p> <pre data-language="erlang">5 server&gt; flush().
      Shell got {ssl,{sslsocket,[...]},"foo"}
      ok</pre> <h2 id="customizing-cipher-suites" class="title-link"> <div class="title-name">3.4 Customizing cipher suites</div>  </h2> <p>Fetch default cipher suite list for a TLS/DTLS version. Change default to all to get all possible cipher suites.</p> <pre data-language="erlang">1&gt;  Default = ssl:cipher_suites(default, 'tlsv1.2').
    [#{cipher =&gt; aes_256_gcm,key_exchange =&gt; ecdhe_ecdsa,
    mac =&gt; aead,prf =&gt; sha384}, ....]</pre> <p>In OTP 20 it is desirable to remove all cipher suites that uses rsa key exchange (removed from default in 21) </p> <pre data-language="erlang">2&gt; NoRSA =
    ssl:filter_cipher_suites(Default,
                            [{key_exchange, fun(rsa) -&gt; false;
			                        (_) -&gt; true
			                    end}]).
    [...]</pre> <p> Pick just a few suites </p> <pre data-language="erlang"> 3&gt; Suites =
    ssl:filter_cipher_suites(Default,
                            [{key_exchange, fun(ecdh_ecdsa) -&gt; true;
          (_) -&gt; false
      end},
                             {cipher, fun(aes_128_cbc) -&gt; true;
    (_) -&gt;false
end}]).
    [#{cipher =&gt; aes_128_cbc,key_exchange =&gt; ecdh_ecdsa,
     mac =&gt; sha256,prf =&gt; sha256},
     #{cipher =&gt; aes_128_cbc,key_exchange =&gt; ecdh_ecdsa,mac =&gt; sha,
     prf =&gt; default_prf}]</pre> <p> Make some particular suites the most preferred, or least preferred by changing prepend to append.</p> <pre data-language="erlang">4&gt;ssl:prepend_cipher_suites(Suites, Default).
 [#{cipher =&gt; aes_128_cbc,key_exchange =&gt; ecdh_ecdsa,
    mac =&gt; sha256,prf =&gt; sha256},
  #{cipher =&gt; aes_128_cbc,key_exchange =&gt; ecdh_ecdsa,mac =&gt; sha,
    prf =&gt; default_prf},
  #{cipher =&gt; aes_256_cbc,key_exchange =&gt; ecdhe_ecdsa,
    mac =&gt; sha384,prf =&gt; sha384}, ...]</pre> <h2 id="using-an-engine-stored-key" class="title-link"> <div class="title-name">3.5 Using an Engine Stored Key</div>  </h2> <p>Erlang ssl application is able to use private keys provided by OpenSSL engines using the following mechanism:</p> <pre data-language="erlang">1&gt; ssl:start().
ok</pre> <p>Load a crypto engine, should be done once per engine used. For example dynamically load the engine called <code>MyEngine</code>: </p> <pre data-language="erlang">2&gt; {ok, EngineRef} =
crypto:engine_load(&lt;&lt;"dynamic"&gt;&gt;,
[{&lt;&lt;"SO_PATH"&gt;&gt;, "/tmp/user/engines/MyEngine"},&lt;&lt;"LOAD"&gt;&gt;],
[]).
{ok,#Ref&lt;0.2399045421.3028942852.173962&gt;}</pre> <p>Create a map with the engine information and the algorithm used by the engine:</p> <pre data-language="erlang">3&gt; PrivKey =
 #{algorithm =&gt; rsa,
   engine =&gt; EngineRef,
   key_id =&gt; "id of the private key in Engine"}.</pre> <p>Use the map in the ssl key option:</p> <pre data-language="erlang">4&gt; {ok, SSLSocket} =
ssl:connect("localhost", 9999,
            [{cacertfile, "cacerts.pem"},
	    {certs_keys, [#{certfile =&gt; "cert.pem", key =&gt; PrivKey}]}
	    ], infinity).</pre> <p>See also <code> crypto documentation</code> </p> <h2 id="session-reuse-pre-tls-1.3" class="title-link"> <div class="title-name">3.6 Session Reuse pre TLS 1.3</div>  </h2> <p>Clients can request to reuse a session established by a previous full handshake between that client and server by sending the id of the session in the initial handshake message. The server may or may not agree to reuse it. If agreed the server will send back the id and if not it will send a new id. The ssl application has several options for handling session reuse.</p> <p>On the client side the ssl application will save session data to try to automate session reuse on behalf of the client processes on the Erlang node. Note that only verified sessions will be saved for security reasons, that is session resumption relies on the certificate validation to have been run in the original handshake. To minimize memory consumption only unique sessions will be saved unless the special <code>save</code> value is specified for the following option <code>{reuse_sessions, boolean() | save}</code> in which case a full handshake will be performed and that specific session will have been saved before the handshake returns. The session id and even an opaque binary containing the session data can be retrieved using <code>ssl:connection_information/1</code> function. A saved session (guaranteed by the save option) can be explicitly reused using <code>{reuse_session, SessionId}</code>. Also it is possible for the client to reuse a session that is not saved by the ssl application using <code>{reuse_session, {SessionId, SessionData}}</code>.</p> <div class="note"> <div class="label">Note</div> <div class="content">

<p>When using explicit session reuse, it is up to the client to make sure that the session being reused is for the correct server and has been verified.</p>
</div> </div> <p>Here follows a client side example, divide into several steps for readability. </p> <p>Step 1 - Automated Session Reuse</p> <pre data-language="erlang">1&gt; ssl:start().
ok

2&gt; {ok, C1} = ssl:connect("localhost", 9999, [{verify, verify_peer},
					      {versions, ['tlsv1.2']},
					      {cacertfile, "cacerts.pem"}]).
{ok,{sslsocket,{gen_tcp,#Port&lt;0.7&gt;,tls_connection,undefined}, ...}}

3&gt; ssl:connection_information(C1, [session_id]).
{ok,[{session_id,&lt;&lt;95,32,43,22,35,63,249,22,26,36,106,
                   152,49,52,124,56,130,192,137,161,
                   146,145,164,232,...&gt;&gt;}]}

%% Reuse session if possible, note that if C2 is really fast the session
%% data might not be available for reuse.
4&gt; {ok, C2} = ssl:connect("localhost", 9999, [{verify, verify_peer},
					      {versions, ['tlsv1.2']},
					      {cacertfile, "cacerts.pem"},
					      {reuse_sessions, true}]).
{ok,{sslsocket,{gen_tcp,#Port&lt;0.8&gt;,tls_connection,undefined}, ...]}}

%% C2 got same session ID as client one, session was automatically reused.
5&gt; ssl:connection_information(C2, [session_id]).
{ok,[{session_id,&lt;&lt;95,32,43,22,35,63,249,22,26,36,106,
                   152,49,52,124,56,130,192,137,161,
                   146,145,164,232,...&gt;&gt;}]}</pre> <p>Step 2- Using <code>save</code> Option </p> <pre data-language="erlang">%% We want save this particular session for
%% reuse although it has the same basis as C1
6&gt; {ok, C3} = ssl:connect("localhost", 9999, [{verify, verify_peer},
					      {versions, ['tlsv1.2']},
					      {cacertfile, "cacerts.pem"},
					      {reuse_sessions, save}]).
{ok,{sslsocket,{gen_tcp,#Port&lt;0.9&gt;,tls_connection,undefined}, ...]}}

%% A full handshake is performed and we get a new session ID
7&gt; {ok, [{session_id, ID}]} = ssl:connection_information(C3, [session_id]).
{ok,[{session_id,&lt;&lt;91,84,27,151,183,39,84,90,143,141,
                   121,190,66,192,10,1,27,192,33,95,78,
                   8,34,180,...&gt;&gt;}]}

%% Use automatic session reuse
8&gt; {ok, C4} = ssl:connect("localhost", 9999, [{verify, verify_peer},
					      {versions, ['tlsv1.2']},
					      {cacertfile, "cacerts.pem"},
					      {reuse_sessions, true}]).
{ok,{sslsocket,{gen_tcp,#Port&lt;0.10&gt;,tls_connection,
                        undefined}, ...]}}

%% The "saved" one happened to be selected, but this is not a guarantee
9&gt; ssl:connection_information(C4, [session_id]).
{ok,[{session_id,&lt;&lt;91,84,27,151,183,39,84,90,143,141,
                   121,190,66,192,10,1,27,192,33,95,78,
                   8,34,180,...&gt;&gt;}]}

%% Make sure to reuse the "saved" session
10&gt; {ok, C5} = ssl:connect("localhost", 9999, [{verify, verify_peer},
					       {versions, ['tlsv1.2']},
					       {cacertfile, "cacerts.pem"},
					       {reuse_session, ID}]).
{ok,{sslsocket,{gen_tcp,#Port&lt;0.11&gt;,tls_connection,
                        undefined}, ...]}}

11&gt; ssl:connection_information(C5, [session_id]).
{ok,[{session_id,&lt;&lt;91,84,27,151,183,39,84,90,143,141,
                   121,190,66,192,10,1,27,192,33,95,78,
                   8,34,180,...&gt;&gt;}]}</pre> <p>Step 3 - Explicit Session Reuse </p> <pre data-language="erlang">%% Perform a full handshake and the session will not be saved for reuse
12&gt; {ok, C9} =
ssl:connect("localhost", 9999, [{verify, verify_peer},
				{versions, ['tlsv1.2']},
		                {cacertfile, "cacerts.pem"},
                                {reuse_sessions, false},
	                        {server_name_indication, disable}]).
{ok,{sslsocket,{gen_tcp,#Port&lt;0.14&gt;,tls_connection, ...}}

%% Fetch session ID and data for C9 connection
12&gt; {ok, [{session_id, ID1}, {session_data, SessData}]} =
	ssl:connection_information(C9, [session_id, session_data]).
{ok,[{session_id,&lt;&lt;9,233,4,54,170,88,170,180,17,96,202,
                   85,85,99,119,47,9,68,195,50,120,52,
                   130,239,...&gt;&gt;},
     {session_data,&lt;&lt;131,104,13,100,0,7,115,101,115,115,105,
                     111,110,109,0,0,0,32,9,233,4,54,170,...&gt;&gt;}]}

%% Explicitly reuse the session from C9
13&gt; {ok, C10} = ssl:connect("localhost", 9999, [{verify, verify_peer},
						{versions, ['tlsv1.2']},
						{cacertfile, "cacerts.pem"},
						{reuse_session, {ID1, SessData}}]).
{ok,{sslsocket,{gen_tcp,#Port&lt;0.15&gt;,tls_connection,
                        undefined}, ...}}

14&gt; ssl:connection_information(C10, [session_id]).
{ok,[{session_id,&lt;&lt;9,233,4,54,170,88,170,180,17,96,202,
                   85,85,99,119,47,9,68,195,50,120,52,
                   130,239,...&gt;&gt;}]}</pre> <p>Step 4 - Not Possible to Reuse Explicit Session by ID Only</p> <pre data-language="erlang">%% Try to reuse the session from C9 using only the id
15&gt; {ok, E} = ssl:connect("localhost", 9999, [{verify, verify_peer},
				              {versions, ['tlsv1.2']},
				              {cacertfile, "cacerts.pem"},
					      {reuse_session, ID1}]).
{ok,{sslsocket,{gen_tcp,#Port&lt;0.18&gt;,tls_connection,
                        undefined}, ...}}

%% This will fail (as it is not saved for reuse)
%% and a full handshake will be performed, we get a new id.
16&gt;  ssl:connection_information(E, [session_id]).
{ok,[{session_id,&lt;&lt;87,46,43,126,175,68,160,153,37,29,
                   196,240,65,160,254,88,65,224,18,63,
                   18,17,174,39,...&gt;&gt;}]}</pre> <p>On the server side the the <code>{reuse_sessions, boolean()}</code> option determines if the server will save session data and allow session reuse or not. This can be further customized by the option <code>{reuse_session, fun()}</code> that may introduce a local policy for session reuse. </p> <h2 id="session-tickets-and-session-resumption-in-tls-1.3" class="title-link"> <div class="title-name">3.7 Session Tickets and Session Resumption in TLS 1.3</div>  </h2> <p> TLS 1.3 introduces a new secure way of resuming sessions by using session tickets. A session ticket is an opaque data structure that is sent in the pre_shared_key extension of a ClientHello, when a client attempts to resume a session with keying material from a previous successful handshake.</p> <p>Session tickets can be stateful or stateless. A stateful session ticket is a database reference (session ticket store) and used with stateful servers, while a stateless ticket is a self-encrypted and self-authenticated data structure with cryptographic keying material and state data, enabling session resumption with stateless servers.</p> <p>The choice between stateful or stateless depends on the server requirements as the session tickets are opaque for the clients. Generally, stateful tickets are smaller and the server can guarantee that tickets are only used once. Stateless tickets contain additional data, require less storage on the server side, but they offer different guarantees against anti-replay. See also <code> Anti-Replay Protection in TLS 1.3</code> </p> <p>Session tickets are sent by servers on newly established TLS connections. The number of tickets sent and their lifetime are configurable by application variables. See also <code> SSL's configuration</code>.</p> <p>Session tickets are protected by application traffic keys, and in stateless tickets, the opaque data structure itself is self-encrypted.</p> <p>An example with automatic and manual session resumption:</p> <pre data-language="erlang">{ok, _} = application:ensure_all_started(ssl).
LOpts = [{certs_keys, [#{certfile =&gt; "cert.pem",
                         keyfile =&gt; "key.pem"}]},
         {versions, ['tlsv1.2','tlsv1.3']},
         {session_tickets, stateless}].
{ok, LSock} = ssl:listen(8001, LOpts).
{ok, CSock} = ssl:transport_accept(LSock).</pre> <p><strong>Step 2 (client):</strong> Start the client and connect to server:</p> <pre data-language="erlang">{ok, _} = application:ensure_all_started(ssl).
COpts = [{cacertfile, "cert.pem"},
         {versions, ['tlsv1.2','tlsv1.3']},
         {log_level, debug},
         {session_tickets, auto}].
ssl:connect("localhost", 8001, COpts).</pre> <p><strong>Step 3 (server):</strong> Start the TLS handshake:</p> <pre data-language="erlang">ssl:handshake(CSock).</pre> <p>A connection is established using a full handshake. Below is a summary of the exchanged messages:</p> <pre data-language="erlang">&gt;&gt;&gt; TLS 1.3 Handshake, ClientHello ...
&lt;&lt;&lt; TLS 1.3 Handshake, ServerHello ...
&lt;&lt;&lt; Handshake, EncryptedExtensions ...
&lt;&lt;&lt; Handshake, Certificate ...
&lt;&lt;&lt; Handshake, CertificateVerify ...
&lt;&lt;&lt; Handshake, Finished ...
&gt;&gt;&gt; Handshake, Finished ...
&lt;&lt;&lt; Post-Handshake, NewSessionTicket ...</pre> <p>At this point the client has stored the received session tickets and ready to use them when establishing new connections to the same server.</p> <p><strong>Step 4 (server):</strong> Accept a new connection on the server:</p> <pre data-language="erlang">{ok, CSock2} = ssl:transport_accept(LSock).</pre> <p><strong>Step 5 (client):</strong> Make a new connection:</p> <pre data-language="erlang">ssl:connect("localhost", 8001, COpts).</pre> <p><strong>Step 6 (server):</strong> Start the handshake:</p> <pre data-language="erlang">ssl:handshake(CSock2).</pre> <p>The second connection is a session resumption using keying material from the previous handshake:</p> <pre data-language="erlang">&gt;&gt;&gt; TLS 1.3 Handshake, ClientHello ...
&lt;&lt;&lt; TLS 1.3 Handshake, ServerHello ...
&lt;&lt;&lt; Handshake, EncryptedExtensions ...
&lt;&lt;&lt; Handshake, Finished ...
&gt;&gt;&gt; Handshake, Finished ...
&lt;&lt;&lt; Post-Handshake, NewSessionTicket ...</pre> <p>Manual handling of session tickets is also supported. In manual mode, it is the responsibility of the client to handle received session tickets.</p> <p><strong>Step 7 (server):</strong> Accept a new connection on the server:</p> <pre data-language="erlang">{ok, CSock3} = ssl:transport_accept(LSock).</pre> <p><strong>Step 8 (client):</strong> Make a new connection to server:</p> <pre data-language="erlang">{ok, _} = application:ensure_all_started(ssl).
COpts2 = [{cacertfile, "cacerts.pem"},
          {versions, ['tlsv1.2','tlsv1.3']},
          {log_level, debug},
          {session_tickets, manual}].
ssl:connect("localhost", 8001, COpts).</pre> <p><strong>Step 9 (server):</strong> Start the handshake:</p> <pre data-language="erlang">ssl:handshake(CSock3).</pre> <p>After the handshake is performed, the user process receivess messages with the tickets sent by the server.</p> <p><strong>Step 10 (client):</strong> Receive a new session ticket:</p> <pre data-language="erlang">Ticket = receive {ssl, session_ticket, {_, TicketData}} -&gt; TicketData end.</pre> <p><strong>Step 11 (server):</strong> Accept a new connection on the server:</p> <pre data-language="erlang">{ok, CSock4} = ssl:transport_accept(LSock).</pre> <p><strong>Step 12 (client):</strong> Initiate a new connection to the server with the session ticket received in Step 10:</p> <pre data-language="erlang">{ok, _} = application:ensure_all_started(ssl).
COpts2 = [{cacertfile, "cert.pem"},
          {versions, ['tlsv1.2','tlsv1.3']},
          {log_level, debug},
          {session_tickets, manual},
          {use_ticket, [Ticket]}].
ssl:connect("localhost", 8001, COpts).</pre> <p><strong>Step 13 (server):</strong> Start the handshake:</p> <pre data-language="erlang">ssl:handshake(CSock3).</pre> <h2 id="early-data-in-tls-1.3" class="title-link"> <div class="title-name">3.8 Early Data in TLS 1.3</div>  </h2> <p>TLS 1.3 allows clients to send data on the first flight if the endpoints have a shared crypographic secret (pre-shared key). This means that clients can send early data if they have a valid session ticket received in a previous successful handshake. For more information about session resumption see <code> Session Tickets and Session Resumption in TLS 1.3</code>. </p> <p>The security properties of Early Data are weaker than other kinds of TLS data. This data is not forward secret, and it is vulnerable to replay attacks. For available mitigation strategies see <code> Anti-Replay Protection in TLS 1.3</code>.</p> <p>In normal operation, clients will not know which, if any, of the available mitigation strategies servers actually implement, and hence must only send early data which they deem safe to be replayed. For example, idempotent HTTP operations, such as HEAD and GET, can usually be regarded as safe but even they can be exploited by a large number of replays causing resource limit exhaustion and other similar problems.</p> <p>An example of sending early data with automatic and manual session ticket handling:</p> <div class="warning"> <div class="label">Warning</div> <div class="content">

<p>The Early Data feature is experimental in this version of OTP. </p> </div> </div> <p><strong>Server (with NSS key logging)</strong></p> <pre data-language="erlang">early_data_server() -&gt;
    application:load(ssl),
    {ok, _} = application:ensure_all_started(ssl),
    Port = 11029,
    LOpts = [{certs_keys, [#{certfile =&gt; "cert.pem", keyfile =&gt; "key.pem"}]},
    {reuseaddr, true},
    {versions, ['tlsv1.2','tlsv1.3']},
    {session_tickets, stateless},
    {early_data, enabled},
    {keep_secrets, true} %% Enable NSS key log (debug option)
    ],
    {ok, LSock} = ssl:listen(Port, LOpts),
    %% Accept first connection
    {ok, CSock0} = ssl:transport_accept(LSock),
    {ok, _} = ssl:handshake(CSock0),
    %% Accept second connection
    {ok, CSock1} = ssl:transport_accept(LSock),
    {ok, Sock} = ssl:handshake(CSock1),
    Sock.</pre> <p><strong>Exporting the secrets (optional)</strong></p> <pre data-language="erlang">{ok, [{keylog, KeylogItems}]} = ssl:connection_information(Sock, [keylog]).
file:write_file("key.log", [[KeylogItem,$\n] || KeylogItem &lt;- KeylogItems]).</pre> <p><strong>Client (automatic ticket handling):</strong></p> <pre data-language="erlang">    early_data_auto() -&gt;
        %% First handshake 1-RTT - get session tickets
application:load(ssl),
{ok, _} = application:ensure_all_started(ssl),
Port = 11029,
Data = &lt;&lt;"HEAD / HTTP/1.1\r\nHost: \r\nConnection: close\r\n"&gt;&gt;,
COpts0 = [{cacertfile, "cacerts.pem"},
          {versions, ['tlsv1.2', 'tlsv1.3']},
          {session_tickets, auto}],
        {ok, Sock0} = ssl:connect("localhost", Port, COpts0),

        %% Wait for session tickets
timer:sleep(500),
%% Close socket if server cannot handle multiple
        %% connections e.g. openssl s_server
ssl:close(Sock0),

        %% Second handshake 0-RTT
COpts1 = [{cacertfile,  "cacerts.pem"},
          {versions, ['tlsv1.2', 'tlsv1.3']},
	  {session_tickets, auto},
	  {early_data, Data}],
        {ok, Sock} = ssl:connect("localhost", Port, COpts1),
Sock.</pre> <p><strong>Client (manual ticket handling):</strong></p> <pre data-language="erlang">    early_data_manual() -&gt;
        %% First handshake 1-RTT - get session tickets
application:load(ssl),
{ok, _} = application:ensure_all_started(ssl),
Port = 11029,
Data = &lt;&lt;"HEAD / HTTP/1.1\r\nHost: \r\nConnection: close\r\n"&gt;&gt;,
COpts0 = [{cacertfile, "cacerts.pem"},
          {versions, ['tlsv1.2', 'tlsv1.3']},
          {session_tickets, manual}],
        {ok, Sock0} = ssl:connect("localhost", Port, COpts0),

        %% Wait for session tickets
Ticket =
    receive
        {ssl, session_ticket, Ticket0} -&gt;
	    Ticket0
            end,

       %% Close socket if server cannot handle multiple connections
       %% e.g. openssl s_server
       ssl:close(Sock0),

       %% Second handshake 0-RTT
       COpts1 = [{cacertfile, "cacerts.pem"},
                 {versions, ['tlsv1.2', 'tlsv1.3']},
	 {session_tickets, manual},
	 {use_ticket, [Ticket]},
	 {early_data, Data}],
       {ok, Sock} = ssl:connect("localhost", Port, COpts1),
       Sock.</pre> <h2 id="anti-replay-protection-in-tls-1.3" class="title-link"> <div class="title-name">3.9 Anti-Replay Protection in TLS 1.3</div>  </h2> <p>The TLS 1.3 protocol does not provide inherent protection for replay of 0-RTT data but describes mechanisms that SHOULD be implemented by compliant server implementations. The implementation of TLS 1.3 in the SSL application employs all standard methods to prevent potential threats. </p> <p><strong>Single-use tickets</strong></p> <p>This mechanism is available with stateful session tickets. Session tickets can only be used once, subsequent use of the same ticket results in a full handshake. Stateful servers enforce this rule by maintaining a database of outstanding valid tickets.</p> <p><strong>Client Hello Recording</strong></p> <p>This mechanism is available with stateless session tickets. The server records a unique value derived from the ClientHello (PSK binder) in a given time window. The ticket's age is verified by using both the "obsfuscated_ticket_age" and an additional timestamp encrypted in the ticket data. As the used datastore allows false positives, apparent replays will be answered by doing a full 1-RTT handshake.</p> <p><strong>Freshness Checks</strong></p> <p>This mechanism is available with the stateless session tickets. As the ticket data has an embedded timestamp, the server can determine if a ClientHello was sent reasonably recently and accept the 0-RTT handshake, otherwise if falls back to a full 1-RTT handshake. This mechanism is tightly coupled with the previous one, it prevents storing an unlimited number of ClientHellos.</p> <p>The current implementation uses a pair of Bloom filters to implement the last two mechanisms. Bloom filters are fast, memory-efficient, probabilistic data structures that can tell if an element may be in a set or if it is definitely not in the set.</p> <p>If the option <code>anti_replay</code> is defined in the server, a pair of Bloom filters (<strong>current</strong> and <strong>old</strong>) are used to record incoming ClientHello messages (it is the unique binder value that is actually stored). The <strong>current</strong> Bloom filter is used for <code>WindowSize</code> seconds to store new elements. At the end of the time window the Bloom filters are rotated (the <strong>current</strong> Bloom filter becomes the <strong>old</strong> and an empty Bloom filter is set as <strong>current</strong>. </p> <p>The Anti-Replay protection feature in stateless servers executes in the following steps when a new ClientHello is received:</p> <ul> <li><p>Reported ticket age (obfuscated ticket age) shall be less than ticket lifetime.</p></li> <li><p>Actual ticket age shall be less than the ticket lifetime (stateless session tickets contain the servers timestamp when the ticket was issued).</p></li> <li><p>ClientHello created with the ticket shall be sent relatively recently (freshness checks).</p></li> <li><p>If all above checks passed both <strong>current</strong> and <strong>old</strong> Bloom filters are checked to detect if binder was already seen. Being a probabilistic data structure, false positives can occur and they trigger a full handshake.</p></li> <li><p>If the binder is not seen, the binder is validated. If the binder is valid, the server proceeds with the 0-RTT handshake.</p></li> </ul> <h2 id="using-dtls" class="title-link"> <div class="title-name">3.10 Using DTLS</div>  </h2> <p> Using DTLS has basically the same API as TLS. You need to add the option {protocol, dtls} to the connect and listen functions. For example</p> <pre data-language="erlang"> client&gt; {ok, Socket} = ssl:connect("localhost", 9999, [{protocol, dtls},
{verify, verify_peer},{cacertfile, "cacerts.pem"}], infinity).
{ok,{sslsocket, [...]}}</pre> </div><div class="_attribution">
  <p class="_attribution-p">
    &copy; 2010&ndash;2023 Ericsson AB<br>Licensed under the Apache License, Version 2.0.<br>
    
  </p>
</div>
