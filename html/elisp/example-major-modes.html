 <h4 class="subsection">Major Mode Examples</h4> <p>Text mode is perhaps the simplest mode besides Fundamental mode. Here are excerpts from <samp>text-mode.el</samp> that illustrate many of the conventions listed above: </p> <div class="example"> <pre class="example">;; <span class="roman">Create the syntax table for this mode.</span>
(defvar text-mode-syntax-table
  (let ((st (make-syntax-table)))
    (modify-syntax-entry ?\" ".   " st)
    (modify-syntax-entry ?\\ ".   " st)
    ;; Add 'p' so M-c on 'hello' leads to 'Hello', not 'hello'.
    (modify-syntax-entry ?' "w p" st)
    …
    st)
  "Syntax table used while in `text-mode'.")
</pre>
<pre class="example">

;; <span class="roman">Create the keymap for this mode.</span>
</pre>
<pre class="example">(defvar text-mode-map
  (let ((map (make-sparse-keymap)))
    (define-key map "\e\t" 'ispell-complete-word)
    …
    map)
  "Keymap for `text-mode'.
Many other modes, such as `mail-mode', `outline-mode' and
`indented-text-mode', inherit all the commands defined in this map.")
</pre>
</div> <p>Here is how the actual mode command is defined now: </p> <div class="example"> <pre class="example">(define-derived-mode text-mode nil "Text"
  "Major mode for editing text written for humans to read.
In this mode, paragraphs are delimited only by blank or white lines.
You can thus get the full benefit of adaptive filling
 (see the variable `adaptive-fill-mode').
\\{text-mode-map}
Turning on Text mode runs the normal hook `text-mode-hook'."
</pre>
<pre class="example">  (setq-local text-mode-variant t)
  (setq-local require-final-newline mode-require-final-newline))
</pre>
</div>  <p>The three Lisp modes (Lisp mode, Emacs Lisp mode, and Lisp Interaction mode) have more features than Text mode and the code is correspondingly more complicated. Here are excerpts from <samp>lisp-mode.el</samp> that illustrate how these modes are written. </p> <p>Here is how the Lisp mode syntax and abbrev tables are defined: </p>  <div class="example"> <pre class="example">;; <span class="roman">Create mode-specific table variables.</span>
(define-abbrev-table 'lisp-mode-abbrev-table ()
  "Abbrev table for Lisp mode.")

(defvar lisp-mode-syntax-table
  (let ((table (make-syntax-table lisp--mode-syntax-table)))
    (modify-syntax-entry ?\[ "_   " table)
    (modify-syntax-entry ?\] "_   " table)
    (modify-syntax-entry ?# "' 14" table)
    (modify-syntax-entry ?| "\" 23bn" table)
    table)
  "Syntax table used in `lisp-mode'.")
</pre>
</div> <p>The three modes for Lisp share much of their code. For instance, Lisp mode and Emacs Lisp mode inherit from Lisp Data mode and Lisp Interaction Mode inherits from Emacs Lisp mode. </p> <p>Amongst other things, Lisp Data mode sets up the <code>comment-start</code> variable to handle Lisp comments: </p> <div class="example"> <pre class="example">  (setq-local comment-start ";")
  …
</pre>
</div> <p>Each of the different Lisp modes has a slightly different keymap. For example, Lisp mode binds <kbd>C-c C-z</kbd> to <code>run-lisp</code>, but the other Lisp modes do not. However, all Lisp modes have some commands in common. The following code sets up the common commands: </p> <div class="example"> <pre class="example">(defvar lisp-mode-shared-map
  (let ((map (make-sparse-keymap)))
    (set-keymap-parent map prog-mode-map)
    (define-key map "\e\C-q" 'indent-sexp)
    (define-key map "\177" 'backward-delete-char-untabify)
    map)
  "Keymap for commands shared by all sorts of Lisp modes.")
</pre>
</div> <p>And here is the code to set up the keymap for Lisp mode: </p> <div class="example"> <pre class="example">(defvar lisp-mode-map
  (let ((map (make-sparse-keymap))
        (menu-map (make-sparse-keymap "Lisp")))
    (set-keymap-parent map lisp-mode-shared-map)
    (define-key map "\e\C-x" 'lisp-eval-defun)
    (define-key map "\C-c\C-z" 'run-lisp)
    …
    map)
  "Keymap for ordinary Lisp mode.
All commands in `lisp-mode-shared-map' are inherited by this map.")
</pre>
</div> <p>Finally, here is the major mode command for Lisp mode: </p> <div class="example"> <pre class="example">(define-derived-mode lisp-mode lisp-data-mode "Lisp"
  "Major mode for editing Lisp code for Lisps other than GNU Emacs Lisp.
Commands:
Delete converts tabs to spaces as it moves back.
Blank lines separate paragraphs.  Semicolons start comments.

\\{lisp-mode-map}
Note that `run-lisp' may be used either to start an inferior Lisp job
or to switch back to an existing one."
</pre>
<pre class="example">  (setq-local find-tag-default-function 'lisp-find-tag-default)
  (setq-local comment-start-skip
              "\\(\\(^\\|[^\\\n]\\)\\(\\\\\\\\\\)*\\)\\(;+\\|#|\\) *")
  (setq imenu-case-fold-search t))
</pre>
</div><div class="_attribution">
  <p class="_attribution-p">
    Copyright &copy; 1990-1996, 1998-2022 Free Software Foundation, Inc. <br>Licensed under the GNU GPL license.<br>
    <a href="https://www.gnu.org/software/emacs/manual/html_node/elisp/Example-Major-Modes.html" class="_attribution-link">https://www.gnu.org/software/emacs/manual/html_node/elisp/Example-Major-Modes.html</a>
  </p>
</div>
