<h1 id="id1">6.19.3. Generic programming</h1>
<div class="_sphinx"> <div itemprop="articleBody">   <p id="generic-programming">There are a few ways to do datatype-generic programming using the <a class="reference external" href="../../libraries/base-4.17.0.0/ghc-generics.html">GHC.Generics</a> framework. One is making use of the <code>Generically</code> and <code>Generically1</code> wrappers from <code>GHC.Generics</code>, instances can be derived via them using <a class="reference internal" href="deriving_via.html#extension-DerivingVia"><code>DerivingVia</code></a>:</p> <pre data-language="haskell">{-# LANGUAGE DeriveGeneric      #-}
{-# LANGUAGE DerivingStrategies #-}
{-# LANGUAGE DerivingVia        #-}

import GHC.Generics

data V4 a = V4 a a a a
  deriving
  stock (Generic, Generic1)

  deriving (Semigroup, Monoid)
  via Generically (V4 a)

  deriving (Functor, Applicative)
  via Generically1 V4
</pre> <p>The older approach uses <a class="reference internal" href="#extension-DeriveGeneric"><code>DeriveGeneric</code></a>, <a class="reference internal" href="default_signatures.html#extension-DefaultSignatures"><code>DefaultSignatures</code></a>, and <a class="reference internal" href="derive_any_class.html#extension-DeriveAnyClass"><code>DeriveAnyClass</code></a>. It derives instances by providing a distinguished generic implementation as part of the type class declaration. This section gives a very brief overview of how to do it.</p> <p>Generic programming support in GHC allows defining classes with methods that do not need a user specification when instantiating: the method body is automatically derived by GHC. This is similar to what happens for standard classes such as <code>Read</code> and <code>Show</code>, for instance, but now for user-defined classes.</p> <div class="admonition note" id="generic-classes"> <p class="first admonition-title">Note</p> <p class="last">GHC used to have an implementation of generic classes as defined in the paper “Derivable type classes”, Ralf Hinze and Simon Peyton Jones, Haskell Workshop, Montreal Sept 2000, pp. 94-105. These have been removed and replaced by the more general support for generic programming.</p> </div>  <h2 id="deriving-representations">6.19.3.1. Deriving representations</h2> <p>The first thing we need is generic representations. The <code>GHC.Generics</code> module defines a couple of primitive types that are used to represent Haskell datatypes:</p> <pre data-language="haskell">-- | Unit: used for constructors without arguments
data U1 p = U1

-- | Constants, additional parameters and recursion of kind Type
newtype K1 i c p = K1 { unK1 :: c }

-- | Meta-information (constructor names, etc.)
newtype M1 i c f p = M1 { unM1 :: f p }

-- | Sums: encode choice between constructors
infixr 5 :+:
data (:+:) f g p = L1 (f p) | R1 (g p)

-- | Products: encode multiple arguments to constructors
infixr 6 :*:
data (:*:) f g p = f p :*: g p
</pre> <p>The <code>Generic</code> and <code>Generic1</code> classes mediate between user-defined datatypes and their internal representation as a sum-of-products:</p> <pre data-language="haskell">class Generic a where
  -- Encode the representation of a user datatype
  type Rep a :: Type -&gt; Type
  -- Convert from the datatype to its representation
  from  :: a -&gt; (Rep a) x
  -- Convert from the representation to the datatype
  to    :: (Rep a) x -&gt; a

class Generic1 (f :: k -&gt; Type) where
  type Rep1 f :: k -&gt; Type

  from1  :: f a -&gt; Rep1 f a
  to1    :: Rep1 f a -&gt; f a
</pre> <p><code>Generic1</code> is used for functions that can only be defined over type containers, such as <code>map</code>. Note that <code>Generic1</code> ranges over types of kind <code>Type -&gt; Type</code> by default, but if the <a class="reference internal" href="poly_kinds.html#extension-PolyKinds"><code>PolyKinds</code></a> extension is enabled, then it can range of types of kind <code>k -&gt; Type</code>, for any kind <code>k</code>.</p> <dl class="extension"> <dt id="extension-DeriveGeneric">
<code>DeriveGeneric</code> </dt> <dd>
<table class="docutils field-list" frame="void" rules="none"> <col class="field-name"> <col class="field-body">  <tr>
<th class="field-name">Since:</th>
<td class="field-body">7.2.1</td> </tr>  </table> <p>Allow automatic deriving of instances for the <code>Generic</code> typeclass.</p> </dd>
</dl> <p>Instances of these classes can be derived by GHC with the <a class="reference internal" href="#extension-DeriveGeneric"><code>DeriveGeneric</code></a> extension, and are necessary to be able to define generic instances automatically.</p> <p>For example, a user-defined datatype of trees</p> <pre data-language="haskell">data UserTree a = Node a (UserTree a) (UserTree a) | Leaf
</pre> <p>in a <code>Main</code> module in a package named <code>foo</code> will get the following representation:</p> <pre data-language="haskell">instance Generic (UserTree a) where
  -- Representation type
  type Rep (UserTree a) =
    M1 D ('MetaData "UserTree" "Main" "package-name" 'False) (
          M1 C ('MetaCons "Node" 'PrefixI 'False) (
                M1 S ('MetaSel 'Nothing
                               'NoSourceUnpackedness
                               'NoSourceStrictness
                               'DecidedLazy)
                     (K1 R a)
            :*: M1 S ('MetaSel 'Nothing
                               'NoSourceUnpackedness
                               'NoSourceStrictness
                               'DecidedLazy)
                     (K1 R (UserTree a))
            :*: M1 S ('MetaSel 'Nothing
                               'NoSourceUnpackedness
                               'NoSourceStrictness
                               'DecidedLazy)
                     (K1 R (UserTree a)))
      :+: M1 C ('MetaCons "Leaf" 'PrefixI 'False) U1)

  -- Conversion functions
  from (Node x l r) = M1 (L1 (M1 (M1 (K1 x) :*: M1 (K1 l) :*: M1 (K1 r))))
  from Leaf         = M1 (R1 (M1 U1))
  to (M1 (L1 (M1 (M1 (K1 x) :*: M1 (K1 l) :*: M1 (K1 r))))) = Node x l r
  to (M1 (R1 (M1 U1)))                                      = Leaf
</pre> <p>This representation is generated automatically if a <code>deriving Generic</code> clause is attached to the datatype. <a class="reference external" href="#stand-alone-deriving">Standalone deriving</a> can also be used.</p>   <h2 id="writing-generic-functions">6.19.3.2. Writing generic functions</h2> <p>A generic function is defined by creating a class and giving instances for each of the representation types of <code>GHC.Generics</code>. As an example we show generic serialization:</p> <pre data-language="haskell">data Bin = O | I

class GSerialize f where
  gput :: f a -&gt; [Bin]

instance GSerialize U1 where
  gput U1 = []

instance (GSerialize a, GSerialize b) =&gt; GSerialize (a :*: b) where
  gput (x :*: y) = gput x ++ gput y

instance (GSerialize a, GSerialize b) =&gt; GSerialize (a :+: b) where
  gput (L1 x) = O : gput x
  gput (R1 x) = I : gput x

instance (GSerialize a) =&gt; GSerialize (M1 i c a) where
  gput (M1 x) = gput x

instance (Serialize a) =&gt; GSerialize (K1 i a) where
  gput (K1 x) = put x
</pre> <p>A caveat: this encoding strategy may not be reliable across different versions of GHC. When deriving a <code>Generic</code> instance is free to choose any nesting of <code>:+:</code> and <code>:*:</code> it chooses, so if GHC chooses <code>(a :+: b) :+: c</code>, then the encoding for <code>a</code> would be <code>[O, O]</code>, <code>b</code> would be <code>[O, I]</code>, and <code>c</code> would be <code>[I]</code>. However, if GHC chooses <code>a :+: (b :+: c)</code>, then the encoding for <code>a</code> would be <code>[O]</code>, <code>b</code> would be <code>[I, O]</code>, and <code>c</code> would be <code>[I, I]</code>. (In practice, the current implementation tries to produce a more-or-less balanced nesting of <code>:+:</code> and <code>:*:</code> so that the traversal of the structure of the datatype from the root to a particular component can be performed in logarithmic rather than linear time.)</p> <p>Typically this <code>GSerialize</code> class will not be exported, as it only makes sense to have instances for the representation types.</p>   <h2 id="unlifted-representation-types">6.19.3.3. Unlifted representation types</h2> <p>The data family <code>URec</code> is provided to enable generic programming over datatypes with certain unlifted arguments. There are six instances corresponding to common unlifted types:</p> <pre data-language="haskell">data family URec a p

data instance URec (Ptr ()) p = UAddr   { uAddr#   :: Addr#   }
data instance URec Char     p = UChar   { uChar#   :: Char#   }
data instance URec Double   p = UDouble { uDouble# :: Double# }
data instance URec Int      p = UInt    { uInt#    :: Int#    }
data instance URec Float    p = UFloat  { uFloat#  :: Float#  }
data instance URec Word     p = UWord   { uWord#   :: Word#   }
</pre> <p>Six type synonyms are provided for convenience:</p> <pre data-language="haskell">type UAddr   = URec (Ptr ())
type UChar   = URec Char
type UDouble = URec Double
type UFloat  = URec Float
type UInt    = URec Int
type UWord   = URec Word
</pre> <p>As an example, this data declaration:</p> <pre data-language="haskell">data IntHash = IntHash Int#
  deriving Generic
</pre> <p>results in the following <code>Generic</code> instance:</p> <pre data-language="haskell">instance 'Generic' IntHash where
  type 'Rep' IntHash =
    'D1' ('MetaData "IntHash" "Main" "package-name" 'False)
      ('C1' ('MetaCons "IntHash" 'PrefixI 'False)
        ('S1' ('MetaSel 'Nothing
                        'NoSourceUnpackedness
                        'NoSourceStrictness
                        'DecidedLazy)
              'UInt'))
</pre> <p>A user could provide, for example, a <code>GSerialize UInt</code> instance so that a <code>Serialize IntHash</code> instance could be easily defined in terms of <code>GSerialize</code>.</p>   <h2 id="generic-defaults">6.19.3.4. Generic defaults</h2> <p>The only thing left to do now is to define a “front-end” class, which is exposed to the user:</p> <pre data-language="haskell">class Serialize a where
  put :: a -&gt; [Bin]

  default put :: (Generic a, GSerialize (Rep a)) =&gt; a -&gt; [Bin]
  put = gput . from
</pre> <p>Here we use a <a class="reference external" href="#class-default-signatures">default signature</a> to specify that the user does not have to provide an implementation for <code>put</code>, as long as there is a <code>Generic</code> instance for the type to instantiate. For the <code>UserTree</code> type, for instance, the user can just write:</p> <pre data-language="haskell">instance (Serialize a) =&gt; Serialize (UserTree a)
</pre> <p>The default method for <code>put</code> is then used, corresponding to the generic implementation of serialization. If you are using <a class="reference internal" href="derive_any_class.html#extension-DeriveAnyClass"><code>DeriveAnyClass</code></a>, the same instance is generated by simply attaching a <code>deriving Serialize</code> clause to the <code>UserTree</code> datatype declaration. For more examples of generic functions please refer to the <a class="reference external" href="https://hackage.haskell.org/package/generic-deriving">generic-deriving</a> package on Hackage.</p>   <h2 id="more-information">6.19.3.5. More information</h2> <p>For more details please refer to the <a class="reference external" href="https://www.haskell.org/haskellwiki/GHC.Generics">Haskell Wiki page</a> or the original paper <a class="reference internal" href="#generics2010" id="id2">[Generics2010]</a>.</p> <table class="docutils citation" frame="void" id="generics2010" rules="none">   <tr>
<td class="label"><a class="fn-backref" href="#id2">[Generics2010]</a></td>
<td>Jose Pedro Magalhaes, Atze Dijkstra, Johan Jeuring, and Andres Loeh. <a class="reference external" href="https://dreixel.net/research/pdf/gdmh.pdf">A generic deriving mechanism for Haskell</a>. Proceedings of the third ACM Haskell symposium on Haskell (Haskell‘2010), pp. 37-48, ACM, 2010.</td>
</tr>  </table>   </div> </div><div class="_attribution">
  <p class="_attribution-p">
    &copy; 2002&ndash;2007 The University Court of the University of Glasgow. All rights reserved.<br>Licensed under the Glasgow Haskell Compiler License.<br>
    <a href="https://downloads.haskell.org/~ghc/9.4.2/docs/users_guide/exts/generics.html" class="_attribution-link">https://downloads.haskell.org/~ghc/9.4.2/docs/users_guide/exts/generics.html</a>
  </p>
</div>
