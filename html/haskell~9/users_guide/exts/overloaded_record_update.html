<h1 id="id1">6.5.10. Overloaded record update</h1>
<div class="_sphinx"> <div itemprop="articleBody">   <dl class="extension" id="overloaded-record-update"> <dt id="extension-OverloadedRecordUpdate">
<code>OverloadedRecordUpdate</code> </dt> <dd>
<table class="docutils field-list" frame="void" rules="none"> <col class="field-name"> <col class="field-body">  <tr>
<th class="field-name">Since:</th>
<td class="field-body">9.2.0</td> </tr>  </table> <p>Provides record ‘.’ syntax in record updates e.g. <code>x{foo.bar = 1}</code>.</p> </dd>
</dl> <p><strong>EXPERIMENTAL</strong> <em>This design of this extension may well change in the future. It would be inadvisable to start using this extension for long-lived libraries just yet.</em></p> <p>It’s usual (but not required) that this extension be used in conjunction with <a class="reference internal" href="overloaded_record_dot.html#overloaded-record-dot"><span class="std std-ref">Overloaded record dot</span></a>.</p> <p>Example:</p> <pre data-language="haskell">{-# LANGUAGE AllowAmbiguousTypes, FunctionalDependencies, ScopedTypeVariables, PolyKinds, TypeApplications, DataKinds, FlexibleInstances #-}
{-# LANGUAGE NamedFieldPuns, RecordWildCards #-}
{-# LANGUAGE OverloadedRecordDot, OverloadedRecordUpdate, RebindableSyntax #-}

import Prelude

class HasField x r a | x r -&gt; a where
  hasField :: r -&gt; (a -&gt; r, a)

getField :: forall x r a . HasField x r a =&gt; r -&gt; a
getField = snd . hasField @x -- Note: a.x = is getField @"x" a.
setField :: forall x r a . HasField x r a =&gt; r -&gt; a -&gt; r
setField = fst . hasField @x -- Note : a{x = b} is setField @"x" a b.

data Person = Person { name :: String } deriving Show
instance HasField "name" Person String where
    hasField r = (\x -&gt; case r of Person { .. } -&gt; Person { name = x, .. }, name r)

data Company = Company { company :: String, owner :: Person } deriving Show
instance HasField "company" Company String where
    hasField r = (\x -&gt; case r of Company { .. } -&gt; Company { company = x, .. }, company r)
instance HasField "owner" Company Person where
    hasField r = (\x -&gt; case r of Company { .. } -&gt; Company { owner = x, .. }, owner r)

main = do
  let c = Company {company = "Acme Corp.", owner = Person { name = "Wile E. Coyote" }}

  -- Top-level update
  print $ c{company = "Acme United"} -- Company {company = "Acme United", owner = Person {name = "Wile E. Coyote"}}

  -- Nested update
  print $ c{owner.name = "Walter C. Johnsen"} -- Company {company = "Acme Corp.", owner = Person {name = "Walter C. Johnsen"}}

  -- Punned update
  let name = "Walter C. Johnsen"
  print $ c{owner.name}  -- Company {company = "Acme Corp.", owner = Person {name = "Walter C. Johnsen"}}
</pre> <p><code>OverloadedRecordUpdate</code> works by desugaring record <code>.</code> update expressions to expressions involving the functions <code>setField</code> and <code>getField</code>. Note that all record updates will be desugared to <code>setField</code> expressions whether they use <code>.</code> notation or not.</p> <p>At this time, <code>RebindableSyntax</code> must be enabled when <code>OverloadedRecordUpdate</code> is and users are required to provide definitions for <code>getField</code> and <code>setField</code>. We anticipate this restriction to be lifted in a future release of GHC with builtin support for <code>setField</code>.</p>  </div> </div><div class="_attribution">
  <p class="_attribution-p">
    &copy; 2002&ndash;2007 The University Court of the University of Glasgow. All rights reserved.<br>Licensed under the Glasgow Haskell Compiler License.<br>
    <a href="https://downloads.haskell.org/~ghc/9.4.2/docs/users_guide/exts/overloaded_record_update.html" class="_attribution-link">https://downloads.haskell.org/~ghc/9.4.2/docs/users_guide/exts/overloaded_record_update.html</a>
  </p>
</div>
