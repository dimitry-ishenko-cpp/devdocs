<h1 class="title">Julia Package</h1>  <p>The DuckDB Julia package provides a high-performance front-end for DuckDB. Much like SQLite, DuckDB runs in-process within the Julia client, and provides a DBInterface front-end.</p> <p>The package also supports multi-threaded execution. It uses Julia threads/tasks for this purpose. If you wish to run queries in parallel, you must launch Julia with multi-threading support (by e.g., setting the <code class="language-plaintext highlighter-rouge">JULIA_NUM_THREADS</code> environment variable).</p> <h2 id="installation"> <a style="text-decoration: none;" href="#installation">Installation</a> </h2> <p>Install DuckDB as follows:</p> <pre class="language-julia highlighter-rouge" data-language="julia">using Pkg
Pkg.add("DuckDB")</pre> <p>Alternatively, enter the package manager using the <code class="language-plaintext highlighter-rouge">]</code> key, and issue the following command:</p> <pre class="language-julia highlighter-rouge" data-language="julia">pkg&gt; add DuckDB</pre> <h2 id="basics"> <a style="text-decoration: none;" href="#basics">Basics</a> </h2> <pre class="language-julia highlighter-rouge" data-language="julia">using DuckDB

# create a new in-memory database
con = DBInterface.connect(DuckDB.DB, ":memory:")

# create a table
DBInterface.execute(con, "CREATE TABLE integers (i INTEGER)")

# insert data by executing a prepared statement
stmt = DBInterface.prepare(con, "INSERT INTO integers VALUES(?)")
DBInterface.execute(stmt, [42])

# query the database
results = DBInterface.execute(con, "SELECT 42 a")
print(results)</pre> <p>Some SQL statements, such as PIVOT and IMPORT DATABASE are executed as multiple prepared statements and will error when using <code class="language-plaintext highlighter-rouge">DuckDB.execute()</code>. Instead they can be run with <code class="language-plaintext highlighter-rouge">DuckDB.query()</code> instead of <code class="language-plaintext highlighter-rouge">DuckDB.execute()</code> and will always return a materialized result.</p> <h2 id="scanning-dataframes"> <a style="text-decoration: none;" href="#scanning-dataframes">Scanning DataFrames</a> </h2> <p>The DuckDB Julia package also provides support for querying Julia DataFrames. Note that the DataFrames are directly read by DuckDB - they are not inserted or copied into the database itself.</p> <p>If you wish to load data from a DataFrame into a DuckDB table you can run a <code class="language-plaintext highlighter-rouge">CREATE TABLE ... AS</code> or <code class="language-plaintext highlighter-rouge">INSERT INTO</code> query.</p> <pre class="language-julia highlighter-rouge" data-language="julia">using DuckDB
using DataFrames

# create a new in-memory dabase
con = DBInterface.connect(DuckDB.DB)

# create a DataFrame
df = DataFrame(a = [1, 2, 3], b = [42, 84, 42])

# register it as a view in the database
DuckDB.register_data_frame(con, df, "my_df")

# run a SQL query over the DataFrame
results = DBInterface.execute(con, "SELECT * FROM my_df")
print(results)</pre> <h2 id="appender-api"> <a style="text-decoration: none;" href="#appender-api">Appender API</a> </h2> <p>The DuckDB Julia package also supports the <a href="../data/appender.html">Appender API</a>, which is much faster than using prepared statements or individual <code class="language-plaintext highlighter-rouge">INSERT INTO</code> statements. Appends are made in row-wise format. For every column, an <code class="language-plaintext highlighter-rouge">append()</code> call should be made, after which the row should be finished by calling <code class="language-plaintext highlighter-rouge">flush()</code>. After all rows have been appended, <code class="language-plaintext highlighter-rouge">close()</code> should be used to finalize the Appender and clean up the resulting memory.</p> <pre class="language-julia highlighter-rouge" data-language="julia">using DuckDB, DataFrames, Dates
db = DuckDB.DB()
# create a table
DBInterface.execute(db,
    "CREATE OR REPLACE TABLE data(id INTEGER PRIMARY KEY, value FLOAT, timestamp TIMESTAMP, date DATE)")
# create data to insert
len = 100
df = DataFrames.DataFrame(
        id = collect(1:len),
        value = rand(len),
        timestamp = Dates.now() + Dates.Second.(1:len),
        date = Dates.today() + Dates.Day.(1:len)
    )
# append data by row
appender = DuckDB.Appender(db, "data")
for i in eachrow(df)
    for j in i
        DuckDB.append(appender, j)
    end
    DuckDB.end_row(appender)
end
# close the appender after all rows
DuckDB.close(appender)</pre> <h2 id="concurrency"> <a style="text-decoration: none;" href="#concurrency">Concurrency</a> </h2> <p>Within a Julia process, tasks are able to concurrently read and write to the database, as long as each task maintains its own connection to the database. In the example below, a single task is spawned to periodically read the database and many tasks are spawned to write to the database using both <a href="../sql/statements/insert.html"><code class="language-plaintext highlighter-rouge">INSERT</code> statements</a> as well as the <a href="../data/appender.html">Appender API</a>.</p> <pre class="language-julia highlighter-rouge" data-language="julia">using Dates, DataFrames, DuckDB
db = DuckDB.DB()
DBInterface.connect(db)
DBInterface.execute(db, "CREATE OR REPLACE TABLE data (date TIMESTAMP, id INTEGER)")

function run_reader(db)
    # create a DuckDB connection specifically for this task
    conn = DBInterface.connect(db)
    while true
        println(DBInterface.execute(conn,
                "SELECT id, count(date) AS count, max(date) AS max_date
                FROM data GROUP BY id ORDER BY id") |&gt; DataFrames.DataFrame)
        Threads.sleep(1)
    end
    DBInterface.close(conn)
end
# spawn one reader task
Threads.@spawn run_reader(db)

function run_inserter(db, id)
    # create a DuckDB connection specifically for this task
    conn = DBInterface.connect(db)
    for i in 1:1000
        Threads.sleep(0.01)
        DuckDB.execute(conn, "INSERT INTO data VALUES (current_timestamp, ?)"; id);
    end
    DBInterface.close(conn)
end
# spawn many insert tasks
for i in 1:100
    Threads.@spawn run_inserter(db, 1)
end

function run_appender(db, id)
    # create a DuckDB connection specifically for this task
    appender = DuckDB.Appender(db, "data")
    for i in 1:1000
        Threads.sleep(0.01)
        row = (Dates.now(Dates.UTC), id)
        for j in row
            DuckDB.append(appender, j);
        end
        DuckDB.end_row(appender);
    end
    DuckDB.close(appender);
end
# spawn many appender tasks
for i in 1:100
    Threads.@spawn run_appender(db, 2)
end</pre> <h2 id="original-julia-connector"> <a style="text-decoration: none;" href="#original-julia-connector">Original Julia Connector</a> </h2> <p>Credits to kimmolinna for the <a href="https://github.com/kimmolinna/DuckDB.jl">original DuckDB Julia connector</a>.</p><div class="_attribution">
  <p class="_attribution-p">
    &copy; Copyright 2018&ndash;2024 Stichting DuckDB Foundation<br>Licensed under the MIT License.<br>
    <a href="https://duckdb.org/docs/api/julia.html" class="_attribution-link">https://duckdb.org/docs/api/julia.html</a>
  </p>
</div>
