<h1>perlsub</h1>   <h2><a id="toc">CONTENTS</a></h2> <ul> <li> <a class="text-decoration-none" href="#NAME">NAME </a> </li> <li> <a class="text-decoration-none" href="#SYNOPSIS">SYNOPSIS</a> </li> <li> <a class="text-decoration-none" href="#DESCRIPTION">DESCRIPTION</a> <ul> <li> <a class="text-decoration-none" href="#Signatures">Signatures</a> </li> <li> <a class="text-decoration-none" href="#Private-Variables-via-my()">Private Variables via my() </a> </li> <li> <a class="text-decoration-none" href="#Persistent-Private-Variables">Persistent Private Variables </a> <ul> <li> <a class="text-decoration-none" href="#Persistent-variables-via-state()">Persistent variables via state()</a> </li> <li> <a class="text-decoration-none" href="#Persistent-variables-with-closures">Persistent variables with closures</a> </li> </ul> </li> <li> <a class="text-decoration-none" href="#Temporary-Values-via-local()">Temporary Values via local() </a> <ul> <li> <a class="text-decoration-none" href="#Grammatical-note-on-local()">Grammatical note on local() </a> </li> <li> <a class="text-decoration-none" href="#Localization-of-special-variables">Localization of special variables </a> </li> <li> <a class="text-decoration-none" href="#Localization-of-globs">Localization of globs </a> </li> <li> <a class="text-decoration-none" href="#Localization-of-elements-of-composite-types">Localization of elements of composite types </a> </li> <li> <a class="text-decoration-none" href="#Localized-deletion-of-elements-of-composite-types">Localized deletion of elements of composite types </a> </li> </ul> </li> <li> <a class="text-decoration-none" href="#Lvalue-subroutines">Lvalue subroutines </a> </li> <li> <a class="text-decoration-none" href="#Lexical-Subroutines">Lexical Subroutines </a> <ul> <li> <a class="text-decoration-none" href="#state-sub-vs-my-sub">state sub vs my sub</a> </li> <li> <a class="text-decoration-none" href="#our-subroutines">our subroutines</a> </li> </ul> </li> <li> <a class="text-decoration-none" href="#Passing-Symbol-Table-Entries-(typeglobs)">Passing Symbol Table Entries (typeglobs) </a> </li> <li> <a class="text-decoration-none" href="#When-to-Still-Use-local()">When to Still Use local() </a> </li> <li> <a class="text-decoration-none" href="#Pass-by-Reference">Pass by Reference </a> </li> <li> <a class="text-decoration-none" href="#Prototypes">Prototypes </a> </li> <li> <a class="text-decoration-none" href="#Constant-Functions">Constant Functions </a> </li> <li> <a class="text-decoration-none" href="#Overriding-Built-in-Functions">Overriding Built-in Functions </a> </li> <li> <a class="text-decoration-none" href="#Autoloading">Autoloading </a> </li> <li> <a class="text-decoration-none" href="#Subroutine-Attributes">Subroutine Attributes </a> </li> </ul> </li> <li> <a class="text-decoration-none" href="#SEE-ALSO">SEE ALSO</a> </li> </ul> <h2 id="NAME">NAME </h2> <p>perlsub - Perl subroutines</p> <h2 id="SYNOPSIS">SYNOPSIS</h2> <p>To declare subroutines: </p> <pre data-language="perl"><code>sub NAME;                       # A "forward" declaration.
sub NAME(PROTO);                #  ditto, but with prototypes
sub NAME : ATTRS;               #  with attributes
sub NAME(PROTO) : ATTRS;        #  with attributes and prototypes

sub NAME BLOCK                  # A declaration and a definition.
sub NAME(PROTO) BLOCK           #  ditto, but with prototypes
sub NAME : ATTRS BLOCK          #  with attributes
sub NAME(PROTO) : ATTRS BLOCK   #  with prototypes and attributes

use feature 'signatures';
sub NAME(SIG) BLOCK                     # with signature
sub NAME :ATTRS (SIG) BLOCK             # with signature, attributes
sub NAME :prototype(PROTO) (SIG) BLOCK  # with signature, prototype</code></pre> <p>To define an anonymous subroutine at runtime: </p> <pre data-language="perl"><code>$subref = sub BLOCK;                    # no proto
$subref = sub (PROTO) BLOCK;            # with proto
$subref = sub : ATTRS BLOCK;            # with attributes
$subref = sub (PROTO) : ATTRS BLOCK;    # with proto and attributes

use feature 'signatures';
$subref = sub (SIG) BLOCK;          # with signature
$subref = sub : ATTRS(SIG) BLOCK;   # with signature, attributes</code></pre> <p>To import subroutines: </p> <pre data-language="perl"><code>use MODULE qw(NAME1 NAME2 NAME3);</code></pre> <p>To call subroutines: </p> <pre data-language="perl"><code>NAME(LIST);     # &amp; is optional with parentheses.
NAME LIST;      # Parentheses optional if predeclared/imported.
&amp;NAME(LIST);    # Circumvent prototypes.
&amp;NAME;          # Makes current @_ visible to called subroutine.</code></pre> <h2 id="DESCRIPTION">DESCRIPTION</h2> <p>Like many languages, Perl provides for user-defined subroutines. These may be located anywhere in the main program, loaded in from other files via the <code>do</code>, <code>require</code>, or <code>use</code> keywords, or generated on the fly using <code>eval</code> or anonymous subroutines. You can even call a function indirectly using a variable containing its name or a CODE reference.</p> <p>The Perl model for function call and return values is simple: all functions are passed as parameters one single flat list of scalars, and all functions likewise return to their caller one single flat list of scalars. Any arrays or hashes in these call and return lists will collapse, losing their identities--but you may always use pass-by-reference instead to avoid this. Both call and return lists may contain as many or as few scalar elements as you'd like. (Often a function without an explicit return statement is called a subroutine, but there's really no difference from Perl's perspective.) </p> <p>In a subroutine that uses signatures (see <a href="#Signatures">"Signatures"</a> below), arguments are assigned into lexical variables introduced by the signature. In the current implementation of Perl they are also accessible in the <code>@_</code> array in the same way as for non-signature subroutines, but accessing them in this manner is now discouraged inside such a signature-using subroutine.</p> <p>In a subroutine that does not use signatures, any arguments passed in show up in the array <code>@_</code>. Therefore, if you called a function with two arguments, those would be stored in <code>$_[0]</code> and <code>$_[1]</code>. The array <code>@_</code> is a local array, but its elements are aliases for the actual scalar parameters. In particular, if an element <code>$_[0]</code> is updated, the corresponding argument is updated (or an error occurs if it is not updatable). If an argument is an array or hash element which did not exist when the function was called, that element is created only when (and if) it is modified or a reference to it is taken. (Some earlier versions of Perl created the element whether or not the element was assigned to.) Assigning to the whole array <code>@_</code> removes that aliasing, and does not update any arguments. </p> <p>When not using signatures, Perl does not otherwise provide a means to create named formal parameters. In practice all you do is assign to a <code>my()</code> list of these. Variables that aren't declared to be private are global variables. For gory details on creating private variables, see <a href="#Private-Variables-via-my%28%29">"Private Variables via my()"</a> and <a href="#Temporary-Values-via-local%28%29">"Temporary Values via local()"</a>. To create protected environments for a set of functions in a separate package (and probably a separate file), see <a href="perlmod.html#Packages">"Packages" in perlmod</a>.</p> <p>A <code>return</code> statement may be used to exit a subroutine, optionally specifying the returned value, which will be evaluated in the appropriate context (list, scalar, or void) depending on the context of the subroutine call. If you specify no return value, the subroutine returns an empty list in list context, the undefined value in scalar context, or nothing in void context. If you return one or more aggregates (arrays and hashes), these will be flattened together into one large indistinguishable list.</p> <p>If no <code>return</code> is found and if the last statement is an expression, its value is returned. If the last statement is a loop control structure like a <code>foreach</code> or a <code>while</code>, the returned value is unspecified. The empty sub returns the empty list. </p> <p>Example:</p> <pre data-language="perl"><code>sub max {
    my $max = shift(@_);
    foreach $foo (@_) {
        $max = $foo if $max &lt; $foo;
    }
    return $max;
}
$bestday = max($mon,$tue,$wed,$thu,$fri);</code></pre> <p>Example:</p> <pre data-language="perl"><code># get a line, combining continuation lines
#  that start with whitespace

sub get_line {
    $thisline = $lookahead;  # global variables!
    LINE: while (defined($lookahead = &lt;STDIN&gt;)) {
        if ($lookahead =~ /^[ \t]/) {
            $thisline .= $lookahead;
        }
        else {
            last LINE;
        }
    }
    return $thisline;
}

$lookahead = &lt;STDIN&gt;;       # get first line
while (defined($line = get_line())) {
    ...
}</code></pre> <p>Assigning to a list of private variables to name your arguments:</p> <pre data-language="perl"><code>sub maybeset {
    my($key, $value) = @_;
    $Foo{$key} = $value unless $Foo{$key};
}</code></pre> <p>Because the assignment copies the values, this also has the effect of turning call-by-reference into call-by-value. Otherwise a function is free to do in-place modifications of <code>@_</code> and change its caller's values. </p> <pre data-language="perl"><code>upcase_in($v1, $v2);  # this changes $v1 and $v2
sub upcase_in {
    for (@_) { tr/a-z/A-Z/ }
}</code></pre> <p>You aren't allowed to modify constants in this way, of course. If an argument were actually literal and you tried to change it, you'd take a (presumably fatal) exception. For example, this won't work: </p> <pre data-language="perl"><code>upcase_in("frederick");</code></pre> <p>It would be much safer if the <code>upcase_in()</code> function were written to return a copy of its parameters instead of changing them in place:</p> <pre data-language="perl"><code>($v3, $v4) = upcase($v1, $v2);  # this doesn't change $v1 and $v2
sub upcase {
    return unless defined wantarray;  # void context, do nothing
    my @parms = @_;
    for (@parms) { tr/a-z/A-Z/ }
    return wantarray ? @parms : $parms[0];
}</code></pre> <p>Notice how this (unprototyped) function doesn't care whether it was passed real scalars or arrays. Perl sees all arguments as one big, long, flat parameter list in <code>@_</code>. This is one area where Perl's simple argument-passing style shines. The <code>upcase()</code> function would work perfectly well without changing the <code>upcase()</code> definition even if we fed it things like this:</p> <pre data-language="perl"><code>@newlist   = upcase(@list1, @list2);
@newlist   = upcase( split /:/, $var );</code></pre> <p>Do not, however, be tempted to do this:</p> <pre data-language="perl"><code>(@x, @y)   = upcase(@list1, @list2);</code></pre> <p>Like the flattened incoming parameter list, the return list is also flattened on return. So all you have managed to do here is stored everything in <code>@x</code> and made <code>@y</code> empty. See <a href="#Pass-by-Reference">"Pass by Reference"</a> for alternatives.</p> <p>A subroutine may be called using an explicit <code>&amp;</code> prefix. The <code>&amp;</code> is optional in modern Perl, as are parentheses if the subroutine has been predeclared. The <code>&amp;</code> is <i>not</i> optional when just naming the subroutine, such as when it's used as an argument to defined() or undef(). Nor is it optional when you want to do an indirect subroutine call with a subroutine name or reference using the <code>&amp;$subref()</code> or <code>&amp;{$subref}()</code> constructs, although the <code>$subref-&gt;()</code> notation solves that problem. See <a href="perlref.html">perlref</a> for more about all that. </p> <p>Subroutines may be called recursively. If a subroutine is called using the <code>&amp;</code> form, the argument list is optional, and if omitted, no <code>@_</code> array is set up for the subroutine: the <code>@_</code> array at the time of the call is visible to subroutine instead. This is an efficiency mechanism that new users may wish to avoid. </p> <pre data-language="perl"><code>&amp;foo(1,2,3);        # pass three arguments
foo(1,2,3);         # the same

foo();              # pass a null list
&amp;foo();             # the same

&amp;foo;               # foo() get current args, like foo(@_) !!
use strict 'subs';
foo;                # like foo() iff sub foo predeclared, else
                    # a compile-time error
no strict 'subs';
foo;                # like foo() iff sub foo predeclared, else
                    # a literal string "foo"</code></pre> <p>Not only does the <code>&amp;</code> form make the argument list optional, it also disables any prototype checking on arguments you do provide. This is partly for historical reasons, and partly for having a convenient way to cheat if you know what you're doing. See <a href="#Prototypes">"Prototypes"</a> below. </p> <p>Since Perl 5.16.0, the <code>__SUB__</code> token is available under <code>use feature 'current_sub'</code> and <code>use v5.16</code>. It will evaluate to a reference to the currently-running sub, which allows for recursive calls without knowing your subroutine's name.</p> <pre data-language="perl"><code>use v5.16;
my $factorial = sub {
    my ($x) = @_;
    return 1 if $x == 1;
    return($x * __SUB__-&gt;( $x - 1 ) );
};</code></pre> <p>The behavior of <code>__SUB__</code> within a regex code block (such as <code>/(?{...})/</code>) is subject to change.</p> <p>Subroutines whose names are in all upper case are reserved to the Perl core, as are modules whose names are in all lower case. A subroutine in all capitals is a loosely-held convention meaning it will be called indirectly by the run-time system itself, usually due to a triggered event. Subroutines whose name start with a left parenthesis are also reserved the same way. The following is a list of some subroutines that currently do special, pre-defined things.</p> <dl> <dt id="documented-later-in-this-document">
documented later in this document</dt> <dd> <p><code>AUTOLOAD</code></p> </dd> <dt id="documented-in-perlmod">
documented in <a href="perlmod.html">perlmod</a>
</dt> <dd> <p><code>CLONE</code>, <code>CLONE_SKIP</code></p> </dd> <dt id="documented-in-perlobj">
documented in <a href="perlobj.html">perlobj</a>
</dt> <dd> <p><code>DESTROY</code>, <code>DOES</code></p> </dd> <dt id="documented-in-perltie">
documented in <a href="perltie.html">perltie</a>
</dt> <dd> <p><code>BINMODE</code>, <code>CLEAR</code>, <code>CLOSE</code>, <code>DELETE</code>, <code>DESTROY</code>, <code>EOF</code>, <code>EXISTS</code>, <code>EXTEND</code>, <code>FETCH</code>, <code>FETCHSIZE</code>, <code>FILENO</code>, <code>FIRSTKEY</code>, <code>GETC</code>, <code>NEXTKEY</code>, <code>OPEN</code>, <code>POP</code>, <code>PRINT</code>, <code>PRINTF</code>, <code>PUSH</code>, <code>READ</code>, <code>READLINE</code>, <code>SCALAR</code>, <code>SEEK</code>, <code>SHIFT</code>, <code>SPLICE</code>, <code>STORE</code>, <code>STORESIZE</code>, <code>TELL</code>, <code>TIEARRAY</code>, <code>TIEHANDLE</code>, <code>TIEHASH</code>, <code>TIESCALAR</code>, <code>UNSHIFT</code>, <code>UNTIE</code>, <code>WRITE</code></p> </dd> <dt id="documented-in-PerlIO::via">
documented in <a href="PerlIO::via.html">PerlIO::via</a>
</dt> <dd> <p><code>BINMODE</code>, <code>CLEARERR</code>, <code>CLOSE</code>, <code>EOF</code>, <code>ERROR</code>, <code>FDOPEN</code>, <code>FILENO</code>, <code>FILL</code>, <code>FLUSH</code>, <code>OPEN</code>, <code>POPPED</code>, <code>PUSHED</code>, <code>READ</code>, <code>SEEK</code>, <code>SETLINEBUF</code>, <code>SYSOPEN</code>, <code>TELL</code>, <code>UNREAD</code>, <code>UTF8</code>, <code>WRITE</code></p> </dd> <dt id="documented-in-perlfunc">
documented in <a href="perlfunc.html">perlfunc</a>
</dt> <dd> <p><a href="perlfunc.html#use"><code>import</code></a>, <a href="perlfunc.html#use"><code>unimport</code></a>, <a href="perlfunc.html#require"><code>INC</code></a></p> </dd> <dt id="documented-in-UNIVERSAL">
documented in <a href="universal.html">UNIVERSAL</a>
</dt> <dd> <p><code>VERSION</code></p> </dd> <dt id="documented-in-perldebguts">
documented in <a href="perldebguts.html">perldebguts</a>
</dt> <dd> <p><code>DB::DB</code>, <code>DB::sub</code>, <code>DB::lsub</code>, <code>DB::goto</code>, <code>DB::postponed</code></p> </dd> <dt id="undocumented,-used-internally-by-the-overload-feature">
undocumented, used internally by the <a href="overload.html">overload</a> feature</dt> <dd> <p>any starting with <code>(</code></p> </dd> </dl> <p>The <code>BEGIN</code>, <code>UNITCHECK</code>, <code>CHECK</code>, <code>INIT</code> and <code>END</code> subroutines are not so much subroutines as named special code blocks, of which you can have more than one in a package, and which you can <b>not</b> call explicitly. See <a href="perlmod.html#BEGIN%2C-UNITCHECK%2C-CHECK%2C-INIT-and-END">"BEGIN, UNITCHECK, CHECK, INIT and END" in perlmod</a></p> <h3 id="Signatures">Signatures</h3>  <p>Perl has a facility to allow a subroutine's formal parameters to be declared by special syntax, separate from the procedural code of the subroutine body. The formal parameter list is known as a <i>signature</i>.</p> <p>This facility must be enabled before it can be used. It is enabled automatically by a <code>use v5.36</code> (or higher) declaration, or more directly by <code>use feature 'signatures'</code>, in the current scope.</p> <p>The signature is part of a subroutine's body. Normally the body of a subroutine is simply a braced block of code, but when using a signature, the signature is a parenthesised list that goes immediately before the block, after any name or attributes.</p> <p>For example,</p> <pre data-language="perl"><code>sub foo :lvalue ($x, $y = 1, @z) { .... }</code></pre> <p>The signature declares lexical variables that are in scope for the block. When the subroutine is called, the signature takes control first. It populates the signature variables from the list of arguments that were passed. If the argument list doesn't meet the requirements of the signature, then it will throw an exception. When the signature processing is complete, control passes to the block.</p> <p>Positional parameters are handled by simply naming scalar variables in the signature. For example,</p> <pre data-language="perl"><code>sub foo ($left, $right) {
    return $left + $right;
}</code></pre> <p>takes two positional parameters, which must be filled at runtime by two arguments. By default the parameters are mandatory, and it is not permitted to pass more arguments than expected. So the above is equivalent to</p> <pre data-language="perl"><code>sub foo {
    die "Too many arguments for subroutine" unless @_ &lt;= 2;
    die "Too few arguments for subroutine" unless @_ &gt;= 2;
    my $left = $_[0];
    my $right = $_[1];
    return $left + $right;
}</code></pre> <p>An argument can be ignored by omitting the main part of the name from a parameter declaration, leaving just a bare <code>$</code> sigil. For example,</p> <pre data-language="perl"><code>sub foo ($first, $, $third) {
    return "first=$first, third=$third";
}</code></pre> <p>Although the ignored argument doesn't go into a variable, it is still mandatory for the caller to pass it.</p> <p>A positional parameter is made optional by giving a default value, separated from the parameter name by <code>=</code>:</p> <pre data-language="perl"><code>sub foo ($left, $right = 0) {
    return $left + $right;
}</code></pre> <p>The above subroutine may be called with either one or two arguments. The default value expression is evaluated when the subroutine is called, so it may provide different default values for different calls. It is only evaluated if the argument was actually omitted from the call. For example,</p> <pre data-language="perl"><code>my $auto_id = 0;
sub foo ($thing, $id = $auto_id++) {
    print "$thing has ID $id";
}</code></pre> <p>automatically assigns distinct sequential IDs to things for which no ID was supplied by the caller. A default value expression may also refer to parameters earlier in the signature, making the default for one parameter vary according to the earlier parameters. For example,</p> <pre data-language="perl"><code>sub foo ($first_name, $surname, $nickname = $first_name) {
    print "$first_name $surname is known as \"$nickname\"";
}</code></pre> <p>A default value expression can also be written using the <code>//=</code> operator, where it will be evaluated and used if the caller omitted a value or the value provided was <code>undef</code>.</p> <pre data-language="perl"><code>sub foo ($name //= "world") {
    print "Hello, $name";
}

foo(undef);  # will print "Hello, world"</code></pre> <p>Similarly, the <code>||=</code> operator can be used to provide a default expression to be used whenever the caller provided a false value (and remember that a missing or <code>undef</code> value are also false).</p> <pre data-language="perl"><code>sub foo ($x ||= 10) {
    return 5 + $x;
}</code></pre> <p>An optional parameter can be nameless just like a mandatory parameter. For example,</p> <pre data-language="perl"><code>sub foo ($thing, $ = 1) {
    print $thing;
}</code></pre> <p>The parameter's default value will still be evaluated if the corresponding argument isn't supplied, even though the value won't be stored anywhere. This is in case evaluating it has important side effects. However, it will be evaluated in void context, so if it doesn't have side effects and is not trivial it will generate a warning if the "void" warning category is enabled. If a nameless optional parameter's default value is not important, it may be omitted just as the parameter's name was:</p> <pre data-language="perl"><code>sub foo ($thing, $=) {
    print $thing;
}</code></pre> <p>Optional positional parameters must come after all mandatory positional parameters. (If there are no mandatory positional parameters then an optional positional parameters can be the first thing in the signature.) If there are multiple optional positional parameters and not enough arguments are supplied to fill them all, they will be filled from left to right.</p> <p>After positional parameters, additional arguments may be captured in a slurpy parameter. The simplest form of this is just an array variable:</p> <pre data-language="perl"><code>sub foo ($filter, @inputs) {
    print $filter-&gt;($_) foreach @inputs;
}</code></pre> <p>With a slurpy parameter in the signature, there is no upper limit on how many arguments may be passed. A slurpy array parameter may be nameless just like a positional parameter, in which case its only effect is to turn off the argument limit that would otherwise apply:</p> <pre data-language="perl"><code>sub foo ($thing, @) {
    print $thing;
}</code></pre> <p>A slurpy parameter may instead be a hash, in which case the arguments available to it are interpreted as alternating keys and values. There must be as many keys as values: if there is an odd argument then an exception will be thrown. Keys will be stringified, and if there are duplicates then the later instance takes precedence over the earlier, as with standard hash construction.</p> <pre data-language="perl"><code>sub foo ($filter, %inputs) {
    print $filter-&gt;($_, $inputs{$_}) foreach sort keys %inputs;
}</code></pre> <p>A slurpy hash parameter may be nameless just like other kinds of parameter. It still insists that the number of arguments available to it be even, even though they're not being put into a variable.</p> <pre data-language="perl"><code>sub foo ($thing, %) {
    print $thing;
}</code></pre> <p>A slurpy parameter, either array or hash, must be the last thing in the signature. It may follow mandatory and optional positional parameters; it may also be the only thing in the signature. Slurpy parameters cannot have default values: if no arguments are supplied for them then you get an empty array or empty hash.</p> <p>A signature may be entirely empty, in which case all it does is check that the caller passed no arguments:</p> <pre data-language="perl"><code>sub foo () {
    return 123;
}</code></pre> <p>Prior to Perl 5.36 these were considered experimental, and emitted a warning in the <code>experimental::signatures</code> category. From Perl 5.36 onwards this no longer happens, though the warning category still exists for back-compatibility with code that attempts to disable it with a statement such as:</p> <pre data-language="perl"><code>no warnings 'experimental::signatures';</code></pre> <p>In the current Perl implementation, when using a signature the arguments are still also available in the special array variable <code>@_</code>. However, accessing them via this array is now discouraged, and should not be relied upon in newly-written code as this ability may change in a future version. Code that attempts to access the <code>@_</code> array will produce warnings in the <code>experimental::args_array_with_signatures</code> category when compiled:</p> <pre data-language="perl"><code>sub f ($x) {
    # This line emits the warning seen below
    print "Arguments are @_";
}</code></pre>  <pre data-language="perl"><code>Use of @_ in join or string with signatured subroutine is
experimental at ...</code></pre> <p>There is a difference between the two ways of accessing the arguments: <code>@_</code> <i>aliases</i> the arguments, but the signature variables get <i>copies</i> of the arguments. So writing to a signature variable only changes that variable, and has no effect on the caller's variables, but writing to an element of <code>@_</code> modifies whatever the caller used to supply that argument.</p> <p>There is a potential syntactic ambiguity between signatures and prototypes (see <a href="#Prototypes">"Prototypes"</a>), because both start with an opening parenthesis and both can appear in some of the same places, such as just after the name in a subroutine declaration. For historical reasons, when signatures are not enabled, any opening parenthesis in such a context will trigger very forgiving prototype parsing. Most signatures will be interpreted as prototypes in those circumstances, but won't be valid prototypes. (A valid prototype cannot contain any alphabetic character.) This will lead to somewhat confusing error messages.</p> <p>To avoid ambiguity, when signatures are enabled the special syntax for prototypes is disabled. There is no attempt to guess whether a parenthesised group was intended to be a prototype or a signature. To give a subroutine a prototype under these circumstances, use a <a href="attributes.html#Built-in-Attributes">prototype attribute</a>. For example,</p> <pre data-language="perl"><code>sub foo :prototype($) { $_[0] }</code></pre> <p>It is entirely possible for a subroutine to have both a prototype and a signature. They do different jobs: the prototype affects compilation of calls to the subroutine, and the signature puts argument values into lexical variables at runtime. You can therefore write</p> <pre data-language="perl"><code>sub foo :prototype($$) ($left, $right) {
    return $left + $right;
}</code></pre> <p>The prototype attribute, and any other attributes, must come before the signature. The signature always immediately precedes the block of the subroutine's body.</p> <h3 id="Private-Variables-via-my()">
Private Variables via my() </h3> <p>Synopsis:</p> <pre data-language="perl"><code>my $foo;            # declare $foo lexically local
my (@wid, %get);    # declare list of variables local
my $foo = "flurp";  # declare $foo lexical, and init it
my @oof = @bar;     # declare @oof lexical, and init it
my $x : Foo = $y;   # similar, with an attribute applied</code></pre> <p><b>WARNING</b>: The use of attribute lists on <code>my</code> declarations is still evolving. The current semantics and interface are subject to change. See <a href="attributes.html">attributes</a> and <a href="Attribute::Handlers.html">Attribute::Handlers</a>.</p> <p>The <code>my</code> operator declares the listed variables to be lexically confined to the enclosing block, conditional (<code>if</code>/<code>unless</code>/<code>elsif</code>/<code>else</code>), loop (<code>for</code>/<code>foreach</code>/<code>while</code>/<code>until</code>/<code>continue</code>), subroutine, <code>eval</code>, or <code>do</code>/<code>require</code>/<code>use</code>'d file. If more than one value is listed, the list must be placed in parentheses. All listed elements must be legal lvalues. Only alphanumeric identifiers may be lexically scoped--magical built-ins like <code>$/</code> must currently be <code>local</code>ized with <code>local</code> instead.</p> <p>Unlike dynamic variables created by the <code>local</code> operator, lexical variables declared with <code>my</code> are totally hidden from the outside world, including any called subroutines. This is true if it's the same subroutine called from itself or elsewhere--every call gets its own copy. </p> <p>This doesn't mean that a <code>my</code> variable declared in a statically enclosing lexical scope would be invisible. Only dynamic scopes are cut off. For example, the <code>bumpx()</code> function below has access to the lexical $x variable because both the <code>my</code> and the <code>sub</code> occurred at the same scope, presumably file scope.</p> <pre data-language="perl"><code>my $x = 10;
sub bumpx { $x++ }</code></pre> <p>An <code>eval()</code>, however, can see lexical variables of the scope it is being evaluated in, so long as the names aren't hidden by declarations within the <code>eval()</code> itself. See <a href="perlref.html">perlref</a>. </p> <p>The parameter list to my() may be assigned to if desired, which allows you to initialize your variables. (If no initializer is given for a particular variable, it is created with the undefined value.) Commonly this is used to name input parameters to a subroutine. Examples:</p> <pre data-language="perl"><code>$arg = "fred";          # "global" variable
$n = cube_root(27);
print "$arg thinks the root is $n\n";
# outputs: fred thinks the root is 3

sub cube_root {
    my $arg = shift;  # name doesn't matter
    $arg **= 1/3;
    return $arg;
}</code></pre> <p>The <code>my</code> is simply a modifier on something you might assign to. So when you do assign to variables in its argument list, <code>my</code> doesn't change whether those variables are viewed as a scalar or an array. So</p> <pre data-language="perl"><code>my ($foo) = &lt;STDIN&gt;;                # WRONG?
my @FOO = &lt;STDIN&gt;;</code></pre> <p>both supply a list context to the right-hand side, while</p> <pre data-language="perl"><code>my $foo = &lt;STDIN&gt;;</code></pre> <p>supplies a scalar context. But the following declares only one variable:</p> <pre data-language="perl"><code>my $foo, $bar = 1;                  # WRONG</code></pre> <p>That has the same effect as</p> <pre data-language="perl"><code>my $foo;
$bar = 1;</code></pre> <p>The declared variable is not introduced (is not visible) until after the current statement. Thus,</p> <pre data-language="perl"><code>my $x = $x;</code></pre> <p>can be used to initialize a new $x with the value of the old $x, and the expression</p> <pre data-language="perl"><code>my $x = 123 and $x == 123</code></pre> <p>is false unless the old $x happened to have the value <code>123</code>.</p> <p>Lexical scopes of control structures are not bounded precisely by the braces that delimit their controlled blocks; control expressions are part of that scope, too. Thus in the loop</p> <pre data-language="perl"><code>while (my $line = &lt;&gt;) {
    $line = lc $line;
} continue {
    print $line;
}</code></pre> <p>the scope of $line extends from its declaration throughout the rest of the loop construct (including the <code>continue</code> clause), but not beyond it. Similarly, in the conditional</p> <pre data-language="perl"><code>if ((my $answer = &lt;STDIN&gt;) =~ /^yes$/i) {
    user_agrees();
} elsif ($answer =~ /^no$/i) {
    user_disagrees();
} else {
    chomp $answer;
    die "'$answer' is neither 'yes' nor 'no'";
}</code></pre> <p>the scope of $answer extends from its declaration through the rest of that conditional, including any <code>elsif</code> and <code>else</code> clauses, but not beyond it. See <a href="perlsyn.html#Simple-Statements">"Simple Statements" in perlsyn</a> for information on the scope of variables in statements with modifiers.</p> <p>The <code>foreach</code> loop defaults to scoping its index variable dynamically in the manner of <code>local</code>. However, if the index variable is prefixed with the keyword <code>my</code>, or if there is already a lexical by that name in scope, then a new lexical is created instead. Thus in the loop </p> <pre data-language="perl"><code>for my $i (1, 2, 3) {
    some_function();
}</code></pre> <p>the scope of $i extends to the end of the loop, but not beyond it, rendering the value of $i inaccessible within <code>some_function()</code>. </p> <p>Some users may wish to encourage the use of lexically scoped variables. As an aid to catching implicit uses to package variables, which are always global, if you say</p> <pre data-language="perl"><code>use strict 'vars';</code></pre> <p>then any variable mentioned from there to the end of the enclosing block must either refer to a lexical variable, be predeclared via <code>our</code> or <code>use vars</code>, or else must be fully qualified with the package name. A compilation error results otherwise. An inner block may countermand this with <code>no strict 'vars'</code>.</p> <p>A <code>my</code> has both a compile-time and a run-time effect. At compile time, the compiler takes notice of it. The principal usefulness of this is to quiet <code>use strict 'vars'</code>, but it is also essential for generation of closures as detailed in <a href="perlref.html">perlref</a>. Actual initialization is delayed until run time, though, so it gets executed at the appropriate time, such as each time through a loop, for example.</p> <p>Variables declared with <code>my</code> are not part of any package and are therefore never fully qualified with the package name. In particular, you're not allowed to try to make a package variable (or other global) lexical:</p> <pre data-language="perl"><code>my $pack::var;      # ERROR!  Illegal syntax</code></pre> <p>In fact, a dynamic variable (also known as package or global variables) are still accessible using the fully qualified <code>::</code> notation even while a lexical of the same name is also visible:</p> <pre data-language="perl"><code>package main;
local $x = 10;
my    $x = 20;
print "$x and $::x\n";</code></pre> <p>That will print out <code>20</code> and <code>10</code>.</p> <p>You may declare <code>my</code> variables at the outermost scope of a file to hide any such identifiers from the world outside that file. This is similar in spirit to C's static variables when they are used at the file level. To do this with a subroutine requires the use of a closure (an anonymous function that accesses enclosing lexicals). If you want to create a private subroutine that cannot be called from outside that block, it can declare a lexical variable containing an anonymous sub reference:</p> <pre data-language="perl"><code>my $secret_version = '1.001-beta';
my $secret_sub = sub { print $secret_version };
&amp;$secret_sub();</code></pre> <p>As long as the reference is never returned by any function within the module, no outside module can see the subroutine, because its name is not in any package's symbol table. Remember that it's not <i>REALLY</i> called <code>$some_pack::secret_version</code> or anything; it's just $secret_version, unqualified and unqualifiable.</p> <p>This does not work with object methods, however; all object methods have to be in the symbol table of some package to be found. See <a href="perlref.html#Function-Templates">"Function Templates" in perlref</a> for something of a work-around to this.</p> <h3 id="Persistent-Private-Variables">
Persistent Private Variables </h3> <p>There are two ways to build persistent private variables in Perl 5.10. First, you can simply use the <code>state</code> feature. Or, you can use closures, if you want to stay compatible with releases older than 5.10.</p> <h4 id="Persistent-variables-via-state()">
Persistent variables via state()</h4> <p>Beginning with Perl 5.10.0, you can declare variables with the <code>state</code> keyword in place of <code>my</code>. For that to work, though, you must have enabled that feature beforehand, either by using the <code>feature</code> pragma, or by using <code>-E</code> on one-liners (see <a href="feature.html">feature</a>). Beginning with Perl 5.16, the <code>CORE::state</code> form does not require the <code>feature</code> pragma.</p> <p>The <code>state</code> keyword creates a lexical variable (following the same scoping rules as <code>my</code>) that persists from one subroutine call to the next. If a state variable resides inside an anonymous subroutine, then each copy of the subroutine has its own copy of the state variable. However, the value of the state variable will still persist between calls to the same copy of the anonymous subroutine. (Don't forget that <code>sub { ... }</code> creates a new subroutine each time it is executed.)</p> <p>For example, the following code maintains a private counter, incremented each time the gimme_another() function is called:</p> <pre data-language="perl"><code>use feature 'state';
sub gimme_another { state $x; return ++$x }</code></pre> <p>And this example uses anonymous subroutines to create separate counters:</p> <pre data-language="perl"><code>use feature 'state';
sub create_counter {
    return sub { state $x; return ++$x }
}</code></pre> <p>Also, since <code>$x</code> is lexical, it can't be reached or modified by any Perl code outside.</p> <p>When combined with variable declaration, simple assignment to <code>state</code> variables (as in <code>state $x = 42</code>) is executed only the first time. When such statements are evaluated subsequent times, the assignment is ignored. The behavior of assignment to <code>state</code> declarations where the left hand side of the assignment involves any parentheses is currently undefined.</p> <h4 id="Persistent-variables-with-closures">
Persistent variables with closures</h4> <p>Just because a lexical variable is lexically (also called statically) scoped to its enclosing block, <code>eval</code>, or <code>do</code> FILE, this doesn't mean that within a function it works like a C static. It normally works more like a C auto, but with implicit garbage collection.</p> <p>Unlike local variables in C or C++, Perl's lexical variables don't necessarily get recycled just because their scope has exited. If something more permanent is still aware of the lexical, it will stick around. So long as something else references a lexical, that lexical won't be freed--which is as it should be. You wouldn't want memory being free until you were done using it, or kept around once you were done. Automatic garbage collection takes care of this for you.</p> <p>This means that you can pass back or save away references to lexical variables, whereas to return a pointer to a C auto is a grave error. It also gives us a way to simulate C's function statics. Here's a mechanism for giving a function private variables with both lexical scoping and a static lifetime. If you do want to create something like C's static variables, just enclose the whole function in an extra block, and put the static variable outside the function but in the block.</p> <pre data-language="perl"><code>{
    my $secret_val = 0;
    sub gimme_another {
        return ++$secret_val;
    }
}
# $secret_val now becomes unreachable by the outside
# world, but retains its value between calls to gimme_another</code></pre> <p>If this function is being sourced in from a separate file via <code>require</code> or <code>use</code>, then this is probably just fine. If it's all in the main program, you'll need to arrange for the <code>my</code> to be executed early, either by putting the whole block above your main program, or more likely, placing merely a <code>BEGIN</code> code block around it to make sure it gets executed before your program starts to run:</p> <pre data-language="perl"><code>BEGIN {
    my $secret_val = 0;
    sub gimme_another {
        return ++$secret_val;
    }
}</code></pre> <p>See <a href="perlmod.html#BEGIN%2C-UNITCHECK%2C-CHECK%2C-INIT-and-END">"BEGIN, UNITCHECK, CHECK, INIT and END" in perlmod</a> about the special triggered code blocks, <code>BEGIN</code>, <code>UNITCHECK</code>, <code>CHECK</code>, <code>INIT</code> and <code>END</code>.</p> <p>If declared at the outermost scope (the file scope), then lexicals work somewhat like C's file statics. They are available to all functions in that same file declared below them, but are inaccessible from outside that file. This strategy is sometimes used in modules to create private variables that the whole module can see.</p> <h3 id="Temporary-Values-via-local()">
Temporary Values via local() </h3> <p><b>WARNING</b>: In general, you should be using <code>my</code> instead of <code>local</code>, because it's faster and safer. Exceptions to this include the global punctuation variables, global filehandles and formats, and direct manipulation of the Perl symbol table itself. <code>local</code> is mostly used when the current value of a variable must be visible to called subroutines.</p> <p>Synopsis:</p> <pre data-language="perl"><code># localization of values

local $foo;                # make $foo dynamically local
local (@wid, %get);        # make list of variables local
local $foo = "flurp";      # make $foo dynamic, and init it
local @oof = @bar;        # make @oof dynamic, and init it

local $hash{key} = "val";  # sets a local value for this hash entry
delete local $hash{key};   # delete this entry for the current block
local ($cond ? $v1 : $v2); # several types of lvalues support
                           # localization

# localization of symbols

local *FH;                 # localize $FH, @FH, %FH, &amp;FH  ...
local *merlyn = *randal;   # now $merlyn is really $randal, plus
                           #     @merlyn is really @randal, etc
local *merlyn = 'randal';  # SAME THING: promote 'randal' to *randal
local *merlyn = \$randal;  # just alias $merlyn, not @merlyn etc</code></pre> <p>A <code>local</code> modifies its listed variables to be "local" to the enclosing block, <code>eval</code>, or <code>do FILE</code>--and to <i>any subroutine called from within that block</i>. A <code>local</code> just gives temporary values to global (meaning package) variables. It does <i>not</i> create a local variable. This is known as dynamic scoping. Lexical scoping is done with <code>my</code>, which works more like C's auto declarations.</p> <p>Some types of lvalues can be localized as well: hash and array elements and slices, conditionals (provided that their result is always localizable), and symbolic references. As for simple variables, this creates new, dynamically scoped values.</p> <p>If more than one variable or expression is given to <code>local</code>, they must be placed in parentheses. This operator works by saving the current values of those variables in its argument list on a hidden stack and restoring them upon exiting the block, subroutine, or eval. This means that called subroutines can also reference the local variable, but not the global one. The argument list may be assigned to if desired, which allows you to initialize your local variables. (If no initializer is given for a particular variable, it is created with an undefined value.)</p> <p>Because <code>local</code> is a run-time operator, it gets executed each time through a loop. Consequently, it's more efficient to localize your variables outside the loop.</p> <h4 id="Grammatical-note-on-local()">
Grammatical note on local() </h4> <p>A <code>local</code> is simply a modifier on an lvalue expression. When you assign to a <code>local</code>ized variable, the <code>local</code> doesn't change whether its list is viewed as a scalar or an array. So</p> <pre data-language="perl"><code>local($foo) = &lt;STDIN&gt;;
local @FOO = &lt;STDIN&gt;;</code></pre> <p>both supply a list context to the right-hand side, while</p> <pre data-language="perl"><code>local $foo = &lt;STDIN&gt;;</code></pre> <p>supplies a scalar context.</p> <h4 id="Localization-of-special-variables">
Localization of special variables </h4> <p>If you localize a special variable, you'll be giving a new value to it, but its magic won't go away. That means that all side-effects related to this magic still work with the localized value.</p> <p>This feature allows code like this to work :</p> <pre data-language="perl"><code># Read the whole contents of FILE in $slurp
{ local $/ = undef; $slurp = &lt;FILE&gt;; }</code></pre> <p>Note, however, that this restricts localization of some values ; for example, the following statement dies, as of Perl 5.10.0, with an error <i>Modification of a read-only value attempted</i>, because the $1 variable is magical and read-only :</p> <pre data-language="perl"><code>local $1 = 2;</code></pre> <p>One exception is the default scalar variable: starting with Perl 5.14 <code>local($_)</code> will always strip all magic from $_, to make it possible to safely reuse $_ in a subroutine.</p> <p><b>WARNING</b>: Localization of tied arrays and hashes does not currently work as described. This will be fixed in a future release of Perl; in the meantime, avoid code that relies on any particular behavior of localising tied arrays or hashes (localising individual elements is still okay). See <a href="https://perldoc.perl.org/5.38.0/perl58delta#Localising-Tied-Arrays-and-Hashes-Is-Broken">"Localising Tied Arrays and Hashes Is Broken" in perl58delta</a> for more details. </p> <h4 id="Localization-of-globs">
Localization of globs </h4> <p>The construct</p> <pre data-language="perl"><code>local *name;</code></pre> <p>creates a whole new symbol table entry for the glob <code>name</code> in the current package. That means that all variables in its glob slot ($name, @name, %name, &amp;name, and the <code>name</code> filehandle) are dynamically reset.</p> <p>This implies, among other things, that any magic eventually carried by those variables is locally lost. In other words, saying <code>local */</code> will not have any effect on the internal value of the input record separator.</p> <h4 id="Localization-of-elements-of-composite-types">
Localization of elements of composite types </h4> <p>It's also worth taking a moment to explain what happens when you <code>local</code>ize a member of a composite type (i.e. an array or hash element). In this case, the element is <code>local</code>ized <i>by name</i>. This means that when the scope of the <code>local()</code> ends, the saved value will be restored to the hash element whose key was named in the <code>local()</code>, or the array element whose index was named in the <code>local()</code>. If that element was deleted while the <code>local()</code> was in effect (e.g. by a <code>delete()</code> from a hash or a <code>shift()</code> of an array), it will spring back into existence, possibly extending an array and filling in the skipped elements with <code>undef</code>. For instance, if you say</p> <pre data-language="perl"><code>%hash = ( 'This' =&gt; 'is', 'a' =&gt; 'test' );
@ary  = ( 0..5 );
{
    local($ary[5]) = 6;
    local($hash{'a'}) = 'drill';
    while (my $e = pop(@ary)) {
        print "$e . . .\n";
        last unless $e &gt; 3;
    }
    if (@ary) {
        $hash{'only a'} = 'test';
        delete $hash{'a'};
    }
}
print join(' ', map { "$_ $hash{$_}" } sort keys %hash),".\n";
print "The array has ",scalar(@ary)," elements: ",
    join(', ', map { defined $_ ? $_ : 'undef' } @ary),"\n";</code></pre> <p>Perl will print</p> <pre data-language="perl"><code class="plaintext">6 . . .
4 . . .
3 . . .
This is a test only a test.
The array has 6 elements: 0, 1, 2, undef, undef, 5</code></pre> <p>The behavior of local() on non-existent members of composite types is subject to change in future. The behavior of local() on array elements specified using negative indexes is particularly surprising, and is very likely to change.</p> <h4 id="Localized-deletion-of-elements-of-composite-types">
Localized deletion of elements of composite types </h4> <p>You can use the <code>delete local $array[$idx]</code> and <code>delete local $hash{key}</code> constructs to delete a composite type entry for the current block and restore it when it ends. They return the array/hash value before the localization, which means that they are respectively equivalent to</p> <pre data-language="perl"><code>do {
    my $val = $array[$idx];
    local  $array[$idx];
    delete $array[$idx];
    $val
}</code></pre> <p>and</p> <pre data-language="perl"><code>do {
    my $val = $hash{key};
    local  $hash{key};
    delete $hash{key};
    $val
}</code></pre> <p>except that for those the <code>local</code> is scoped to the <code>do</code> block. Slices are also accepted.</p> <pre data-language="perl"><code>my %hash = (
    a =&gt; [ 7, 8, 9 ],
    b =&gt; 1,
)

{
    my $x = delete local $hash{a};
    # $x is [ 7, 8, 9 ]
    # %hash is (b =&gt; 1)

    {
        my @nums = delete local @$x[0, 2]
        # @nums is (7, 9)
        # $x is [ undef, 8 ]

        $x[0] = 999; # will be erased when the scope ends
    }
    # $x is back to [ 7, 8, 9 ]

}
# %hash is back to its original state</code></pre> <p>This construct is supported since Perl v5.12.</p> <h3 id="Lvalue-subroutines">
Lvalue subroutines </h3> <p>It is possible to return a modifiable value from a subroutine. To do this, you have to declare the subroutine to return an lvalue.</p> <pre data-language="perl"><code>my $val;
sub canmod : lvalue {
    $val;  # or:  return $val;
}
sub nomod {
    $val;
}

canmod() = 5;   # assigns to $val
nomod()  = 5;   # ERROR</code></pre> <p>The scalar/list context for the subroutine and for the right-hand side of assignment is determined as if the subroutine call is replaced by a scalar. For example, consider:</p> <pre data-language="perl"><code>data(2,3) = get_data(3,4);</code></pre> <p>Both subroutines here are called in a scalar context, while in:</p> <pre data-language="perl"><code>(data(2,3)) = get_data(3,4);</code></pre> <p>and in:</p> <pre data-language="perl"><code>(data(2),data(3)) = get_data(3,4);</code></pre> <p>all the subroutines are called in a list context.</p> <p>Lvalue subroutines are convenient, but you have to keep in mind that, when used with objects, they may violate encapsulation. A normal mutator can check the supplied argument before setting the attribute it is protecting, an lvalue subroutine cannot. If you require any special processing when storing and retrieving the values, consider using the CPAN module Sentinel or something similar.</p> <h3 id="Lexical-Subroutines">
Lexical Subroutines </h3> <p>Beginning with Perl 5.18, you can declare a private subroutine with <code>my</code> or <code>state</code>. As with state variables, the <code>state</code> keyword is only available under <code>use feature 'state'</code> or <code>use v5.10</code> or higher.</p> <p>Prior to Perl 5.26, lexical subroutines were deemed experimental and were available only under the <code>use feature 'lexical_subs'</code> pragma. They also produced a warning unless the "experimental::lexical_subs" warnings category was disabled.</p> <p>These subroutines are only visible within the block in which they are declared, and only after that declaration:</p> <pre data-language="perl"><code># Include these two lines if your code is intended to run under Perl
# versions earlier than 5.26.
no warnings "experimental::lexical_subs";
use feature 'lexical_subs';

foo();              # calls the package/global subroutine
state sub foo {
    foo();          # also calls the package subroutine
}
foo();              # calls "state" sub
my $ref = \&amp;foo;    # take a reference to "state" sub

my sub bar { ... }
bar();              # calls "my" sub</code></pre> <p>You can't (directly) write a recursive lexical subroutine:</p> <pre data-language="perl"><code># WRONG
my sub baz {
    baz();
}</code></pre> <p>This example fails because <code>baz()</code> refers to the package/global subroutine <code>baz</code>, not the lexical subroutine currently being defined.</p> <p>The solution is to use <a href="perlfunc.html#__SUB__"><code>__SUB__</code></a>:</p> <pre data-language="perl"><code>my sub baz {
    __SUB__-&gt;();    # calls itself
}</code></pre> <p>It is possible to predeclare a lexical subroutine. The <code>sub foo {...}</code> subroutine definition syntax respects any previous <code>my sub;</code> or <code>state sub;</code> declaration. Using this to define recursive subroutines is a bad idea, however:</p> <pre data-language="perl"><code>my sub baz;         # predeclaration
sub baz {           # define the "my" sub
    baz();          # WRONG: calls itself, but leaks memory
}</code></pre> <p>Just like <code>my $f; $f = sub { $f-&gt;() }</code>, this example leaks memory. The name <code>baz</code> is a reference to the subroutine, and the subroutine uses the name <code>baz</code>; they keep each other alive (see <a href="perlref.html#Circular-References">"Circular References" in perlref</a>).</p> <h4 id="state-sub-vs-my-sub">
<code>state sub</code> vs <code>my sub</code>
</h4> <p>What is the difference between "state" subs and "my" subs? Each time that execution enters a block when "my" subs are declared, a new copy of each sub is created. "State" subroutines persist from one execution of the containing block to the next.</p> <p>So, in general, "state" subroutines are faster. But "my" subs are necessary if you want to create closures:</p> <pre data-language="perl"><code>sub whatever {
    my $x = shift;
    my sub inner {
        ... do something with $x ...
    }
    inner();
}</code></pre> <p>In this example, a new <code>$x</code> is created when <code>whatever</code> is called, and also a new <code>inner</code>, which can see the new <code>$x</code>. A "state" sub will only see the <code>$x</code> from the first call to <code>whatever</code>.</p> <h4 id="our-subroutines">
<code>our</code> subroutines</h4> <p>Like <code>our $variable</code>, <code>our sub</code> creates a lexical alias to the package subroutine of the same name.</p> <p>The two main uses for this are to switch back to using the package sub inside an inner scope:</p> <pre data-language="perl"><code>sub foo { ... }

sub bar {
    my sub foo { ... }
    {
        # need to use the outer foo here
        our sub foo;
        foo();
    }
}</code></pre> <p>and to make a subroutine visible to other packages in the same scope:</p> <pre data-language="perl"><code>package MySneakyModule;

our sub do_something { ... }

sub do_something_with_caller {
    package DB;
    () = caller 1;          # sets @DB::args
    do_something(@args);    # uses MySneakyModule::do_something
}</code></pre> <h3 id="Passing-Symbol-Table-Entries-(typeglobs)">
Passing Symbol Table Entries (typeglobs) </h3> <p><b>WARNING</b>: The mechanism described in this section was originally the only way to simulate pass-by-reference in older versions of Perl. While it still works fine in modern versions, the new reference mechanism is generally easier to work with. See below.</p> <p>Sometimes you don't want to pass the value of an array to a subroutine but rather the name of it, so that the subroutine can modify the global copy of it rather than working with a local copy. In Perl you can refer to all objects of a particular name by prefixing the name with a star: <code>*foo</code>. This is often known as a "typeglob", because the star on the front can be thought of as a wildcard match for all the funny prefix characters on variables and subroutines and such.</p> <p>When evaluated, the typeglob produces a scalar value that represents all the objects of that name, including any filehandle, format, or subroutine. When assigned to, it causes the name mentioned to refer to whatever <code>*</code> value was assigned to it. Example:</p> <pre data-language="perl"><code>sub doubleary {
    local(*someary) = @_;
    foreach $elem (@someary) {
        $elem *= 2;
    }
}
doubleary(*foo);
doubleary(*bar);</code></pre> <p>Scalars are already passed by reference, so you can modify scalar arguments without using this mechanism by referring explicitly to <code>$_[0]</code> etc. You can modify all the elements of an array by passing all the elements as scalars, but you have to use the <code>*</code> mechanism (or the equivalent reference mechanism) to <code>push</code>, <code>pop</code>, or change the size of an array. It will certainly be faster to pass the typeglob (or reference).</p> <p>Even if you don't want to modify an array, this mechanism is useful for passing multiple arrays in a single LIST, because normally the LIST mechanism will merge all the array values so that you can't extract out the individual arrays. For more on typeglobs, see <a href="perldata.html#Typeglobs-and-Filehandles">"Typeglobs and Filehandles" in perldata</a>.</p> <h3 id="When-to-Still-Use-local()">
When to Still Use local() </h3> <p>Despite the existence of <code>my</code>, there are still three places where the <code>local</code> operator still shines. In fact, in these three places, you <i>must</i> use <code>local</code> instead of <code>my</code>.</p> <ol> <li>
<p>You need to give a global variable a temporary value, especially $_.</p> <p>The global variables, like <code>@ARGV</code> or the punctuation variables, must be <code>local</code>ized with <code>local()</code>. This block reads in <i>/etc/motd</i>, and splits it up into chunks separated by lines of equal signs, which are placed in <code>@Fields</code>.</p> <pre data-language="perl"><code>{
    local @ARGV = ("/etc/motd");
    local $/ = undef;
    local $_ = &lt;&gt;;
    @Fields = split /^\s*=+\s*$/;
}</code></pre> <p>It particular, it's important to <code>local</code>ize $_ in any routine that assigns to it. Look out for implicit assignments in <code>while</code> conditionals.</p> </li> <li>
<p>You need to create a local file or directory handle or a local function.</p> <p>A function that needs a filehandle of its own must use <code>local()</code> on a complete typeglob. This can be used to create new symbol table entries:</p> <pre data-language="perl"><code>sub ioqueue {
    local  (*READER, *WRITER);    # not my!
    pipe    (READER,  WRITER)     or die "pipe: $!";
    return (*READER, *WRITER);
}
($head, $tail) = ioqueue();</code></pre> <p>See the Symbol module for a way to create anonymous symbol table entries.</p> <p>Because assignment of a reference to a typeglob creates an alias, this can be used to create what is effectively a local function, or at least, a local alias.</p> <pre data-language="perl"><code>{
    local *grow = \&amp;shrink; # only until this block exits
    grow();                # really calls shrink()
    move();                # if move() grow()s, it shrink()s too
}
grow();                    # get the real grow() again</code></pre> <p>See <a href="perlref.html#Function-Templates">"Function Templates" in perlref</a> for more about manipulating functions by name in this way.</p> </li> <li>
<p>You want to temporarily change just one element of an array or hash.</p> <p>You can <code>local</code>ize just one element of an aggregate. Usually this is done on dynamics:</p> <pre data-language="perl"><code>{
    local $SIG{INT} = 'IGNORE';
    funct();                            # uninterruptible
}
# interruptibility automatically restored here</code></pre> <p>But it also works on lexically declared aggregates.</p> </li> </ol> <h3 id="Pass-by-Reference">
Pass by Reference </h3> <p>If you want to pass more than one array or hash into a function--or return them from it--and have them maintain their integrity, then you're going to have to use an explicit pass-by-reference. Before you do that, you need to understand references as detailed in <a href="perlref.html">perlref</a>. This section may not make much sense to you otherwise.</p> <p>Here are a few simple examples. First, let's pass in several arrays to a function and have it <code>pop</code> all of then, returning a new list of all their former last elements:</p> <pre data-language="perl"><code>@tailings = popmany ( \@w, \@x, \@y, \@z );

sub popmany {
    my $aref;
    my @retlist;
    foreach $aref ( @_ ) {
        push @retlist, pop @$aref;
    }
    return @retlist;
}</code></pre> <p>Here's how you might write a function that returns a list of keys occurring in all the hashes passed to it:</p> <pre data-language="perl"><code>@common = inter( \%foo, \%bar, \%joe );
sub inter {
    my ($k, $href, %seen); # locals
    foreach $href (@_) {
        while ( $k = each %$href ) {
            $seen{$k}++;
        }
    }
    return grep { $seen{$_} == @_ } keys %seen;
}</code></pre> <p>So far, we're using just the normal list return mechanism. What happens if you want to pass or return a hash? Well, if you're using only one of them, or you don't mind them concatenating, then the normal calling convention is ok, although a little expensive.</p> <p>Where people get into trouble is here:</p> <pre data-language="perl"><code>(@w, @x) = func(@y, @z);
or
(%w, %x) = func(%y, %z);</code></pre> <p>That syntax simply won't work. It sets just <code>@w</code> or <code>%w</code> and clears the <code>@x</code> or <code>%x</code>. Plus the function didn't get passed into two separate arrays or hashes: it got one long list in <code>@_</code>, as always.</p> <p>If you can arrange for everyone to deal with this through references, it's cleaner code, although not so nice to look at. Here's a function that takes two array references as arguments, returning the two array elements in order of how many elements they have in them:</p> <pre data-language="perl"><code>($wref, $xref) = func(\@y, \@z);
print "@$wref has more than @$xref\n";
sub func {
    my ($yref, $zref) = @_;
    if (@$yref &gt; @$zref) {
        return ($yref, $zref);
    } else {
        return ($zref, $yref);
    }
}</code></pre> <p>It turns out that you can actually do this also:</p> <pre data-language="perl"><code>(*w, *x) = func(\@y, \@z);
print "@w has more than @x\n";
sub func {
    local (*y, *z) = @_;
    if (@y &gt; @z) {
        return (\@y, \@z);
    } else {
        return (\@z, \@y);
    }
}</code></pre> <p>Here we're using the typeglobs to do symbol table aliasing. It's a tad subtle, though, and also won't work if you're using <code>my</code> variables, because only globals (even in disguise as <code>local</code>s) are in the symbol table.</p> <p>If you're passing around filehandles, you could usually just use the bare typeglob, like <code>*STDOUT</code>, but typeglobs references work, too. For example:</p> <pre data-language="perl"><code>splutter(\*STDOUT);
sub splutter {
    my $fh = shift;
    print $fh "her um well a hmmm\n";
}

$rec = get_rec(\*STDIN);
sub get_rec {
    my $fh = shift;
    return scalar &lt;$fh&gt;;
}</code></pre> <p>If you're planning on generating new filehandles, you could do this. Notice to pass back just the bare *FH, not its reference.</p> <pre data-language="perl"><code>sub openit {
    my $path = shift;
    local *FH;
    return open (FH, $path) ? *FH : undef;
}</code></pre> <h3 id="Prototypes">Prototypes </h3> <p>Perl supports a very limited kind of compile-time argument checking using function prototyping. This can be declared in either the PROTO section or with a <a href="attributes.html#Built-in-Attributes">prototype attribute</a>. If you declare either of</p> <pre data-language="perl"><code class="plaintext">sub mypush (\@@)
sub mypush :prototype(\@@)</code></pre> <p>then <code>mypush()</code> takes arguments exactly like <code>push()</code> does.</p> <p>If subroutine signatures are enabled (see <a href="#Signatures">"Signatures"</a>), then the shorter PROTO syntax is unavailable, because it would clash with signatures. In that case, a prototype can only be declared in the form of an attribute.</p> <p>The function declaration must be visible at compile time. The prototype affects only interpretation of new-style calls to the function, where new-style is defined as not using the <code>&amp;</code> character. In other words, if you call it like a built-in function, then it behaves like a built-in function. If you call it like an old-fashioned subroutine, then it behaves like an old-fashioned subroutine. It naturally falls out from this rule that prototypes have no influence on subroutine references like <code>\&amp;foo</code> or on indirect subroutine calls like <code>&amp;{$subref}</code> or <code>$subref-&gt;()</code>.</p> <p>Method calls are not influenced by prototypes either, because the function to be called is indeterminate at compile time, since the exact code called depends on inheritance.</p> <p>Because the intent of this feature is primarily to let you define subroutines that work like built-in functions, here are prototypes for some other functions that parse almost exactly like the corresponding built-in.</p> <pre data-language="perl"><code>Declared as             Called as

sub mylink ($$)         mylink $old, $new
sub myvec ($$$)         myvec $var, $offset, 1
sub myindex ($$;$)      myindex &amp;getstring, "substr"
sub mysyswrite ($$$;$)  mysyswrite $buf, 0, length($buf) - $off, $off
sub myreverse (@)       myreverse $x, $y, $z
sub myjoin ($@)         myjoin ":", $x, $y, $z
sub mypop (\@)          mypop @array
sub mysplice (\@$$@)    mysplice @array, 0, 2, @pushme
sub mykeys (\[%@])      mykeys $hashref-&gt;%*
sub myopen (*;$)        myopen HANDLE, $name
sub mypipe (**)         mypipe READHANDLE, WRITEHANDLE
sub mygrep (&amp;@)         mygrep { /foo/ } $x, $y, $z
sub myrand (;$)         myrand 42
sub mytime ()           mytime</code></pre> <p>Any backslashed prototype character represents an actual argument that must start with that character (optionally preceded by <code>my</code>, <code>our</code> or <code>local</code>), with the exception of <code>$</code>, which will accept any scalar lvalue expression, such as <code>$foo = 7</code> or <code>my_function()-&gt;[0]</code>. The value passed as part of <code>@_</code> will be a reference to the actual argument given in the subroutine call, obtained by applying <code>\</code> to that argument.</p> <p>You can use the <code>\[]</code> backslash group notation to specify more than one allowed argument type. For example:</p> <pre data-language="perl"><code class="plaintext">sub myref (\[$@%&amp;*])</code></pre> <p>will allow calling myref() as</p> <pre data-language="perl"><code>myref $var
myref @array
myref %hash
myref &amp;sub
myref *glob</code></pre> <p>and the first argument of myref() will be a reference to a scalar, an array, a hash, a code, or a glob.</p> <p>Unbackslashed prototype characters have special meanings. Any unbackslashed <code>@</code> or <code>%</code> eats all remaining arguments, and forces list context. An argument represented by <code>$</code> forces scalar context. An <code>&amp;</code> requires an anonymous subroutine, which, if passed as the first argument, does not require the <code>sub</code> keyword or a subsequent comma.</p> <p>A <code>*</code> allows the subroutine to accept a bareword, constant, scalar expression, typeglob, or a reference to a typeglob in that slot. The value will be available to the subroutine either as a simple scalar, or (in the latter two cases) as a reference to the typeglob. If you wish to always convert such arguments to a typeglob reference, use Symbol::qualify_to_ref() as follows:</p> <pre data-language="perl"><code>use Symbol 'qualify_to_ref';

sub foo (*) {
    my $fh = qualify_to_ref(shift, caller);
    ...
}</code></pre> <p>The <code>+</code> prototype is a special alternative to <code>$</code> that will act like <code>\[@%]</code> when given a literal array or hash variable, but will otherwise force scalar context on the argument. This is useful for functions which should accept either a literal array or an array reference as the argument:</p> <pre data-language="perl"><code>sub mypush (+@) {
    my $aref = shift;
    die "Not an array or arrayref" unless ref $aref eq 'ARRAY';
    push @$aref, @_;
}</code></pre> <p>When using the <code>+</code> prototype, your function must check that the argument is of an acceptable type.</p> <p>A semicolon (<code>;</code>) separates mandatory arguments from optional arguments. It is redundant before <code>@</code> or <code>%</code>, which gobble up everything else.</p> <p>As the last character of a prototype, or just before a semicolon, a <code>@</code> or a <code>%</code>, you can use <code>_</code> in place of <code>$</code>: if this argument is not provided, <code>$_</code> will be used instead.</p> <p>Note how the last three examples in the table above are treated specially by the parser. <code>mygrep()</code> is parsed as a true list operator, <code>myrand()</code> is parsed as a true unary operator with unary precedence the same as <code>rand()</code>, and <code>mytime()</code> is truly without arguments, just like <code>time()</code>. That is, if you say</p> <pre data-language="perl"><code>mytime +2;</code></pre> <p>you'll get <code>mytime() + 2</code>, not <code>mytime(2)</code>, which is how it would be parsed without a prototype. If you want to force a unary function to have the same precedence as a list operator, add <code>;</code> to the end of the prototype:</p> <pre data-language="perl"><code>sub mygetprotobynumber($;);
mygetprotobynumber $x &gt; $y; # parsed as mygetprotobynumber($x &gt; $y)</code></pre> <p>The interesting thing about <code>&amp;</code> is that you can generate new syntax with it, provided it's in the initial position: </p> <pre data-language="perl"><code>sub try (&amp;@) {
    my($try,$catch) = @_;
    eval { &amp;$try };
    if ($@) {
        local $_ = $@;
        &amp;$catch;
    }
}
sub catch (&amp;) { $_[0] }

try {
    die "phooey";
} catch {
    /phooey/ and print "unphooey\n";
};</code></pre> <p>That prints <code>"unphooey"</code>. (Yes, there are still unresolved issues having to do with visibility of <code>@_</code>. I'm ignoring that question for the moment. (But note that if we make <code>@_</code> lexically scoped, those anonymous subroutines can act like closures... (Gee, is this sounding a little Lispish? (Never mind.))))</p> <p>And here's a reimplementation of the Perl <code>grep</code> operator: </p> <pre data-language="perl"><code>sub mygrep (&amp;@) {
    my $code = shift;
    my @result;
    foreach $_ (@_) {
        push(@result, $_) if &amp;$code;
    }
    @result;
}</code></pre> <p>Some folks would prefer full alphanumeric prototypes. Alphanumerics have been intentionally left out of prototypes for the express purpose of someday in the future adding named, formal parameters. The current mechanism's main goal is to let module writers provide better diagnostics for module users. Larry feels the notation quite understandable to Perl programmers, and that it will not intrude greatly upon the meat of the module, nor make it harder to read. The line noise is visually encapsulated into a small pill that's easy to swallow.</p> <p>If you try to use an alphanumeric sequence in a prototype you will generate an optional warning - "Illegal character in prototype...". Unfortunately earlier versions of Perl allowed the prototype to be used as long as its prefix was a valid prototype. The warning may be upgraded to a fatal error in a future version of Perl once the majority of offending code is fixed.</p> <p>It's probably best to prototype new functions, not retrofit prototyping into older ones. That's because you must be especially careful about silent impositions of differing list versus scalar contexts. For example, if you decide that a function should take just one parameter, like this:</p> <pre data-language="perl"><code>sub func ($) {
    my $n = shift;
    print "you gave me $n\n";
}</code></pre> <p>and someone has been calling it with an array or expression returning a list:</p> <pre data-language="perl"><code>func(@foo);
func( $text =~ /\w+/g );</code></pre> <p>Then you've just supplied an automatic <code>scalar</code> in front of their argument, which can be more than a bit surprising. The old <code>@foo</code> which used to hold one thing doesn't get passed in. Instead, <code>func()</code> now gets passed in a <code>1</code>; that is, the number of elements in <code>@foo</code>. And the <code>m//g</code> gets called in scalar context so instead of a list of words it returns a boolean result and advances <code>pos($text)</code>. Ouch!</p> <p>If a sub has both a PROTO and a BLOCK, the prototype is not applied until after the BLOCK is completely defined. This means that a recursive function with a prototype has to be predeclared for the prototype to take effect, like so:</p> <pre data-language="perl"><code>sub foo($$);
sub foo($$) {
    foo 1, 2;
}</code></pre> <p>This is all very powerful, of course, and should be used only in moderation to make the world a better place.</p> <h3 id="Constant-Functions">
Constant Functions </h3> <p>Functions with a prototype of <code>()</code> are potential candidates for inlining. If the result after optimization and constant folding is either a constant or a lexically-scoped scalar which has no other references, then it will be used in place of function calls made without <code>&amp;</code>. Calls made using <code>&amp;</code> are never inlined. (See <a href="constant.html">constant</a> for an easy way to declare most constants.)</p> <p>The following functions would all be inlined:</p> <pre data-language="perl"><code class="plaintext">sub pi ()           { 3.14159 }             # Not exact, but close.
sub PI ()           { 4 * atan2 1, 1 }      # As good as it gets,
                                            # and it's inlined, too!
sub ST_DEV ()       { 0 }
sub ST_INO ()       { 1 }

sub FLAG_FOO ()     { 1 &lt;&lt; 8 }
sub FLAG_BAR ()     { 1 &lt;&lt; 9 }
sub FLAG_MASK ()    { FLAG_FOO | FLAG_BAR }

sub OPT_BAZ ()      { not (0x1B58 &amp; FLAG_MASK) }

sub N () { int(OPT_BAZ) / 3 }

sub FOO_SET () { 1 if FLAG_MASK &amp; FLAG_FOO }
sub FOO_SET2 () { if (FLAG_MASK &amp; FLAG_FOO) { 1 } }</code></pre> <p>(Be aware that the last example was not always inlined in Perl 5.20 and earlier, which did not behave consistently with subroutines containing inner scopes.) You can countermand inlining by using an explicit <code>return</code>:</p> <pre data-language="perl"><code>sub baz_val () {
    if (OPT_BAZ) {
        return 23;
    }
    else {
        return 42;
    }
}
sub bonk_val () { return 12345 }</code></pre> <p>As alluded to earlier you can also declare inlined subs dynamically at BEGIN time if their body consists of a lexically-scoped scalar which has no other references. Only the first example here will be inlined:</p> <pre data-language="perl"><code>BEGIN {
    my $var = 1;
    no strict 'refs';
    *INLINED = sub () { $var };
}

BEGIN {
    my $var = 1;
    my $ref = \$var;
    no strict 'refs';
    *NOT_INLINED = sub () { $var };
}</code></pre> <p>A not so obvious caveat with this (see [RT #79908]) is what happens if the variable is potentially modifiable. For example:</p> <pre data-language="perl"><code>BEGIN {
    my $x = 10;
    *FOO = sub () { $x };
    $x++;
}
print FOO(); # printed 10 prior to 5.32.0</code></pre> <p>From Perl 5.22 onwards this gave a deprecation warning, and from Perl 5.32 onwards it became a run-time error. Previously the variable was immediately inlined, and stopped behaving like a normal lexical variable; so it printed <code>10</code>, not <code>11</code>.</p> <p>If you still want such a subroutine to be inlined (with no warning), make sure the variable is not used in a context where it could be modified aside from where it is declared.</p> <pre data-language="perl"><code># Fine, no warning
BEGIN {
    my $x = 54321;
    *INLINED = sub () { $x };
}
# Error
BEGIN {
    my $x;
    $x = 54321;
    *ALSO_INLINED = sub () { $x };
}</code></pre> <p>Perl 5.22 also introduces the experimental "const" attribute as an alternative. (Disable the "experimental::const_attr" warnings if you want to use it.) When applied to an anonymous subroutine, it forces the sub to be called when the <code>sub</code> expression is evaluated. The return value is captured and turned into a constant subroutine:</p> <pre data-language="perl"><code>my $x = 54321;
*INLINED = sub : const { $x };
$x++;</code></pre> <p>The return value of <code>INLINED</code> in this example will always be 54321, regardless of later modifications to $x. You can also put any arbitrary code inside the sub, at it will be executed immediately and its return value captured the same way.</p> <p>If you really want a subroutine with a <code>()</code> prototype that returns a lexical variable you can easily force it to not be inlined by adding an explicit <code>return</code>:</p> <pre data-language="perl"><code>BEGIN {
    my $x = 10;
    *FOO = sub () { return $x };
    $x++;
}
print FOO(); # prints 11</code></pre> <p>The easiest way to tell if a subroutine was inlined is by using <a href="B::Deparse.html">B::Deparse</a>. Consider this example of two subroutines returning <code>1</code>, one with a <code>()</code> prototype causing it to be inlined, and one without (with deparse output truncated for clarity):</p> <pre data-language="perl"><code>$ perl -MO=Deparse -e 'sub ONE { 1 } if (ONE) { print ONE if ONE }'
sub ONE {
    1;
}
if (ONE ) {
    print ONE() if ONE ;
}

$ perl -MO=Deparse -e 'sub ONE () { 1 } if (ONE) { print ONE if ONE }'
sub ONE () { 1 }
do {
    print 1
};</code></pre> <p>If you redefine a subroutine that was eligible for inlining, you'll get a warning by default. You can use this warning to tell whether or not a particular subroutine is considered inlinable, since it's different than the warning for overriding non-inlined subroutines:</p> <pre data-language="perl"><code class="plaintext">$ perl -e 'sub one () {1} sub one () {2}'
Constant subroutine one redefined at -e line 1.
$ perl -we 'sub one {1} sub one {2}'
Subroutine one redefined at -e line 1.</code></pre> <p>The warning is considered severe enough not to be affected by the <b>-w</b> switch (or its absence) because previously compiled invocations of the function will still be using the old value of the function. If you need to be able to redefine the subroutine, you need to ensure that it isn't inlined, either by dropping the <code>()</code> prototype (which changes calling semantics, so beware) or by thwarting the inlining mechanism in some other way, e.g. by adding an explicit <code>return</code>, as mentioned above:</p> <pre data-language="perl"><code class="plaintext">sub not_inlined () { return 23 }</code></pre> <h3 id="Overriding-Built-in-Functions">
Overriding Built-in Functions </h3> <p>Many built-in functions may be overridden, though this should be tried only occasionally and for good reason. Typically this might be done by a package attempting to emulate missing built-in functionality on a non-Unix system.</p> <p>Overriding may be done only by importing the name from a module at compile time--ordinary predeclaration isn't good enough. However, the <code>use subs</code> pragma lets you, in effect, predeclare subs via the import syntax, and these names may then override built-in ones:</p> <pre data-language="perl"><code>use subs 'chdir', 'chroot', 'chmod', 'chown';
chdir $somewhere;
sub chdir { ... }</code></pre> <p>To unambiguously refer to the built-in form, precede the built-in name with the special package qualifier <code>CORE::</code>. For example, saying <code>CORE::open()</code> always refers to the built-in <code>open()</code>, even if the current package has imported some other subroutine called <code>&amp;open()</code> from elsewhere. Even though it looks like a regular function call, it isn't: the <code>CORE::</code> prefix in that case is part of Perl's syntax, and works for any keyword, regardless of what is in the <code>CORE</code> package. Taking a reference to it, that is, <code>\&amp;CORE::open</code>, only works for some keywords. See <a href="core.html">CORE</a>.</p> <p>Library modules should not in general export built-in names like <code>open</code> or <code>chdir</code> as part of their default <code>@EXPORT</code> list, because these may sneak into someone else's namespace and change the semantics unexpectedly. Instead, if the module adds that name to <code>@EXPORT_OK</code>, then it's possible for a user to import the name explicitly, but not implicitly. That is, they could say</p> <pre data-language="perl"><code>use Module 'open';</code></pre> <p>and it would import the <code>open</code> override. But if they said</p> <pre data-language="perl"><code>use Module;</code></pre> <p>they would get the default imports without overrides.</p> <p>The foregoing mechanism for overriding built-in is restricted, quite deliberately, to the package that requests the import. There is a second method that is sometimes applicable when you wish to override a built-in everywhere, without regard to namespace boundaries. This is achieved by importing a sub into the special namespace <code>CORE::GLOBAL::</code>. Here is an example that quite brazenly replaces the <code>glob</code> operator with something that understands regular expressions.</p> <pre data-language="perl"><code>package REGlob;
require Exporter;
@ISA = 'Exporter';
@EXPORT_OK = 'glob';

sub import {
    my $pkg = shift;
    return unless @_;
    my $sym = shift;
    my $where = ($sym =~ s/^GLOBAL_// ? 'CORE::GLOBAL' : caller(0));
    $pkg-&gt;export($where, $sym, @_);
}

sub glob {
    my $pat = shift;
    my @got;
    if (opendir my $d, '.') {
        @got = grep /$pat/, readdir $d;
        closedir $d;
    }
    return @got;
}
1;</code></pre> <p>And here's how it could be (ab)used:</p> <pre data-language="perl"><code>#use REGlob 'GLOBAL_glob';      # override glob() in ALL namespaces
package Foo;
use REGlob 'glob';              # override glob() in Foo:: only
print for &lt;^[a-z_]+\.pm\$&gt;;     # show all pragmatic modules</code></pre> <p>The initial comment shows a contrived, even dangerous example. By overriding <code>glob</code> globally, you would be forcing the new (and subversive) behavior for the <code>glob</code> operator for <i>every</i> namespace, without the complete cognizance or cooperation of the modules that own those namespaces. Naturally, this should be done with extreme caution--if it must be done at all.</p> <p>The <code>REGlob</code> example above does not implement all the support needed to cleanly override Perl's <code>glob</code> operator. The built-in <code>glob</code> has different behaviors depending on whether it appears in a scalar or list context, but our <code>REGlob</code> doesn't. Indeed, many Perl built-ins have such context sensitive behaviors, and these must be adequately supported by a properly written override. For a fully functional example of overriding <code>glob</code>, study the implementation of <code>File::DosGlob</code> in the standard library.</p> <p>When you override a built-in, your replacement should be consistent (if possible) with the built-in native syntax. You can achieve this by using a suitable prototype. To get the prototype of an overridable built-in, use the <code>prototype</code> function with an argument of <code>"CORE::builtin_name"</code> (see <a href="perlfunc.html#prototype">"prototype" in perlfunc</a>).</p> <p>Note however that some built-ins can't have their syntax expressed by a prototype (such as <code>system</code> or <code>chomp</code>). If you override them you won't be able to fully mimic their original syntax.</p> <p>The built-ins <code>do</code>, <code>require</code> and <code>glob</code> can also be overridden, but due to special magic, their original syntax is preserved, and you don't have to define a prototype for their replacements. (You can't override the <code>do BLOCK</code> syntax, though).</p> <p><code>require</code> has special additional dark magic: if you invoke your <code>require</code> replacement as <code>require Foo::Bar</code>, it will actually receive the argument <code>"Foo/Bar.pm"</code> in @_. See <a href="perlfunc.html#require">"require" in perlfunc</a>.</p> <p>And, as you'll have noticed from the previous example, if you override <code>glob</code>, the <code>&lt;*&gt;</code> glob operator is overridden as well.</p> <p>In a similar fashion, overriding the <code>readline</code> function also overrides the equivalent I/O operator <code>&lt;FILEHANDLE&gt;</code>. Also, overriding <code>readpipe</code> also overrides the operators <code>``</code> and <code>qx//</code>.</p> <p>Finally, some built-ins (e.g. <code>exists</code> or <code>grep</code>) can't be overridden.</p> <h3 id="Autoloading">Autoloading </h3> <p>If you call a subroutine that is undefined, you would ordinarily get an immediate, fatal error complaining that the subroutine doesn't exist. (Likewise for subroutines being used as methods, when the method doesn't exist in any base class of the class's package.) However, if an <code>AUTOLOAD</code> subroutine is defined in the package or packages used to locate the original subroutine, then that <code>AUTOLOAD</code> subroutine is called with the arguments that would have been passed to the original subroutine. The fully qualified name of the original subroutine magically appears in the global $AUTOLOAD variable of the same package as the <code>AUTOLOAD</code> routine. The name is not passed as an ordinary argument because, er, well, just because, that's why. (As an exception, a method call to a nonexistent <code>import</code> or <code>unimport</code> method is just skipped instead. Also, if the AUTOLOAD subroutine is an XSUB, there are other ways to retrieve the subroutine name. See <a href="perlguts.html#Autoloading-with-XSUBs">"Autoloading with XSUBs" in perlguts</a> for details.)</p> <p>Many <code>AUTOLOAD</code> routines load in a definition for the requested subroutine using eval(), then execute that subroutine using a special form of goto() that erases the stack frame of the <code>AUTOLOAD</code> routine without a trace. (See the source to the standard module documented in <a href="autoloader.html">AutoLoader</a>, for example.) But an <code>AUTOLOAD</code> routine can also just emulate the routine and never define it. For example, let's pretend that a function that wasn't defined should just invoke <code>system</code> with those arguments. All you'd do is:</p> <pre data-language="perl"><code>sub AUTOLOAD {
    our $AUTOLOAD;              # keep 'use strict' happy
    my $program = $AUTOLOAD;
    $program =~ s/.*:://;
    system($program, @_);
}
date();
who();
ls('-l');</code></pre> <p>In fact, if you predeclare functions you want to call that way, you don't even need parentheses:</p> <pre data-language="perl"><code>use subs qw(date who ls);
date;
who;
ls '-l';</code></pre> <p>A more complete example of this is the Shell module on CPAN, which can treat undefined subroutine calls as calls to external programs.</p> <p>Mechanisms are available to help modules writers split their modules into autoloadable files. See the standard AutoLoader module described in <a href="autoloader.html">AutoLoader</a> and in <a href="autosplit.html">AutoSplit</a>, the standard SelfLoader modules in <a href="selfloader.html">SelfLoader</a>, and the document on adding C functions to Perl code in <a href="perlxs.html">perlxs</a>.</p> <h3 id="Subroutine-Attributes">
Subroutine Attributes </h3> <p>A subroutine declaration or definition may have a list of attributes associated with it. If such an attribute list is present, it is broken up at space or colon boundaries and treated as though a <code>use attributes</code> had been seen. See <a href="attributes.html">attributes</a> for details about what attributes are currently supported. Unlike the limitation with the obsolescent <code>use attrs</code>, the <code>sub : ATTRLIST</code> syntax works to associate the attributes with a pre-declaration, and not just with a subroutine definition.</p> <p>The attributes must be valid as simple identifier names (without any punctuation other than the '_' character). They may have a parameter list appended, which is only checked for whether its parentheses ('(',')') nest properly.</p> <p>Examples of valid syntax (even though the attributes are unknown):</p> <pre data-language="perl"><code>sub fnord (&amp;\%) : switch(10,foo(7,3))  :  expensive;
sub plugh () : Ugly('\(") :Bad;
sub xyzzy : _5x5 { ... }</code></pre> <p>Examples of invalid syntax:</p> <pre data-language="perl"><code>sub fnord : switch(10,foo();    # ()-string not balanced
sub snoid : Ugly('(');          # ()-string not balanced
sub xyzzy : 5x5;                # "5x5" not a valid identifier
sub plugh : Y2::north;          # "Y2::north" not a simple identifier
sub snurt : foo + bar;          # "+" not a colon or space</code></pre> <p>The attribute list is passed as a list of constant strings to the code which associates them with the subroutine. In particular, the second example of valid syntax above currently looks like this in terms of how it's parsed and invoked:</p> <pre data-language="perl"><code>use attributes __PACKAGE__, \&amp;plugh, q[Ugly('\(")], 'Bad';</code></pre> <p>For further details on attribute lists and their manipulation, see <a href="attributes.html">attributes</a> and <a href="Attribute::Handlers.html">Attribute::Handlers</a>.</p> <h2 id="SEE-ALSO">
SEE ALSO</h2> <p>See <a href="perlref.html#Function-Templates">"Function Templates" in perlref</a> for more about references and closures. See <a href="perlxs.html">perlxs</a> if you'd like to learn about calling C subroutines from Perl. See <a href="perlembed.html">perlembed</a> if you'd like to learn about calling Perl subroutines from C. See <a href="perlmod.html">perlmod</a> to learn about bundling up your functions in separate files. See <a href="perlmodlib.html">perlmodlib</a> to learn what library modules come standard on your system. See <a href="perlootut.html">perlootut</a> to learn how to make object method calls.</p><div class="_attribution">
  <p class="_attribution-p">
    &copy; 1993&ndash;2023 Larry Wall and others<br>Licensed under the GNU General Public License version 1 or later, or the Artistic License.<br>The Perl logo is a trademark of the Perl Foundation.<br>
    <a href="https://perldoc.perl.org/5.38.0/perlsub" class="_attribution-link">https://perldoc.perl.org/5.38.0/perlsub</a>
  </p>
</div>
