<h1>perldsc</h1>   <h2><a id="toc">CONTENTS</a></h2> <ul> <li> <a class="text-decoration-none" href="#NAME">NAME </a> </li> <li> <a class="text-decoration-none" href="#DESCRIPTION">DESCRIPTION</a> </li> <li> <a class="text-decoration-none" href="#REFERENCES">REFERENCES </a> </li> <li> <a class="text-decoration-none" href="#COMMON-MISTAKES">COMMON MISTAKES</a> </li> <li> <a class="text-decoration-none" href="#CAVEAT-ON-PRECEDENCE">CAVEAT ON PRECEDENCE </a> </li> <li> <a class="text-decoration-none" href="#WHY-YOU-SHOULD-ALWAYS-use-VERSION">WHY YOU SHOULD ALWAYS use VERSION</a> </li> <li> <a class="text-decoration-none" href="#DEBUGGING">DEBUGGING </a> </li> <li> <a class="text-decoration-none" href="#CODE-EXAMPLES">CODE EXAMPLES</a> </li> <li> <a class="text-decoration-none" href="#ARRAYS-OF-ARRAYS">ARRAYS OF ARRAYS </a> <ul> <li> <a class="text-decoration-none" href="#Declaration-of-an-ARRAY-OF-ARRAYS">Declaration of an ARRAY OF ARRAYS</a> </li> <li> <a class="text-decoration-none" href="#Generation-of-an-ARRAY-OF-ARRAYS">Generation of an ARRAY OF ARRAYS</a> </li> <li> <a class="text-decoration-none" href="#Access-and-Printing-of-an-ARRAY-OF-ARRAYS">Access and Printing of an ARRAY OF ARRAYS</a> </li> </ul> </li> <li> <a class="text-decoration-none" href="#HASHES-OF-ARRAYS">HASHES OF ARRAYS </a> <ul> <li> <a class="text-decoration-none" href="#Declaration-of-a-HASH-OF-ARRAYS">Declaration of a HASH OF ARRAYS</a> </li> <li> <a class="text-decoration-none" href="#Generation-of-a-HASH-OF-ARRAYS">Generation of a HASH OF ARRAYS</a> </li> <li> <a class="text-decoration-none" href="#Access-and-Printing-of-a-HASH-OF-ARRAYS">Access and Printing of a HASH OF ARRAYS</a> </li> </ul> </li> <li> <a class="text-decoration-none" href="#ARRAYS-OF-HASHES">ARRAYS OF HASHES </a> <ul> <li> <a class="text-decoration-none" href="#Declaration-of-an-ARRAY-OF-HASHES">Declaration of an ARRAY OF HASHES</a> </li> <li> <a class="text-decoration-none" href="#Generation-of-an-ARRAY-OF-HASHES">Generation of an ARRAY OF HASHES</a> </li> <li> <a class="text-decoration-none" href="#Access-and-Printing-of-an-ARRAY-OF-HASHES">Access and Printing of an ARRAY OF HASHES</a> </li> </ul> </li> <li> <a class="text-decoration-none" href="#HASHES-OF-HASHES">HASHES OF HASHES </a> <ul> <li> <a class="text-decoration-none" href="#Declaration-of-a-HASH-OF-HASHES">Declaration of a HASH OF HASHES</a> </li> <li> <a class="text-decoration-none" href="#Generation-of-a-HASH-OF-HASHES">Generation of a HASH OF HASHES</a> </li> <li> <a class="text-decoration-none" href="#Access-and-Printing-of-a-HASH-OF-HASHES">Access and Printing of a HASH OF HASHES</a> </li> </ul> </li> <li> <a class="text-decoration-none" href="#MORE-ELABORATE-RECORDS">MORE ELABORATE RECORDS </a> <ul> <li> <a class="text-decoration-none" href="#Declaration-of-MORE-ELABORATE-RECORDS">Declaration of MORE ELABORATE RECORDS</a> </li> <li> <a class="text-decoration-none" href="#Declaration-of-a-HASH-OF-COMPLEX-RECORDS">Declaration of a HASH OF COMPLEX RECORDS</a> </li> <li> <a class="text-decoration-none" href="#Generation-of-a-HASH-OF-COMPLEX-RECORDS">Generation of a HASH OF COMPLEX RECORDS</a> </li> </ul> </li> <li> <a class="text-decoration-none" href="#Database-Ties">Database Ties</a> </li> <li> <a class="text-decoration-none" href="#SEE-ALSO">SEE ALSO</a> </li> <li> <a class="text-decoration-none" href="#AUTHOR">AUTHOR</a> </li> </ul> <h2 id="NAME">NAME </h2> <p>perldsc - Perl Data Structures Cookbook</p> <h2 id="DESCRIPTION">DESCRIPTION</h2> <p>Perl lets us have complex data structures. You can write something like this and all of a sudden, you'd have an array with three dimensions!</p> <pre data-language="perl"><code>for my $x (1 .. 10) {
    for my $y (1 .. 10) {
        for my $z (1 .. 10) {
            $AoA[$x][$y][$z] =
                $x ** $y + $z;
        }
    }
}</code></pre> <p>Alas, however simple this may appear, underneath it's a much more elaborate construct than meets the eye!</p> <p>How do you print it out? Why can't you say just <code>print @AoA</code>? How do you sort it? How can you pass it to a function or get one of these back from a function? Is it an object? Can you save it to disk to read back later? How do you access whole rows or columns of that matrix? Do all the values have to be numeric?</p> <p>As you see, it's quite easy to become confused. While some small portion of the blame for this can be attributed to the reference-based implementation, it's really more due to a lack of existing documentation with examples designed for the beginner.</p> <p>This document is meant to be a detailed but understandable treatment of the many different sorts of data structures you might want to develop. It should also serve as a cookbook of examples. That way, when you need to create one of these complex data structures, you can just pinch, pilfer, or purloin a drop-in example from here.</p> <p>Let's look at each of these possible constructs in detail. There are separate sections on each of the following:</p> <ul> <li>
<p>arrays of arrays</p> </li> <li>
<p>hashes of arrays</p> </li> <li>
<p>arrays of hashes</p> </li> <li>
<p>hashes of hashes</p> </li> <li>
<p>more elaborate constructs</p> </li> </ul> <p>But for now, let's look at general issues common to all these types of data structures.</p> <h2 id="REFERENCES">REFERENCES </h2> <p>The most important thing to understand about all data structures in Perl--including multidimensional arrays--is that even though they might appear otherwise, Perl <code>@ARRAY</code>s and <code>%HASH</code>es are all internally one-dimensional. They can hold only scalar values (meaning a string, number, or a reference). They cannot directly contain other arrays or hashes, but instead contain <i>references</i> to other arrays or hashes. </p> <p>You can't use a reference to an array or hash in quite the same way that you would a real array or hash. For C or C++ programmers unused to distinguishing between arrays and pointers to the same, this can be confusing. If so, just think of it as the difference between a structure and a pointer to a structure.</p> <p>You can (and should) read more about references in <a href="perlref.html">perlref</a>. Briefly, references are rather like pointers that know what they point to. (Objects are also a kind of reference, but we won't be needing them right away--if ever.) This means that when you have something which looks to you like an access to a two-or-more-dimensional array and/or hash, what's really going on is that the base type is merely a one-dimensional entity that contains references to the next level. It's just that you can <i>use</i> it as though it were a two-dimensional one. This is actually the way almost all C multidimensional arrays work as well.</p> <pre data-language="perl"><code>$array[7][12]                       # array of arrays
$array[7]{string}                   # array of hashes
$hash{string}[7]                    # hash of arrays
$hash{string}{'another string'}     # hash of hashes</code></pre> <p>Now, because the top level contains only references, if you try to print out your array in with a simple print() function, you'll get something that doesn't look very nice, like this:</p> <pre data-language="perl"><code>  my @AoA = ( [2, 3], [4, 5, 7], [0] );
  print $AoA[1][2];
7
  print @AoA;
ARRAY(0x83c38)ARRAY(0x8b194)ARRAY(0x8b1d0)</code></pre> <p>That's because Perl doesn't (ever) implicitly dereference your variables. If you want to get at the thing a reference is referring to, then you have to do this yourself using either prefix typing indicators, like <code>${$blah}</code>, <code>@{$blah}</code>, <code>@{$blah[$i]}</code>, or else postfix pointer arrows, like <code>$a-&gt;[3]</code>, <code>$h-&gt;{fred}</code>, or even <code>$ob-&gt;method()-&gt;[3]</code>.</p> <h2 id="COMMON-MISTAKES">
COMMON MISTAKES</h2> <p>The two most common mistakes made in constructing something like an array of arrays is either accidentally counting the number of elements or else taking a reference to the same memory location repeatedly. Here's the case where you just get the count instead of a nested array:</p> <pre data-language="perl"><code>for my $i (1..10) {
    my @array = somefunc($i);
    $AoA[$i] = @array;      # WRONG!
}</code></pre> <p>That's just the simple case of assigning an array to a scalar and getting its element count. If that's what you really and truly want, then you might do well to consider being a tad more explicit about it, like this:</p> <pre data-language="perl"><code>for my $i (1..10) {
    my @array = somefunc($i);
    $counts[$i] = scalar @array;
}</code></pre> <p>Here's the case of taking a reference to the same memory location again and again:</p> <pre data-language="perl"><code># Either without strict or having an outer-scope my @array;
# declaration.

for my $i (1..10) {
    @array = somefunc($i);
    $AoA[$i] = \@array;     # WRONG!
}</code></pre> <p>So, what's the big problem with that? It looks right, doesn't it? After all, I just told you that you need an array of references, so by golly, you've made me one!</p> <p>Unfortunately, while this is true, it's still broken. All the references in @AoA refer to the <i>very same place</i>, and they will therefore all hold whatever was last in @array! It's similar to the problem demonstrated in the following C program:</p> <pre data-language="perl"><code>#include &lt;pwd.h&gt;
main() {
    struct passwd *getpwnam(), *rp, *dp;
    rp = getpwnam("root");
    dp = getpwnam("daemon");

    printf("daemon name is %s\nroot name is %s\n",
            dp-&gt;pw_name, rp-&gt;pw_name);
}</code></pre> <p>Which will print</p> <pre data-language="perl"><code class="plaintext">daemon name is daemon
root name is daemon</code></pre> <p>The problem is that both <code>rp</code> and <code>dp</code> are pointers to the same location in memory! In C, you'd have to remember to malloc() yourself some new memory. In Perl, you'll want to use the array constructor <code>[]</code> or the hash constructor <code>{}</code> instead. Here's the right way to do the preceding broken code fragments: </p> <pre data-language="perl"><code># Either without strict or having an outer-scope my @array;
# declaration.

for my $i (1..10) {
    @array = somefunc($i);
    $AoA[$i] = [ @array ];
}</code></pre> <p>The square brackets make a reference to a new array with a <i>copy</i> of what's in @array at the time of the assignment. This is what you want.</p> <p>Note that this will produce something similar:</p> <pre data-language="perl"><code># Either without strict or having an outer-scope my @array;
# declaration.
for my $i (1..10) {
    @array = 0 .. $i;
    $AoA[$i]-&gt;@* = @array;
}</code></pre> <p>Is it the same? Well, maybe so--and maybe not. The subtle difference is that when you assign something in square brackets, you know for sure it's always a brand new reference with a new <i>copy</i> of the data. Something else could be going on in this new case with the <code>$AoA[$i]-&gt;@*</code> dereference on the left-hand-side of the assignment. It all depends on whether <code>$AoA[$i]</code> had been undefined to start with, or whether it already contained a reference. If you had already populated @AoA with references, as in</p> <pre data-language="perl"><code>$AoA[3] = \@another_array;</code></pre> <p>Then the assignment with the indirection on the left-hand-side would use the existing reference that was already there:</p> <pre data-language="perl"><code>$AoA[3]-&gt;@* = @array;</code></pre> <p>Of course, this <i>would</i> have the "interesting" effect of clobbering @another_array. (Have you ever noticed how when a programmer says something is "interesting", that rather than meaning "intriguing", they're disturbingly more apt to mean that it's "annoying", "difficult", or both? :-)</p> <p>So just remember always to use the array or hash constructors with <code>[]</code> or <code>{}</code>, and you'll be fine, although it's not always optimally efficient.</p> <p>Surprisingly, the following dangerous-looking construct will actually work out fine:</p> <pre data-language="perl"><code>for my $i (1..10) {
    my @array = somefunc($i);
    $AoA[$i] = \@array;
}</code></pre> <p>That's because my() is more of a run-time statement than it is a compile-time declaration <i>per se</i>. This means that the my() variable is remade afresh each time through the loop. So even though it <i>looks</i> as though you stored the same variable reference each time, you actually did not! This is a subtle distinction that can produce more efficient code at the risk of misleading all but the most experienced of programmers. So I usually advise against teaching it to beginners. In fact, except for passing arguments to functions, I seldom like to see the gimme-a-reference operator (backslash) used much at all in code. Instead, I advise beginners that they (and most of the rest of us) should try to use the much more easily understood constructors <code>[]</code> and <code>{}</code> instead of relying upon lexical (or dynamic) scoping and hidden reference-counting to do the right thing behind the scenes.</p> <p>Note also that there exists another way to write a dereference! These two lines are equivalent:</p> <pre data-language="perl"><code>$AoA[$i]-&gt;@* = @array;
@{ $AoA[$i] } = @array;</code></pre> <p>The first form, called <i>postfix dereference</i> is generally easier to read, because the expression can be read from left to right, and there are no enclosing braces to balance. On the other hand, it is also newer. It was added to the language in 2014, so you will often encounter the other form, <i>circumfix dereference</i>, in older code.</p> <p>In summary:</p> <pre data-language="perl"><code>$AoA[$i] = [ @array ];     # usually best
$AoA[$i] = \@array;        # perilous; just how my() was that array?
$AoA[$i]-&gt;@*  = @array;    # way too tricky for most programmers
@{ $AoA[$i] } = @array;    # just as tricky, and also harder to read</code></pre> <h2 id="CAVEAT-ON-PRECEDENCE">
CAVEAT ON PRECEDENCE </h2> <p>Speaking of things like <code>@{$AoA[$i]}</code>, the following are actually the same thing: </p> <pre data-language="perl"><code>$aref-&gt;[2][2]       # clear
$$aref[2][2]        # confusing</code></pre> <p>That's because Perl's precedence rules on its five prefix dereferencers (which look like someone swearing: <code>$ @ * % &amp;</code>) make them bind more tightly than the postfix subscripting brackets or braces! This will no doubt come as a great shock to the C or C++ programmer, who is quite accustomed to using <code>*a[i]</code> to mean what's pointed to by the <i>i'th</i> element of <code>a</code>. That is, they first take the subscript, and only then dereference the thing at that subscript. That's fine in C, but this isn't C.</p> <p>The seemingly equivalent construct in Perl, <code>$$aref[$i]</code> first does the deref of $aref, making it take $aref as a reference to an array, and then dereference that, and finally tell you the <i>i'th</i> value of the array pointed to by $AoA. If you wanted the C notion, you could write <code>$AoA[$i]-&gt;$*</code> to explicitly dereference the <i>i'th</i> item, reading left to right.</p> <h2 id="WHY-YOU-SHOULD-ALWAYS-use-VERSION">
WHY YOU SHOULD ALWAYS <code>use VERSION</code>
</h2> <p>If this is starting to sound scarier than it's worth, relax. Perl has some features to help you avoid its most common pitfalls. One way to avoid getting confused is to start every program with:</p> <pre data-language="perl"><code>use strict;</code></pre> <p>This way, you'll be forced to declare all your variables with my() and also disallow accidental "symbolic dereferencing". Therefore if you'd done this:</p> <pre data-language="perl"><code>my $aref = [
    [ "fred", "barney", "pebbles", "bambam", "dino", ],
    [ "homer", "bart", "marge", "maggie", ],
    [ "george", "jane", "elroy", "judy", ],
];

print $aref[2][2];</code></pre> <p>The compiler would immediately flag that as an error <i>at compile time</i>, because you were accidentally accessing <code>@aref</code>, an undeclared variable, and it would thereby remind you to write instead:</p> <pre data-language="perl"><code>print $aref-&gt;[2][2]</code></pre> <p>Since Perl version 5.12, a <code>use VERSION</code> declaration will also enable the <code>strict</code> pragma. In addition, it will also enable a feature bundle, giving more useful features. Since version 5.36 it will also enable the <code>warnings</code> pragma. Often the best way to activate all these things at once is to start a file with:</p> <pre data-language="perl"><code>use v5.36;</code></pre> <p>In this way, every file will start with <code>strict</code>, <code>warnings</code>, and many useful named features all switched on, as well as several older features being switched off (such as <a href="feature.html#The-%27indirect%27-feature"><code>indirect</code></a>). For more information, see <a href="perlfunc.html#use-VERSION">"use VERSION" in perlfunc</a>.</p> <h2 id="DEBUGGING">DEBUGGING </h2> <p>You can use the debugger's <code>x</code> command to dump out complex data structures. For example, given the assignment to $AoA above, here's the debugger output:</p> <pre data-language="perl"><code>DB&lt;1&gt; x $AoA
$AoA = ARRAY(0x13b5a0)
   0  ARRAY(0x1f0a24)
      0  'fred'
      1  'barney'
      2  'pebbles'
      3  'bambam'
      4  'dino'
   1  ARRAY(0x13b558)
      0  'homer'
      1  'bart'
      2  'marge'
      3  'maggie'
   2  ARRAY(0x13b540)
      0  'george'
      1  'jane'
      2  'elroy'
      3  'judy'</code></pre> <h2 id="CODE-EXAMPLES">
CODE EXAMPLES</h2> <p>Presented with little comment here are short code examples illustrating access of various types of data structures.</p> <h2 id="ARRAYS-OF-ARRAYS">
ARRAYS OF ARRAYS </h2> <h3 id="Declaration-of-an-ARRAY-OF-ARRAYS">
Declaration of an ARRAY OF ARRAYS</h3> <pre data-language="perl"><code>my @AoA = (
       [ "fred", "barney" ],
       [ "george", "jane", "elroy" ],
       [ "homer", "marge", "bart" ],
     );</code></pre> <h3 id="Generation-of-an-ARRAY-OF-ARRAYS">
Generation of an ARRAY OF ARRAYS</h3> <pre data-language="perl"><code># reading from file
while ( &lt;&gt; ) {
    push @AoA, [ split ];
}

# calling a function
for my $i ( 1 .. 10 ) {
    $AoA[$i] = [ somefunc($i) ];
}

# using temp vars
for my $i ( 1 .. 10 ) {
    my @tmp = somefunc($i);
    $AoA[$i] = [ @tmp ];
}

# add to an existing row
push $AoA[0]-&gt;@*, "wilma", "betty";</code></pre> <h3 id="Access-and-Printing-of-an-ARRAY-OF-ARRAYS">
Access and Printing of an ARRAY OF ARRAYS</h3> <pre data-language="perl"><code># one element
$AoA[0][0] = "Fred";

# another element
$AoA[1][1] =~ s/(\w)/\u$1/;

# print the whole thing with refs
for my $aref ( @AoA ) {
    print "\t [ @$aref ],\n";
}

# print the whole thing with indices
for my $i ( 0 .. $#AoA ) {
    print "\t [ $AoA[$i]-&gt;@* ],\n";
}

# print the whole thing one at a time
for my $i ( 0 .. $#AoA ) {
    for my $j ( 0 .. $AoA[$i]-&gt;$#* ) {
        print "elem at ($i, $j) is $AoA[$i][$j]\n";
    }
}</code></pre> <h2 id="HASHES-OF-ARRAYS">
HASHES OF ARRAYS </h2> <h3 id="Declaration-of-a-HASH-OF-ARRAYS">
Declaration of a HASH OF ARRAYS</h3> <pre data-language="perl"><code>my %HoA = (
       flintstones        =&gt; [ "fred", "barney" ],
       jetsons            =&gt; [ "george", "jane", "elroy" ],
       simpsons           =&gt; [ "homer", "marge", "bart" ],
     );</code></pre> <h3 id="Generation-of-a-HASH-OF-ARRAYS">
Generation of a HASH OF ARRAYS</h3> <pre data-language="perl"><code># reading from file
# flintstones: fred barney wilma dino
while ( &lt;&gt; ) {
    next unless s/^(.*?):\s*//;
    $HoA{$1} = [ split ];
}

# reading from file; more temps
# flintstones: fred barney wilma dino
while ( my $line = &lt;&gt; ) {
    my ($who, $rest) = split /:\s*/, $line, 2;
    my @fields = split ' ', $rest;
    $HoA{$who} = [ @fields ];
}

# calling a function that returns a list
for my $group ( "simpsons", "jetsons", "flintstones" ) {
    $HoA{$group} = [ get_family($group) ];
}

# likewise, but using temps
for my $group ( "simpsons", "jetsons", "flintstones" ) {
    my @members = get_family($group);
    $HoA{$group} = [ @members ];
}

# append new members to an existing family
push $HoA{flintstones}-&gt;@*, "wilma", "betty";</code></pre> <h3 id="Access-and-Printing-of-a-HASH-OF-ARRAYS">
Access and Printing of a HASH OF ARRAYS</h3> <pre data-language="perl"><code># one element
$HoA{flintstones}[0] = "Fred";

# another element
$HoA{simpsons}[1] =~ s/(\w)/\u$1/;

# print the whole thing
foreach my $family ( keys %HoA ) {
    print "$family: $HoA{$family}-&gt;@* \n"
}

# print the whole thing with indices
foreach my $family ( keys %HoA ) {
    print "family: ";
    foreach my $i ( 0 .. $HoA{$family}-&gt;$#* ) {
        print " $i = $HoA{$family}[$i]";
    }
    print "\n";
}

# print the whole thing sorted by number of members
foreach my $family ( sort { $HoA{$b}-&gt;@* &lt;=&gt; $HoA{$a}-&gt;@* } keys %HoA ) {
    print "$family: $HoA{$family}-&gt;@* \n"
}

# print the whole thing sorted by number of members and name
foreach my $family ( sort {
                           $HoA{$b}-&gt;@* &lt;=&gt; $HoA{$a}-&gt;@*
                                         ||
                                     $a cmp $b
           } keys %HoA )
{
    print "$family: ", join(", ", sort $HoA{$family}-&gt;@* ), "\n";
}</code></pre> <h2 id="ARRAYS-OF-HASHES">
ARRAYS OF HASHES </h2> <h3 id="Declaration-of-an-ARRAY-OF-HASHES">
Declaration of an ARRAY OF HASHES</h3> <pre data-language="perl"><code>my @AoH = (
       {
           Lead     =&gt; "fred",
           Friend   =&gt; "barney",
       },
       {
           Lead     =&gt; "george",
           Wife     =&gt; "jane",
           Son      =&gt; "elroy",
       },
       {
           Lead     =&gt; "homer",
           Wife     =&gt; "marge",
           Son      =&gt; "bart",
       }
 );</code></pre> <h3 id="Generation-of-an-ARRAY-OF-HASHES">
Generation of an ARRAY OF HASHES</h3> <pre data-language="perl"><code># reading from file
# format: LEAD=fred FRIEND=barney
while ( &lt;&gt; ) {
    my $rec = {};
    for my $field ( split ) {
        my ($key, $value) = split /=/, $field;
        $rec-&gt;{$key} = $value;
    }
    push @AoH, $rec;
}


# reading from file
# format: LEAD=fred FRIEND=barney
# no temp
while ( &lt;&gt; ) {
    push @AoH, { split /[\s+=]/ };
}

# calling a function  that returns a key/value pair list, like
# "lead","fred","daughter","pebbles"
while ( my %fields = getnextpairset() ) {
    push @AoH, { %fields };
}

# likewise, but using no temp vars
while (&lt;&gt;) {
    push @AoH, { parsepairs($_) };
}

# add key/value to an element
$AoH[0]{pet} = "dino";
$AoH[2]{pet} = "santa's little helper";</code></pre> <h3 id="Access-and-Printing-of-an-ARRAY-OF-HASHES">
Access and Printing of an ARRAY OF HASHES</h3> <pre data-language="perl"><code># one element
$AoH[0]{lead} = "fred";

# another element
$AoH[1]{lead} =~ s/(\w)/\u$1/;

# print the whole thing with refs
for my $href ( @AoH ) {
    print "{ ";
    for my $role ( keys %$href ) {
        print "$role=$href-&gt;{$role} ";
    }
    print "}\n";
}

# print the whole thing with indices
for my $i ( 0 .. $#AoH ) {
    print "$i is { ";
    for my $role ( keys $AoH[$i]-&gt;%* ) {
        print "$role=$AoH[$i]{$role} ";
    }
    print "}\n";
}

# print the whole thing one at a time
for my $i ( 0 .. $#AoH ) {
    for my $role ( keys $AoH[$i]-&gt;%* ) {
        print "elem at ($i, $role) is $AoH[$i]{$role}\n";
    }
}</code></pre> <h2 id="HASHES-OF-HASHES">
HASHES OF HASHES </h2> <h3 id="Declaration-of-a-HASH-OF-HASHES">
Declaration of a HASH OF HASHES</h3> <pre data-language="perl"><code>my %HoH = (
       flintstones =&gt; {
               lead      =&gt; "fred",
               pal       =&gt; "barney",
       },
       jetsons     =&gt; {
               lead      =&gt; "george",
               wife      =&gt; "jane",
               "his boy" =&gt; "elroy",
       },
       simpsons    =&gt; {
               lead      =&gt; "homer",
               wife      =&gt; "marge",
               kid       =&gt; "bart",
       },
);</code></pre> <h3 id="Generation-of-a-HASH-OF-HASHES">
Generation of a HASH OF HASHES</h3> <pre data-language="perl"><code># reading from file
# flintstones: lead=fred pal=barney wife=wilma pet=dino
while ( &lt;&gt; ) {
    next unless s/^(.*?):\s*//;
    my $who = $1;
    for my $field ( split ) {
        my ($key, $value) = split /=/, $field;
        $HoH{$who}{$key} = $value;
    }
}


# reading from file; more temps
while ( &lt;&gt; ) {
    next unless s/^(.*?):\s*//;
    my $who = $1;
    my $rec = {};
    $HoH{$who} = $rec;
    for my $field ( split ) {
        my ($key, $value) = split /=/, $field;
        $rec-&gt;{$key} = $value;
    }
}

# calling a function  that returns a key,value hash
for my $group ( "simpsons", "jetsons", "flintstones" ) {
    $HoH{$group} = { get_family($group) };
}

# likewise, but using temps
for my $group ( "simpsons", "jetsons", "flintstones" ) {
    my %members = get_family($group);
    $HoH{$group} = { %members };
}

# append new members to an existing family
my %new_folks = (
    wife =&gt; "wilma",
    pet  =&gt; "dino",
);

for my $what (keys %new_folks) {
    $HoH{flintstones}{$what} = $new_folks{$what};
}</code></pre> <h3 id="Access-and-Printing-of-a-HASH-OF-HASHES">
Access and Printing of a HASH OF HASHES</h3> <pre data-language="perl"><code># one element
$HoH{flintstones}{wife} = "wilma";

# another element
$HoH{simpsons}{lead} =~ s/(\w)/\u$1/;

# print the whole thing
foreach my $family ( keys %HoH ) {
    print "$family: { ";
    for my $role ( keys $HoH{$family}-&gt;%* ) {
        print "$role=$HoH{$family}{$role} ";
    }
    print "}\n";
}

# print the whole thing  somewhat sorted
foreach my $family ( sort keys %HoH ) {
    print "$family: { ";
    for my $role ( sort keys $HoH{$family}-&gt;%* ) {
        print "$role=$HoH{$family}{$role} ";
    }
    print "}\n";
}


# print the whole thing sorted by number of members
foreach my $family ( sort { $HoH{$b}-&gt;%* &lt;=&gt; $HoH{$a}-&gt;%* } keys %HoH ) {
    print "$family: { ";
    for my $role ( sort keys $HoH{$family}-&gt;%* ) {
        print "$role=$HoH{$family}{$role} ";
    }
    print "}\n";
}

# establish a sort order (rank) for each role
my $i = 0;
my %rank;
for ( qw(lead wife son daughter pal pet) ) { $rank{$_} = ++$i }

# now print the whole thing sorted by number of members
foreach my $family ( sort { $HoH{$b}-&gt;%* &lt;=&gt; $HoH{$a}-&gt;%* } keys %HoH ) {
    print "$family: { ";
    # and print these according to rank order
    for my $role ( sort { $rank{$a} &lt;=&gt; $rank{$b} }
                                              keys $HoH{$family}-&gt;%* )
    {
        print "$role=$HoH{$family}{$role} ";
    }
    print "}\n";
}</code></pre> <h2 id="MORE-ELABORATE-RECORDS">
MORE ELABORATE RECORDS </h2> <h3 id="Declaration-of-MORE-ELABORATE-RECORDS">
Declaration of MORE ELABORATE RECORDS</h3> <p>Here's a sample showing how to create and use a record whose fields are of many different sorts:</p> <pre data-language="perl"><code>my $rec = {
    TEXT      =&gt; $string,
    SEQUENCE  =&gt; [ @old_values ],
    LOOKUP    =&gt; { %some_table },
    THATCODE  =&gt; \&amp;some_function,
    THISCODE  =&gt; sub { $_[0] ** $_[1] },
    HANDLE    =&gt; \*STDOUT,
};

print $rec-&gt;{TEXT};

print $rec-&gt;{SEQUENCE}[0];
my $last = pop $rec-&gt;{SEQUENCE}-&gt;@*;

print $rec-&gt;{LOOKUP}{"key"};
my ($first_k, $first_v) = each $rec-&gt;{LOOKUP}-&gt;%*;

my $answer = $rec-&gt;{THATCODE}-&gt;($arg);
$answer = $rec-&gt;{THISCODE}-&gt;($arg1, $arg2);

# careful of extra block braces on fh ref
print { $rec-&gt;{HANDLE} } "a string\n";

use FileHandle;
$rec-&gt;{HANDLE}-&gt;autoflush(1);
$rec-&gt;{HANDLE}-&gt;print(" a string\n");</code></pre> <h3 id="Declaration-of-a-HASH-OF-COMPLEX-RECORDS">
Declaration of a HASH OF COMPLEX RECORDS</h3> <pre data-language="perl"><code>my %TV = (
   flintstones =&gt; {
       series   =&gt; "flintstones",
       nights   =&gt; [ qw(monday thursday friday) ],
       members  =&gt; [
           { name =&gt; "fred",    role =&gt; "lead", age  =&gt; 36, },
           { name =&gt; "wilma",   role =&gt; "wife", age  =&gt; 31, },
           { name =&gt; "pebbles", role =&gt; "kid",  age  =&gt;  4, },
       ],
   },

   jetsons     =&gt; {
       series   =&gt; "jetsons",
       nights   =&gt; [ qw(wednesday saturday) ],
       members  =&gt; [
           { name =&gt; "george",  role =&gt; "lead", age  =&gt; 41, },
           { name =&gt; "jane",    role =&gt; "wife", age  =&gt; 39, },
           { name =&gt; "elroy",   role =&gt; "kid",  age  =&gt;  9, },
       ],
    },

   simpsons    =&gt; {
       series   =&gt; "simpsons",
       nights   =&gt; [ qw(monday) ],
       members  =&gt; [
           { name =&gt; "homer", role =&gt; "lead", age  =&gt; 34, },
           { name =&gt; "marge", role =&gt; "wife", age =&gt; 37, },
           { name =&gt; "bart",  role =&gt; "kid",  age  =&gt;  11, },
       ],
    },
 );</code></pre> <h3 id="Generation-of-a-HASH-OF-COMPLEX-RECORDS">
Generation of a HASH OF COMPLEX RECORDS</h3> <pre data-language="perl"><code># reading from file
# this is most easily done by having the file itself be
# in the raw data format as shown above.  perl is happy
# to parse complex data structures if declared as data, so
# sometimes it's easiest to do that

# here's a piece by piece build up
my $rec = {};
$rec-&gt;{series} = "flintstones";
$rec-&gt;{nights} = [ find_days() ];

my @members = ();
# assume this file in field=value syntax
while (&lt;&gt;) {
    my %fields = split /[\s=]+/;
    push @members, { %fields };
}
$rec-&gt;{members} = [ @members ];

# now remember the whole thing
$TV{ $rec-&gt;{series} } = $rec;

###########################################################
# now, you might want to make interesting extra fields that
# include pointers back into the same data structure so if
# change one piece, it changes everywhere, like for example
# if you wanted a {kids} field that was a reference
# to an array of the kids' records without having duplicate
# records and thus update problems.
###########################################################
foreach my $family (keys %TV) {
    my $rec = $TV{$family}; # temp pointer
    my @kids = ();
    for my $person ( $rec-&gt;{members}-&gt;@* ) {
        if ($person-&gt;{role} =~ /kid|son|daughter/) {
            push @kids, $person;
        }
    }
    # REMEMBER: $rec and $TV{$family} point to same data!!
    $rec-&gt;{kids} = [ @kids ];
}

# you copied the array, but the array itself contains pointers
# to uncopied objects. this means that if you make bart get
# older via

$TV{simpsons}{kids}[0]{age}++;

# then this would also change in
print $TV{simpsons}{members}[2]{age};

# because $TV{simpsons}{kids}[0] and $TV{simpsons}{members}[2]
# both point to the same underlying anonymous hash table

# print the whole thing
foreach my $family ( keys %TV ) {
    print "the $family";
    print " is on during $TV{$family}{nights}-&gt;@*\n";
    print "its members are:\n";
    for my $who ( $TV{$family}{members}-&gt;@* ) {
        print " $who-&gt;{name} ($who-&gt;{role}), age $who-&gt;{age}\n";
    }
    print "it turns out that $TV{$family}{lead} has ";
    print scalar ( $TV{$family}{kids}-&gt;@* ), " kids named ";
    print join (", ", map { $_-&gt;{name} } $TV{$family}{kids}-&gt;@* );
    print "\n";
}</code></pre> <h2 id="Database-Ties">
Database Ties</h2> <p>You cannot easily tie a multilevel data structure (such as a hash of hashes) to a dbm file. The first problem is that all but GDBM and Berkeley DB have size limitations, but beyond that, you also have problems with how references are to be represented on disk. One experimental module that does partially attempt to address this need is the MLDBM module. Check your nearest CPAN site as described in <a href="perlmodlib.html">perlmodlib</a> for source code to MLDBM.</p> <h2 id="SEE-ALSO">
SEE ALSO</h2> <p><a href="perlref.html">perlref</a>, <a href="perllol.html">perllol</a>, <a href="perldata.html">perldata</a>, <a href="perlobj.html">perlobj</a></p> <h2 id="AUTHOR">AUTHOR</h2> <p>Tom Christiansen &lt;<i>tchrist@perl.com</i>&gt;</p><div class="_attribution">
  <p class="_attribution-p">
    &copy; 1993&ndash;2023 Larry Wall and others<br>Licensed under the GNU General Public License version 1 or later, or the Artistic License.<br>The Perl logo is a trademark of the Perl Foundation.<br>
    <a href="https://perldoc.perl.org/5.38.0/perldsc" class="_attribution-link">https://perldoc.perl.org/5.38.0/perldsc</a>
  </p>
</div>
