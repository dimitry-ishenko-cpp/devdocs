<h1>perltie</h1>   <h2><a id="toc">CONTENTS</a></h2> <ul> <li> <a class="text-decoration-none" href="#NAME">NAME </a> </li> <li> <a class="text-decoration-none" href="#SYNOPSIS">SYNOPSIS</a> </li> <li> <a class="text-decoration-none" href="#DESCRIPTION">DESCRIPTION</a> <ul> <li> <a class="text-decoration-none" href="#Tying-Scalars">Tying Scalars </a> </li> <li> <a class="text-decoration-none" href="#Tying-Arrays">Tying Arrays </a> </li> <li> <a class="text-decoration-none" href="#Tying-Hashes">Tying Hashes </a> </li> <li> <a class="text-decoration-none" href="#Tying-FileHandles">Tying FileHandles </a> </li> <li> <a class="text-decoration-none" href="#UNTIE-this4">UNTIE this </a> </li> <li> <a class="text-decoration-none" href="#The-untie-Gotcha">The untie Gotcha </a> </li> </ul> </li> <li> <a class="text-decoration-none" href="#SEE-ALSO">SEE ALSO</a> </li> <li> <a class="text-decoration-none" href="#BUGS">BUGS</a> </li> <li> <a class="text-decoration-none" href="#AUTHOR">AUTHOR</a> </li> </ul> <h2 id="NAME">NAME </h2> <p>perltie - how to hide an object class in a simple variable</p> <h2 id="SYNOPSIS">SYNOPSIS</h2> <pre data-language="perl"><code>tie VARIABLE, CLASSNAME, LIST

$object = tied VARIABLE

untie VARIABLE</code></pre> <h2 id="DESCRIPTION">DESCRIPTION</h2> <p>Prior to release 5.0 of Perl, a programmer could use dbmopen() to connect an on-disk database in the standard Unix dbm(3x) format magically to a %HASH in their program. However, their Perl was either built with one particular dbm library or another, but not both, and you couldn't extend this mechanism to other packages or types of variables.</p> <p>Now you can.</p> <p>The tie() function binds a variable to a class (package) that will provide the implementation for access methods for that variable. Once this magic has been performed, accessing a tied variable automatically triggers method calls in the proper class. The complexity of the class is hidden behind magic methods calls. The method names are in ALL CAPS, which is a convention that Perl uses to indicate that they're called implicitly rather than explicitly--just like the BEGIN() and END() functions.</p> <p>In the tie() call, <code>VARIABLE</code> is the name of the variable to be enchanted. <code>CLASSNAME</code> is the name of a class implementing objects of the correct type. Any additional arguments in the <code>LIST</code> are passed to the appropriate constructor method for that class--meaning TIESCALAR(), TIEARRAY(), TIEHASH(), or TIEHANDLE(). (Typically these are arguments such as might be passed to the dbminit() function of C.) The object returned by the "new" method is also returned by the tie() function, which would be useful if you wanted to access other methods in <code>CLASSNAME</code>. (You don't actually have to return a reference to a right "type" (e.g., HASH or <code>CLASSNAME</code>) so long as it's a properly blessed object.) You can also retrieve a reference to the underlying object using the tied() function.</p> <p>Unlike dbmopen(), the tie() function will not <code>use</code> or <code>require</code> a module for you--you need to do that explicitly yourself.</p> <h3 id="Tying-Scalars">
Tying Scalars </h3> <p>A class implementing a tied scalar should define the following methods: TIESCALAR, FETCH, STORE, and possibly UNTIE and/or DESTROY.</p> <p>Let's look at each in turn, using as an example a tie class for scalars that allows the user to do something like:</p> <pre data-language="perl"><code>tie $his_speed, 'Nice', getppid();
tie $my_speed,  'Nice', $$;</code></pre> <p>And now whenever either of those variables is accessed, its current system priority is retrieved and returned. If those variables are set, then the process's priority is changed!</p> <p>We'll use Jarkko Hietaniemi &lt;<i>jhi@iki.fi</i>&gt;'s BSD::Resource class (not included) to access the PRIO_PROCESS, PRIO_MIN, and PRIO_MAX constants from your system, as well as the getpriority() and setpriority() system calls. Here's the preamble of the class.</p> <pre data-language="perl"><code>package Nice;
use Carp;
use BSD::Resource;
use strict;
$Nice::DEBUG = 0 unless defined $Nice::DEBUG;</code></pre> <dl> <dt id="TIESCALAR-classname,-LIST">
TIESCALAR classname, LIST </dt> <dd> <p>This is the constructor for the class. That means it is expected to return a blessed reference to a new scalar (probably anonymous) that it's creating. For example:</p> <pre data-language="perl"><code>sub TIESCALAR {
    my $class = shift;
    my $pid = shift || $$; # 0 means me

    if ($pid !~ /^\d+$/) {
        carp "Nice::Tie::Scalar got non-numeric pid $pid" if $^W;
        return undef;
    }

    unless (kill 0, $pid) { # EPERM or ERSCH, no doubt
        carp "Nice::Tie::Scalar got bad pid $pid: $!" if $^W;
        return undef;
    }

    return bless \$pid, $class;
}</code></pre> <p>This tie class has chosen to return an error rather than raising an exception if its constructor should fail. While this is how dbmopen() works, other classes may well not wish to be so forgiving. It checks the global variable <code>$^W</code> to see whether to emit a bit of noise anyway.</p> </dd> <dt id="FETCH-this">
FETCH this </dt> <dd> <p>This method will be triggered every time the tied variable is accessed (read). It takes no arguments beyond its self reference, which is the object representing the scalar we're dealing with. Because in this case we're using just a SCALAR ref for the tied scalar object, a simple $$self allows the method to get at the real value stored there. In our example below, that real value is the process ID to which we've tied our variable.</p> <pre data-language="perl"><code>sub FETCH {
    my $self = shift;
    confess "wrong type" unless ref $self;
    croak "usage error" if @_;
    my $nicety;
    local($!) = 0;
    $nicety = getpriority(PRIO_PROCESS, $$self);
    if ($!) { croak "getpriority failed: $!" }
    return $nicety;
}</code></pre> <p>This time we've decided to blow up (raise an exception) if the renice fails--there's no place for us to return an error otherwise, and it's probably the right thing to do.</p> </dd> <dt id="STORE-this,-value">
STORE this, value </dt> <dd> <p>This method will be triggered every time the tied variable is set (assigned). Beyond its self reference, it also expects one (and only one) argument: the new value the user is trying to assign. Don't worry about returning a value from STORE; the semantic of assignment returning the assigned value is implemented with FETCH.</p> <pre data-language="perl"><code>sub STORE {
    my $self = shift;
    confess "wrong type" unless ref $self;
    my $new_nicety = shift;
    croak "usage error" if @_;

    if ($new_nicety &lt; PRIO_MIN) {
        carp sprintf
          "WARNING: priority %d less than minimum system priority %d",
              $new_nicety, PRIO_MIN if $^W;
        $new_nicety = PRIO_MIN;
    }

    if ($new_nicety &gt; PRIO_MAX) {
        carp sprintf
          "WARNING: priority %d greater than maximum system priority %d",
              $new_nicety, PRIO_MAX if $^W;
        $new_nicety = PRIO_MAX;
    }

    unless (defined setpriority(PRIO_PROCESS,
                                $$self,
                                $new_nicety))
    {
        confess "setpriority failed: $!";
    }
}</code></pre> </dd> <dt id="UNTIE-this">
UNTIE this </dt> <dd> <p>This method will be triggered when the <code>untie</code> occurs. This can be useful if the class needs to know when no further calls will be made. (Except DESTROY of course.) See <a href="#The-untie-Gotcha">"The <code>untie</code> Gotcha"</a> below for more details.</p> </dd> <dt id="DESTROY-this">
DESTROY this </dt> <dd> <p>This method will be triggered when the tied variable needs to be destructed. As with other object classes, such a method is seldom necessary, because Perl deallocates its moribund object's memory for you automatically--this isn't C++, you know. We'll use a DESTROY method here for debugging purposes only.</p> <pre data-language="perl"><code>sub DESTROY {
    my $self = shift;
    confess "wrong type" unless ref $self;
    carp "[ Nice::DESTROY pid $$self ]" if $Nice::DEBUG;
}</code></pre> </dd> </dl> <p>That's about all there is to it. Actually, it's more than all there is to it, because we've done a few nice things here for the sake of completeness, robustness, and general aesthetics. Simpler TIESCALAR classes are certainly possible.</p> <h3 id="Tying-Arrays">
Tying Arrays </h3> <p>A class implementing a tied ordinary array should define the following methods: TIEARRAY, FETCH, STORE, FETCHSIZE, STORESIZE, CLEAR and perhaps UNTIE and/or DESTROY.</p> <p>FETCHSIZE and STORESIZE are used to provide <code>$#array</code> and equivalent <code>scalar(@array)</code> access.</p> <p>The methods POP, PUSH, SHIFT, UNSHIFT, SPLICE, DELETE, and EXISTS are required if the perl operator with the corresponding (but lowercase) name is to operate on the tied array. The <b>Tie::Array</b> class can be used as a base class to implement the first five of these in terms of the basic methods above. The default implementations of DELETE and EXISTS in <b>Tie::Array</b> simply <code>croak</code>.</p> <p>In addition EXTEND will be called when perl would have pre-extended allocation in a real array.</p> <p>For this discussion, we'll implement an array whose elements are a fixed size at creation. If you try to create an element larger than the fixed size, you'll take an exception. For example:</p> <pre data-language="perl"><code>use FixedElem_Array;
tie @array, 'FixedElem_Array', 3;
$array[0] = 'cat';  # ok.
$array[1] = 'dogs'; # exception, length('dogs') &gt; 3.</code></pre> <p>The preamble code for the class is as follows:</p> <pre data-language="perl"><code>package FixedElem_Array;
use Carp;
use strict;</code></pre> <dl> <dt id="TIEARRAY-classname,-LIST">
TIEARRAY classname, LIST </dt> <dd> <p>This is the constructor for the class. That means it is expected to return a blessed reference through which the new array (probably an anonymous ARRAY ref) will be accessed.</p> <p>In our example, just to show you that you don't <i>really</i> have to return an ARRAY reference, we'll choose a HASH reference to represent our object. A HASH works out well as a generic record type: the <code>{ELEMSIZE}</code> field will store the maximum element size allowed, and the <code>{ARRAY}</code> field will hold the true ARRAY ref. If someone outside the class tries to dereference the object returned (doubtless thinking it an ARRAY ref), they'll blow up. This just goes to show you that you should respect an object's privacy.</p> <pre data-language="perl"><code>sub TIEARRAY {
  my $class    = shift;
  my $elemsize = shift;
  if ( @_ || $elemsize =~ /\D/ ) {
    croak "usage: tie ARRAY, '" . __PACKAGE__ . "', elem_size";
  }
  return bless {
    ELEMSIZE =&gt; $elemsize,
    ARRAY    =&gt; [],
  }, $class;
}</code></pre> </dd> <dt id="FETCH-this,-index">
FETCH this, index </dt> <dd> <p>This method will be triggered every time an individual element the tied array is accessed (read). It takes one argument beyond its self reference: the index whose value we're trying to fetch.</p> <pre data-language="perl"><code>sub FETCH {
  my $self  = shift;
  my $index = shift;
  return $self-&gt;{ARRAY}-&gt;[$index];
}</code></pre> <p>If a negative array index is used to read from an array, the index will be translated to a positive one internally by calling FETCHSIZE before being passed to FETCH. You may disable this feature by assigning a true value to the variable <code>$NEGATIVE_INDICES</code> in the tied array class.</p> <p>As you may have noticed, the name of the FETCH method (et al.) is the same for all accesses, even though the constructors differ in names (TIESCALAR vs TIEARRAY). While in theory you could have the same class servicing several tied types, in practice this becomes cumbersome, and it's easiest to keep them at simply one tie type per class.</p> </dd> <dt id="STORE-this,-index,-value">
STORE this, index, value </dt> <dd> <p>This method will be triggered every time an element in the tied array is set (written). It takes two arguments beyond its self reference: the index at which we're trying to store something and the value we're trying to put there.</p> <p>In our example, <code>undef</code> is really <code>$self-&gt;{ELEMSIZE}</code> number of spaces so we have a little more work to do here:</p> <pre data-language="perl"><code>sub STORE {
  my $self = shift;
  my( $index, $value ) = @_;
  if ( length $value &gt; $self-&gt;{ELEMSIZE} ) {
    croak "length of $value is greater than $self-&gt;{ELEMSIZE}";
  }
  # fill in the blanks
  $self-&gt;STORESIZE( $index ) if $index &gt; $self-&gt;FETCHSIZE();
  # right justify to keep element size for smaller elements
  $self-&gt;{ARRAY}-&gt;[$index] = sprintf "%$self-&gt;{ELEMSIZE}s", $value;
}</code></pre> <p>Negative indexes are treated the same as with FETCH.</p> </dd> <dt id="FETCHSIZE-this">
FETCHSIZE this </dt> <dd> <p>Returns the total number of items in the tied array associated with object <i>this</i>. (Equivalent to <code>scalar(@array)</code>). For example:</p> <pre data-language="perl"><code>sub FETCHSIZE {
  my $self = shift;
  return scalar $self-&gt;{ARRAY}-&gt;@*;
}</code></pre> </dd> <dt id="STORESIZE-this,-count">
STORESIZE this, count </dt> <dd> <p>Sets the total number of items in the tied array associated with object <i>this</i> to be <i>count</i>. If this makes the array larger then class's mapping of <code>undef</code> should be returned for new positions. If the array becomes smaller then entries beyond count should be deleted.</p> <p>In our example, 'undef' is really an element containing <code>$self-&gt;{ELEMSIZE}</code> number of spaces. Observe:</p> <pre data-language="perl"><code>sub STORESIZE {
  my $self  = shift;
  my $count = shift;
  if ( $count &gt; $self-&gt;FETCHSIZE() ) {
    foreach ( $count - $self-&gt;FETCHSIZE() .. $count ) {
      $self-&gt;STORE( $_, '' );
    }
  } elsif ( $count &lt; $self-&gt;FETCHSIZE() ) {
    foreach ( 0 .. $self-&gt;FETCHSIZE() - $count - 2 ) {
      $self-&gt;POP();
    }
  }
}</code></pre> </dd> <dt id="EXTEND-this,-count">
EXTEND this, count </dt> <dd> <p>Informative call that array is likely to grow to have <i>count</i> entries. Can be used to optimize allocation. This method need do nothing.</p> <p>In our example there is no reason to implement this method, so we leave it as a no-op. This method is only relevant to tied array implementations where there is the possibility of having the allocated size of the array be larger than is visible to a perl programmer inspecting the size of the array. Many tied array implementations will have no reason to implement it.</p> <pre data-language="perl"><code>sub EXTEND {   
  my $self  = shift;
  my $count = shift;
  # nothing to see here, move along.
}</code></pre> <p><b>NOTE:</b> It is generally an error to make this equivalent to STORESIZE. Perl may from time to time call EXTEND without wanting to actually change the array size directly. Any tied array should function correctly if this method is a no-op, even if perhaps they might not be as efficient as they would if this method was implemented.</p> </dd> <dt id="EXISTS-this,-key">
EXISTS this, key </dt> <dd> <p>Verify that the element at index <i>key</i> exists in the tied array <i>this</i>.</p> <p>In our example, we will determine that if an element consists of <code>$self-&gt;{ELEMSIZE}</code> spaces only, it does not exist:</p> <pre data-language="perl"><code>sub EXISTS {
  my $self  = shift;
  my $index = shift;
  return 0 if ! defined $self-&gt;{ARRAY}-&gt;[$index] ||
              $self-&gt;{ARRAY}-&gt;[$index] eq ' ' x $self-&gt;{ELEMSIZE};
  return 1;
}</code></pre> </dd> <dt id="DELETE-this,-key">
DELETE this, key </dt> <dd> <p>Delete the element at index <i>key</i> from the tied array <i>this</i>.</p> <p>In our example, a deleted item is <code>$self-&gt;{ELEMSIZE}</code> spaces:</p> <pre data-language="perl"><code>sub DELETE {
  my $self  = shift;
  my $index = shift;
  return $self-&gt;STORE( $index, '' );
}</code></pre> </dd> <dt id="CLEAR-this">
CLEAR this </dt> <dd> <p>Clear (remove, delete, ...) all values from the tied array associated with object <i>this</i>. For example:</p> <pre data-language="perl"><code>sub CLEAR {
  my $self = shift;
  return $self-&gt;{ARRAY} = [];
}</code></pre> </dd> <dt id="PUSH-this,-LIST">
PUSH this, LIST </dt> <dd> <p>Append elements of <i>LIST</i> to the array. For example:</p> <pre data-language="perl"><code>sub PUSH {  
  my $self = shift;
  my @list = @_;
  my $last = $self-&gt;FETCHSIZE();
  $self-&gt;STORE( $last + $_, $list[$_] ) foreach 0 .. $#list;
  return $self-&gt;FETCHSIZE();
}   </code></pre> </dd> <dt id="POP-this">
POP this </dt> <dd> <p>Remove last element of the array and return it. For example:</p> <pre data-language="perl"><code>sub POP {
  my $self = shift;
  return pop $self-&gt;{ARRAY}-&gt;@*;
}</code></pre> </dd> <dt id="SHIFT-this">
SHIFT this </dt> <dd> <p>Remove the first element of the array (shifting other elements down) and return it. For example:</p> <pre data-language="perl"><code>sub SHIFT {
  my $self = shift;
  return shift $self-&gt;{ARRAY}-&gt;@*;
}</code></pre> </dd> <dt id="UNSHIFT-this,-LIST">
UNSHIFT this, LIST </dt> <dd> <p>Insert LIST elements at the beginning of the array, moving existing elements up to make room. For example:</p> <pre data-language="perl"><code>sub UNSHIFT {
  my $self = shift;
  my @list = @_;
  my $size = scalar( @list );
  # make room for our list
  $self-&gt;{ARRAY}[ $size .. $self-&gt;{ARRAY}-&gt;$#* + $size ]-&gt;@*
   = $self-&gt;{ARRAY}-&gt;@*
  $self-&gt;STORE( $_, $list[$_] ) foreach 0 .. $#list;
}</code></pre> </dd> <dt id="SPLICE-this,-offset,-length,-LIST">
SPLICE this, offset, length, LIST </dt> <dd> <p>Perform the equivalent of <code>splice</code> on the array.</p> <p><i>offset</i> is optional and defaults to zero, negative values count back from the end of the array.</p> <p><i>length</i> is optional and defaults to rest of the array.</p> <p><i>LIST</i> may be empty.</p> <p>Returns a list of the original <i>length</i> elements at <i>offset</i>.</p> <p>In our example, we'll use a little shortcut if there is a <i>LIST</i>:</p> <pre data-language="perl"><code>sub SPLICE {
  my $self   = shift;
  my $offset = shift || 0;
  my $length = shift || $self-&gt;FETCHSIZE() - $offset;
  my @list   = (); 
  if ( @_ ) {
    tie @list, __PACKAGE__, $self-&gt;{ELEMSIZE};
    @list   = @_;
  }
  return splice $self-&gt;{ARRAY}-&gt;@*, $offset, $length, @list;
}</code></pre> </dd> <dt id="UNTIE-this1">
UNTIE this </dt> <dd> <p>Will be called when <code>untie</code> happens. (See <a href="#The-untie-Gotcha">"The <code>untie</code> Gotcha"</a> below.)</p> </dd> <dt id="DESTROY-this1">
DESTROY this </dt> <dd> <p>This method will be triggered when the tied variable needs to be destructed. As with the scalar tie class, this is almost never needed in a language that does its own garbage collection, so this time we'll just leave it out.</p> </dd> </dl> <h3 id="Tying-Hashes">
Tying Hashes </h3> <p>Hashes were the first Perl data type to be tied (see dbmopen()). A class implementing a tied hash should define the following methods: TIEHASH is the constructor. FETCH and STORE access the key and value pairs. EXISTS reports whether a key is present in the hash, and DELETE deletes one. CLEAR empties the hash by deleting all the key and value pairs. FIRSTKEY and NEXTKEY implement the keys() and each() functions to iterate over all the keys. SCALAR is triggered when the tied hash is evaluated in scalar context, and in 5.28 onwards, by <code>keys</code> in boolean context. UNTIE is called when <code>untie</code> happens, and DESTROY is called when the tied variable is garbage collected.</p> <p>If this seems like a lot, then feel free to inherit from merely the standard Tie::StdHash module for most of your methods, redefining only the interesting ones. See <a href="Tie::Hash.html">Tie::Hash</a> for details.</p> <p>Remember that Perl distinguishes between a key not existing in the hash, and the key existing in the hash but having a corresponding value of <code>undef</code>. The two possibilities can be tested with the <code>exists()</code> and <code>defined()</code> functions.</p> <p>Here's an example of a somewhat interesting tied hash class: it gives you a hash representing a particular user's dot files. You index into the hash with the name of the file (minus the dot) and you get back that dot file's contents. For example:</p> <pre data-language="perl"><code>    use DotFiles;
    tie %dot, 'DotFiles';
    if ( $dot{profile} =~ /MANPATH/ ||
         $dot{login}   =~ /MANPATH/ ||
         $dot{cshrc}   =~ /MANPATH/    )
    {
	print "you seem to set your MANPATH\n";
    }</code></pre> <p>Or here's another sample of using our tied class:</p> <pre data-language="perl"><code>    tie %him, 'DotFiles', 'daemon';
    foreach $f ( keys %him ) {
	printf "daemon dot file %s is size %d\n",
	    $f, length $him{$f};
    }</code></pre> <p>In our tied hash DotFiles example, we use a regular hash for the object containing several important fields, of which only the <code>{LIST}</code> field will be what the user thinks of as the real hash.</p> <dl> <dt id="USER">USER</dt> <dd> <p>whose dot files this object represents</p> </dd> <dt id="HOME">HOME</dt> <dd> <p>where those dot files live</p> </dd> <dt id="CLOBBER">CLOBBER</dt> <dd> <p>whether we should try to change or remove those dot files</p> </dd> <dt id="LIST">LIST</dt> <dd> <p>the hash of dot file names and content mappings</p> </dd> </dl> <p>Here's the start of <i>Dotfiles.pm</i>:</p> <pre data-language="perl"><code>package DotFiles;
use Carp;
sub whowasi { (caller(1))[3] . '()' }
my $DEBUG = 0;
sub debug { $DEBUG = @_ ? shift : 1 }</code></pre> <p>For our example, we want to be able to emit debugging info to help in tracing during development. We keep also one convenience function around internally to help print out warnings; whowasi() returns the function name that calls it.</p> <p>Here are the methods for the DotFiles tied hash.</p> <dl> <dt id="TIEHASH-classname,-LIST">
TIEHASH classname, LIST </dt> <dd> <p>This is the constructor for the class. That means it is expected to return a blessed reference through which the new object (probably but not necessarily an anonymous hash) will be accessed.</p> <p>Here's the constructor:</p> <pre data-language="perl"><code>    sub TIEHASH {
	my $class = shift;
	my $user = shift || $&gt;;
	my $dotdir = shift || '';
	croak "usage: @{[&amp;whowasi]} [USER [DOTDIR]]" if @_;
	$user = getpwuid($user) if $user =~ /^\d+$/;
	my $dir = (getpwnam($user))[7]
		|| croak "@{[&amp;whowasi]}: no user $user";
	$dir .= "/$dotdir" if $dotdir;

	my $node = {
	    USER    =&gt; $user,
	    HOME    =&gt; $dir,
	    LIST    =&gt; {},
	    CLOBBER =&gt; 0,
	};

	opendir(DIR, $dir)
		|| croak "@{[&amp;whowasi]}: can't opendir $dir: $!";
	foreach $dot ( grep /^\./ &amp;&amp; -f "$dir/$_", readdir(DIR)) {
	    $dot =~ s/^\.//;
	    $node-&gt;{LIST}{$dot} = undef;
	}
	closedir DIR;
	return bless $node, $class;
    }</code></pre> <p>It's probably worth mentioning that if you're going to filetest the return values out of a readdir, you'd better prepend the directory in question. Otherwise, because we didn't chdir() there, it would have been testing the wrong file.</p> </dd> <dt id="FETCH-this,-key">
FETCH this, key </dt> <dd> <p>This method will be triggered every time an element in the tied hash is accessed (read). It takes one argument beyond its self reference: the key whose value we're trying to fetch.</p> <p>Here's the fetch for our DotFiles example.</p> <pre data-language="perl"><code>    sub FETCH {
	carp &amp;whowasi if $DEBUG;
	my $self = shift;
	my $dot = shift;
	my $dir = $self-&gt;{HOME};
	my $file = "$dir/.$dot";

	unless (exists $self-&gt;{LIST}-&gt;{$dot} || -f $file) {
	    carp "@{[&amp;whowasi]}: no $dot file" if $DEBUG;
	    return undef;
	}

	if (defined $self-&gt;{LIST}-&gt;{$dot}) {
	    return $self-&gt;{LIST}-&gt;{$dot};
	} else {
	    return $self-&gt;{LIST}-&gt;{$dot} = `cat $dir/.$dot`;
	}
    }</code></pre> <p>It was easy to write by having it call the Unix cat(1) command, but it would probably be more portable to open the file manually (and somewhat more efficient). Of course, because dot files are a Unixy concept, we're not that concerned.</p> </dd> <dt id="STORE-this,-key,-value">
STORE this, key, value </dt> <dd> <p>This method will be triggered every time an element in the tied hash is set (written). It takes two arguments beyond its self reference: the index at which we're trying to store something, and the value we're trying to put there.</p> <p>Here in our DotFiles example, we'll be careful not to let them try to overwrite the file unless they've called the clobber() method on the original object reference returned by tie().</p> <pre data-language="perl"><code>    sub STORE {
	carp &amp;whowasi if $DEBUG;
	my $self = shift;
	my $dot = shift;
	my $value = shift;
	my $file = $self-&gt;{HOME} . "/.$dot";
	my $user = $self-&gt;{USER};

	croak "@{[&amp;whowasi]}: $file not clobberable"
	    unless $self-&gt;{CLOBBER};

	open(my $f, '&gt;', $file) || croak "can't open $file: $!";
	print $f $value;
	close($f);
    }</code></pre> <p>If they wanted to clobber something, they might say:</p> <pre data-language="perl"><code>$ob = tie %daemon_dots, 'daemon';
$ob-&gt;clobber(1);
$daemon_dots{signature} = "A true daemon\n";</code></pre> <p>Another way to lay hands on a reference to the underlying object is to use the tied() function, so they might alternately have set clobber using:</p> <pre data-language="perl"><code>tie %daemon_dots, 'daemon';
tied(%daemon_dots)-&gt;clobber(1);</code></pre> <p>The clobber method is simply:</p> <pre data-language="perl"><code>    sub clobber {
	my $self = shift;
	$self-&gt;{CLOBBER} = @_ ? shift : 1;
    }</code></pre> </dd> <dt id="DELETE-this,-key1">
DELETE this, key </dt> <dd> <p>This method is triggered when we remove an element from the hash, typically by using the delete() function. Again, we'll be careful to check whether they really want to clobber files.</p> <pre data-language="perl"><code>sub DELETE   {
    carp &amp;whowasi if $DEBUG;

    my $self = shift;
    my $dot = shift;
    my $file = $self-&gt;{HOME} . "/.$dot";
    croak "@{[&amp;whowasi]}: won't remove file $file"
        unless $self-&gt;{CLOBBER};
    delete $self-&gt;{LIST}-&gt;{$dot};
    my $success = unlink($file);
    carp "@{[&amp;whowasi]}: can't unlink $file: $!" unless $success;
    $success;
}</code></pre> <p>The value returned by DELETE becomes the return value of the call to delete(). If you want to emulate the normal behavior of delete(), you should return whatever FETCH would have returned for this key. In this example, we have chosen instead to return a value which tells the caller whether the file was successfully deleted.</p> </dd> <dt id="CLEAR-this1">
CLEAR this </dt> <dd> <p>This method is triggered when the whole hash is to be cleared, usually by assigning the empty list to it.</p> <p>In our example, that would remove all the user's dot files! It's such a dangerous thing that they'll have to set CLOBBER to something higher than 1 to make it happen.</p> <pre data-language="perl"><code>sub CLEAR    {
    carp &amp;whowasi if $DEBUG;
    my $self = shift;
    croak "@{[&amp;whowasi]}: won't remove all dot files for $self-&gt;{USER}"
        unless $self-&gt;{CLOBBER} &gt; 1;
    my $dot;
    foreach $dot ( keys $self-&gt;{LIST}-&gt;%* ) {
        $self-&gt;DELETE($dot);
    }
}</code></pre> </dd> <dt id="EXISTS-this,-key1">
EXISTS this, key </dt> <dd> <p>This method is triggered when the user uses the exists() function on a particular hash. In our example, we'll look at the <code>{LIST}</code> hash element for this:</p> <pre data-language="perl"><code>    sub EXISTS   {
	carp &amp;whowasi if $DEBUG;
	my $self = shift;
	my $dot = shift;
	return exists $self-&gt;{LIST}-&gt;{$dot};
    }</code></pre> </dd> <dt id="FIRSTKEY-this">
FIRSTKEY this </dt> <dd> <p>This method will be triggered when the user is going to iterate through the hash, such as via a keys(), values(), or each() call.</p> <pre data-language="perl"><code>    sub FIRSTKEY {
	carp &amp;whowasi if $DEBUG;
	my $self = shift;
	my $a = keys $self-&gt;{LIST}-&gt;%*;  # reset each() iterator
	each $self-&gt;{LIST}-&gt;%*
    }</code></pre> <p>FIRSTKEY is always called in scalar context and it should just return the first key. values(), and each() in list context, will call FETCH for the returned keys.</p> </dd> <dt id="NEXTKEY-this,-lastkey">
NEXTKEY this, lastkey </dt> <dd> <p>This method gets triggered during a keys(), values(), or each() iteration. It has a second argument which is the last key that had been accessed. This is useful if you're caring about ordering or calling the iterator from more than one sequence, or not really storing things in a hash anywhere.</p> <p>NEXTKEY is always called in scalar context and it should just return the next key. values(), and each() in list context, will call FETCH for the returned keys.</p> <p>For our example, we're using a real hash so we'll do just the simple thing, but we'll have to go through the LIST field indirectly.</p> <pre data-language="perl"><code>    sub NEXTKEY  {
	carp &amp;whowasi if $DEBUG;
	my $self = shift;
	return each $self-&gt;{LIST}-&gt;%*
    }</code></pre> <p>If the object underlying your tied hash isn't a real hash and you don't have <code>each</code> available, then you should return <code>undef</code> or the empty list once you've reached the end of your list of keys. See <a href="perlfunc.html#each"><code>each's own documentation</code></a> for more details.</p> </dd> <dt id="SCALAR-this">
SCALAR this </dt> <dd> <p>This is called when the hash is evaluated in scalar context, and in 5.28 onwards, by <code>keys</code> in boolean context. In order to mimic the behaviour of untied hashes, this method must return a value which when used as boolean, indicates whether the tied hash is considered empty. If this method does not exist, perl will make some educated guesses and return true when the hash is inside an iteration. If this isn't the case, FIRSTKEY is called, and the result will be a false value if FIRSTKEY returns the empty list, true otherwise.</p> <p>However, you should <b>not</b> blindly rely on perl always doing the right thing. Particularly, perl will mistakenly return true when you clear the hash by repeatedly calling DELETE until it is empty. You are therefore advised to supply your own SCALAR method when you want to be absolutely sure that your hash behaves nicely in scalar context.</p> <p>In our example we can just call <code>scalar</code> on the underlying hash referenced by <code>$self-&gt;{LIST}</code>:</p> <pre data-language="perl"><code>    sub SCALAR {
	carp &amp;whowasi if $DEBUG;
	my $self = shift;
	return scalar $self-&gt;{LIST}-&gt;%*
    }</code></pre> <p>NOTE: In perl 5.25 the behavior of scalar %hash on an untied hash changed to return the count of keys. Prior to this it returned a string containing information about the bucket setup of the hash. See <a href="Hash::Util.html#bucket_ratio">"bucket_ratio" in Hash::Util</a> for a backwards compatibility path.</p> </dd> <dt id="UNTIE-this2">
UNTIE this </dt> <dd> <p>This is called when <code>untie</code> occurs. See <a href="#The-untie-Gotcha">"The <code>untie</code> Gotcha"</a> below.</p> </dd> <dt id="DESTROY-this2">
DESTROY this </dt> <dd> <p>This method is triggered when a tied hash is about to go out of scope. You don't really need it unless you're trying to add debugging or have auxiliary state to clean up. Here's a very simple function:</p> <pre data-language="perl"><code>    sub DESTROY  {
	carp &amp;whowasi if $DEBUG;
    }</code></pre> </dd> </dl> <p>Note that functions such as keys() and values() may return huge lists when used on large objects, like DBM files. You may prefer to use the each() function to iterate over such. Example:</p> <pre data-language="perl"><code># print out history file offsets
use NDBM_File;
tie(%HIST, 'NDBM_File', '/usr/lib/news/history', 1, 0);
while (($key,$val) = each %HIST) {
    print $key, ' = ', unpack('L',$val), "\n";
}
untie(%HIST);</code></pre> <h3 id="Tying-FileHandles">
Tying FileHandles </h3> <p>This is partially implemented now.</p> <p>A class implementing a tied filehandle should define the following methods: TIEHANDLE, at least one of PRINT, PRINTF, WRITE, READLINE, GETC, READ, and possibly CLOSE, UNTIE and DESTROY. The class can also provide: BINMODE, OPEN, EOF, FILENO, SEEK, TELL - if the corresponding perl operators are used on the handle.</p> <p>When STDERR is tied, its PRINT method will be called to issue warnings and error messages. This feature is temporarily disabled during the call, which means you can use <code>warn()</code> inside PRINT without starting a recursive loop. And just like <code>__WARN__</code> and <code>__DIE__</code> handlers, STDERR's PRINT method may be called to report parser errors, so the caveats mentioned under <a href="perlvar.html#%25SIG">"%SIG" in perlvar</a> apply.</p> <p>All of this is especially useful when perl is embedded in some other program, where output to STDOUT and STDERR may have to be redirected in some special way. See nvi and the Apache module for examples.</p> <p>When tying a handle, the first argument to <code>tie</code> should begin with an asterisk. So, if you are tying STDOUT, use <code>*STDOUT</code>. If you have assigned it to a scalar variable, say <code>$handle</code>, use <code>*$handle</code>. <code>tie $handle</code> ties the scalar variable <code>$handle</code>, not the handle inside it.</p> <p>In our example we're going to create a shouting handle.</p> <pre data-language="perl"><code>package Shout;</code></pre> <dl> <dt id="TIEHANDLE-classname,-LIST">
TIEHANDLE classname, LIST </dt> <dd> <p>This is the constructor for the class. That means it is expected to return a blessed reference of some sort. The reference can be used to hold some internal information.</p> <pre data-language="perl"><code>sub TIEHANDLE { print "&lt;shout&gt;\n"; my $i; bless \$i, shift }</code></pre> </dd> <dt id="WRITE-this,-LIST">
WRITE this, LIST </dt> <dd> <p>This method will be called when the handle is written to via the <code>syswrite</code> function.</p> <pre data-language="perl"><code>sub WRITE {
    $r = shift;
    my($buf,$len,$offset) = @_;
    print "WRITE called, \$buf=$buf, \$len=$len, \$offset=$offset";
}</code></pre> </dd> <dt id="PRINT-this,-LIST">
PRINT this, LIST </dt> <dd> <p>This method will be triggered every time the tied handle is printed to with the <code>print()</code> or <code>say()</code> functions. Beyond its self reference it also expects the list that was passed to the print function.</p> <pre data-language="perl"><code>sub PRINT { $r = shift; $$r++; print join($,,map(uc($_),@_)),$\ }</code></pre> <p><code>say()</code> acts just like <code>print()</code> except $\ will be localized to <code>\n</code> so you need do nothing special to handle <code>say()</code> in <code>PRINT()</code>.</p> </dd> <dt id="PRINTF-this,-LIST">
PRINTF this, LIST </dt> <dd> <p>This method will be triggered every time the tied handle is printed to with the <code>printf()</code> function. Beyond its self reference it also expects the format and list that was passed to the printf function.</p> <pre data-language="perl"><code>sub PRINTF {
    shift;
    my $fmt = shift;
    print sprintf($fmt, @_);
}</code></pre> </dd> <dt id="READ-this,-LIST">
READ this, LIST </dt> <dd> <p>This method will be called when the handle is read from via the <code>read</code> or <code>sysread</code> functions.</p> <pre data-language="perl"><code>sub READ {
  my $self = shift;
  my $bufref = \$_[0];
  my(undef,$len,$offset) = @_;
  print "READ called, \$buf=$bufref, \$len=$len, \$offset=$offset";
  # add to $$bufref, set $len to number of characters read
  $len;
}</code></pre> </dd> <dt id="READLINE-this">
READLINE this </dt> <dd> <p>This method is called when the handle is read via <code>&lt;HANDLE&gt;</code> or <code>readline HANDLE</code>.</p> <p>As per <a href="perlfunc.html#readline"><code>readline</code></a>, in scalar context it should return the next line, or <code>undef</code> for no more data. In list context it should return all remaining lines, or an empty list for no more data. The strings returned should include the input record separator <code>$/</code> (see <a href="perlvar.html">perlvar</a>), unless it is <code>undef</code> (which means "slurp" mode).</p> <pre data-language="perl"><code>sub READLINE {
  my $r = shift;
  if (wantarray) {
    return ("all remaining\n",
            "lines up\n",
            "to eof\n");
  } else {
    return "READLINE called " . ++$$r . " times\n";
  }
}</code></pre> </dd> <dt id="GETC-this">
GETC this </dt> <dd> <p>This method will be called when the <code>getc</code> function is called.</p> <pre data-language="perl"><code class="plaintext">sub GETC { print "Don't GETC, Get Perl"; return "a"; }</code></pre> </dd> <dt id="EOF-this">
EOF this </dt> <dd> <p>This method will be called when the <code>eof</code> function is called.</p> <p>Starting with Perl 5.12, an additional integer parameter will be passed. It will be zero if <code>eof</code> is called without parameter; <code>1</code> if <code>eof</code> is given a filehandle as a parameter, e.g. <code>eof(FH)</code>; and <code>2</code> in the very special case that the tied filehandle is <code>ARGV</code> and <code>eof</code> is called with an empty parameter list, e.g. <code>eof()</code>.</p> <pre data-language="perl"><code>sub EOF { not length $stringbuf }</code></pre> </dd> <dt id="CLOSE-this">
CLOSE this </dt> <dd> <p>This method will be called when the handle is closed via the <code>close</code> function.</p> <pre data-language="perl"><code class="plaintext">sub CLOSE { print "CLOSE called.\n" }</code></pre> </dd> <dt id="UNTIE-this3">
UNTIE this </dt> <dd> <p>As with the other types of ties, this method will be called when <code>untie</code> happens. It may be appropriate to "auto CLOSE" when this occurs. See <a href="#The-untie-Gotcha">"The <code>untie</code> Gotcha"</a> below.</p> </dd> <dt id="DESTROY-this3">
DESTROY this </dt> <dd> <p>As with the other types of ties, this method will be called when the tied handle is about to be destroyed. This is useful for debugging and possibly cleaning up.</p> <pre data-language="perl"><code class="plaintext">sub DESTROY { print "&lt;/shout&gt;\n" }</code></pre> </dd> </dl> <p>Here's how to use our little example:</p> <pre data-language="perl"><code>tie(*FOO,'Shout');
print FOO "hello\n";
$a = 4; $b = 6;
print FOO $a, " plus ", $b, " equals ", $a + $b, "\n";
print &lt;FOO&gt;;</code></pre> <h3 id="UNTIE-this4">
UNTIE this </h3> <p>You can define for all tie types an UNTIE method that will be called at untie(). See <a href="#The-untie-Gotcha">"The <code>untie</code> Gotcha"</a> below.</p> <h3 id="The-untie-Gotcha">
The <code>untie</code> Gotcha </h3> <p>If you intend making use of the object returned from either tie() or tied(), and if the tie's target class defines a destructor, there is a subtle gotcha you <i>must</i> guard against.</p> <p>As setup, consider this (admittedly rather contrived) example of a tie; all it does is use a file to keep a log of the values assigned to a scalar.</p> <pre data-language="perl"><code>package Remember;

use v5.36;
use IO::File;

sub TIESCALAR {
    my $class = shift;
    my $filename = shift;
    my $handle = IO::File-&gt;new( "&gt; $filename" )
                     or die "Cannot open $filename: $!\n";

    print $handle "The Start\n";
    bless {FH =&gt; $handle, Value =&gt; 0}, $class;
}

sub FETCH {
    my $self = shift;
    return $self-&gt;{Value};
}

sub STORE {
    my $self = shift;
    my $value = shift;
    my $handle = $self-&gt;{FH};
    print $handle "$value\n";
    $self-&gt;{Value} = $value;
}

sub DESTROY {
    my $self = shift;
    my $handle = $self-&gt;{FH};
    print $handle "The End\n";
    close $handle;
}

1;</code></pre> <p>Here is an example that makes use of this tie:</p> <pre data-language="perl"><code>use strict;
use Remember;

my $fred;
tie $fred, 'Remember', 'myfile.txt';
$fred = 1;
$fred = 4;
$fred = 5;
untie $fred;
system "cat myfile.txt";</code></pre> <p>This is the output when it is executed:</p> <pre data-language="perl"><code class="plaintext">The Start
1
4
5
The End</code></pre> <p>So far so good. Those of you who have been paying attention will have spotted that the tied object hasn't been used so far. So lets add an extra method to the Remember class to allow comments to be included in the file; say, something like this:</p> <pre data-language="perl"><code>sub comment {
    my $self = shift;
    my $text = shift;
    my $handle = $self-&gt;{FH};
    print $handle $text, "\n";
}</code></pre> <p>And here is the previous example modified to use the <code>comment</code> method (which requires the tied object):</p> <pre data-language="perl"><code>use strict;
use Remember;

my ($fred, $x);
$x = tie $fred, 'Remember', 'myfile.txt';
$fred = 1;
$fred = 4;
comment $x "changing...";
$fred = 5;
untie $fred;
system "cat myfile.txt";</code></pre> <p>When this code is executed there is no output. Here's why:</p> <p>When a variable is tied, it is associated with the object which is the return value of the TIESCALAR, TIEARRAY, or TIEHASH function. This object normally has only one reference, namely, the implicit reference from the tied variable. When untie() is called, that reference is destroyed. Then, as in the first example above, the object's destructor (DESTROY) is called, which is normal for objects that have no more valid references; and thus the file is closed.</p> <p>In the second example, however, we have stored another reference to the tied object in $x. That means that when untie() gets called there will still be a valid reference to the object in existence, so the destructor is not called at that time, and thus the file is not closed. The reason there is no output is because the file buffers have not been flushed to disk.</p> <p>Now that you know what the problem is, what can you do to avoid it? Prior to the introduction of the optional UNTIE method the only way was the good old <code>-w</code> flag. Which will spot any instances where you call untie() and there are still valid references to the tied object. If the second script above this near the top <code>use warnings 'untie'</code> or was run with the <code>-w</code> flag, Perl prints this warning message:</p> <pre data-language="perl"><code class="plaintext">untie attempted while 1 inner references still exist</code></pre> <p>To get the script to work properly and silence the warning make sure there are no valid references to the tied object <i>before</i> untie() is called:</p> <pre data-language="perl"><code>undef $x;
untie $fred;</code></pre> <p>Now that UNTIE exists the class designer can decide which parts of the class functionality are really associated with <code>untie</code> and which with the object being destroyed. What makes sense for a given class depends on whether the inner references are being kept so that non-tie-related methods can be called on the object. But in most cases it probably makes sense to move the functionality that would have been in DESTROY to the UNTIE method.</p> <p>If the UNTIE method exists then the warning above does not occur. Instead the UNTIE method is passed the count of "extra" references and can issue its own warning if appropriate. e.g. to replicate the no UNTIE case this method can be used:</p> <pre data-language="perl"><code>sub UNTIE
{
 my ($obj,$count) = @_;
 carp "untie attempted while $count inner references still exist"
                                                             if $count;
}</code></pre> <h2 id="SEE-ALSO">
SEE ALSO</h2> <p>See <a href="db_file.html">DB_File</a> or <a href="config.html">Config</a> for some interesting tie() implementations. A good starting point for many tie() implementations is with one of the modules <a href="Tie::Scalar.html">Tie::Scalar</a>, <a href="Tie::Array.html">Tie::Array</a>, <a href="Tie::Hash.html">Tie::Hash</a>, or <a href="Tie::Handle.html">Tie::Handle</a>.</p> <h2 id="BUGS">BUGS</h2> <p>The normal return provided by <code>scalar(%hash)</code> is not available. What this means is that using %tied_hash in boolean context doesn't work right (currently this always tests false, regardless of whether the hash is empty or hash elements). [ This paragraph needs review in light of changes in 5.25 ]</p> <p>Localizing tied arrays or hashes does not work. After exiting the scope the arrays or the hashes are not restored.</p> <p>Counting the number of entries in a hash via <code>scalar(keys(%hash))</code> or <code>scalar(values(%hash)</code>) is inefficient since it needs to iterate through all the entries with FIRSTKEY/NEXTKEY.</p> <p>Tied hash/array slices cause multiple FETCH/STORE pairs, there are no tie methods for slice operations.</p> <p>You cannot easily tie a multilevel data structure (such as a hash of hashes) to a dbm file. The first problem is that all but GDBM and Berkeley DB have size limitations, but beyond that, you also have problems with how references are to be represented on disk. One module that does attempt to address this need is DBM::Deep. Check your nearest CPAN site as described in <a href="perlmodlib.html">perlmodlib</a> for source code. Note that despite its name, DBM::Deep does not use dbm. Another earlier attempt at solving the problem is MLDBM, which is also available on the CPAN, but which has some fairly serious limitations.</p> <p>Tied filehandles are still incomplete. sysopen(), truncate(), flock(), fcntl(), stat() and -X can't currently be trapped.</p> <h2 id="AUTHOR">AUTHOR</h2> <p>Tom Christiansen</p> <p>TIEHANDLE by Sven Verdoolaege &lt;<i>skimo@dns.ufsia.ac.be</i>&gt; and Doug MacEachern &lt;<i>dougm@osf.org</i>&gt;</p> <p>UNTIE by Nick Ing-Simmons &lt;<i>nick@ing-simmons.net</i>&gt;</p> <p>SCALAR by Tassilo von Parseval &lt;<i>tassilo.von.parseval@rwth-aachen.de</i>&gt;</p> <p>Tying Arrays by Casey West &lt;<i>casey@geeknest.com</i>&gt;</p><div class="_attribution">
  <p class="_attribution-p">
    &copy; 1993&ndash;2023 Larry Wall and others<br>Licensed under the GNU General Public License version 1 or later, or the Artistic License.<br>The Perl logo is a trademark of the Perl Foundation.<br>
    <a href="https://perldoc.perl.org/5.38.0/perltie" class="_attribution-link">https://perldoc.perl.org/5.38.0/perltie</a>
  </p>
</div>
