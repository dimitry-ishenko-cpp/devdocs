<h1>DB_File</h1>   <h2><a id="toc">CONTENTS</a></h2> <ul> <li> <a class="text-decoration-none" href="#NAME">NAME</a> </li> <li> <a class="text-decoration-none" href="#SYNOPSIS">SYNOPSIS</a> </li> <li> <a class="text-decoration-none" href="#DESCRIPTION">DESCRIPTION</a> <ul> <li> <a class="text-decoration-none" href="#Using-DB_File-with-Berkeley-DB-version-2-or-greater">Using DB_File with Berkeley DB version 2 or greater</a> </li> <li> <a class="text-decoration-none" href="#Interface-to-Berkeley-DB">Interface to Berkeley DB</a> </li> <li> <a class="text-decoration-none" href="#Opening-a-Berkeley-DB-Database-File">Opening a Berkeley DB Database File</a> </li> <li> <a class="text-decoration-none" href="#Default-Parameters">Default Parameters</a> </li> <li> <a class="text-decoration-none" href="#In-Memory-Databases">In Memory Databases</a> </li> </ul> </li> <li> <a class="text-decoration-none" href="#DB_HASH1">DB_HASH</a> <ul> <li> <a class="text-decoration-none" href="#A-Simple-Example">A Simple Example</a> </li> </ul> </li> <li> <a class="text-decoration-none" href="#DB_BTREE1">DB_BTREE</a> <ul> <li> <a class="text-decoration-none" href="#Changing-the-BTREE-sort-order">Changing the BTREE sort order</a> </li> <li> <a class="text-decoration-none" href="#Handling-Duplicate-Keys">Handling Duplicate Keys</a> </li> <li> <a class="text-decoration-none" href="#The-get_dup()-Method">The get_dup() Method</a> </li> <li> <a class="text-decoration-none" href="#The-find_dup()-Method">The find_dup() Method</a> </li> <li> <a class="text-decoration-none" href="#The-del_dup()-Method">The del_dup() Method</a> </li> <li> <a class="text-decoration-none" href="#Matching-Partial-Keys">Matching Partial Keys</a> </li> </ul> </li> <li> <a class="text-decoration-none" href="#DB_RECNO1">DB_RECNO</a> <ul> <li> <a class="text-decoration-none" href="#The-'bval'-Option">The 'bval' Option</a> </li> <li> <a class="text-decoration-none" href="#A-Simple-Example1">A Simple Example</a> </li> <li> <a class="text-decoration-none" href="#Extra-RECNO-Methods">Extra RECNO Methods</a> </li> <li> <a class="text-decoration-none" href="#Another-Example">Another Example</a> </li> </ul> </li> <li> <a class="text-decoration-none" href="#THE-API-INTERFACE">THE API INTERFACE</a> </li> <li> <a class="text-decoration-none" href="#DBM-FILTERS">DBM FILTERS</a> <ul> <li> <a class="text-decoration-none" href="#DBM-Filter-Low-level-API">DBM Filter Low-level API</a> </li> <li> <a class="text-decoration-none" href="#The-Filter">The Filter</a> </li> <li> <a class="text-decoration-none" href="#An-Example-the-NULL-termination-problem.">An Example -- the NULL termination problem.</a> </li> <li> <a class="text-decoration-none" href="#Another-Example-Key-is-a-C-int.">Another Example -- Key is a C int.</a> </li> </ul> </li> <li> <a class="text-decoration-none" href="#HINTS-AND-TIPS">HINTS AND TIPS</a> <ul> <li> <a class="text-decoration-none" href="#Locking:-The-Trouble-with-fd">Locking: The Trouble with fd</a> </li> <li> <a class="text-decoration-none" href="#Safe-ways-to-lock-a-database">Safe ways to lock a database</a> </li> <li> <a class="text-decoration-none" href="#Sharing-Databases-With-C-Applications">Sharing Databases With C Applications</a> </li> <li> <a class="text-decoration-none" href="#The-untie()-Gotcha">The untie() Gotcha</a> </li> </ul> </li> <li> <a class="text-decoration-none" href="#COMMON-QUESTIONS">COMMON QUESTIONS</a> <ul> <li> <a class="text-decoration-none" href="#Why-is-there-Perl-source-in-my-database?">Why is there Perl source in my database?</a> </li> <li> <a class="text-decoration-none" href="#How-do-I-store-complex-data-structures-with-DB_File?">How do I store complex data structures with DB_File?</a> </li> <li> <a class="text-decoration-none" href="#What-does-%22wide-character-in-subroutine-entry%22-mean?">What does "wide character in subroutine entry" mean?</a> </li> <li> <a class="text-decoration-none" href="#What-does-%22Invalid-Argument%22-mean?">What does "Invalid Argument" mean?</a> </li> <li> <a class="text-decoration-none" href="#What-does-%22Bareword-'DB_File'-not-allowed%22-mean?">What does "Bareword 'DB_File' not allowed" mean?</a> </li> </ul> </li> <li> <a class="text-decoration-none" href="#REFERENCES">REFERENCES</a> </li> <li> <a class="text-decoration-none" href="#HISTORY">HISTORY</a> </li> <li> <a class="text-decoration-none" href="#BUGS">BUGS</a> </li> <li> <a class="text-decoration-none" href="#SUPPORT">SUPPORT</a> </li> <li> <a class="text-decoration-none" href="#AVAILABILITY">AVAILABILITY</a> </li> <li> <a class="text-decoration-none" href="#COPYRIGHT">COPYRIGHT</a> </li> <li> <a class="text-decoration-none" href="#SEE-ALSO">SEE ALSO</a> </li> <li> <a class="text-decoration-none" href="#AUTHOR">AUTHOR</a> </li> </ul> <h2 id="NAME">NAME</h2> <p>DB_File - Perl5 access to Berkeley DB version 1.x</p> <h2 id="SYNOPSIS">SYNOPSIS</h2> <pre data-language="perl"><code>use DB_File;

[$X =] tie %hash,  'DB_File', [$filename, $flags, $mode, $DB_HASH] ;
[$X =] tie %hash,  'DB_File', $filename, $flags, $mode, $DB_BTREE ;
[$X =] tie @array, 'DB_File', $filename, $flags, $mode, $DB_RECNO ;

$status = $X-&gt;del($key [, $flags]) ;
$status = $X-&gt;put($key, $value [, $flags]) ;
$status = $X-&gt;get($key, $value [, $flags]) ;
$status = $X-&gt;seq($key, $value, $flags) ;
$status = $X-&gt;sync([$flags]) ;
$status = $X-&gt;fd ;

# BTREE only
$count = $X-&gt;get_dup($key) ;
@list  = $X-&gt;get_dup($key) ;
%list  = $X-&gt;get_dup($key, 1) ;
$status = $X-&gt;find_dup($key, $value) ;
$status = $X-&gt;del_dup($key, $value) ;

# RECNO only
$a = $X-&gt;length;
$a = $X-&gt;pop ;
$X-&gt;push(list);
$a = $X-&gt;shift;
$X-&gt;unshift(list);
@r = $X-&gt;splice(offset, length, elements);

# DBM Filters
$old_filter = $db-&gt;filter_store_key  ( sub { ... } ) ;
$old_filter = $db-&gt;filter_store_value( sub { ... } ) ;
$old_filter = $db-&gt;filter_fetch_key  ( sub { ... } ) ;
$old_filter = $db-&gt;filter_fetch_value( sub { ... } ) ;

untie %hash ;
untie @array ;</code></pre> <h2 id="DESCRIPTION">DESCRIPTION</h2> <p><b>DB_File</b> is a module which allows Perl programs to make use of the facilities provided by Berkeley DB version 1.x (if you have a newer version of DB, see <a href="#Using-DB_File-with-Berkeley-DB-version-2-or-greater">"Using DB_File with Berkeley DB version 2 or greater"</a>). It is assumed that you have a copy of the Berkeley DB manual pages at hand when reading this documentation. The interface defined here mirrors the Berkeley DB interface closely.</p> <p>Berkeley DB is a C library which provides a consistent interface to a number of database formats. <b>DB_File</b> provides an interface to all three of the database types currently supported by Berkeley DB.</p> <p>The file types are:</p> <dl> <dt id="DB_HASH"><b>DB_HASH</b></dt> <dd> <p>This database type allows arbitrary key/value pairs to be stored in data files. This is equivalent to the functionality provided by other hashing packages like DBM, NDBM, ODBM, GDBM, and SDBM. Remember though, the files created using DB_HASH are not compatible with any of the other packages mentioned.</p> <p>A default hashing algorithm, which will be adequate for most applications, is built into Berkeley DB. If you do need to use your own hashing algorithm it is possible to write your own in Perl and have <b>DB_File</b> use it instead.</p> </dd> <dt id="DB_BTREE"><b>DB_BTREE</b></dt> <dd> <p>The btree format allows arbitrary key/value pairs to be stored in a sorted, balanced binary tree.</p> <p>As with the DB_HASH format, it is possible to provide a user defined Perl routine to perform the comparison of keys. By default, though, the keys are stored in lexical order.</p> </dd> <dt id="DB_RECNO"><b>DB_RECNO</b></dt> <dd> <p>DB_RECNO allows both fixed-length and variable-length flat text files to be manipulated using the same key/value pair interface as in DB_HASH and DB_BTREE. In this case the key will consist of a record (line) number.</p> </dd> </dl> <h3 id="Using-DB_File-with-Berkeley-DB-version-2-or-greater">
Using DB_File with Berkeley DB version 2 or greater</h3> <p>Although <b>DB_File</b> is intended to be used with Berkeley DB version 1, it can also be used with version 2, 3 or 4. In this case the interface is limited to the functionality provided by Berkeley DB 1.x. Anywhere the version 2 or greater interface differs, <b>DB_File</b> arranges for it to work like version 1. This feature allows <b>DB_File</b> scripts that were built with version 1 to be migrated to version 2 or greater without any changes.</p> <p>If you want to make use of the new features available in Berkeley DB 2.x or greater, use the Perl module <a href="https://metacpan.org/pod/BerkeleyDB">BerkeleyDB</a> instead.</p> <p><b>Note:</b> The database file format has changed multiple times in Berkeley DB version 2, 3 and 4. If you cannot recreate your databases, you must dump any existing databases with either the <code>db_dump</code> or the <code>db_dump185</code> utility that comes with Berkeley DB. Once you have rebuilt DB_File to use Berkeley DB version 2 or greater, your databases can be recreated using <code>db_load</code>. Refer to the Berkeley DB documentation for further details.</p> <p>Please read <a href="#COPYRIGHT">"COPYRIGHT"</a> before using version 2.x or greater of Berkeley DB with DB_File.</p> <h3 id="Interface-to-Berkeley-DB">
Interface to Berkeley DB</h3> <p><b>DB_File</b> allows access to Berkeley DB files using the tie() mechanism in Perl 5 (for full details, see <a href="perlfunc.html#tie%28%29">"tie()" in perlfunc</a>). This facility allows <b>DB_File</b> to access Berkeley DB files using either an associative array (for DB_HASH &amp; DB_BTREE file types) or an ordinary array (for the DB_RECNO file type).</p> <p>In addition to the tie() interface, it is also possible to access most of the functions provided in the Berkeley DB API directly. See <a href="#THE-API-INTERFACE">"THE API INTERFACE"</a>.</p> <h3 id="Opening-a-Berkeley-DB-Database-File">
Opening a Berkeley DB Database File</h3> <p>Berkeley DB uses the function dbopen() to open or create a database. Here is the C prototype for dbopen():</p> <pre data-language="perl"><code class="plaintext">DB*
dbopen (const char * file, int flags, int mode,
        DBTYPE type, const void * openinfo)</code></pre> <p>The parameter <code>type</code> is an enumeration which specifies which of the 3 interface methods (DB_HASH, DB_BTREE or DB_RECNO) is to be used. Depending on which of these is actually chosen, the final parameter, <i>openinfo</i> points to a data structure which allows tailoring of the specific interface method.</p> <p>This interface is handled slightly differently in <b>DB_File</b>. Here is an equivalent call using <b>DB_File</b>:</p> <pre data-language="perl"><code>tie %array, 'DB_File', $filename, $flags, $mode, $DB_HASH ;</code></pre> <p>The <code>filename</code>, <code>flags</code> and <code>mode</code> parameters are the direct equivalent of their dbopen() counterparts. The final parameter $DB_HASH performs the function of both the <code>type</code> and <code>openinfo</code> parameters in dbopen().</p> <p>In the example above $DB_HASH is actually a pre-defined reference to a hash object. <b>DB_File</b> has three of these pre-defined references. Apart from $DB_HASH, there is also $DB_BTREE and $DB_RECNO.</p> <p>The keys allowed in each of these pre-defined references is limited to the names used in the equivalent C structure. So, for example, the $DB_HASH reference will only allow keys called <code>bsize</code>, <code>cachesize</code>, <code>ffactor</code>, <code>hash</code>, <code>lorder</code> and <code>nelem</code>.</p> <p>To change one of these elements, just assign to it like this:</p> <pre data-language="perl"><code>$DB_HASH-&gt;{'cachesize'} = 10000 ;</code></pre> <p>The three predefined variables $DB_HASH, $DB_BTREE and $DB_RECNO are usually adequate for most applications. If you do need to create extra instances of these objects, constructors are available for each file type.</p> <p>Here are examples of the constructors and the valid options available for DB_HASH, DB_BTREE and DB_RECNO respectively.</p> <pre data-language="perl"><code>$a = DB_File::HASHINFO-&gt;new();
$a-&gt;{'bsize'} ;
$a-&gt;{'cachesize'} ;
$a-&gt;{'ffactor'};
$a-&gt;{'hash'} ;
$a-&gt;{'lorder'} ;
$a-&gt;{'nelem'} ;

$b = DB_File::BTREEINFO-&gt;new();
$b-&gt;{'flags'} ;
$b-&gt;{'cachesize'} ;
$b-&gt;{'maxkeypage'} ;
$b-&gt;{'minkeypage'} ;
$b-&gt;{'psize'} ;
$b-&gt;{'compare'} ;
$b-&gt;{'prefix'} ;
$b-&gt;{'lorder'} ;

$c = DB_File::RECNOINFO-&gt;new();
$c-&gt;{'bval'} ;
$c-&gt;{'cachesize'} ;
$c-&gt;{'psize'} ;
$c-&gt;{'flags'} ;
$c-&gt;{'lorder'} ;
$c-&gt;{'reclen'} ;
$c-&gt;{'bfname'} ;</code></pre> <p>The values stored in the hashes above are mostly the direct equivalent of their C counterpart. Like their C counterparts, all are set to a default values - that means you don't have to set <i>all</i> of the values when you only want to change one. Here is an example:</p> <pre data-language="perl"><code>$a = DB_File::HASHINFO-&gt;new();
$a-&gt;{'cachesize'} =  12345 ;
tie %y, 'DB_File', "filename", $flags, 0777, $a ;</code></pre> <p>A few of the options need extra discussion here. When used, the C equivalent of the keys <code>hash</code>, <code>compare</code> and <code>prefix</code> store pointers to C functions. In <b>DB_File</b> these keys are used to store references to Perl subs. Below are templates for each of the subs:</p> <pre data-language="perl"><code>sub hash
{
    my ($data) = @_ ;
    ...
    # return the hash value for $data
    return $hash ;
}

sub compare
{
    my ($key, $key2) = @_ ;
    ...
    # return  0 if $key1 eq $key2
    #        -1 if $key1 lt $key2
    #         1 if $key1 gt $key2
    return (-1 , 0 or 1) ;
}

sub prefix
{
    my ($key, $key2) = @_ ;
    ...
    # return number of bytes of $key2 which are
    # necessary to determine that it is greater than $key1
    return $bytes ;
}</code></pre> <p>See <a href="#Changing-the-BTREE-sort-order">"Changing the BTREE sort order"</a> for an example of using the <code>compare</code> template.</p> <p>If you are using the DB_RECNO interface and you intend making use of <code>bval</code>, you should check out <a href="#The-%27bval%27-Option">"The 'bval' Option"</a>.</p> <h3 id="Default-Parameters">
Default Parameters</h3> <p>It is possible to omit some or all of the final 4 parameters in the call to <code>tie</code> and let them take default values. As DB_HASH is the most common file format used, the call:</p> <pre data-language="perl"><code>tie %A, "DB_File", "filename" ;</code></pre> <p>is equivalent to:</p> <pre data-language="perl"><code>tie %A, "DB_File", "filename", O_CREAT|O_RDWR, 0666, $DB_HASH ;</code></pre> <p>It is also possible to omit the filename parameter as well, so the call:</p> <pre data-language="perl"><code>tie %A, "DB_File" ;</code></pre> <p>is equivalent to:</p> <pre data-language="perl"><code>tie %A, "DB_File", undef, O_CREAT|O_RDWR, 0666, $DB_HASH ;</code></pre> <p>See <a href="#In-Memory-Databases">"In Memory Databases"</a> for a discussion on the use of <code>undef</code> in place of a filename.</p> <h3 id="In-Memory-Databases">
In Memory Databases</h3> <p>Berkeley DB allows the creation of in-memory databases by using NULL (that is, a <code>(char *)0</code> in C) in place of the filename. <b>DB_File</b> uses <code>undef</code> instead of NULL to provide this functionality.</p> <h2 id="DB_HASH1">DB_HASH</h2> <p>The DB_HASH file format is probably the most commonly used of the three file formats that <b>DB_File</b> supports. It is also very straightforward to use.</p> <h3 id="A-Simple-Example">
A Simple Example</h3> <p>This example shows how to create a database, add key/value pairs to the database, delete keys/value pairs and finally how to enumerate the contents of the database.</p> <pre data-language="perl"><code>use warnings ;
use strict ;
use DB_File ;
our (%h, $k, $v) ;

unlink "fruit" ;
tie %h, "DB_File", "fruit", O_RDWR|O_CREAT, 0666, $DB_HASH
    or die "Cannot open file 'fruit': $!\n";

# Add a few key/value pairs to the file
$h{"apple"} = "red" ;
$h{"orange"} = "orange" ;
$h{"banana"} = "yellow" ;
$h{"tomato"} = "red" ;

# Check for existence of a key
print "Banana Exists\n\n" if $h{"banana"} ;

# Delete a key/value pair.
delete $h{"apple"} ;

# print the contents of the file
while (($k, $v) = each %h)
  { print "$k -&gt; $v\n" }

untie %h ;</code></pre> <p>here is the output:</p> <pre data-language="perl"><code class="plaintext">Banana Exists

orange -&gt; orange
tomato -&gt; red
banana -&gt; yellow</code></pre> <p>Note that the like ordinary associative arrays, the order of the keys retrieved is in an apparently random order.</p> <h2 id="DB_BTREE1">DB_BTREE</h2> <p>The DB_BTREE format is useful when you want to store data in a given order. By default the keys will be stored in lexical order, but as you will see from the example shown in the next section, it is very easy to define your own sorting function.</p> <h3 id="Changing-the-BTREE-sort-order">
Changing the BTREE sort order</h3> <p>This script shows how to override the default sorting algorithm that BTREE uses. Instead of using the normal lexical ordering, a case insensitive compare function will be used.</p> <pre data-language="perl"><code>use warnings ;
use strict ;
use DB_File ;

my %h ;

sub Compare
{
    my ($key1, $key2) = @_ ;
    "\L$key1" cmp "\L$key2" ;
}

# specify the Perl sub that will do the comparison
$DB_BTREE-&gt;{'compare'} = \&amp;Compare ;

unlink "tree" ;
tie %h, "DB_File", "tree", O_RDWR|O_CREAT, 0666, $DB_BTREE
    or die "Cannot open file 'tree': $!\n" ;

# Add a key/value pair to the file
$h{'Wall'} = 'Larry' ;
$h{'Smith'} = 'John' ;
$h{'mouse'} = 'mickey' ;
$h{'duck'}  = 'donald' ;

# Delete
delete $h{"duck"} ;

# Cycle through the keys printing them in order.
# Note it is not necessary to sort the keys as
# the btree will have kept them in order automatically.
foreach (keys %h)
  { print "$_\n" }

untie %h ;</code></pre> <p>Here is the output from the code above.</p> <pre data-language="perl"><code class="plaintext">mouse
Smith
Wall</code></pre> <p>There are a few point to bear in mind if you want to change the ordering in a BTREE database:</p> <ol> <li>
<p>The new compare function must be specified when you create the database.</p> </li> <li>
<p>You cannot change the ordering once the database has been created. Thus you must use the same compare function every time you access the database.</p> </li> <li>
<p>Duplicate keys are entirely defined by the comparison function. In the case-insensitive example above, the keys: 'KEY' and 'key' would be considered duplicates, and assigning to the second one would overwrite the first. If duplicates are allowed for (with the R_DUP flag discussed below), only a single copy of duplicate keys is stored in the database --- so (again with example above) assigning three values to the keys: 'KEY', 'Key', and 'key' would leave just the first key: 'KEY' in the database with three values. For some situations this results in information loss, so care should be taken to provide fully qualified comparison functions when necessary. For example, the above comparison routine could be modified to additionally compare case-sensitively if two keys are equal in the case insensitive comparison:</p> <pre data-language="perl"><code>sub compare {
    my($key1, $key2) = @_;
    lc $key1 cmp lc $key2 ||
    $key1 cmp $key2;
}</code></pre> <p>And now you will only have duplicates when the keys themselves are truly the same. (note: in versions of the db library prior to about November 1996, such duplicate keys were retained so it was possible to recover the original keys in sets of keys that compared as equal).</p> </li> </ol> <h3 id="Handling-Duplicate-Keys">
Handling Duplicate Keys</h3> <p>The BTREE file type optionally allows a single key to be associated with an arbitrary number of values. This option is enabled by setting the flags element of <code>$DB_BTREE</code> to R_DUP when creating the database.</p> <p>There are some difficulties in using the tied hash interface if you want to manipulate a BTREE database with duplicate keys. Consider this code:</p> <pre data-language="perl"><code>use warnings ;
use strict ;
use DB_File ;

my ($filename, %h) ;

$filename = "tree" ;
unlink $filename ;

# Enable duplicate records
$DB_BTREE-&gt;{'flags'} = R_DUP ;

tie %h, "DB_File", $filename, O_RDWR|O_CREAT, 0666, $DB_BTREE
    or die "Cannot open $filename: $!\n";

# Add some key/value pairs to the file
$h{'Wall'} = 'Larry' ;
$h{'Wall'} = 'Brick' ; # Note the duplicate key
$h{'Wall'} = 'Brick' ; # Note the duplicate key and value
$h{'Smith'} = 'John' ;
$h{'mouse'} = 'mickey' ;

# iterate through the associative array
# and print each key/value pair.
foreach (sort keys %h)
  { print "$_  -&gt; $h{$_}\n" }

untie %h ;</code></pre> <p>Here is the output:</p> <pre data-language="perl"><code class="plaintext">Smith   -&gt; John
Wall    -&gt; Larry
Wall    -&gt; Larry
Wall    -&gt; Larry
mouse   -&gt; mickey</code></pre> <p>As you can see 3 records have been successfully created with key <code>Wall</code> - the only thing is, when they are retrieved from the database they <i>seem</i> to have the same value, namely <code>Larry</code>. The problem is caused by the way that the associative array interface works. Basically, when the associative array interface is used to fetch the value associated with a given key, it will only ever retrieve the first value.</p> <p>Although it may not be immediately obvious from the code above, the associative array interface can be used to write values with duplicate keys, but it cannot be used to read them back from the database.</p> <p>The way to get around this problem is to use the Berkeley DB API method called <code>seq</code>. This method allows sequential access to key/value pairs. See <a href="#THE-API-INTERFACE">"THE API INTERFACE"</a> for details of both the <code>seq</code> method and the API in general.</p> <p>Here is the script above rewritten using the <code>seq</code> API method.</p> <pre data-language="perl"><code>use warnings ;
use strict ;
use DB_File ;

my ($filename, $x, %h, $status, $key, $value) ;

$filename = "tree" ;
unlink $filename ;

# Enable duplicate records
$DB_BTREE-&gt;{'flags'} = R_DUP ;

$x = tie %h, "DB_File", $filename, O_RDWR|O_CREAT, 0666, $DB_BTREE
    or die "Cannot open $filename: $!\n";

# Add some key/value pairs to the file
$h{'Wall'} = 'Larry' ;
$h{'Wall'} = 'Brick' ; # Note the duplicate key
$h{'Wall'} = 'Brick' ; # Note the duplicate key and value
$h{'Smith'} = 'John' ;
$h{'mouse'} = 'mickey' ;

# iterate through the btree using seq
# and print each key/value pair.
$key = $value = 0 ;
for ($status = $x-&gt;seq($key, $value, R_FIRST) ;
     $status == 0 ;
     $status = $x-&gt;seq($key, $value, R_NEXT) )
  {  print "$key -&gt; $value\n" }

undef $x ;
untie %h ;</code></pre> <p>that prints:</p> <pre data-language="perl"><code class="plaintext">Smith   -&gt; John
Wall    -&gt; Brick
Wall    -&gt; Brick
Wall    -&gt; Larry
mouse   -&gt; mickey</code></pre> <p>This time we have got all the key/value pairs, including the multiple values associated with the key <code>Wall</code>.</p> <p>To make life easier when dealing with duplicate keys, <b>DB_File</b> comes with a few utility methods.</p> <h3 id="The-get_dup()-Method">
The get_dup() Method</h3> <p>The <code>get_dup</code> method assists in reading duplicate values from BTREE databases. The method can take the following forms:</p> <pre data-language="perl"><code>$count = $x-&gt;get_dup($key) ;
@list  = $x-&gt;get_dup($key) ;
%list  = $x-&gt;get_dup($key, 1) ;</code></pre> <p>In a scalar context the method returns the number of values associated with the key, <code>$key</code>.</p> <p>In list context, it returns all the values which match <code>$key</code>. Note that the values will be returned in an apparently random order.</p> <p>In list context, if the second parameter is present and evaluates TRUE, the method returns an associative array. The keys of the associative array correspond to the values that matched in the BTREE and the values of the array are a count of the number of times that particular value occurred in the BTREE.</p> <p>So assuming the database created above, we can use <code>get_dup</code> like this:</p> <pre data-language="perl"><code>use warnings ;
use strict ;
use DB_File ;

my ($filename, $x, %h) ;

$filename = "tree" ;

# Enable duplicate records
$DB_BTREE-&gt;{'flags'} = R_DUP ;

$x = tie %h, "DB_File", $filename, O_RDWR|O_CREAT, 0666, $DB_BTREE
    or die "Cannot open $filename: $!\n";

my $cnt  = $x-&gt;get_dup("Wall") ;
print "Wall occurred $cnt times\n" ;

my %hash = $x-&gt;get_dup("Wall", 1) ;
print "Larry is there\n" if $hash{'Larry'} ;
print "There are $hash{'Brick'} Brick Walls\n" ;

my @list = sort $x-&gt;get_dup("Wall") ;
print "Wall =&gt;      [@list]\n" ;

@list = $x-&gt;get_dup("Smith") ;
print "Smith =&gt;     [@list]\n" ;

@list = $x-&gt;get_dup("Dog") ;
print "Dog =&gt;       [@list]\n" ;</code></pre> <p>and it will print:</p> <pre data-language="perl"><code class="plaintext">Wall occurred 3 times
Larry is there
There are 2 Brick Walls
Wall =&gt;     [Brick Brick Larry]
Smith =&gt;    [John]
Dog =&gt;      []</code></pre> <h3 id="The-find_dup()-Method">
The find_dup() Method</h3> <pre data-language="perl"><code>$status = $X-&gt;find_dup($key, $value) ;</code></pre> <p>This method checks for the existence of a specific key/value pair. If the pair exists, the cursor is left pointing to the pair and the method returns 0. Otherwise the method returns a non-zero value.</p> <p>Assuming the database from the previous example:</p> <pre data-language="perl"><code>use warnings ;
use strict ;
use DB_File ;

my ($filename, $x, %h, $found) ;

$filename = "tree" ;

# Enable duplicate records
$DB_BTREE-&gt;{'flags'} = R_DUP ;

$x = tie %h, "DB_File", $filename, O_RDWR|O_CREAT, 0666, $DB_BTREE
    or die "Cannot open $filename: $!\n";

$found = ( $x-&gt;find_dup("Wall", "Larry") == 0 ? "" : "not") ;
print "Larry Wall is $found there\n" ;

$found = ( $x-&gt;find_dup("Wall", "Harry") == 0 ? "" : "not") ;
print "Harry Wall is $found there\n" ;

undef $x ;
untie %h ;</code></pre> <p>prints this</p> <pre data-language="perl"><code class="plaintext">Larry Wall is  there
Harry Wall is not there</code></pre> <h3 id="The-del_dup()-Method">
The del_dup() Method</h3> <pre data-language="perl"><code>$status = $X-&gt;del_dup($key, $value) ;</code></pre> <p>This method deletes a specific key/value pair. It returns 0 if they exist and have been deleted successfully. Otherwise the method returns a non-zero value.</p> <p>Again assuming the existence of the <code>tree</code> database</p> <pre data-language="perl"><code>use warnings ;
use strict ;
use DB_File ;

my ($filename, $x, %h, $found) ;

$filename = "tree" ;

# Enable duplicate records
$DB_BTREE-&gt;{'flags'} = R_DUP ;

$x = tie %h, "DB_File", $filename, O_RDWR|O_CREAT, 0666, $DB_BTREE
    or die "Cannot open $filename: $!\n";

$x-&gt;del_dup("Wall", "Larry") ;

$found = ( $x-&gt;find_dup("Wall", "Larry") == 0 ? "" : "not") ;
print "Larry Wall is $found there\n" ;

undef $x ;
untie %h ;</code></pre> <p>prints this</p> <pre data-language="perl"><code class="plaintext">Larry Wall is not there</code></pre> <h3 id="Matching-Partial-Keys">
Matching Partial Keys</h3> <p>The BTREE interface has a feature which allows partial keys to be matched. This functionality is <i>only</i> available when the <code>seq</code> method is used along with the R_CURSOR flag.</p> <pre data-language="perl"><code>$x-&gt;seq($key, $value, R_CURSOR) ;</code></pre> <p>Here is the relevant quote from the dbopen man page where it defines the use of the R_CURSOR flag with seq:</p> <pre data-language="perl"><code class="plaintext">Note, for the DB_BTREE access method, the returned key is not
necessarily an exact match for the specified key. The returned key
is the smallest key greater than or equal to the specified key,
permitting partial key matches and range searches.</code></pre> <p>In the example script below, the <code>match</code> sub uses this feature to find and print the first matching key/value pair given a partial key.</p> <pre data-language="perl"><code>use warnings ;
use strict ;
use DB_File ;
use Fcntl ;

my ($filename, $x, %h, $st, $key, $value) ;

sub match
{
    my $key = shift ;
    my $value = 0;
    my $orig_key = $key ;
    $x-&gt;seq($key, $value, R_CURSOR) ;
    print "$orig_key\t-&gt; $key\t-&gt; $value\n" ;
}

$filename = "tree" ;
unlink $filename ;

$x = tie %h, "DB_File", $filename, O_RDWR|O_CREAT, 0666, $DB_BTREE
    or die "Cannot open $filename: $!\n";

# Add some key/value pairs to the file
$h{'mouse'} = 'mickey' ;
$h{'Wall'} = 'Larry' ;
$h{'Walls'} = 'Brick' ;
$h{'Smith'} = 'John' ;


$key = $value = 0 ;
print "IN ORDER\n" ;
for ($st = $x-&gt;seq($key, $value, R_FIRST) ;
     $st == 0 ;
     $st = $x-&gt;seq($key, $value, R_NEXT) )

  {  print "$key    -&gt; $value\n" }

print "\nPARTIAL MATCH\n" ;

match "Wa" ;
match "A" ;
match "a" ;

undef $x ;
untie %h ;</code></pre> <p>Here is the output:</p> <pre data-language="perl"><code class="plaintext">IN ORDER
Smith -&gt; John
Wall  -&gt; Larry
Walls -&gt; Brick
mouse -&gt; mickey

PARTIAL MATCH
Wa -&gt; Wall  -&gt; Larry
A  -&gt; Smith -&gt; John
a  -&gt; mouse -&gt; mickey</code></pre> <h2 id="DB_RECNO1">DB_RECNO</h2> <p>DB_RECNO provides an interface to flat text files. Both variable and fixed length records are supported.</p> <p>In order to make RECNO more compatible with Perl, the array offset for all RECNO arrays begins at 0 rather than 1 as in Berkeley DB.</p> <p>As with normal Perl arrays, a RECNO array can be accessed using negative indexes. The index -1 refers to the last element of the array, -2 the second last, and so on. Attempting to access an element before the start of the array will raise a fatal run-time error.</p> <h3 id="The-'bval'-Option">
The 'bval' Option</h3> <p>The operation of the bval option warrants some discussion. Here is the definition of bval from the Berkeley DB 1.85 recno manual page:</p> <pre data-language="perl"><code class="plaintext">The delimiting byte to be used to mark  the  end  of  a
record for variable-length records, and the pad charac-
ter for fixed-length records.  If no  value  is  speci-
fied,  newlines  (``\n'')  are  used to mark the end of
variable-length records and  fixed-length  records  are
padded with spaces.</code></pre> <p>The second sentence is wrong. In actual fact bval will only default to <code>"\n"</code> when the openinfo parameter in dbopen is NULL. If a non-NULL openinfo parameter is used at all, the value that happens to be in bval will be used. That means you always have to specify bval when making use of any of the options in the openinfo parameter. This documentation error will be fixed in the next release of Berkeley DB.</p> <p>That clarifies the situation with regards Berkeley DB itself. What about <b>DB_File</b>? Well, the behavior defined in the quote above is quite useful, so <b>DB_File</b> conforms to it.</p> <p>That means that you can specify other options (e.g. cachesize) and still have bval default to <code>"\n"</code> for variable length records, and space for fixed length records.</p> <p>Also note that the bval option only allows you to specify a single byte as a delimiter.</p> <h3 id="A-Simple-Example1">
A Simple Example</h3> <p>Here is a simple example that uses RECNO (if you are using a version of Perl earlier than 5.004_57 this example won't work -- see <a href="#Extra-RECNO-Methods">"Extra RECNO Methods"</a> for a workaround).</p> <pre data-language="perl"><code>use warnings ;
use strict ;
use DB_File ;

my $filename = "text" ;
unlink $filename ;

my @h ;
tie @h, "DB_File", $filename, O_RDWR|O_CREAT, 0666, $DB_RECNO
    or die "Cannot open file 'text': $!\n" ;

# Add a few key/value pairs to the file
$h[0] = "orange" ;
$h[1] = "blue" ;
$h[2] = "yellow" ;

push @h, "green", "black" ;

my $elements = scalar @h ;
print "The array contains $elements entries\n" ;

my $last = pop @h ;
print "popped $last\n" ;

unshift @h, "white" ;
my $first = shift @h ;
print "shifted $first\n" ;

# Check for existence of a key
print "Element 1 Exists with value $h[1]\n" if $h[1] ;

# use a negative index
print "The last element is $h[-1]\n" ;
print "The 2nd last element is $h[-2]\n" ;

untie @h ;</code></pre> <p>Here is the output from the script:</p> <pre data-language="perl"><code class="plaintext">The array contains 5 entries
popped black
shifted white
Element 1 Exists with value blue
The last element is green
The 2nd last element is yellow</code></pre> <h3 id="Extra-RECNO-Methods">
Extra RECNO Methods</h3> <p>If you are using a version of Perl earlier than 5.004_57, the tied array interface is quite limited. In the example script above <code>push</code>, <code>pop</code>, <code>shift</code>, <code>unshift</code> or determining the array length will not work with a tied array.</p> <p>To make the interface more useful for older versions of Perl, a number of methods are supplied with <b>DB_File</b> to simulate the missing array operations. All these methods are accessed via the object returned from the tie call.</p> <p>Here are the methods:</p> <dl> <dt id="$X-&gt;push(list)-;">
<b>$X-&gt;push(list) ;</b>
</dt> <dd> <p>Pushes the elements of <code>list</code> to the end of the array.</p> </dd> <dt id="$value-=-$X-&gt;pop-;">
<b>$value = $X-&gt;pop ;</b>
</dt> <dd> <p>Removes and returns the last element of the array.</p> </dd> <dt id="$X-&gt;shift">
<b>$X-&gt;shift</b>
</dt> <dd> <p>Removes and returns the first element of the array.</p> </dd> <dt id="$X-&gt;unshift(list)-;">
<b>$X-&gt;unshift(list) ;</b>
</dt> <dd> <p>Pushes the elements of <code>list</code> to the start of the array.</p> </dd> <dt id="$X-&gt;length">
<b>$X-&gt;length</b>
</dt> <dd> <p>Returns the number of elements in the array.</p> </dd> <dt id="$X-&gt;splice(offset,-length,-elements);">
<b>$X-&gt;splice(offset, length, elements);</b>
</dt> <dd> <p>Returns a splice of the array.</p> </dd> </dl> <h3 id="Another-Example">
Another Example</h3> <p>Here is a more complete example that makes use of some of the methods described above. It also makes use of the API interface directly (see <a href="#THE-API-INTERFACE">"THE API INTERFACE"</a>).</p> <pre data-language="perl"><code>use warnings ;
use strict ;
my (@h, $H, $file, $i) ;
use DB_File ;
use Fcntl ;

$file = "text" ;

unlink $file ;

$H = tie @h, "DB_File", $file, O_RDWR|O_CREAT, 0666, $DB_RECNO
    or die "Cannot open file $file: $!\n" ;

# first create a text file to play with
$h[0] = "zero" ;
$h[1] = "one" ;
$h[2] = "two" ;
$h[3] = "three" ;
$h[4] = "four" ;


# Print the records in order.
#
# The length method is needed here because evaluating a tied
# array in a scalar context does not return the number of
# elements in the array.

print "\nORIGINAL\n" ;
foreach $i (0 .. $H-&gt;length - 1) {
    print "$i: $h[$i]\n" ;
}

# use the push &amp; pop methods
$a = $H-&gt;pop ;
$H-&gt;push("last") ;
print "\nThe last record was [$a]\n" ;

# and the shift &amp; unshift methods
$a = $H-&gt;shift ;
$H-&gt;unshift("first") ;
print "The first record was [$a]\n" ;

# Use the API to add a new record after record 2.
$i = 2 ;
$H-&gt;put($i, "Newbie", R_IAFTER) ;

# and a new record before record 1.
$i = 1 ;
$H-&gt;put($i, "New One", R_IBEFORE) ;

# delete record 3
$H-&gt;del(3) ;

# now print the records in reverse order
print "\nREVERSE\n" ;
for ($i = $H-&gt;length - 1 ; $i &gt;= 0 ; -- $i)
  { print "$i: $h[$i]\n" }

# same again, but use the API functions instead
print "\nREVERSE again\n" ;
my ($s, $k, $v)  = (0, 0, 0) ;
for ($s = $H-&gt;seq($k, $v, R_LAST) ;
         $s == 0 ;
         $s = $H-&gt;seq($k, $v, R_PREV))
  { print "$k: $v\n" }

undef $H ;
untie @h ;</code></pre> <p>and this is what it outputs:</p> <pre data-language="perl"><code class="plaintext">ORIGINAL
0: zero
1: one
2: two
3: three
4: four

The last record was [four]
The first record was [zero]

REVERSE
5: last
4: three
3: Newbie
2: one
1: New One
0: first

REVERSE again
5: last
4: three
3: Newbie
2: one
1: New One
0: first</code></pre> <p>Notes:</p> <ol> <li>
<p>Rather than iterating through the array, <code>@h</code> like this:</p> <pre data-language="perl"><code>foreach $i (@h)</code></pre> <p>it is necessary to use either this:</p> <pre data-language="perl"><code>foreach $i (0 .. $H-&gt;length - 1)</code></pre> <p>or this:</p> <pre data-language="perl"><code>for ($a = $H-&gt;get($k, $v, R_FIRST) ;
     $a == 0 ;
     $a = $H-&gt;get($k, $v, R_NEXT) )</code></pre> </li> <li>
<p>Notice that both times the <code>put</code> method was used the record index was specified using a variable, <code>$i</code>, rather than the literal value itself. This is because <code>put</code> will return the record number of the inserted line via that parameter.</p> </li> </ol> <h2 id="THE-API-INTERFACE">
THE API INTERFACE</h2> <p>As well as accessing Berkeley DB using a tied hash or array, it is also possible to make direct use of most of the API functions defined in the Berkeley DB documentation.</p> <p>To do this you need to store a copy of the object returned from the tie.</p> <pre data-language="perl"><code>$db = tie %hash, "DB_File", "filename" ;</code></pre> <p>Once you have done that, you can access the Berkeley DB API functions as <b>DB_File</b> methods directly like this:</p> <pre data-language="perl"><code>$db-&gt;put($key, $value, R_NOOVERWRITE) ;</code></pre> <p><b>Important:</b> If you have saved a copy of the object returned from <code>tie</code>, the underlying database file will <i>not</i> be closed until both the tied variable is untied and all copies of the saved object are destroyed.</p> <pre data-language="perl"><code>use DB_File ;
$db = tie %hash, "DB_File", "filename"
    or die "Cannot tie filename: $!" ;
...
undef $db ;
untie %hash ;</code></pre> <p>See <a href="#The-untie%28%29-Gotcha">"The untie() Gotcha"</a> for more details.</p> <p>All the functions defined in <a href="dbopen.html">dbopen</a> are available except for close() and dbopen() itself. The <b>DB_File</b> method interface to the supported functions have been implemented to mirror the way Berkeley DB works whenever possible. In particular note that:</p> <ul> <li>
<p>The methods return a status value. All return 0 on success. All return -1 to signify an error and set <code>$!</code> to the exact error code. The return code 1 generally (but not always) means that the key specified did not exist in the database.</p> <p>Other return codes are defined. See below and in the Berkeley DB documentation for details. The Berkeley DB documentation should be used as the definitive source.</p> </li> <li>
<p>Whenever a Berkeley DB function returns data via one of its parameters, the equivalent <b>DB_File</b> method does exactly the same.</p> </li> <li>
<p>If you are careful, it is possible to mix API calls with the tied hash/array interface in the same piece of code. Although only a few of the methods used to implement the tied interface currently make use of the cursor, you should always assume that the cursor has been changed any time the tied hash/array interface is used. As an example, this code will probably not do what you expect:</p> <pre data-language="perl"><code>$X = tie %x, 'DB_File', $filename, O_RDWR|O_CREAT, 0777, $DB_BTREE
    or die "Cannot tie $filename: $!" ;

# Get the first key/value pair and set  the cursor
$X-&gt;seq($key, $value, R_FIRST) ;

# this line will modify the cursor
$count = scalar keys %x ;

# Get the second key/value pair.
# oops, it didn't, it got the last key/value pair!
$X-&gt;seq($key, $value, R_NEXT) ;</code></pre> <p>The code above can be rearranged to get around the problem, like this:</p> <pre data-language="perl"><code>$X = tie %x, 'DB_File', $filename, O_RDWR|O_CREAT, 0777, $DB_BTREE
    or die "Cannot tie $filename: $!" ;

# this line will modify the cursor
$count = scalar keys %x ;

# Get the first key/value pair and set  the cursor
$X-&gt;seq($key, $value, R_FIRST) ;

# Get the second key/value pair.
# worked this time.
$X-&gt;seq($key, $value, R_NEXT) ;</code></pre> </li> </ul> <p>All the constants defined in <a href="dbopen.html">dbopen</a> for use in the flags parameters in the methods defined below are also available. Refer to the Berkeley DB documentation for the precise meaning of the flags values.</p> <p>Below is a list of the methods available.</p> <dl> <dt id="$status-=-$X-&gt;get($key,-$value-[,-$flags])-;">
<b>$status = $X-&gt;get($key, $value [, $flags]) ;</b>
</dt> <dd> <p>Given a key (<code>$key</code>) this method reads the value associated with it from the database. The value read from the database is returned in the <code>$value</code> parameter.</p> <p>If the key does not exist the method returns 1.</p> <p>No flags are currently defined for this method.</p> </dd> <dt id="$status-=-$X-&gt;put($key,-$value-[,-$flags])-;">
<b>$status = $X-&gt;put($key, $value [, $flags]) ;</b>
</dt> <dd> <p>Stores the key/value pair in the database.</p> <p>If you use either the R_IAFTER or R_IBEFORE flags, the <code>$key</code> parameter will have the record number of the inserted key/value pair set.</p> <p>Valid flags are R_CURSOR, R_IAFTER, R_IBEFORE, R_NOOVERWRITE and R_SETCURSOR.</p> </dd> <dt id="$status-=-$X-&gt;del($key-[,-$flags])-;">
<b>$status = $X-&gt;del($key [, $flags]) ;</b>
</dt> <dd> <p>Removes all key/value pairs with key <code>$key</code> from the database.</p> <p>A return code of 1 means that the requested key was not in the database.</p> <p>R_CURSOR is the only valid flag at present.</p> </dd> <dt id="$status-=-$X-&gt;fd-;">
<b>$status = $X-&gt;fd ;</b>
</dt> <dd> <p>Returns the file descriptor for the underlying database.</p> <p>See <a href="#Locking%3A-The-Trouble-with-fd">"Locking: The Trouble with fd"</a> for an explanation for why you should not use <code>fd</code> to lock your database.</p> </dd> <dt id="$status-=-$X-&gt;seq($key,-$value,-$flags)-;">
<b>$status = $X-&gt;seq($key, $value, $flags) ;</b>
</dt> <dd> <p>This interface allows sequential retrieval from the database. See <a href="dbopen.html">dbopen</a> for full details.</p> <p>Both the <code>$key</code> and <code>$value</code> parameters will be set to the key/value pair read from the database.</p> <p>The flags parameter is mandatory. The valid flag values are R_CURSOR, R_FIRST, R_LAST, R_NEXT and R_PREV.</p> </dd> <dt id="$status-=-$X-&gt;sync([$flags])-;">
<b>$status = $X-&gt;sync([$flags]) ;</b>
</dt> <dd> <p>Flushes any cached buffers to disk.</p> <p>R_RECNOSYNC is the only valid flag at present.</p> </dd> </dl> <h2 id="DBM-FILTERS">
DBM FILTERS</h2> <p>A DBM Filter is a piece of code that is be used when you <i>always</i> want to make the same transformation to all keys and/or values in a DBM database. An example is when you need to encode your data in UTF-8 before writing to the database and then decode the UTF-8 when reading from the database file.</p> <p>There are two ways to use a DBM Filter.</p> <ol> <li>
<p>Using the low-level API defined below.</p> </li> <li>
<p>Using the <a href="dbm_filter.html">DBM_Filter</a> module. This module hides the complexity of the API defined below and comes with a number of "canned" filters that cover some of the common use-cases.</p> </li> </ol> <p>Use of the <a href="dbm_filter.html">DBM_Filter</a> module is recommended.</p> <h3 id="DBM-Filter-Low-level-API">
DBM Filter Low-level API</h3> <p>There are four methods associated with DBM Filters. All work identically, and each is used to install (or uninstall) a single DBM Filter. Each expects a single parameter, namely a reference to a sub. The only difference between them is the place that the filter is installed.</p> <p>To summarise:</p> <dl> <dt id="filter_store_key"><b>filter_store_key</b></dt> <dd> <p>If a filter has been installed with this method, it will be invoked every time you write a key to a DBM database.</p> </dd> <dt id="filter_store_value"><b>filter_store_value</b></dt> <dd> <p>If a filter has been installed with this method, it will be invoked every time you write a value to a DBM database.</p> </dd> <dt id="filter_fetch_key"><b>filter_fetch_key</b></dt> <dd> <p>If a filter has been installed with this method, it will be invoked every time you read a key from a DBM database.</p> </dd> <dt id="filter_fetch_value"><b>filter_fetch_value</b></dt> <dd> <p>If a filter has been installed with this method, it will be invoked every time you read a value from a DBM database.</p> </dd> </dl> <p>You can use any combination of the methods, from none, to all four.</p> <p>All filter methods return the existing filter, if present, or <code>undef</code> in not.</p> <p>To delete a filter pass <code>undef</code> to it.</p> <h3 id="The-Filter">
The Filter</h3> <p>When each filter is called by Perl, a local copy of <code>$_</code> will contain the key or value to be filtered. Filtering is achieved by modifying the contents of <code>$_</code>. The return code from the filter is ignored.</p> <h3 id="An-Example-the-NULL-termination-problem.">
An Example -- the NULL termination problem.</h3> <p>Consider the following scenario. You have a DBM database that you need to share with a third-party C application. The C application assumes that <i>all</i> keys and values are NULL terminated. Unfortunately when Perl writes to DBM databases it doesn't use NULL termination, so your Perl application will have to manage NULL termination itself. When you write to the database you will have to use something like this:</p> <pre data-language="perl"><code>$hash{"$key\0"} = "$value\0" ;</code></pre> <p>Similarly the NULL needs to be taken into account when you are considering the length of existing keys/values.</p> <p>It would be much better if you could ignore the NULL terminations issue in the main application code and have a mechanism that automatically added the terminating NULL to all keys and values whenever you write to the database and have them removed when you read from the database. As I'm sure you have already guessed, this is a problem that DBM Filters can fix very easily.</p> <pre data-language="perl"><code>use warnings ;
use strict ;
use DB_File ;

my %hash ;
my $filename = "filt" ;
unlink $filename ;

my $db = tie %hash, 'DB_File', $filename, O_CREAT|O_RDWR, 0666, $DB_HASH
  or die "Cannot open $filename: $!\n" ;

# Install DBM Filters
$db-&gt;filter_fetch_key  ( sub { s/\0$//    } ) ;
$db-&gt;filter_store_key  ( sub { $_ .= "\0" } ) ;
$db-&gt;filter_fetch_value( sub { s/\0$//    } ) ;
$db-&gt;filter_store_value( sub { $_ .= "\0" } ) ;

$hash{"abc"} = "def" ;
my $a = $hash{"ABC"} ;
# ...
undef $db ;
untie %hash ;</code></pre> <p>Hopefully the contents of each of the filters should be self-explanatory. Both "fetch" filters remove the terminating NULL, and both "store" filters add a terminating NULL.</p> <h3 id="Another-Example-Key-is-a-C-int.">
Another Example -- Key is a C int.</h3> <p>Here is another real-life example. By default, whenever Perl writes to a DBM database it always writes the key and value as strings. So when you use this:</p> <pre data-language="perl"><code>$hash{12345} = "something" ;</code></pre> <p>the key 12345 will get stored in the DBM database as the 5 byte string "12345". If you actually want the key to be stored in the DBM database as a C int, you will have to use <code>pack</code> when writing, and <code>unpack</code> when reading.</p> <p>Here is a DBM Filter that does it:</p> <pre data-language="perl"><code>use warnings ;
use strict ;
use DB_File ;
my %hash ;
my $filename = "filt" ;
unlink $filename ;


my $db = tie %hash, 'DB_File', $filename, O_CREAT|O_RDWR, 0666, $DB_HASH
  or die "Cannot open $filename: $!\n" ;

$db-&gt;filter_fetch_key  ( sub { $_ = unpack("i", $_) } ) ;
$db-&gt;filter_store_key  ( sub { $_ = pack ("i", $_) } ) ;
$hash{123} = "def" ;
# ...
undef $db ;
untie %hash ;</code></pre> <p>This time only two filters have been used -- we only need to manipulate the contents of the key, so it wasn't necessary to install any value filters.</p> <h2 id="HINTS-AND-TIPS">
HINTS AND TIPS</h2> <h3 id="Locking:-The-Trouble-with-fd">
Locking: The Trouble with fd</h3> <p>Until version 1.72 of this module, the recommended technique for locking <b>DB_File</b> databases was to flock the filehandle returned from the "fd" function. Unfortunately this technique has been shown to be fundamentally flawed (Kudos to David Harris for tracking this down). Use it at your own peril!</p> <p>The locking technique went like this.</p> <pre data-language="perl"><code>$db = tie(%db, 'DB_File', 'foo.db', O_CREAT|O_RDWR, 0644)
    || die "dbcreat foo.db $!";
$fd = $db-&gt;fd;
open(DB_FH, "+&lt;&amp;=$fd") || die "dup $!";
flock (DB_FH, LOCK_EX) || die "flock: $!";
...
$db{"Tom"} = "Jerry" ;
...
flock(DB_FH, LOCK_UN);
undef $db;
untie %db;
close(DB_FH);</code></pre> <p>In simple terms, this is what happens:</p> <ol> <li>
<p>Use "tie" to open the database.</p> </li> <li>
<p>Lock the database with fd &amp; flock.</p> </li> <li>
<p>Read &amp; Write to the database.</p> </li> <li>
<p>Unlock and close the database.</p> </li> </ol> <p>Here is the crux of the problem. A side-effect of opening the <b>DB_File</b> database in step 2 is that an initial block from the database will get read from disk and cached in memory.</p> <p>To see why this is a problem, consider what can happen when two processes, say "A" and "B", both want to update the same <b>DB_File</b> database using the locking steps outlined above. Assume process "A" has already opened the database and has a write lock, but it hasn't actually updated the database yet (it has finished step 2, but not started step 3 yet). Now process "B" tries to open the same database - step 1 will succeed, but it will block on step 2 until process "A" releases the lock. The important thing to notice here is that at this point in time both processes will have cached identical initial blocks from the database.</p> <p>Now process "A" updates the database and happens to change some of the data held in the initial buffer. Process "A" terminates, flushing all cached data to disk and releasing the database lock. At this point the database on disk will correctly reflect the changes made by process "A".</p> <p>With the lock released, process "B" can now continue. It also updates the database and unfortunately it too modifies the data that was in its initial buffer. Once that data gets flushed to disk it will overwrite some/all of the changes process "A" made to the database.</p> <p>The result of this scenario is at best a database that doesn't contain what you expect. At worst the database will corrupt.</p> <p>The above won't happen every time competing process update the same <b>DB_File</b> database, but it does illustrate why the technique should not be used.</p> <h3 id="Safe-ways-to-lock-a-database">
Safe ways to lock a database</h3> <p>Starting with version 2.x, Berkeley DB has internal support for locking. The companion module to this one, <a href="https://metacpan.org/pod/BerkeleyDB">BerkeleyDB</a>, provides an interface to this locking functionality. If you are serious about locking Berkeley DB databases, I strongly recommend using <a href="https://metacpan.org/pod/BerkeleyDB">BerkeleyDB</a>.</p> <p>If using <a href="https://metacpan.org/pod/BerkeleyDB">BerkeleyDB</a> isn't an option, there are a number of modules available on CPAN that can be used to implement locking. Each one implements locking differently and has different goals in mind. It is therefore worth knowing the difference, so that you can pick the right one for your application. Here are the three locking wrappers:</p> <dl> <dt id="Tie::DB_Lock">
<b>Tie::DB_Lock</b>
</dt> <dd> <p>A <b>DB_File</b> wrapper which creates copies of the database file for read access, so that you have a kind of a multiversioning concurrent read system. However, updates are still serial. Use for databases where reads may be lengthy and consistency problems may occur.</p> </dd> <dt id="Tie::DB_LockFile">
<b>Tie::DB_LockFile</b>
</dt> <dd> <p>A <b>DB_File</b> wrapper that has the ability to lock and unlock the database while it is being used. Avoids the tie-before-flock problem by simply re-tie-ing the database when you get or drop a lock. Because of the flexibility in dropping and re-acquiring the lock in the middle of a session, this can be massaged into a system that will work with long updates and/or reads if the application follows the hints in the POD documentation.</p> </dd> <dt id="DB_File::Lock">
<b>DB_File::Lock</b>
</dt> <dd> <p>An extremely lightweight <b>DB_File</b> wrapper that simply flocks a lockfile before tie-ing the database and drops the lock after the untie. Allows one to use the same lockfile for multiple databases to avoid deadlock problems, if desired. Use for databases where updates are reads are quick and simple flock locking semantics are enough.</p> </dd> </dl> <h3 id="Sharing-Databases-With-C-Applications">
Sharing Databases With C Applications</h3> <p>There is no technical reason why a Berkeley DB database cannot be shared by both a Perl and a C application.</p> <p>The vast majority of problems that are reported in this area boil down to the fact that C strings are NULL terminated, whilst Perl strings are not. See <a href="#DBM-FILTERS">"DBM FILTERS"</a> for a generic way to work around this problem.</p> <p>Here is a real example. Netscape 2.0 keeps a record of the locations you visit along with the time you last visited them in a DB_HASH database. This is usually stored in the file <i>~/.netscape/history.db</i>. The key field in the database is the location string and the value field is the time the location was last visited stored as a 4 byte binary value.</p> <p>If you haven't already guessed, the location string is stored with a terminating NULL. This means you need to be careful when accessing the database.</p> <p>Here is a snippet of code that is loosely based on Tom Christiansen's <i>ggh</i> script (available from your nearest CPAN archive in <i>authors/id/TOMC/scripts/nshist.gz</i>).</p> <pre data-language="perl"><code>use warnings ;
use strict ;
use DB_File ;
use Fcntl ;

my ($dotdir, $HISTORY, %hist_db, $href, $binary_time, $date) ;
$dotdir = $ENV{HOME} || $ENV{LOGNAME};

$HISTORY = "$dotdir/.netscape/history.db";

tie %hist_db, 'DB_File', $HISTORY
    or die "Cannot open $HISTORY: $!\n" ;;

# Dump the complete database
while ( ($href, $binary_time) = each %hist_db ) {

    # remove the terminating NULL
    $href =~ s/\x00$// ;

    # convert the binary time into a user friendly string
    $date = localtime unpack("V", $binary_time);
    print "$date $href\n" ;
}

# check for the existence of a specific key
# remember to add the NULL
if ( $binary_time = $hist_db{"http://mox.perl.com/\x00"} ) {
    $date = localtime unpack("V", $binary_time) ;
    print "Last visited mox.perl.com on $date\n" ;
}
else {
    print "Never visited mox.perl.com\n"
}

untie %hist_db ;</code></pre> <h3 id="The-untie()-Gotcha">
The untie() Gotcha</h3> <p>If you make use of the Berkeley DB API, it is <i>very</i> strongly recommended that you read <a href="perltie.html#The-untie-Gotcha">"The untie Gotcha" in perltie</a>.</p> <p>Even if you don't currently make use of the API interface, it is still worth reading it.</p> <p>Here is an example which illustrates the problem from a <b>DB_File</b> perspective:</p> <pre data-language="perl"><code>use DB_File ;
use Fcntl ;

my %x ;
my $X ;

$X = tie %x, 'DB_File', 'tst.fil' , O_RDWR|O_TRUNC
    or die "Cannot tie first time: $!" ;

$x{123} = 456 ;

untie %x ;

tie %x, 'DB_File', 'tst.fil' , O_RDWR|O_CREAT
    or die "Cannot tie second time: $!" ;

untie %x ;</code></pre> <p>When run, the script will produce this error message:</p> <pre data-language="perl"><code class="plaintext">Cannot tie second time: Invalid argument at bad.file line 14.</code></pre> <p>Although the error message above refers to the second tie() statement in the script, the source of the problem is really with the untie() statement that precedes it.</p> <p>Having read <a href="perltie.html">perltie</a> you will probably have already guessed that the error is caused by the extra copy of the tied object stored in <code>$X</code>. If you haven't, then the problem boils down to the fact that the <b>DB_File</b> destructor, DESTROY, will not be called until <i>all</i> references to the tied object are destroyed. Both the tied variable, <code>%x</code>, and <code>$X</code> above hold a reference to the object. The call to untie() will destroy the first, but <code>$X</code> still holds a valid reference, so the destructor will not get called and the database file <i>tst.fil</i> will remain open. The fact that Berkeley DB then reports the attempt to open a database that is already open via the catch-all "Invalid argument" doesn't help.</p> <p>If you run the script with the <code>-w</code> flag the error message becomes:</p> <pre data-language="perl"><code class="plaintext">untie attempted while 1 inner references still exist at bad.file line 12.
Cannot tie second time: Invalid argument at bad.file line 14.</code></pre> <p>which pinpoints the real problem. Finally the script can now be modified to fix the original problem by destroying the API object before the untie:</p> <pre data-language="perl"><code>...
$x{123} = 456 ;

undef $X ;
untie %x ;

$X = tie %x, 'DB_File', 'tst.fil' , O_RDWR|O_CREAT
...</code></pre> <h2 id="COMMON-QUESTIONS">
COMMON QUESTIONS</h2> <h3 id="Why-is-there-Perl-source-in-my-database?">
Why is there Perl source in my database?</h3> <p>If you look at the contents of a database file created by DB_File, there can sometimes be part of a Perl script included in it.</p> <p>This happens because Berkeley DB uses dynamic memory to allocate buffers which will subsequently be written to the database file. Being dynamic, the memory could have been used for anything before DB malloced it. As Berkeley DB doesn't clear the memory once it has been allocated, the unused portions will contain random junk. In the case where a Perl script gets written to the database, the random junk will correspond to an area of dynamic memory that happened to be used during the compilation of the script.</p> <p>Unless you don't like the possibility of there being part of your Perl scripts embedded in a database file, this is nothing to worry about.</p> <h3 id="How-do-I-store-complex-data-structures-with-DB_File?">
How do I store complex data structures with DB_File?</h3> <p>Although <b>DB_File</b> cannot do this directly, there is a module which can layer transparently over <b>DB_File</b> to accomplish this feat.</p> <p>Check out the MLDBM module, available on CPAN in the directory <i>modules/by-module/MLDBM</i>.</p> <h3 id='What-does-"wide-character-in-subroutine-entry"-mean?'>
What does "wide character in subroutine entry" mean?</h3> <p>You will usually get this message if you are working with UTF-8 data and want to read/write it from/to a Berkeley DB database file.</p> <p>The easist way to deal with this issue is to use the pre-defined "utf8" <b>DBM_Filter</b> (see <a href="dbm_filter.html">DBM_Filter</a>) that was designed to deal with this situation.</p> <p>The example below shows what you need if <i>both</i> the key and value are expected to be in UTF-8.</p> <pre data-language="perl"><code>use DB_File;
use DBM_Filter;

my $db = tie %h, 'DB_File', '/tmp/try.db', O_CREAT|O_RDWR, 0666, $DB_BTREE;
$db-&gt;Filter_Key_Push('utf8');
$db-&gt;Filter_Value_Push('utf8');

my $key = "\N{LATIN SMALL LETTER A WITH ACUTE}";
my $value = "\N{LATIN SMALL LETTER E WITH ACUTE}";
$h{ $key } = $value;</code></pre> <h3 id='What-does-"Invalid-Argument"-mean?'>
What does "Invalid Argument" mean?</h3> <p>You will get this error message when one of the parameters in the <code>tie</code> call is wrong. Unfortunately there are quite a few parameters to get wrong, so it can be difficult to figure out which one it is.</p> <p>Here are a couple of possibilities:</p> <ol> <li>
<p>Attempting to reopen a database without closing it.</p> </li> <li>
<p>Using the O_WRONLY flag.</p> </li> </ol> <h3 id="What-does-&quot;Bareword-'DB_File'-not-allowed&quot;-mean?">
What does "Bareword 'DB_File' not allowed" mean?</h3> <p>You will encounter this particular error message when you have the <code>strict 'subs'</code> pragma (or the full strict pragma) in your script. Consider this script:</p> <pre data-language="perl"><code>use warnings ;
use strict ;
use DB_File ;
my %x ;
tie %x, DB_File, "filename" ;</code></pre> <p>Running it produces the error in question:</p> <pre data-language="perl"><code class="plaintext">Bareword "DB_File" not allowed while "strict subs" in use</code></pre> <p>To get around the error, place the word <code>DB_File</code> in either single or double quotes, like this:</p> <pre data-language="perl"><code>tie %x, "DB_File", "filename" ;</code></pre> <p>Although it might seem like a real pain, it is really worth the effort of having a <code>use strict</code> in all your scripts.</p> <h2 id="REFERENCES">REFERENCES</h2> <p>Articles that are either about <b>DB_File</b> or make use of it.</p> <ol> <li>
<p><i>Full-Text Searching in Perl</i>, Tim Kientzle (tkientzle@ddj.com), Dr. Dobb's Journal, Issue 295, January 1999, pp 34-41</p> </li> </ol> <h2 id="HISTORY">HISTORY</h2> <p>Moved to the Changes file.</p> <h2 id="BUGS">BUGS</h2> <p>Some older versions of Berkeley DB had problems with fixed length records using the RECNO file format. This problem has been fixed since version 1.85 of Berkeley DB.</p> <p>I am sure there are bugs in the code. If you do find any, or can suggest any enhancements, I would welcome your comments.</p> <h2 id="SUPPORT">SUPPORT</h2> <p>General feedback/questions/bug reports should be sent to <a href="https://github.com/pmqs/DB_File/issues">https://github.com/pmqs/DB_File/issues</a> (preferred) or <a href="https://rt.cpan.org/Public/Dist/Display.html?Name=DB_File">https://rt.cpan.org/Public/Dist/Display.html?Name=DB_File</a>.</p> <h2 id="AVAILABILITY">AVAILABILITY</h2> <p><b>DB_File</b> comes with the standard Perl source distribution. Look in the directory <i>ext/DB_File</i>. Given the amount of time between releases of Perl the version that ships with Perl is quite likely to be out of date, so the most recent version can always be found on CPAN (see <a href="perlmodlib.html#CPAN">"CPAN" in perlmodlib</a> for details), in the directory <i>modules/by-module/DB_File</i>.</p> <p><b>DB_File</b> is designed to work with any version of Berkeley DB, but is limited to the functionality provided by version 1. If you want to make use of the new features available in Berkeley DB 2.x, or greater, use the Perl module <a href="https://metacpan.org/pod/BerkeleyDB">BerkeleyDB</a> instead.</p> <p>The official web site for Berkeley DB is <a href="http://www.oracle.com/technology/products/berkeley-db/db/index.html">http://www.oracle.com/technology/products/berkeley-db/db/index.html</a>. All versions of Berkeley DB are available there.</p> <p>Alternatively, Berkeley DB version 1 is available at your nearest CPAN archive in <i>src/misc/db.1.85.tar.gz</i>.</p> <h2 id="COPYRIGHT">COPYRIGHT</h2> <p>Copyright (c) 1995-2022 Paul Marquess. All rights reserved. This program is free software; you can redistribute it and/or modify it under the same terms as Perl itself.</p> <p>Although <b>DB_File</b> is covered by the Perl license, the library it makes use of, namely Berkeley DB, is not. Berkeley DB has its own copyright and its own license. See <a href="https://www.oracle.com/downloads/licenses/berkeleydb-oslicense.html">AGPL</a> for more details. Please take the time to read the Berkeley DB license and decide how it impacts your use of this Perl module.</p> <h2 id="SEE-ALSO">
SEE ALSO</h2> <p><a href="perl.html">perl</a>, <a href="http://man.he.net/man3/dbopen">dbopen(3)</a>, <a href="http://man.he.net/man3/hash">hash(3)</a>, <a href="http://man.he.net/man3/recno">recno(3)</a>, <a href="http://man.he.net/man3/btree">btree(3)</a>, <a href="perldbmfilter.html">perldbmfilter</a>, <a href="dbm_filter.html">DBM_Filter</a></p> <h2 id="AUTHOR">AUTHOR</h2> <p>The DB_File interface was written by Paul Marquess &lt;pmqs@cpan.org&gt;.</p><div class="_attribution">
  <p class="_attribution-p">
    &copy; 1993&ndash;2023 Larry Wall and others<br>Licensed under the GNU General Public License version 1 or later, or the Artistic License.<br>The Perl logo is a trademark of the Perl Foundation.<br>
    <a href="https://perldoc.perl.org/5.38.0/DB_File" class="_attribution-link">https://perldoc.perl.org/5.38.0/DB_File</a>
  </p>
</div>
