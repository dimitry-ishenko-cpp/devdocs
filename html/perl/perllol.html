<h1>perllol</h1>   <h2><a id="toc">CONTENTS</a></h2> <ul> <li> <a class="text-decoration-none" href="#NAME">NAME</a> </li> <li> <a class="text-decoration-none" href="#DESCRIPTION">DESCRIPTION</a> <ul> <li> <a class="text-decoration-none" href="#Declaration-and-Access-of-Arrays-of-Arrays">Declaration and Access of Arrays of Arrays</a> </li> <li> <a class="text-decoration-none" href="#Growing-Your-Own">Growing Your Own</a> </li> <li> <a class="text-decoration-none" href="#Access-and-Printing">Access and Printing</a> </li> <li> <a class="text-decoration-none" href="#Slices">Slices</a> </li> </ul> </li> <li> <a class="text-decoration-none" href="#SEE-ALSO">SEE ALSO</a> </li> <li> <a class="text-decoration-none" href="#AUTHOR">AUTHOR</a> </li> </ul> <h2 id="NAME">NAME</h2> <p>perllol - Manipulating Arrays of Arrays in Perl</p> <h2 id="DESCRIPTION">DESCRIPTION</h2> <h3 id="Declaration-and-Access-of-Arrays-of-Arrays">
Declaration and Access of Arrays of Arrays</h3> <p>The simplest two-level data structure to build in Perl is an array of arrays, sometimes casually called a list of lists. It's reasonably easy to understand, and almost everything that applies here will also be applicable later on with the fancier data structures.</p> <p>An array of an array is just a regular old array @AoA that you can get at with two subscripts, like <code>$AoA[3][2]</code>. Here's a declaration of the array:</p> <pre data-language="perl"><code>    use v5.10;  # so we can use say()

    # assign to our array, an array of array references
    @AoA = (
	   [ "fred", "barney", "pebbles", "bambam", "dino", ],
	   [ "george", "jane", "elroy", "judy", ],
	   [ "homer", "bart", "marge", "maggie", ],
    );
    say $AoA[2][1];
  bart</code></pre> <p>Now you should be very careful that the outer bracket type is a round one, that is, a parenthesis. That's because you're assigning to an @array, so you need parentheses. If you wanted there <i>not</i> to be an @AoA, but rather just a reference to it, you could do something more like this:</p> <pre data-language="perl"><code>    # assign a reference to array of array references
    $ref_to_AoA = [
	[ "fred", "barney", "pebbles", "bambam", "dino", ],
	[ "george", "jane", "elroy", "judy", ],
	[ "homer", "bart", "marge", "maggie", ],
    ];
    say $ref_to_AoA-&gt;[2][1];
  bart</code></pre> <p>Notice that the outer bracket type has changed, and so our access syntax has also changed. That's because unlike C, in perl you can't freely interchange arrays and references thereto. $ref_to_AoA is a reference to an array, whereas @AoA is an array proper. Likewise, <code>$AoA[2]</code> is not an array, but an array ref. So how come you can write these:</p> <pre data-language="perl"><code>$AoA[2][2]
$ref_to_AoA-&gt;[2][2]</code></pre> <p>instead of having to write these:</p> <pre data-language="perl"><code>$AoA[2]-&gt;[2]
$ref_to_AoA-&gt;[2]-&gt;[2]</code></pre> <p>Well, that's because the rule is that on adjacent brackets only (whether square or curly), you are free to omit the pointer dereferencing arrow. But you cannot do so for the very first one if it's a scalar containing a reference, which means that $ref_to_AoA always needs it.</p> <h3 id="Growing-Your-Own">
Growing Your Own</h3> <p>That's all well and good for declaration of a fixed data structure, but what if you wanted to add new elements on the fly, or build it up entirely from scratch?</p> <p>First, let's look at reading it in from a file. This is something like adding a row at a time. We'll assume that there's a flat file in which each line is a row and each word an element. If you're trying to develop an @AoA array containing all these, here's the right way to do that:</p> <pre data-language="perl"><code>    while (&lt;&gt;) {
	@tmp = split;
	push @AoA, [ @tmp ];
    }</code></pre> <p>You might also have loaded that from a function:</p> <pre data-language="perl"><code>    for $i ( 1 .. 10 ) {
	$AoA[$i] = [ somefunc($i) ];
    }</code></pre> <p>Or you might have had a temporary variable sitting around with the array in it.</p> <pre data-language="perl"><code>    for $i ( 1 .. 10 ) {
	@tmp = somefunc($i);
	$AoA[$i] = [ @tmp ];
    }</code></pre> <p>It's important you make sure to use the <code>[ ]</code> array reference constructor. That's because this wouldn't work:</p> <pre data-language="perl"><code>$AoA[$i] = @tmp;   # WRONG!</code></pre> <p>The reason that doesn't do what you want is because assigning a named array like that to a scalar is taking an array in scalar context, which means just counts the number of elements in @tmp.</p> <p>If you are running under <code>use strict</code> (and if you aren't, why in the world aren't you?), you'll have to add some declarations to make it happy:</p> <pre data-language="perl"><code>    use strict;
    my(@AoA, @tmp);
    while (&lt;&gt;) {
	@tmp = split;
	push @AoA, [ @tmp ];
    }</code></pre> <p>Of course, you don't need the temporary array to have a name at all:</p> <pre data-language="perl"><code>    while (&lt;&gt;) {
	push @AoA, [ split ];
    }</code></pre> <p>You also don't have to use push(). You could just make a direct assignment if you knew where you wanted to put it:</p> <pre data-language="perl"><code>    my (@AoA, $i, $line);
    for $i ( 0 .. 10 ) {
	$line = &lt;&gt;;
	$AoA[$i] = [ split " ", $line ];
    }</code></pre> <p>or even just</p> <pre data-language="perl"><code>    my (@AoA, $i);
    for $i ( 0 .. 10 ) {
	$AoA[$i] = [ split " ", &lt;&gt; ];
    }</code></pre> <p>You should in general be leery of using functions that could potentially return lists in scalar context without explicitly stating such. This would be clearer to the casual reader:</p> <pre data-language="perl"><code>    my (@AoA, $i);
    for $i ( 0 .. 10 ) {
	$AoA[$i] = [ split " ", scalar(&lt;&gt;) ];
    }</code></pre> <p>If you wanted to have a $ref_to_AoA variable as a reference to an array, you'd have to do something like this:</p> <pre data-language="perl"><code>    while (&lt;&gt;) {
	push @$ref_to_AoA, [ split ];
    }</code></pre> <p>Now you can add new rows. What about adding new columns? If you're dealing with just matrices, it's often easiest to use simple assignment:</p> <pre data-language="perl"><code>    for $x (1 .. 10) {
	for $y (1 .. 10) {
	    $AoA[$x][$y] = func($x, $y);
	}
    }

    for $x ( 3, 7, 9 ) {
	$AoA[$x][20] += func2($x);
    }</code></pre> <p>It doesn't matter whether those elements are already there or not: it'll gladly create them for you, setting intervening elements to <code>undef</code> as need be.</p> <p>If you wanted just to append to a row, you'd have to do something a bit funnier looking:</p> <pre data-language="perl"><code># add new columns to an existing row
push $AoA[0]-&gt;@*, "wilma", "betty";   # explicit deref</code></pre> <h3 id="Access-and-Printing">
Access and Printing</h3> <p>Now it's time to print your data structure out. How are you going to do that? Well, if you want only one of the elements, it's trivial:</p> <pre data-language="perl"><code>print $AoA[0][0];</code></pre> <p>If you want to print the whole thing, though, you can't say</p> <pre data-language="perl"><code>print @AoA;		# WRONG</code></pre> <p>because you'll get just references listed, and perl will never automatically dereference things for you. Instead, you have to roll yourself a loop or two. This prints the whole structure, using the shell-style for() construct to loop across the outer set of subscripts.</p> <pre data-language="perl"><code>    for $aref ( @AoA ) {
	say "\t [ @$aref ],";
    }</code></pre> <p>If you wanted to keep track of subscripts, you might do this:</p> <pre data-language="perl"><code>    for $i ( 0 .. $#AoA ) {
	say "\t elt $i is [ @{$AoA[$i]} ],";
    }</code></pre> <p>or maybe even this. Notice the inner loop.</p> <pre data-language="perl"><code>    for $i ( 0 .. $#AoA ) {
	for $j ( 0 .. $#{$AoA[$i]} ) {
	    say "elt $i $j is $AoA[$i][$j]";
	}
    }</code></pre> <p>As you can see, it's getting a bit complicated. That's why sometimes is easier to take a temporary on your way through:</p> <pre data-language="perl"><code>    for $i ( 0 .. $#AoA ) {
	$aref = $AoA[$i];
	for $j ( 0 .. $#{$aref} ) {
	    say "elt $i $j is $AoA[$i][$j]";
	}
    }</code></pre> <p>Hmm... that's still a bit ugly. How about this:</p> <pre data-language="perl"><code>    for $i ( 0 .. $#AoA ) {
	$aref = $AoA[$i];
	$n = @$aref - 1;
	for $j ( 0 .. $n ) {
	    say "elt $i $j is $AoA[$i][$j]";
	}
    }</code></pre> <p>When you get tired of writing a custom print for your data structures, you might look at the standard <a href="dumpvalue.html">Dumpvalue</a> or <a href="Data::Dumper.html">Data::Dumper</a> modules. The former is what the Perl debugger uses, while the latter generates parsable Perl code. For example:</p> <pre data-language="perl"><code> use v5.14;     # using the + prototype, new to v5.14

 sub show(+) {
	require Dumpvalue;
	state $prettily = new Dumpvalue::
			    tick        =&gt; q("),
			    compactDump =&gt; 1,  # comment these two lines
                                               # out
			    veryCompact =&gt; 1,  # if you want a bigger
                                               # dump
			;
	dumpValue $prettily @_;
 }

 # Assign a list of array references to an array.
 my @AoA = (
	   [ "fred", "barney" ],
	   [ "george", "jane", "elroy" ],
	   [ "homer", "marge", "bart" ],
 );
 push $AoA[0]-&gt;@*, "wilma", "betty";
 show @AoA;</code></pre> <p>will print out:</p> <pre data-language="perl"><code class="plaintext">0  0..3  "fred" "barney" "wilma" "betty"
1  0..2  "george" "jane" "elroy"
2  0..2  "homer" "marge" "bart"</code></pre> <p>Whereas if you comment out the two lines I said you might wish to, then it shows it to you this way instead:</p> <pre data-language="perl"><code class="plaintext">0  ARRAY(0x8031d0)
   0  "fred"
   1  "barney"
   2  "wilma"
   3  "betty"
1  ARRAY(0x803d40)
   0  "george"
   1  "jane"
   2  "elroy"
2  ARRAY(0x803e10)
   0  "homer"
   1  "marge"
   2  "bart"</code></pre> <h3 id="Slices">Slices</h3> <p>If you want to get at a slice (part of a row) in a multidimensional array, you're going to have to do some fancy subscripting. That's because while we have a nice synonym for single elements via the pointer arrow for dereferencing, no such convenience exists for slices.</p> <p>Here's how to do one operation using a loop. We'll assume an @AoA variable as before.</p> <pre data-language="perl"><code>    @part = ();
    $x = 4;
    for ($y = 7; $y &lt; 13; $y++) {
	push @part, $AoA[$x][$y];
    }</code></pre> <p>That same loop could be replaced with a slice operation:</p> <pre data-language="perl"><code>@part = $AoA[4]-&gt;@[ 7..12 ];</code></pre> <p>Now, what if you wanted a <i>two-dimensional slice</i>, such as having $x run from 4..8 and $y run from 7 to 12? Hmm... here's the simple way:</p> <pre data-language="perl"><code>    @newAoA = ();
    for ($startx = $x = 4; $x &lt;= 8; $x++) {
	for ($starty = $y = 7; $y &lt;= 12; $y++) {
	    $newAoA[$x - $startx][$y - $starty] = $AoA[$x][$y];
	}
    }</code></pre> <p>We can reduce some of the looping through slices</p> <pre data-language="perl"><code>    for ($x = 4; $x &lt;= 8; $x++) {
	push @newAoA, [ $AoA[$x]-&gt;@[ 7..12 ] ];
    }</code></pre> <p>If you were into Schwartzian Transforms, you would probably have selected map for that</p> <pre data-language="perl"><code>@newAoA = map { [ $AoA[$_]-&gt;@[ 7..12 ] ] } 4 .. 8;</code></pre> <p>Although if your manager accused you of seeking job security (or rapid insecurity) through inscrutable code, it would be hard to argue. :-) If I were you, I'd put that in a function:</p> <pre data-language="perl"><code>    @newAoA = splice_2D( \@AoA, 4 =&gt; 8, 7 =&gt; 12 );
    sub splice_2D {
	my $lrr = shift; 	# ref to array of array refs!
	my ($x_lo, $x_hi,
	    $y_lo, $y_hi) = @_;

	return map {
	    [ $lrr-&gt;[$_]-&gt;@[ $y_lo .. $y_hi ] ]
	} $x_lo .. $x_hi;
    }</code></pre> <h2 id="SEE-ALSO">
SEE ALSO</h2> <p><a href="perldata.html">perldata</a>, <a href="perlref.html">perlref</a>, <a href="perldsc.html">perldsc</a></p> <h2 id="AUTHOR">AUTHOR</h2> <p>Tom Christiansen &lt;<i>tchrist@perl.com</i>&gt;</p> <p>Last update: Tue Apr 26 18:30:55 MDT 2011</p><div class="_attribution">
  <p class="_attribution-p">
    &copy; 1993&ndash;2023 Larry Wall and others<br>Licensed under the GNU General Public License version 1 or later, or the Artistic License.<br>The Perl logo is a trademark of the Perl Foundation.<br>
    <a href="https://perldoc.perl.org/5.38.0/perllol" class="_attribution-link">https://perldoc.perl.org/5.38.0/perllol</a>
  </p>
</div>
