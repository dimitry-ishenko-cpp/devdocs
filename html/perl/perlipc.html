<h1>perlipc</h1>   <h2><a id="toc">CONTENTS</a></h2> <ul> <li> <a class="text-decoration-none" href="#NAME">NAME</a> </li> <li> <a class="text-decoration-none" href="#DESCRIPTION">DESCRIPTION</a> </li> <li> <a class="text-decoration-none" href="#Signals">Signals</a> <ul> <li> <a class="text-decoration-none" href="#Handling-the-SIGHUP-Signal-in-Daemons">Handling the SIGHUP Signal in Daemons</a> </li> <li> <a class="text-decoration-none" href="#Deferred-Signals-(Safe-Signals)">Deferred Signals (Safe Signals)</a> </li> </ul> </li> <li> <a class="text-decoration-none" href="#Named-Pipes">Named Pipes</a> </li> <li> <a class="text-decoration-none" href="#Using-open()-for-IPC">Using open() for IPC</a> <ul> <li> <a class="text-decoration-none" href="#Filehandles">Filehandles</a> </li> <li> <a class="text-decoration-none" href="#Background-Processes">Background Processes</a> </li> <li> <a class="text-decoration-none" href="#Complete-Dissociation-of-Child-from-Parent">Complete Dissociation of Child from Parent</a> </li> <li> <a class="text-decoration-none" href="#Safe-Pipe-Opens">Safe Pipe Opens</a> </li> <li> <a class="text-decoration-none" href="#Avoiding-Pipe-Deadlocks">Avoiding Pipe Deadlocks</a> </li> <li> <a class="text-decoration-none" href="#Bidirectional-Communication-with-Another-Process">Bidirectional Communication with Another Process</a> </li> <li> <a class="text-decoration-none" href="#Bidirectional-Communication-with-Yourself">Bidirectional Communication with Yourself</a> </li> </ul> </li> <li> <a class="text-decoration-none" href="#Sockets:-Client/Server-Communication">Sockets: Client/Server Communication</a> <ul> <li> <a class="text-decoration-none" href="#Internet-Line-Terminators">Internet Line Terminators</a> </li> <li> <a class="text-decoration-none" href="#Internet-TCP-Clients-and-Servers">Internet TCP Clients and Servers</a> </li> <li> <a class="text-decoration-none" href="#Unix-Domain-TCP-Clients-and-Servers">Unix-Domain TCP Clients and Servers</a> </li> </ul> </li> <li> <a class="text-decoration-none" href="#TCP-Clients-with-IO::Socket">TCP Clients with IO::Socket</a> <ul> <li> <a class="text-decoration-none" href="#A-Simple-Client">A Simple Client</a> </li> <li> <a class="text-decoration-none" href="#A-Webget-Client">A Webget Client</a> </li> <li> <a class="text-decoration-none" href="#Interactive-Client-with-IO::Socket">Interactive Client with IO::Socket</a> </li> </ul> </li> <li> <a class="text-decoration-none" href="#TCP-Servers-with-IO::Socket">TCP Servers with IO::Socket</a> </li> <li> <a class="text-decoration-none" href="#UDP:-Message-Passing">UDP: Message Passing</a> </li> <li> <a class="text-decoration-none" href="#SysV-IPC">SysV IPC</a> </li> <li> <a class="text-decoration-none" href="#NOTES">NOTES</a> </li> <li> <a class="text-decoration-none" href="#BUGS">BUGS</a> </li> <li> <a class="text-decoration-none" href="#AUTHOR">AUTHOR</a> </li> <li> <a class="text-decoration-none" href="#SEE-ALSO">SEE ALSO</a> </li> </ul> <h2 id="NAME">NAME</h2> <p>perlipc - Perl interprocess communication (signals, fifos, pipes, safe subprocesses, sockets, and semaphores)</p> <h2 id="DESCRIPTION">DESCRIPTION</h2> <p>The basic IPC facilities of Perl are built out of the good old Unix signals, named pipes, pipe opens, the Berkeley socket routines, and SysV IPC calls. Each is used in slightly different situations.</p> <h2 id="Signals">Signals</h2> <p>Perl uses a simple signal handling model: the %SIG hash contains names or references of user-installed signal handlers. These handlers will be called with an argument which is the name of the signal that triggered it. A signal may be generated intentionally from a particular keyboard sequence like control-C or control-Z, sent to you from another process, or triggered automatically by the kernel when special events transpire, like a child process exiting, your own process running out of stack space, or hitting a process file-size limit.</p> <p>For example, to trap an interrupt signal, set up a handler like this:</p> <pre data-language="perl"><code>our $shucks;

sub catch_zap {
    my $signame = shift;
    $shucks++;
    die "Somebody sent me a SIG$signame";
}
$SIG{INT} = __PACKAGE__ . "::catch_zap";
$SIG{INT} = \&amp;catch_zap;  # best strategy</code></pre> <p>Prior to Perl 5.8.0 it was necessary to do as little as you possibly could in your handler; notice how all we do is set a global variable and then raise an exception. That's because on most systems, libraries are not re-entrant; particularly, memory allocation and I/O routines are not. That meant that doing nearly <i>anything</i> in your handler could in theory trigger a memory fault and subsequent core dump - see <a href="#Deferred-Signals-%28Safe-Signals%29">"Deferred Signals (Safe Signals)"</a> below.</p> <p>The names of the signals are the ones listed out by <code>kill -l</code> on your system, or you can retrieve them using the CPAN module <a href="IPC::Signal.html">IPC::Signal</a>.</p> <p>You may also choose to assign the strings <code>"IGNORE"</code> or <code>"DEFAULT"</code> as the handler, in which case Perl will try to discard the signal or do the default thing.</p> <p>On most Unix platforms, the <code>CHLD</code> (sometimes also known as <code>CLD</code>) signal has special behavior with respect to a value of <code>"IGNORE"</code>. Setting <code>$SIG{CHLD}</code> to <code>"IGNORE"</code> on such a platform has the effect of not creating zombie processes when the parent process fails to <code>wait()</code> on its child processes (i.e., child processes are automatically reaped). Calling <code>wait()</code> with <code>$SIG{CHLD}</code> set to <code>"IGNORE"</code> usually returns <code>-1</code> on such platforms.</p> <p>Some signals can be neither trapped nor ignored, such as the KILL and STOP (but not the TSTP) signals. Note that ignoring signals makes them disappear. If you only want them blocked temporarily without them getting lost you'll have to use the <code>POSIX</code> module's <a href="posix.html#sigprocmask">sigprocmask</a>.</p> <p>Sending a signal to a negative process ID means that you send the signal to the entire Unix process group. This code sends a hang-up signal to all processes in the current process group, and also sets $SIG{HUP} to <code>"IGNORE"</code> so it doesn't kill itself:</p> <pre data-language="perl"><code># block scope for local
{
    local $SIG{HUP} = "IGNORE";
    kill HUP =&gt; -getpgrp();
    # snazzy writing of: kill("HUP", -getpgrp())
}</code></pre> <p>Another interesting signal to send is signal number zero. This doesn't actually affect a child process, but instead checks whether it's alive or has changed its UIDs.</p> <pre data-language="perl"><code>unless (kill 0 =&gt; $kid_pid) {
    warn "something wicked happened to $kid_pid";
}</code></pre> <p>Signal number zero may fail because you lack permission to send the signal when directed at a process whose real or saved UID is not identical to the real or effective UID of the sending process, even though the process is alive. You may be able to determine the cause of failure using <code>$!</code> or <code>%!</code>.</p> <pre data-language="perl"><code>unless (kill(0 =&gt; $pid) || $!{EPERM}) {
    warn "$pid looks dead";
}</code></pre> <p>You might also want to employ anonymous functions for simple signal handlers:</p> <pre data-language="perl"><code>$SIG{INT} = sub { die "\nOutta here!\n" };</code></pre> <p>SIGCHLD handlers require some special care. If a second child dies while in the signal handler caused by the first death, we won't get another signal. So must loop here else we will leave the unreaped child as a zombie. And the next time two children die we get another zombie. And so on.</p> <pre data-language="perl"><code>use POSIX ":sys_wait_h";
$SIG{CHLD} = sub {
    while ((my $child = waitpid(-1, WNOHANG)) &gt; 0) {
        $Kid_Status{$child} = $?;
    }
};
# do something that forks...</code></pre> <p>Be careful: qx(), system(), and some modules for calling external commands do a fork(), then wait() for the result. Thus, your signal handler will be called. Because wait() was already called by system() or qx(), the wait() in the signal handler will see no more zombies and will therefore block.</p> <p>The best way to prevent this issue is to use waitpid(), as in the following example:</p> <pre data-language="perl"><code>use POSIX ":sys_wait_h"; # for nonblocking read

my %children;

$SIG{CHLD} = sub {
    # don't change $! and $? outside handler
    local ($!, $?);
    while ( (my $pid = waitpid(-1, WNOHANG)) &gt; 0 ) {
        delete $children{$pid};
        cleanup_child($pid, $?);
    }
};

while (1) {
    my $pid = fork();
    die "cannot fork" unless defined $pid;
    if ($pid == 0) {
        # ...
        exit 0;
    } else {
        $children{$pid}=1;
        # ...
        system($command);
        # ...
   }
}</code></pre> <p>Signal handling is also used for timeouts in Unix. While safely protected within an <code>eval{}</code> block, you set a signal handler to trap alarm signals and then schedule to have one delivered to you in some number of seconds. Then try your blocking operation, clearing the alarm when it's done but not before you've exited your <code>eval{}</code> block. If it goes off, you'll use die() to jump out of the block.</p> <p>Here's an example:</p> <pre data-language="perl"><code>my $ALARM_EXCEPTION = "alarm clock restart";
eval {
    local $SIG{ALRM} = sub { die $ALARM_EXCEPTION };
    alarm 10;
    flock($fh, 2)    # blocking write lock
                    || die "cannot flock: $!";
    alarm 0;
};
if ($@ &amp;&amp; $@ !~ quotemeta($ALARM_EXCEPTION)) { die }</code></pre> <p>If the operation being timed out is system() or qx(), this technique is liable to generate zombies. If this matters to you, you'll need to do your own fork() and exec(), and kill the errant child process.</p> <p>For more complex signal handling, you might see the standard POSIX module. Lamentably, this is almost entirely undocumented, but the <i>ext/POSIX/t/sigaction.t</i> file from the Perl source distribution has some examples in it.</p> <h3 id="Handling-the-SIGHUP-Signal-in-Daemons">
Handling the SIGHUP Signal in Daemons</h3> <p>A process that usually starts when the system boots and shuts down when the system is shut down is called a daemon (Disk And Execution MONitor). If a daemon process has a configuration file which is modified after the process has been started, there should be a way to tell that process to reread its configuration file without stopping the process. Many daemons provide this mechanism using a <code>SIGHUP</code> signal handler. When you want to tell the daemon to reread the file, simply send it the <code>SIGHUP</code> signal.</p> <p>The following example implements a simple daemon, which restarts itself every time the <code>SIGHUP</code> signal is received. The actual code is located in the subroutine <code>code()</code>, which just prints some debugging info to show that it works; it should be replaced with the real code.</p> <pre data-language="perl"><code>#!/usr/bin/perl

use v5.36;

use POSIX ();
use FindBin ();
use File::Basename ();
use File::Spec::Functions qw(catfile);

$| = 1;

# make the daemon cross-platform, so exec always calls the script
# itself with the right path, no matter how the script was invoked.
my $script = File::Basename::basename($0);
my $SELF  = catfile($FindBin::Bin, $script);

# POSIX unmasks the sigprocmask properly
$SIG{HUP} = sub {
    print "got SIGHUP\n";
    exec($SELF, @ARGV)        || die "$0: couldn't restart: $!";
};

code();

sub code {
    print "PID: $$\n";
    print "ARGV: @ARGV\n";
    my $count = 0;
    while (1) {
        sleep 2;
        print ++$count, "\n";
    }
}</code></pre> <h3 id="Deferred-Signals-(Safe-Signals)">
Deferred Signals (Safe Signals)</h3> <p>Before Perl 5.8.0, installing Perl code to deal with signals exposed you to danger from two things. First, few system library functions are re-entrant. If the signal interrupts while Perl is executing one function (like malloc(3) or printf(3)), and your signal handler then calls the same function again, you could get unpredictable behavior--often, a core dump. Second, Perl isn't itself re-entrant at the lowest levels. If the signal interrupts Perl while Perl is changing its own internal data structures, similarly unpredictable behavior may result.</p> <p>There were two things you could do, knowing this: be paranoid or be pragmatic. The paranoid approach was to do as little as possible in your signal handler. Set an existing integer variable that already has a value, and return. This doesn't help you if you're in a slow system call, which will just restart. That means you have to <code>die</code> to longjmp(3) out of the handler. Even this is a little cavalier for the true paranoiac, who avoids <code>die</code> in a handler because the system <i>is</i> out to get you. The pragmatic approach was to say "I know the risks, but prefer the convenience", and to do anything you wanted in your signal handler, and be prepared to clean up core dumps now and again.</p> <p>Perl 5.8.0 and later avoid these problems by "deferring" signals. That is, when the signal is delivered to the process by the system (to the C code that implements Perl) a flag is set, and the handler returns immediately. Then at strategic "safe" points in the Perl interpreter (e.g. when it is about to execute a new opcode) the flags are checked and the Perl level handler from %SIG is executed. The "deferred" scheme allows much more flexibility in the coding of signal handlers as we know the Perl interpreter is in a safe state, and that we are not in a system library function when the handler is called. However the implementation does differ from previous Perls in the following ways:</p> <dl> <dt id="Long-running-opcodes">
Long-running opcodes</dt> <dd> <p>As the Perl interpreter looks at signal flags only when it is about to execute a new opcode, a signal that arrives during a long-running opcode (e.g. a regular expression operation on a very large string) will not be seen until the current opcode completes.</p> <p>If a signal of any given type fires multiple times during an opcode (such as from a fine-grained timer), the handler for that signal will be called only once, after the opcode completes; all other instances will be discarded. Furthermore, if your system's signal queue gets flooded to the point that there are signals that have been raised but not yet caught (and thus not deferred) at the time an opcode completes, those signals may well be caught and deferred during subsequent opcodes, with sometimes surprising results. For example, you may see alarms delivered even after calling <code>alarm(0)</code> as the latter stops the raising of alarms but does not cancel the delivery of alarms raised but not yet caught. Do not depend on the behaviors described in this paragraph as they are side effects of the current implementation and may change in future versions of Perl.</p> </dd> <dt id="Interrupting-IO">
Interrupting IO</dt> <dd> <p>When a signal is delivered (e.g., SIGINT from a control-C) the operating system breaks into IO operations like <i>read</i>(2), which is used to implement Perl's readline() function, the <code>&lt;&gt;</code> operator. On older Perls the handler was called immediately (and as <code>read</code> is not "unsafe", this worked well). With the "deferred" scheme the handler is <i>not</i> called immediately, and if Perl is using the system's <code>stdio</code> library that library may restart the <code>read</code> without returning to Perl to give it a chance to call the %SIG handler. If this happens on your system the solution is to use the <code>:perlio</code> layer to do IO--at least on those handles that you want to be able to break into with signals. (The <code>:perlio</code> layer checks the signal flags and calls %SIG handlers before resuming IO operation.)</p> <p>The default in Perl 5.8.0 and later is to automatically use the <code>:perlio</code> layer.</p> <p>Note that it is not advisable to access a file handle within a signal handler where that signal has interrupted an I/O operation on that same handle. While perl will at least try hard not to crash, there are no guarantees of data integrity; for example, some data might get dropped or written twice.</p> <p>Some networking library functions like gethostbyname() are known to have their own implementations of timeouts which may conflict with your timeouts. If you have problems with such functions, try using the POSIX sigaction() function, which bypasses Perl safe signals. Be warned that this does subject you to possible memory corruption, as described above.</p> <p>Instead of setting <code>$SIG{ALRM}</code>:</p> <pre data-language="perl"><code>local $SIG{ALRM} = sub { die "alarm" };</code></pre> <p>try something like the following:</p> <pre data-language="perl"><code>use POSIX qw(SIGALRM);
POSIX::sigaction(SIGALRM,
                 POSIX::SigAction-&gt;new(sub { die "alarm" }))
         || die "Error setting SIGALRM handler: $!\n";</code></pre> <p>Another way to disable the safe signal behavior locally is to use the <code>Perl::Unsafe::Signals</code> module from CPAN, which affects all signals.</p> </dd> <dt id="Restartable-system-calls">
Restartable system calls</dt> <dd> <p>On systems that supported it, older versions of Perl used the SA_RESTART flag when installing %SIG handlers. This meant that restartable system calls would continue rather than returning when a signal arrived. In order to deliver deferred signals promptly, Perl 5.8.0 and later do <i>not</i> use SA_RESTART. Consequently, restartable system calls can fail (with $! set to <code>EINTR</code>) in places where they previously would have succeeded.</p> <p>The default <code>:perlio</code> layer retries <code>read</code>, <code>write</code> and <code>close</code> as described above; interrupted <code>wait</code> and <code>waitpid</code> calls will always be retried.</p> </dd> <dt id='Signals-as-"faults"'>
Signals as "faults"</dt> <dd> <p>Certain signals like SEGV, ILL, BUS and FPE are generated by virtual memory addressing errors and similar "faults". These are normally fatal: there is little a Perl-level handler can do with them. So Perl delivers them immediately rather than attempting to defer them.</p> <p>It is possible to catch these with a <code>%SIG</code> handler (see <a href="perlvar.html">perlvar</a>), but on top of the usual problems of "unsafe" signals the signal is likely to get rethrown immediately on return from the signal handler, so such a handler should <code>die</code> or <code>exit</code> instead.</p> </dd> <dt id="Signals-triggered-by-operating-system-state">
Signals triggered by operating system state</dt> <dd> <p>On some operating systems certain signal handlers are supposed to "do something" before returning. One example can be CHLD or CLD, which indicates a child process has completed. On some operating systems the signal handler is expected to <code>wait</code> for the completed child process. On such systems the deferred signal scheme will not work for those signals: it does not do the <code>wait</code>. Again the failure will look like a loop as the operating system will reissue the signal because there are completed child processes that have not yet been <code>wait</code>ed for.</p> </dd> </dl> <p>If you want the old signal behavior back despite possible memory corruption, set the environment variable <code>PERL_SIGNALS</code> to <code>"unsafe"</code>. This feature first appeared in Perl 5.8.1.</p> <h2 id="Named-Pipes">
Named Pipes</h2> <p>A named pipe (often referred to as a FIFO) is an old Unix IPC mechanism for processes communicating on the same machine. It works just like regular anonymous pipes, except that the processes rendezvous using a filename and need not be related.</p> <p>To create a named pipe, use the <code>POSIX::mkfifo()</code> function.</p> <pre data-language="perl"><code>use POSIX qw(mkfifo);
mkfifo($path, 0700)     ||  die "mkfifo $path failed: $!";</code></pre> <p>You can also use the Unix command mknod(1), or on some systems, mkfifo(1). These may not be in your normal path, though.</p> <pre data-language="perl"><code># system return val is backwards, so &amp;&amp; not ||
#
$ENV{PATH} .= ":/etc:/usr/etc";
if  (      system("mknod",  $path, "p")
        &amp;&amp; system("mkfifo", $path) )
{
    die "mk{nod,fifo} $path failed";
}</code></pre> <p>A fifo is convenient when you want to connect a process to an unrelated one. When you open a fifo, the program will block until there's something on the other end.</p> <p>For example, let's say you'd like to have your <i>.signature</i> file be a named pipe that has a Perl program on the other end. Now every time any program (like a mailer, news reader, finger program, etc.) tries to read from that file, the reading program will read the new signature from your program. We'll use the pipe-checking file-test operator, <b>-p</b>, to find out whether anyone (or anything) has accidentally removed our fifo.</p> <pre data-language="perl"><code>chdir();    # go home
my $FIFO = ".signature";

while (1) {
    unless (-p $FIFO) {
        unlink $FIFO;   # discard any failure, will catch later
        require POSIX;  # delayed loading of heavy module
        POSIX::mkfifo($FIFO, 0700)
                              || die "can't mkfifo $FIFO: $!";
    }

    # next line blocks till there's a reader
    open (my $fh, "&gt;", $FIFO) || die "can't open $FIFO: $!";
    print $fh "John Smith (smith\@host.org)\n", `fortune -s`;
    close($fh)                || die "can't close $FIFO: $!";
    sleep 2;                # to avoid dup signals
}</code></pre> <h2 id="Using-open()-for-IPC">
Using open() for IPC</h2> <p>Perl's basic open() statement can also be used for unidirectional interprocess communication by specifying the open mode as <code>|-</code> or <code>-|</code>. Here's how to start something up in a child process you intend to write to:</p> <pre data-language="perl"><code>open(my $spooler, "|-", "cat -v | lpr -h 2&gt;/dev/null")
                    || die "can't fork: $!";
local $SIG{PIPE} = sub { die "spooler pipe broke" };
print $spooler "stuff\n";
close $spooler      || die "bad spool: $! $?";</code></pre> <p>And here's how to start up a child process you intend to read from:</p> <pre data-language="perl"><code>open(my $status, "-|", "netstat -an 2&gt;&amp;1")
                    || die "can't fork: $!";
while (&lt;$status&gt;) {
    next if /^(tcp|udp)/;
    print;
}
close $status       || die "bad netstat: $! $?";</code></pre> <p>Be aware that these operations are full Unix forks, which means they may not be correctly implemented on all alien systems. See <a href="perlport.html#open">"open" in perlport</a> for portability details.</p> <p>In the two-argument form of open(), a pipe open can be achieved by either appending or prepending a pipe symbol to the second argument:</p> <pre data-language="perl"><code>open(my $spooler, "| cat -v | lpr -h 2&gt;/dev/null")
                    || die "can't fork: $!";
open(my $status, "netstat -an 2&gt;&amp;1 |")
                    || die "can't fork: $!";</code></pre> <p>This can be used even on systems that do not support forking, but this possibly allows code intended to read files to unexpectedly execute programs. If one can be sure that a particular program is a Perl script expecting filenames in @ARGV using the two-argument form of open() or the <code>&lt;&gt;</code> operator, the clever programmer can write something like this:</p> <pre data-language="perl"><code class="plaintext">% program f1 "cmd1|" - f2 "cmd2|" f3 &lt; tmpfile</code></pre> <p>and no matter which sort of shell it's called from, the Perl program will read from the file <i>f1</i>, the process <i>cmd1</i>, standard input (<i>tmpfile</i> in this case), the <i>f2</i> file, the <i>cmd2</i> command, and finally the <i>f3</i> file. Pretty nifty, eh?</p> <p>You might notice that you could use backticks for much the same effect as opening a pipe for reading:</p> <pre data-language="perl"><code>print grep { !/^(tcp|udp)/ } `netstat -an 2&gt;&amp;1`;
die "bad netstatus ($?)" if $?;</code></pre> <p>While this is true on the surface, it's much more efficient to process the file one line or record at a time because then you don't have to read the whole thing into memory at once. It also gives you finer control of the whole process, letting you kill off the child process early if you'd like.</p> <p>Be careful to check the return values from both open() and close(). If you're <i>writing</i> to a pipe, you should also trap SIGPIPE. Otherwise, think of what happens when you start up a pipe to a command that doesn't exist: the open() will in all likelihood succeed (it only reflects the fork()'s success), but then your output will fail--spectacularly. Perl can't know whether the command worked, because your command is actually running in a separate process whose exec() might have failed. Therefore, while readers of bogus commands return just a quick EOF, writers to bogus commands will get hit with a signal, which they'd best be prepared to handle. Consider:</p> <pre data-language="perl"><code>open(my $fh, "|-", "bogus") || die "can't fork: $!";
print $fh "bang\n";         #  neither necessary nor sufficient
                            #  to check print retval!
close($fh)                  || die "can't close: $!";</code></pre> <p>The reason for not checking the return value from print() is because of pipe buffering; physical writes are delayed. That won't blow up until the close, and it will blow up with a SIGPIPE. To catch it, you could use this:</p> <pre data-language="perl"><code>$SIG{PIPE} = "IGNORE";
open(my $fh, "|-", "bogus") || die "can't fork: $!";
print $fh "bang\n";
close($fh)                  || die "can't close: status=$?";</code></pre> <h3 id="Filehandles">Filehandles</h3> <p>Both the main process and any child processes it forks share the same STDIN, STDOUT, and STDERR filehandles. If both processes try to access them at once, strange things can happen. You may also want to close or reopen the filehandles for the child. You can get around this by opening your pipe with open(), but on some systems this means that the child process cannot outlive the parent.</p> <h3 id="Background-Processes">
Background Processes</h3> <p>You can run a command in the background with:</p> <pre data-language="perl"><code>system("cmd &amp;");</code></pre> <p>The command's STDOUT and STDERR (and possibly STDIN, depending on your shell) will be the same as the parent's. You won't need to catch SIGCHLD because of the double-fork taking place; see below for details.</p> <h3 id="Complete-Dissociation-of-Child-from-Parent">
Complete Dissociation of Child from Parent</h3> <p>In some cases (starting server processes, for instance) you'll want to completely dissociate the child process from the parent. This is often called daemonization. A well-behaved daemon will also chdir() to the root directory so it doesn't prevent unmounting the filesystem containing the directory from which it was launched, and redirect its standard file descriptors from and to <i>/dev/null</i> so that random output doesn't wind up on the user's terminal.</p> <pre data-language="perl"><code>use POSIX "setsid";

sub daemonize {
    chdir("/")                     || die "can't chdir to /: $!";
    open(STDIN,  "&lt;", "/dev/null") || die "can't read /dev/null: $!";
    open(STDOUT, "&gt;", "/dev/null") || die "can't write /dev/null: $!";
    defined(my $pid = fork())      || die "can't fork: $!";
    exit if $pid;              # non-zero now means I am the parent
    (setsid() != -1)           || die "Can't start a new session: $!";
    open(STDERR, "&gt;&amp;", STDOUT) || die "can't dup stdout: $!";
}</code></pre> <p>The fork() has to come before the setsid() to ensure you aren't a process group leader; the setsid() will fail if you are. If your system doesn't have the setsid() function, open <i>/dev/tty</i> and use the <code>TIOCNOTTY</code> ioctl() on it instead. See tty(4) for details.</p> <p>Non-Unix users should check their <code><i>Your_OS</i>::Process</code> module for other possible solutions.</p> <h3 id="Safe-Pipe-Opens">
Safe Pipe Opens</h3> <p>Another interesting approach to IPC is making your single program go multiprocess and communicate between--or even amongst--yourselves. The two-argument form of the open() function will accept a file argument of either <code>"-|"</code> or <code>"|-"</code> to do a very interesting thing: it forks a child connected to the filehandle you've opened. The child is running the same program as the parent. This is useful for safely opening a file when running under an assumed UID or GID, for example. If you open a pipe <i>to</i> minus, you can write to the filehandle you opened and your kid will find it in <i>his</i> STDIN. If you open a pipe <i>from</i> minus, you can read from the filehandle you opened whatever your kid writes to <i>his</i> STDOUT.</p> <pre data-language="perl"><code>my $PRECIOUS = "/path/to/some/safe/file";
my $sleep_count;
my $pid;
my $kid_to_write;

do {
    $pid = open($kid_to_write, "|-");
    unless (defined $pid) {
        warn "cannot fork: $!";
        die "bailing out" if $sleep_count++ &gt; 6;
        sleep 10;
    }
} until defined $pid;

if ($pid) {                 # I am the parent
    print $kid_to_write @some_data;
    close($kid_to_write)    || warn "kid exited $?";
} else {                    # I am the child
    # drop permissions in setuid and/or setgid programs:
    ($&gt;, $)) = ($&lt;, $();
    open (my $outfile, "&gt;", $PRECIOUS)
                            || die "can't open $PRECIOUS: $!";
    while (&lt;STDIN&gt;) {
        print $outfile;     # child STDIN is parent $kid_to_write
    }
    close($outfile)         || die "can't close $PRECIOUS: $!";
    exit(0);                # don't forget this!!
}</code></pre> <p>Another common use for this construct is when you need to execute something without the shell's interference. With system(), it's straightforward, but you can't use a pipe open or backticks safely. That's because there's no way to stop the shell from getting its hands on your arguments. Instead, use lower-level control to call exec() directly.</p> <p>Here's a safe backtick or pipe open for read:</p> <pre data-language="perl"><code>my $pid = open(my $kid_to_read, "-|");
defined($pid)            || die "can't fork: $!";

if ($pid) {             # parent
    while (&lt;$kid_to_read&gt;) {
                        # do something interesting
    }
    close($kid_to_read)  || warn "kid exited $?";

} else {                # child
    ($&gt;, $)) = ($&lt;, $(); # suid only
    exec($program, @options, @args)
                         || die "can't exec program: $!";
    # NOTREACHED
}</code></pre> <p>And here's a safe pipe open for writing:</p> <pre data-language="perl"><code>my $pid = open(my $kid_to_write, "|-");
defined($pid)            || die "can't fork: $!";

$SIG{PIPE} = sub { die "whoops, $program pipe broke" };

if ($pid) {             # parent
    print $kid_to_write @data;
    close($kid_to_write) || warn "kid exited $?";

} else {                # child
    ($&gt;, $)) = ($&lt;, $();
    exec($program, @options, @args)
                         || die "can't exec program: $!";
    # NOTREACHED
}</code></pre> <p>It is very easy to dead-lock a process using this form of open(), or indeed with any use of pipe() with multiple subprocesses. The example above is "safe" because it is simple and calls exec(). See <a href="#Avoiding-Pipe-Deadlocks">"Avoiding Pipe Deadlocks"</a> for general safety principles, but there are extra gotchas with Safe Pipe Opens.</p> <p>In particular, if you opened the pipe using <code>open $fh, "|-"</code>, then you cannot simply use close() in the parent process to close an unwanted writer. Consider this code:</p> <pre data-language="perl"><code>my $pid = open(my $writer, "|-");        # fork open a kid
defined($pid)               || die "first fork failed: $!";
if ($pid) {
    if (my $sub_pid = fork()) {
        defined($sub_pid)   || die "second fork failed: $!";
        close($writer)      || die "couldn't close writer: $!";
        # now do something else...
    }
    else {
        # first write to $writer
        # ...
        # then when finished
        close($writer)      || die "couldn't close writer: $!";
        exit(0);
    }
}
else {
    # first do something with STDIN, then
    exit(0);
}</code></pre> <p>In the example above, the true parent does not want to write to the $writer filehandle, so it closes it. However, because $writer was opened using <code>open $fh, "|-"</code>, it has a special behavior: closing it calls waitpid() (see <a href="perlfunc.html#waitpid">"waitpid" in perlfunc</a>), which waits for the subprocess to exit. If the child process ends up waiting for something happening in the section marked "do something else", you have deadlock.</p> <p>This can also be a problem with intermediate subprocesses in more complicated code, which will call waitpid() on all open filehandles during global destruction--in no predictable order.</p> <p>To solve this, you must manually use pipe(), fork(), and the form of open() which sets one file descriptor to another, as shown below:</p> <pre data-language="perl"><code>pipe(my $reader, my $writer)   || die "pipe failed: $!";
my $pid = fork();
defined($pid)                  || die "first fork failed: $!";
if ($pid) {
    close $reader;
    if (my $sub_pid = fork()) {
        defined($sub_pid)      || die "first fork failed: $!";
        close($writer)         || die "can't close writer: $!";
    }
    else {
        # write to $writer...
        # ...
        # then  when finished
        close($writer)         || die "can't close writer: $!";
        exit(0);
    }
    # write to $writer...
}
else {
    open(STDIN, "&lt;&amp;", $reader) || die "can't reopen STDIN: $!";
    close($writer)             || die "can't close writer: $!";
    # do something...
    exit(0);
}</code></pre> <p>Since Perl 5.8.0, you can also use the list form of <code>open</code> for pipes. This is preferred when you wish to avoid having the shell interpret metacharacters that may be in your command string.</p> <p>So for example, instead of using:</p> <pre data-language="perl"><code>open(my $ps_pipe, "-|", "ps aux") || die "can't open ps pipe: $!";</code></pre> <p>One would use either of these:</p> <pre data-language="perl"><code>open(my $ps_pipe, "-|", "ps", "aux")
                                  || die "can't open ps pipe: $!";

my @ps_args = qw[ ps aux ];
open(my $ps_pipe, "-|", @ps_args)
                                  || die "can't open @ps_args|: $!";</code></pre> <p>Because there are more than three arguments to open(), it forks the ps(1) command <i>without</i> spawning a shell, and reads its standard output via the <code>$ps_pipe</code> filehandle. The corresponding syntax to <i>write</i> to command pipes is to use <code>"|-"</code> in place of <code>"-|"</code>.</p> <p>This was admittedly a rather silly example, because you're using string literals whose content is perfectly safe. There is therefore no cause to resort to the harder-to-read, multi-argument form of pipe open(). However, whenever you cannot be assured that the program arguments are free of shell metacharacters, the fancier form of open() should be used. For example:</p> <pre data-language="perl"><code>my @grep_args = ("egrep", "-i", $some_pattern, @many_files);
open(my $grep_pipe, "-|", @grep_args)
                    || die "can't open @grep_args|: $!";</code></pre> <p>Here the multi-argument form of pipe open() is preferred because the pattern and indeed even the filenames themselves might hold metacharacters.</p> <h3 id="Avoiding-Pipe-Deadlocks">
Avoiding Pipe Deadlocks</h3> <p>Whenever you have more than one subprocess, you must be careful that each closes whichever half of any pipes created for interprocess communication it is not using. This is because any child process reading from the pipe and expecting an EOF will never receive it, and therefore never exit. A single process closing a pipe is not enough to close it; the last process with the pipe open must close it for it to read EOF.</p> <p>Certain built-in Unix features help prevent this most of the time. For instance, filehandles have a "close on exec" flag, which is set <i>en masse</i> under control of the <code>$^F</code> variable. This is so any filehandles you didn't explicitly route to the STDIN, STDOUT or STDERR of a child <i>program</i> will be automatically closed.</p> <p>Always explicitly and immediately call close() on the writable end of any pipe, unless that process is actually writing to it. Even if you don't explicitly call close(), Perl will still close() all filehandles during global destruction. As previously discussed, if those filehandles have been opened with Safe Pipe Open, this will result in calling waitpid(), which may again deadlock.</p> <h3 id="Bidirectional-Communication-with-Another-Process">
Bidirectional Communication with Another Process</h3> <p>While this works reasonably well for unidirectional communication, what about bidirectional communication? The most obvious approach doesn't work:</p> <pre data-language="perl"><code># THIS DOES NOT WORK!!
open(my $prog_for_reading_and_writing, "| some program |")</code></pre> <p>If you forget to <code>use warnings</code>, you'll miss out entirely on the helpful diagnostic message:</p> <pre data-language="perl"><code class="plaintext">Can't do bidirectional pipe at -e line 1.</code></pre> <p>If you really want to, you can use the standard open2() from the <a href="IPC::Open2.html">IPC::Open2</a> module to catch both ends. There's also an open3() in <a href="IPC::Open3.html">IPC::Open3</a> for tridirectional I/O so you can also catch your child's STDERR, but doing so would then require an awkward select() loop and wouldn't allow you to use normal Perl input operations.</p> <p>If you look at its source, you'll see that open2() uses low-level primitives like the pipe() and exec() syscalls to create all the connections. Although it might have been more efficient by using socketpair(), this would have been even less portable than it already is. The open2() and open3() functions are unlikely to work anywhere except on a Unix system, or at least one purporting POSIX compliance.</p> <p>Here's an example of using open2():</p> <pre data-language="perl"><code>use IPC::Open2;
my $pid = open2(my $reader, my $writer, "cat -un");
print $writer "stuff\n";
my $got = &lt;$reader&gt;;
waitpid $pid, 0;</code></pre> <p>The problem with this is that buffering is really going to ruin your day. Even though your <code>$writer</code> filehandle is auto-flushed so the process on the other end gets your data in a timely manner, you can't usually do anything to force that process to give its data to you in a similarly quick fashion. In this special case, we could actually so, because we gave <i>cat</i> a <b>-u</b> flag to make it unbuffered. But very few commands are designed to operate over pipes, so this seldom works unless you yourself wrote the program on the other end of the double-ended pipe.</p> <p>A solution to this is to use a library which uses pseudottys to make your program behave more reasonably. This way you don't have to have control over the source code of the program you're using. The <code>Expect</code> module from CPAN also addresses this kind of thing. This module requires two other modules from CPAN, <code>IO::Pty</code> and <code>IO::Stty</code>. It sets up a pseudo terminal to interact with programs that insist on talking to the terminal device driver. If your system is supported, this may be your best bet.</p> <h3 id="Bidirectional-Communication-with-Yourself">
Bidirectional Communication with Yourself</h3> <p>If you want, you may make low-level pipe() and fork() syscalls to stitch this together by hand. This example only talks to itself, but you could reopen the appropriate handles to STDIN and STDOUT and call other processes. (The following example lacks proper error checking.)</p> <pre data-language="perl"><code>#!/usr/bin/perl
# pipe1 - bidirectional communication using two pipe pairs
#         designed for the socketpair-challenged
use v5.36;
use IO::Handle;  # enable autoflush method before Perl 5.14
pipe(my $parent_rdr, my $child_wtr);  # XXX: check failure?
pipe(my $child_rdr,  my $parent_wtr); # XXX: check failure?
$child_wtr-&gt;autoflush(1);
$parent_wtr-&gt;autoflush(1);

if ($pid = fork()) {
    close $parent_rdr;
    close $parent_wtr;
    print $child_wtr "Parent Pid $$ is sending this\n";
    chomp(my $line = &lt;$child_rdr&gt;);
    print "Parent Pid $$ just read this: '$line'\n";
    close $child_rdr; close $child_wtr;
    waitpid($pid, 0);
} else {
    die "cannot fork: $!" unless defined $pid;
    close $child_rdr;
    close $child_wtr;
    chomp(my $line = &lt;$parent_rdr&gt;);
    print "Child Pid $$ just read this: '$line'\n";
    print $parent_wtr "Child Pid $$ is sending this\n";
    close $parent_rdr;
    close $parent_wtr;
    exit(0);
}</code></pre> <p>But you don't actually have to make two pipe calls. If you have the socketpair() system call, it will do this all for you.</p> <pre data-language="perl"><code>#!/usr/bin/perl
# pipe2 - bidirectional communication using socketpair
#   "the best ones always go both ways"

use v5.36;
use Socket;
use IO::Handle;  # enable autoflush method before Perl 5.14

# We say AF_UNIX because although *_LOCAL is the
# POSIX 1003.1g form of the constant, many machines
# still don't have it.
socketpair(my $child, my $parent, AF_UNIX, SOCK_STREAM, PF_UNSPEC)
                            ||  die "socketpair: $!";

$child-&gt;autoflush(1);
$parent-&gt;autoflush(1);

if ($pid = fork()) {
    close $parent;
    print $child "Parent Pid $$ is sending this\n";
    chomp(my $line = &lt;$child&gt;);
    print "Parent Pid $$ just read this: '$line'\n";
    close $child;
    waitpid($pid, 0);
} else {
    die "cannot fork: $!" unless defined $pid;
    close $child;
    chomp(my $line = &lt;$parent&gt;);
    print "Child Pid $$ just read this: '$line'\n";
    print $parent "Child Pid $$ is sending this\n";
    close $parent;
    exit(0);
}</code></pre> <h2 id="Sockets:-Client/Server-Communication">
Sockets: Client/Server Communication</h2> <p>While not entirely limited to Unix-derived operating systems (e.g., WinSock on PCs provides socket support, as do some VMS libraries), you might not have sockets on your system, in which case this section probably isn't going to do you much good. With sockets, you can do both virtual circuits like TCP streams and datagrams like UDP packets. You may be able to do even more depending on your system.</p> <p>The Perl functions for dealing with sockets have the same names as the corresponding system calls in C, but their arguments tend to differ for two reasons. First, Perl filehandles work differently than C file descriptors. Second, Perl already knows the length of its strings, so you don't need to pass that information.</p> <p>One of the major problems with ancient, antemillennial socket code in Perl was that it used hard-coded values for some of the constants, which severely hurt portability. If you ever see code that does anything like explicitly setting <code>$AF_INET = 2</code>, you know you're in for big trouble. An immeasurably superior approach is to use the <a href="socket.html">Socket</a> module, which more reliably grants access to the various constants and functions you'll need.</p> <p>If you're not writing a server/client for an existing protocol like NNTP or SMTP, you should give some thought to how your server will know when the client has finished talking, and vice-versa. Most protocols are based on one-line messages and responses (so one party knows the other has finished when a "\n" is received) or multi-line messages and responses that end with a period on an empty line ("\n.\n" terminates a message/response).</p> <h3 id="Internet-Line-Terminators">
Internet Line Terminators</h3> <p>The Internet line terminator is "\015\012". Under ASCII variants of Unix, that could usually be written as "\r\n", but under other systems, "\r\n" might at times be "\015\015\012", "\012\012\015", or something completely different. The standards specify writing "\015\012" to be conformant (be strict in what you provide), but they also recommend accepting a lone "\012" on input (be lenient in what you require). We haven't always been very good about that in the code in this manpage, but unless you're on a Mac from way back in its pre-Unix dark ages, you'll probably be ok.</p> <h3 id="Internet-TCP-Clients-and-Servers">
Internet TCP Clients and Servers</h3> <p>Use Internet-domain sockets when you want to do client-server communication that might extend to machines outside of your own system.</p> <p>Here's a sample TCP client using Internet-domain sockets:</p> <pre data-language="perl"><code>#!/usr/bin/perl
use v5.36;
use Socket;

my $remote  = shift || "localhost";
my $port    = shift || 2345;  # random port
if ($port =~ /\D/) { $port = getservbyname($port, "tcp") }
die "No port" unless $port;
my $iaddr   = inet_aton($remote)       || die "no host: $remote";
my $paddr   = sockaddr_in($port, $iaddr);

my $proto   = getprotobyname("tcp");
socket(my $sock, PF_INET, SOCK_STREAM, $proto)  || die "socket: $!";
connect($sock, $paddr)              || die "connect: $!";
while (my $line = &lt;$sock&gt;) {
    print $line;
}

close ($sock)                        || die "close: $!";
exit(0);</code></pre> <p>And here's a corresponding server to go along with it. We'll leave the address as <code>INADDR_ANY</code> so that the kernel can choose the appropriate interface on multihomed hosts. If you want sit on a particular interface (like the external side of a gateway or firewall machine), fill this in with your real address instead.</p> <pre data-language="perl"><code>#!/usr/bin/perl -T
use v5.36;
BEGIN { $ENV{PATH} = "/usr/bin:/bin" }
use Socket;
use Carp;
my $EOL = "\015\012";

sub logmsg { print "$0 $$: @_ at ", scalar localtime(), "\n" }

my $port  = shift || 2345;
die "invalid port" unless $port =~ /^ \d+ $/x;

my $proto = getprotobyname("tcp");

socket(my $server, PF_INET, SOCK_STREAM, $proto) || die "socket: $!";
setsockopt($server, SOL_SOCKET, SO_REUSEADDR, pack("l", 1))
                                              || die "setsockopt: $!";
bind($server, sockaddr_in($port, INADDR_ANY)) || die "bind: $!";
listen($server, SOMAXCONN)                    || die "listen: $!";

logmsg "server started on port $port";

for (my $paddr; $paddr = accept(my $client, $server); close $client) {
    my($port, $iaddr) = sockaddr_in($paddr);
    my $name = gethostbyaddr($iaddr, AF_INET);

    logmsg "connection from $name [",
            inet_ntoa($iaddr), "]
            at port $port";

    print $client "Hello there, $name, it's now ",
                    scalar localtime(), $EOL;
}</code></pre> <p>And here's a multitasking version. It's multitasked in that like most typical servers, it spawns (fork()s) a child server to handle the client request so that the master server can quickly go back to service a new client.</p> <pre data-language="perl"><code>#!/usr/bin/perl -T
use v5.36;
BEGIN { $ENV{PATH} = "/usr/bin:/bin" }
use Socket;
use Carp;
my $EOL = "\015\012";

sub spawn;  # forward declaration
sub logmsg { print "$0 $$: @_ at ", scalar localtime(), "\n" }

my $port  = shift || 2345;
die "invalid port" unless $port =~ /^ \d+ $/x;

my $proto = getprotobyname("tcp");

socket(my $server, PF_INET, SOCK_STREAM, $proto) || die "socket: $!";
setsockopt($server, SOL_SOCKET, SO_REUSEADDR, pack("l", 1))
                                              || die "setsockopt: $!";
bind($server, sockaddr_in($port, INADDR_ANY)) || die "bind: $!";
listen($server, SOMAXCONN)                    || die "listen: $!";

logmsg "server started on port $port";

my $waitedpid = 0;

use POSIX ":sys_wait_h";
use Errno;

sub REAPER {
    local $!;   # don't let waitpid() overwrite current error
    while ((my $pid = waitpid(-1, WNOHANG)) &gt; 0 &amp;&amp; WIFEXITED($?)) {
        logmsg "reaped $waitedpid" . ($? ? " with exit $?" : "");
    }
    $SIG{CHLD} = \&amp;REAPER;  # loathe SysV
}

$SIG{CHLD} = \&amp;REAPER;

while (1) {
    my $paddr = accept(my $client, $server) || do {
        # try again if accept() returned because got a signal
        next if $!{EINTR};
        die "accept: $!";
    };
    my ($port, $iaddr) = sockaddr_in($paddr);
    my $name = gethostbyaddr($iaddr, AF_INET);

    logmsg "connection from $name [",
           inet_ntoa($iaddr),
           "] at port $port";

    spawn $client, sub {
        $| = 1;
        print "Hello there, $name, it's now ",
              scalar localtime(),
              $EOL;
        exec "/usr/games/fortune"       # XXX: "wrong" line terminators
            or confess "can't exec fortune: $!";
    };
    close $client;
}

sub spawn {
    my $client = shift;
    my $coderef = shift;

    unless (@_ == 0 &amp;&amp; $coderef &amp;&amp; ref($coderef) eq "CODE") {
        confess "usage: spawn CLIENT CODEREF";
    }

    my $pid;
    unless (defined($pid = fork())) {
        logmsg "cannot fork: $!";
        return;
    }
    elsif ($pid) {
        logmsg "begat $pid";
        return; # I'm the parent
    }
    # else I'm the child -- go spawn

    open(STDIN,  "&lt;&amp;", $client)   || die "can't dup client to stdin";
    open(STDOUT, "&gt;&amp;", $client)   || die "can't dup client to stdout";
    ## open(STDERR, "&gt;&amp;", STDOUT) || die "can't dup stdout to stderr";
    exit($coderef-&gt;());
}</code></pre> <p>This server takes the trouble to clone off a child version via fork() for each incoming request. That way it can handle many requests at once, which you might not always want. Even if you don't fork(), the listen() will allow that many pending connections. Forking servers have to be particularly careful about cleaning up their dead children (called "zombies" in Unix parlance), because otherwise you'll quickly fill up your process table. The REAPER subroutine is used here to call waitpid() for any child processes that have finished, thereby ensuring that they terminate cleanly and don't join the ranks of the living dead.</p> <p>Within the while loop we call accept() and check to see if it returns a false value. This would normally indicate a system error needs to be reported. However, the introduction of safe signals (see <a href="#Deferred-Signals-%28Safe-Signals%29">"Deferred Signals (Safe Signals)"</a> above) in Perl 5.8.0 means that accept() might also be interrupted when the process receives a signal. This typically happens when one of the forked subprocesses exits and notifies the parent process with a CHLD signal.</p> <p>If accept() is interrupted by a signal, $! will be set to EINTR. If this happens, we can safely continue to the next iteration of the loop and another call to accept(). It is important that your signal handling code not modify the value of $!, or else this test will likely fail. In the REAPER subroutine we create a local version of $! before calling waitpid(). When waitpid() sets $! to ECHILD as it inevitably does when it has no more children waiting, it updates the local copy and leaves the original unchanged.</p> <p>You should use the <b>-T</b> flag to enable taint checking (see <a href="perlsec.html">perlsec</a>) even if we aren't running setuid or setgid. This is always a good idea for servers or any program run on behalf of someone else (like CGI scripts), because it lessens the chances that people from the outside will be able to compromise your system. Note that perl can be built without taint support. There are two different modes: in one, <b>-T</b> will silently do nothing. In the other mode <b>-T</b> results in a fatal error.</p> <p>Let's look at another TCP client. This one connects to the TCP "time" service on a number of different machines and shows how far their clocks differ from the system on which it's being run:</p> <pre data-language="perl"><code>#!/usr/bin/perl
use v5.36;
use Socket;

my $SECS_OF_70_YEARS = 2208988800;
sub ctime { scalar localtime(shift() || time()) }

my $iaddr = gethostbyname("localhost");
my $proto = getprotobyname("tcp");
my $port = getservbyname("time", "tcp");
my $paddr = sockaddr_in(0, $iaddr);

$| = 1;
printf "%-24s %8s %s\n", "localhost", 0, ctime();

foreach my $host (@ARGV) {
    printf "%-24s ", $host;
    my $hisiaddr = inet_aton($host)     || die "unknown host";
    my $hispaddr = sockaddr_in($port, $hisiaddr);
    socket(my $socket, PF_INET, SOCK_STREAM, $proto)
                                        || die "socket: $!";
    connect($socket, $hispaddr)         || die "connect: $!";
    my $rtime = pack("C4", ());
    read($socket, $rtime, 4);
    close($socket);
    my $histime = unpack("N", $rtime) - $SECS_OF_70_YEARS;
    printf "%8d %s\n", $histime - time(), ctime($histime);
}</code></pre> <h3 id="Unix-Domain-TCP-Clients-and-Servers">
Unix-Domain TCP Clients and Servers</h3> <p>That's fine for Internet-domain clients and servers, but what about local communications? While you can use the same setup, sometimes you don't want to. Unix-domain sockets are local to the current host, and are often used internally to implement pipes. Unlike Internet domain sockets, Unix domain sockets can show up in the file system with an ls(1) listing.</p> <pre data-language="perl"><code class="plaintext">% ls -l /dev/log
srw-rw-rw-  1 root            0 Oct 31 07:23 /dev/log</code></pre> <p>You can test for these with Perl's <b>-S</b> file test:</p> <pre data-language="perl"><code>unless (-S "/dev/log") {
    die "something's wicked with the log system";
}</code></pre> <p>Here's a sample Unix-domain client:</p> <pre data-language="perl"><code>#!/usr/bin/perl
use v5.36;
use Socket;

my $rendezvous = shift || "catsock";
socket(my $sock, PF_UNIX, SOCK_STREAM, 0) || die "socket: $!";
connect($sock, sockaddr_un($rendezvous))  || die "connect: $!";
while (defined(my $line = &lt;$sock&gt;)) {
    print $line;
}
exit(0);</code></pre> <p>And here's a corresponding server. You don't have to worry about silly network terminators here because Unix domain sockets are guaranteed to be on the localhost, and thus everything works right.</p> <pre data-language="perl"><code>#!/usr/bin/perl -T
use v5.36;
use Socket;
use Carp;

BEGIN { $ENV{PATH} = "/usr/bin:/bin" }
sub spawn;  # forward declaration
sub logmsg { print "$0 $$: @_ at ", scalar localtime(), "\n" }

my $NAME = "catsock";
my $uaddr = sockaddr_un($NAME);
my $proto = getprotobyname("tcp");

socket(my $server, PF_UNIX, SOCK_STREAM, 0) || die "socket: $!";
unlink($NAME);
bind  ($server, $uaddr)                     || die "bind: $!";
listen($server, SOMAXCONN)                  || die "listen: $!";

logmsg "server started on $NAME";

my $waitedpid;

use POSIX ":sys_wait_h";
sub REAPER {
    my $child;
    while (($waitedpid = waitpid(-1, WNOHANG)) &gt; 0) {
        logmsg "reaped $waitedpid" . ($? ? " with exit $?" : "");
    }
    $SIG{CHLD} = \&amp;REAPER;  # loathe SysV
}

$SIG{CHLD} = \&amp;REAPER;


for ( $waitedpid = 0;
      accept(my $client, $server) || $waitedpid;
      $waitedpid = 0, close $client)
{
    next if $waitedpid;
    logmsg "connection on $NAME";
    spawn $client, sub {
        print "Hello there, it's now ", scalar localtime(), "\n";
        exec("/usr/games/fortune")  || die "can't exec fortune: $!";
    };
}

sub spawn {
    my $client = shift();
    my $coderef = shift();

    unless (@_ == 0 &amp;&amp; $coderef &amp;&amp; ref($coderef) eq "CODE") {
        confess "usage: spawn CLIENT CODEREF";
    }

    my $pid;
    unless (defined($pid = fork())) {
        logmsg "cannot fork: $!";
        return;
    }
    elsif ($pid) {
        logmsg "begat $pid";
        return; # I'm the parent
    }
    else {
        # I'm the child -- go spawn
    }

    open(STDIN,  "&lt;&amp;", $client)
        || die "can't dup client to stdin";
    open(STDOUT, "&gt;&amp;", $client)
        || die "can't dup client to stdout";
    ## open(STDERR, "&gt;&amp;", STDOUT)
    ##  || die "can't dup stdout to stderr";
    exit($coderef-&gt;());
}</code></pre> <p>As you see, it's remarkably similar to the Internet domain TCP server, so much so, in fact, that we've omitted several duplicate functions--spawn(), logmsg(), ctime(), and REAPER()--which are the same as in the other server.</p> <p>So why would you ever want to use a Unix domain socket instead of a simpler named pipe? Because a named pipe doesn't give you sessions. You can't tell one process's data from another's. With socket programming, you get a separate session for each client; that's why accept() takes two arguments.</p> <p>For example, let's say that you have a long-running database server daemon that you want folks to be able to access from the Web, but only if they go through a CGI interface. You'd have a small, simple CGI program that does whatever checks and logging you feel like, and then acts as a Unix-domain client and connects to your private server.</p> <h2 id="TCP-Clients-with-IO::Socket">
TCP Clients with IO::Socket</h2> <p>For those preferring a higher-level interface to socket programming, the IO::Socket module provides an object-oriented approach. If for some reason you lack this module, you can just fetch IO::Socket from CPAN, where you'll also find modules providing easy interfaces to the following systems: DNS, FTP, Ident (RFC 931), NIS and NISPlus, NNTP, Ping, POP3, SMTP, SNMP, SSLeay, Telnet, and Time--to name just a few.</p> <h3 id="A-Simple-Client">
A Simple Client</h3> <p>Here's a client that creates a TCP connection to the "daytime" service at port 13 of the host name "localhost" and prints out everything that the server there cares to provide.</p> <pre data-language="perl"><code>#!/usr/bin/perl
use v5.36;
use IO::Socket;
my $remote = IO::Socket::INET-&gt;new(
                    Proto    =&gt; "tcp",
                    PeerAddr =&gt; "localhost",
                    PeerPort =&gt; "daytime(13)",
                )
             || die "can't connect to daytime service on localhost";
while (&lt;$remote&gt;) { print }</code></pre> <p>When you run this program, you should get something back that looks like this:</p> <pre data-language="perl"><code class="plaintext">Wed May 14 08:40:46 MDT 1997</code></pre> <p>Here are what those parameters to the new() constructor mean:</p> <dl> <dt id="Proto"><code>Proto</code></dt> <dd> <p>This is which protocol to use. In this case, the socket handle returned will be connected to a TCP socket, because we want a stream-oriented connection, that is, one that acts pretty much like a plain old file. Not all sockets are this of this type. For example, the UDP protocol can be used to make a datagram socket, used for message-passing.</p> </dd> <dt id="PeerAddr"><code>PeerAddr</code></dt> <dd> <p>This is the name or Internet address of the remote host the server is running on. We could have specified a longer name like <code>"www.perl.com"</code>, or an address like <code>"207.171.7.72"</code>. For demonstration purposes, we've used the special hostname <code>"localhost"</code>, which should always mean the current machine you're running on. The corresponding Internet address for localhost is <code>"127.0.0.1"</code>, if you'd rather use that.</p> </dd> <dt id="PeerPort"><code>PeerPort</code></dt> <dd> <p>This is the service name or port number we'd like to connect to. We could have gotten away with using just <code>"daytime"</code> on systems with a well-configured system services file,[FOOTNOTE: The system services file is found in <i>/etc/services</i> under Unixy systems.] but here we've specified the port number (13) in parentheses. Using just the number would have also worked, but numeric literals make careful programmers nervous.</p> </dd> </dl> <h3 id="A-Webget-Client">
A Webget Client</h3> <p>Here's a simple client that takes a remote host to fetch a document from, and then a list of files to get from that host. This is a more interesting client than the previous one because it first sends something to the server before fetching the server's response.</p> <pre data-language="perl"><code>#!/usr/bin/perl
use v5.36;
use IO::Socket;
unless (@ARGV &gt; 1) { die "usage: $0 host url ..." }
my $host = shift(@ARGV);
my $EOL = "\015\012";
my $BLANK = $EOL x 2;
for my $document (@ARGV) {
    my $remote = IO::Socket::INET-&gt;new( Proto     =&gt; "tcp",
                                        PeerAddr  =&gt; $host,
                                        PeerPort  =&gt; "http(80)",
              )     || die "cannot connect to httpd on $host";
    $remote-&gt;autoflush(1);
    print $remote "GET $document HTTP/1.0" . $BLANK;
    while ( &lt;$remote&gt; ) { print }
    close $remote;
}</code></pre> <p>The web server handling the HTTP service is assumed to be at its standard port, number 80. If the server you're trying to connect to is at a different port, like 1080 or 8080, you should specify it as the named-parameter pair, <code>PeerPort =&gt; 8080</code>. The <code>autoflush</code> method is used on the socket because otherwise the system would buffer up the output we sent it. (If you're on a prehistoric Mac, you'll also need to change every <code>"\n"</code> in your code that sends data over the network to be a <code>"\015\012"</code> instead.)</p> <p>Connecting to the server is only the first part of the process: once you have the connection, you have to use the server's language. Each server on the network has its own little command language that it expects as input. The string that we send to the server starting with "GET" is in HTTP syntax. In this case, we simply request each specified document. Yes, we really are making a new connection for each document, even though it's the same host. That's the way you always used to have to speak HTTP. Recent versions of web browsers may request that the remote server leave the connection open a little while, but the server doesn't have to honor such a request.</p> <p>Here's an example of running that program, which we'll call <i>webget</i>:</p> <pre data-language="perl"><code class="plaintext">% webget www.perl.com /guanaco.html
HTTP/1.1 404 File Not Found
Date: Thu, 08 May 1997 18:02:32 GMT
Server: Apache/1.2b6
Connection: close
Content-type: text/html

&lt;HEAD&gt;&lt;TITLE&gt;404 File Not Found&lt;/TITLE&gt;&lt;/HEAD&gt;
&lt;BODY&gt;&lt;H1&gt;File Not Found&lt;/H1&gt;
The requested URL /guanaco.html was not found on this server.&lt;P&gt;
&lt;/BODY&gt;</code></pre> <p>Ok, so that's not very interesting, because it didn't find that particular document. But a long response wouldn't have fit on this page.</p> <p>For a more featureful version of this program, you should look to the <i>lwp-request</i> program included with the LWP modules from CPAN.</p> <h3 id="Interactive-Client-with-IO::Socket">
Interactive Client with IO::Socket</h3> <p>Well, that's all fine if you want to send one command and get one answer, but what about setting up something fully interactive, somewhat like the way <i>telnet</i> works? That way you can type a line, get the answer, type a line, get the answer, etc.</p> <p>This client is more complicated than the two we've done so far, but if you're on a system that supports the powerful <code>fork</code> call, the solution isn't that rough. Once you've made the connection to whatever service you'd like to chat with, call <code>fork</code> to clone your process. Each of these two identical process has a very simple job to do: the parent copies everything from the socket to standard output, while the child simultaneously copies everything from standard input to the socket. To accomplish the same thing using just one process would be <i>much</i> harder, because it's easier to code two processes to do one thing than it is to code one process to do two things. (This keep-it-simple principle a cornerstones of the Unix philosophy, and good software engineering as well, which is probably why it's spread to other systems.)</p> <p>Here's the code:</p> <pre data-language="perl"><code>#!/usr/bin/perl
use v5.36;
use IO::Socket;

unless (@ARGV == 2) { die "usage: $0 host port" }
my ($host, $port) = @ARGV;

# create a tcp connection to the specified host and port
my $handle = IO::Socket::INET-&gt;new(Proto     =&gt; "tcp",
                                   PeerAddr  =&gt; $host,
                                   PeerPort  =&gt; $port)
           || die "can't connect to port $port on $host: $!";

$handle-&gt;autoflush(1);       # so output gets there right away
print STDERR "[Connected to $host:$port]\n";

# split the program into two processes, identical twins
die "can't fork: $!" unless defined(my $kidpid = fork());

# the if{} block runs only in the parent process
if ($kidpid) {
    # copy the socket to standard output
    while (defined (my $line = &lt;$handle&gt;)) {
        print STDOUT $line;
    }
    kill("TERM", $kidpid);   # send SIGTERM to child
}
# the else{} block runs only in the child process
else {
    # copy standard input to the socket
    while (defined (my $line = &lt;STDIN&gt;)) {
        print $handle $line;
    }
    exit(0);                # just in case
}</code></pre> <p>The <code>kill</code> function in the parent's <code>if</code> block is there to send a signal to our child process, currently running in the <code>else</code> block, as soon as the remote server has closed its end of the connection.</p> <p>If the remote server sends data a byte at time, and you need that data immediately without waiting for a newline (which might not happen), you may wish to replace the <code>while</code> loop in the parent with the following:</p> <pre data-language="perl"><code>my $byte;
while (sysread($handle, $byte, 1) == 1) {
    print STDOUT $byte;
}</code></pre> <p>Making a system call for each byte you want to read is not very efficient (to put it mildly) but is the simplest to explain and works reasonably well.</p> <h2 id="TCP-Servers-with-IO::Socket">
TCP Servers with IO::Socket</h2> <p>As always, setting up a server is little bit more involved than running a client. The model is that the server creates a special kind of socket that does nothing but listen on a particular port for incoming connections. It does this by calling the <code>IO::Socket::INET-&gt;new()</code> method with slightly different arguments than the client did.</p> <dl> <dt id="Proto1">Proto</dt> <dd> <p>This is which protocol to use. Like our clients, we'll still specify <code>"tcp"</code> here.</p> </dd> <dt id="LocalPort">LocalPort</dt> <dd> <p>We specify a local port in the <code>LocalPort</code> argument, which we didn't do for the client. This is service name or port number for which you want to be the server. (Under Unix, ports under 1024 are restricted to the superuser.) In our sample, we'll use port 9000, but you can use any port that's not currently in use on your system. If you try to use one already in used, you'll get an "Address already in use" message. Under Unix, the <code>netstat -a</code> command will show which services current have servers.</p> </dd> <dt id="Listen">Listen</dt> <dd> <p>The <code>Listen</code> parameter is set to the maximum number of pending connections we can accept until we turn away incoming clients. Think of it as a call-waiting queue for your telephone. The low-level Socket module has a special symbol for the system maximum, which is SOMAXCONN.</p> </dd> <dt id="Reuse">Reuse</dt> <dd> <p>The <code>Reuse</code> parameter is needed so that we restart our server manually without waiting a few minutes to allow system buffers to clear out.</p> </dd> </dl> <p>Once the generic server socket has been created using the parameters listed above, the server then waits for a new client to connect to it. The server blocks in the <code>accept</code> method, which eventually accepts a bidirectional connection from the remote client. (Make sure to autoflush this handle to circumvent buffering.)</p> <p>To add to user-friendliness, our server prompts the user for commands. Most servers don't do this. Because of the prompt without a newline, you'll have to use the <code>sysread</code> variant of the interactive client above.</p> <p>This server accepts one of five different commands, sending output back to the client. Unlike most network servers, this one handles only one incoming client at a time. Multitasking servers are covered in Chapter 16 of the Camel.</p> <p>Here's the code.</p> <pre data-language="perl"><code>#!/usr/bin/perl
use v5.36;
use IO::Socket;
use Net::hostent;      # for OOish version of gethostbyaddr

my $PORT = 9000;       # pick something not in use

my $server = IO::Socket::INET-&gt;new( Proto     =&gt; "tcp",
                                    LocalPort =&gt; $PORT,
                                    Listen    =&gt; SOMAXCONN,
                                    Reuse     =&gt; 1);

die "can't setup server" unless $server;
print "[Server $0 accepting clients]\n";

while (my $client = $server-&gt;accept()) {
  $client-&gt;autoflush(1);
  print $client "Welcome to $0; type help for command list.\n";
  my $hostinfo = gethostbyaddr($client-&gt;peeraddr);
  printf "[Connect from %s]\n",
         $hostinfo ? $hostinfo-&gt;name : $client-&gt;peerhost;
  print $client "Command? ";
  while ( &lt;$client&gt;) {
    next unless /\S/;     # blank line
    if    (/quit|exit/i)  { last                                      }
    elsif (/date|time/i)  { printf $client "%s\n", scalar localtime() }
    elsif (/who/i )       { print  $client `who 2&gt;&amp;1`                 }
    elsif (/cookie/i )    { print  $client `/usr/games/fortune 2&gt;&amp;1`  }
    elsif (/motd/i )      { print  $client `cat /etc/motd 2&gt;&amp;1`       }
    else {
      print $client "Commands: quit date who cookie motd\n";
    }
  } continue {
     print $client "Command? ";
  }
  close $client;
}</code></pre> <h2 id="UDP:-Message-Passing">
UDP: Message Passing</h2> <p>Another kind of client-server setup is one that uses not connections, but messages. UDP communications involve much lower overhead but also provide less reliability, as there are no promises that messages will arrive at all, let alone in order and unmangled. Still, UDP offers some advantages over TCP, including being able to "broadcast" or "multicast" to a whole bunch of destination hosts at once (usually on your local subnet). If you find yourself overly concerned about reliability and start building checks into your message system, then you probably should use just TCP to start with.</p> <p>UDP datagrams are <i>not</i> a bytestream and should not be treated as such. This makes using I/O mechanisms with internal buffering like stdio (i.e. print() and friends) especially cumbersome. Use syswrite(), or better send(), like in the example below.</p> <p>Here's a UDP program similar to the sample Internet TCP client given earlier. However, instead of checking one host at a time, the UDP version will check many of them asynchronously by simulating a multicast and then using select() to do a timed-out wait for I/O. To do something similar with TCP, you'd have to use a different socket handle for each host.</p> <pre data-language="perl"><code>#!/usr/bin/perl
use v5.36;
use Socket;
use Sys::Hostname;

my $SECS_OF_70_YEARS = 2_208_988_800;

my $iaddr = gethostbyname(hostname());
my $proto = getprotobyname("udp");
my $port = getservbyname("time", "udp");
my $paddr = sockaddr_in(0, $iaddr); # 0 means let kernel pick

socket(my $socket, PF_INET, SOCK_DGRAM, $proto) || die "socket: $!";
bind($socket, $paddr)                           || die "bind: $!";

$| = 1;
printf "%-12s %8s %s\n",  "localhost", 0, scalar localtime();
my $count = 0;
for my $host (@ARGV) {
    $count++;
    my $hisiaddr = inet_aton($host)         || die "unknown host";
    my $hispaddr = sockaddr_in($port, $hisiaddr);
    defined(send($socket, 0, 0, $hispaddr)) || die "send $host: $!";
}

my $rout = my $rin = "";
vec($rin, fileno($socket), 1) = 1;

# timeout after 10.0 seconds
while ($count &amp;&amp; select($rout = $rin, undef, undef, 10.0)) {
    my $rtime = "";
    my $hispaddr = recv($socket, $rtime, 4, 0) || die "recv: $!";
    my ($port, $hisiaddr) = sockaddr_in($hispaddr);
    my $host = gethostbyaddr($hisiaddr, AF_INET);
    my $histime = unpack("N", $rtime) - $SECS_OF_70_YEARS;
    printf "%-12s ", $host;
    printf "%8d %s\n", $histime - time(), scalar localtime($histime);
    $count--;
}</code></pre> <p>This example does not include any retries and may consequently fail to contact a reachable host. The most prominent reason for this is congestion of the queues on the sending host if the number of hosts to contact is sufficiently large.</p> <h2 id="SysV-IPC">
SysV IPC</h2> <p>While System V IPC isn't so widely used as sockets, it still has some interesting uses. However, you cannot use SysV IPC or Berkeley mmap() to have a variable shared amongst several processes. That's because Perl would reallocate your string when you weren't wanting it to. You might look into the <code>IPC::Shareable</code> or <code>threads::shared</code> modules for that.</p> <p>Here's a small example showing shared memory usage.</p> <pre data-language="perl"><code>use IPC::SysV qw(IPC_PRIVATE IPC_RMID S_IRUSR S_IWUSR);

my $size = 2000;
my $id = shmget(IPC_PRIVATE, $size, S_IRUSR | S_IWUSR);
defined($id)                    || die "shmget: $!";
print "shm key $id\n";

my $message = "Message #1";
shmwrite($id, $message, 0, 60)  || die "shmwrite: $!";
print "wrote: '$message'\n";
shmread($id, my $buff, 0, 60)      || die "shmread: $!";
print "read : '$buff'\n";

# the buffer of shmread is zero-character end-padded.
substr($buff, index($buff, "\0")) = "";
print "un" unless $buff eq $message;
print "swell\n";

print "deleting shm $id\n";
shmctl($id, IPC_RMID, 0)        || die "shmctl: $!";</code></pre> <p>Here's an example of a semaphore:</p> <pre data-language="perl"><code>use IPC::SysV qw(IPC_CREAT);

my $IPC_KEY = 1234;
my $id = semget($IPC_KEY, 10, 0666 | IPC_CREAT);
defined($id)                    || die "semget: $!";
print "sem id $id\n";</code></pre> <p>Put this code in a separate file to be run in more than one process. Call the file <i>take</i>:</p> <pre data-language="perl"><code># create a semaphore

my $IPC_KEY = 1234;
my $id = semget($IPC_KEY, 0, 0);
defined($id)                    || die "semget: $!";

my $semnum  = 0;
my $semflag = 0;

# "take" semaphore
# wait for semaphore to be zero
my $semop = 0;
my $opstring1 = pack("s!s!s!", $semnum, $semop, $semflag);

# Increment the semaphore count
$semop = 1;
my $opstring2 = pack("s!s!s!", $semnum, $semop,  $semflag);
my $opstring  = $opstring1 . $opstring2;

semop($id, $opstring)   || die "semop: $!";</code></pre> <p>Put this code in a separate file to be run in more than one process. Call this file <i>give</i>:</p> <pre data-language="perl"><code># "give" the semaphore
# run this in the original process and you will see
# that the second process continues

my $IPC_KEY = 1234;
my $id = semget($IPC_KEY, 0, 0);
die unless defined($id);

my $semnum  = 0;
my $semflag = 0;

# Decrement the semaphore count
my $semop = -1;
my $opstring = pack("s!s!s!", $semnum, $semop, $semflag);

semop($id, $opstring)   || die "semop: $!";</code></pre> <p>The SysV IPC code above was written long ago, and it's definitely clunky looking. For a more modern look, see the IPC::SysV module.</p> <p>A small example demonstrating SysV message queues:</p> <pre data-language="perl"><code>use IPC::SysV qw(IPC_PRIVATE IPC_RMID IPC_CREAT S_IRUSR S_IWUSR);

my $id = msgget(IPC_PRIVATE, IPC_CREAT | S_IRUSR | S_IWUSR);
defined($id)                || die "msgget failed: $!";

my $sent      = "message";
my $type_sent = 1234;

msgsnd($id, pack("l! a*", $type_sent, $sent), 0)
                            || die "msgsnd failed: $!";

msgrcv($id, my $rcvd_buf, 60, 0, 0)
                            || die "msgrcv failed: $!";

my($type_rcvd, $rcvd) = unpack("l! a*", $rcvd_buf);

if ($rcvd eq $sent) {
    print "okay\n";
} else {
    print "not okay\n";
}

msgctl($id, IPC_RMID, 0)    || die "msgctl failed: $!\n";</code></pre> <h2 id="NOTES">NOTES</h2> <p>Most of these routines quietly but politely return <code>undef</code> when they fail instead of causing your program to die right then and there due to an uncaught exception. (Actually, some of the new <i>Socket</i> conversion functions do croak() on bad arguments.) It is therefore essential to check return values from these functions. Always begin your socket programs this way for optimal success, and don't forget to add the <b>-T</b> taint-checking flag to the <code>#!</code> line for servers:</p> <pre data-language="perl"><code>#!/usr/bin/perl -T
use v5.36;
use sigtrap;
use Socket;</code></pre> <h2 id="BUGS">BUGS</h2> <p>These routines all create system-specific portability problems. As noted elsewhere, Perl is at the mercy of your C libraries for much of its system behavior. It's probably safest to assume broken SysV semantics for signals and to stick with simple TCP and UDP socket operations; e.g., don't try to pass open file descriptors over a local UDP datagram socket if you want your code to stand a chance of being portable.</p> <h2 id="AUTHOR">AUTHOR</h2> <p>Tom Christiansen, with occasional vestiges of Larry Wall's original version and suggestions from the Perl Porters.</p> <h2 id="SEE-ALSO">
SEE ALSO</h2> <p>There's a lot more to networking than this, but this should get you started.</p> <p>For intrepid programmers, the indispensable textbook is <i>Unix Network Programming, 2nd Edition, Volume 1</i> by W. Richard Stevens (published by Prentice-Hall). Most books on networking address the subject from the perspective of a C programmer; translation to Perl is left as an exercise for the reader.</p> <p>The IO::Socket(3) manpage describes the object library, and the Socket(3) manpage describes the low-level interface to sockets. Besides the obvious functions in <a href="perlfunc.html">perlfunc</a>, you should also check out the <i>modules</i> file at your nearest CPAN site, especially <a href="http://www.cpan.org/modules/00modlist.long.html#ID5_Networking_">http://www.cpan.org/modules/00modlist.long.html#ID5_Networking_</a>. See <a href="perlmodlib.html">perlmodlib</a> or best yet, the <i>Perl FAQ</i> for a description of what CPAN is and where to get it if the previous link doesn't work for you.</p> <p>Section 5 of CPAN's <i>modules</i> file is devoted to "Networking, Device Control (modems), and Interprocess Communication", and contains numerous unbundled modules numerous networking modules, Chat and Expect operations, CGI programming, DCE, FTP, IPC, NNTP, Proxy, Ptty, RPC, SNMP, SMTP, Telnet, Threads, and ToolTalk--to name just a few.</p><div class="_attribution">
  <p class="_attribution-p">
    &copy; 1993&ndash;2023 Larry Wall and others<br>Licensed under the GNU General Public License version 1 or later, or the Artistic License.<br>The Perl logo is a trademark of the Perl Foundation.<br>
    <a href="https://perldoc.perl.org/5.38.0/perlipc" class="_attribution-link">https://perldoc.perl.org/5.38.0/perlipc</a>
  </p>
</div>
