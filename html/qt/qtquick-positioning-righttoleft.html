<h1 class="title">Right-to-left User Interfaces</h1>   <h2 id="overview">Overview
</h2> <p>This chapter discusses different approaches and options available for implementing right-to-left language support for Qt Quick applications. Some common right-to-left languages include Arabic, Hebrew, Persian and Urdu. Most changes include making sure that text translated to right-to-left languages is properly aligned to the right, and horizontally ordered content in views, lists and grids flows correctly from the right to left.</p> <p>In right-to-left language speaking cultures, people naturally scan and read graphic elements and text from the right to left. The general rule of thumb is that content (like photos, videos and maps) is not mirrored, but positioning of the content (like application layouts and the flow of visual elements) is mirrored. For example, photos shown in chronological order should flow from right to left, the low end range of the horizontal sliders should be located at the right side of the slider, and text lines should be aligned to the right side of the available text area. The location of visual elements should not be mirrored when the position is related to a content; for example, when a position marker is shown to indicate a location on a map. Also, there are some special cases you may need to take into account where right-to-left language speakers are used to left-to-right positioning, for example when using number dialers in phones and media play, pause, rewind and forward buttons in music players.</p> <h2 id="text-alignment">Text Alignment
</h2> <p>(This applies to the <a href="qml-qtquick-text.html">Text</a>, <a href="qml-qtquick-textinput.html">TextInput</a> and <a href="qml-qtquick-textedit.html">TextEdit</a> types.)</p> <p>When the horizontal alignment of a text item is not explicitly set, the text element is automatically aligned to the natural reading direction of the text. By default left-to-right text like English is aligned to the left side of the text area, and right-to-left text like Arabic is aligned to the right side of the text area. The alignment of a text element with empty text takes its alignment cue from <a href="qinputmethod.html#inputDirection-prop">QInputMethod::inputDirection</a>(), which is based on the active system locale.</p> <p>This default locale-based alignment can be overridden by setting the <code>horizontalAlignment</code> property for the text element, or by enabling layout mirroring using the <a href="qml-qtquick-layoutmirroring.html">LayoutMirroring</a> attached property, which causes any explicit left and right horizontal alignments to be mirrored. Note that when <a href="qml-qtquick-layoutmirroring.html">LayoutMirroring</a> is set, the <code>horizontalAlignment</code> property value remains unchanged; the effective alignment of the text element that takes the mirroring into account can be read from the <code>effectiveHorizontalAlignment</code> property.</p> <pre data-language="qml">// automatically aligned to the left
Text {
    text: "Phone"
    width: 200
}

// automatically aligned to the right
Text {
    text: "خامل"
    width: 200
}

// aligned to the left
Text {
    text: "خامل"
    horizontalAlignment: Text.AlignLeft
    width: 200
}

// aligned to the right
Text {
    text: "خامل"
    horizontalAlignment: Text.AlignLeft
    LayoutMirroring.enabled: true
    width: 200
}</pre> <h2 id="layout-direction-of-positioners-and-views">Layout Direction of Positioners and Views
</h2> <p>(This applies to the <a href="qml-qtquick-row.html">Row</a>, <a href="qml-qtquick-grid.html">Grid</a>, <a href="qml-qtquick-flow.html">Flow</a>, <a href="qml-qtquick-listview.html">ListView</a> and <a href="qml-qtquick-gridview.html">GridView</a> types.)</p> <p>Types used for horizontal positioning and model views have the <code>layoutDirection</code> property for controlling the horizontal direction of the layouts. Setting <code>layoutDirection</code> to <code>Qt.RightToLeft</code> causes items to be laid out from the right to left. By default Qt Quick follows the left-to-right layout direction.</p> <p>The horizontal layout direction can also be reversed through the <a href="qml-qtquick-layoutmirroring.html">LayoutMirroring</a> attached property. This causes the effective <code>layoutDirection</code> of positioners and views to be mirrored. Note the actual value of the <code>layoutDirection</code> property will remain unchanged; the effective layout direction of positioners and views that takes the mirroring into account can be read from the <code>effectiveLayoutDirection</code> property.</p> <pre data-language="qml">// by default child items are positioned from left to right
Row {
    Child {}
    Child {}
}

// position child items from right to left
Row {
    layoutDirection: Qt.RightToLeft
    Child {}
    Child {}
}

// position child items from left to right
Row {
    LayoutMirroring.enabled: true
    layoutDirection: Qt.RightToLeft
    Child {}
    Child {}
}</pre> <h2 id="layout-mirroring">Layout Mirroring
</h2> <p>The attached property <a href="qml-qtquick-layoutmirroring.html">LayoutMirroring</a> is provided as a convenience for easily implementing right-to-left support for existing left-to-right Qt Quick applications. It mirrors the behavior of <a href="qtquick-positioning-anchors.html">Item anchors</a>, the layout direction of <a href="qtquick-positioning-layouts.html">positioners</a> and <a href="qtquick-modelviewsdata-modelview.html">model views</a>, and the explicit text alignment of QML text types.</p> <p>You can enable layout mirroring for a particular <a href="qml-qtquick-item.html">Item</a>:</p> <pre data-language="qml">Item {
    height: 50; width: 150

    LayoutMirroring.enabled: true
    anchors.left: parent.left   // anchor left becomes right

    Row {
        // items flow from left to right (as per default)
        Child {}
        Child {}
        Child {}
    }
}</pre> <p>Or set all child types to also inherit the layout direction:</p> <pre data-language="qml">Item {
    height: 50; width: 150

    LayoutMirroring.enabled: true
    LayoutMirroring.childrenInherit: true
    anchors.left: parent.left   // anchor left becomes right

    Row {
        // setting childrenInherit in the parent causes these
        // items to flow from right to left instead
        Child {}
        Child {}
        Child {}
    }
}</pre> <p>Applying mirroring in this manner does not change the actual value of the relevant anchor, <code>layoutDirection</code> or <code>horizontalAlignment</code> properties. The separate read-only property <code>effectiveLayoutDirection</code> can be used to query the effective layout direction of positioners and model views that takes the mirroring into account. Similarly the <a href="qml-qtquick-text.html">Text</a>, <a href="qml-qtquick-textinput.html">TextInput</a> and <a href="qml-qtquick-textedit.html">TextEdit</a> types have gained the read-only property <code>effectiveHorizontalAlignment</code> for querying the effective visual alignment of text. For anchors, the read only <a href="qml-qtquick-item.html#anchors.top-prop">anchors.mirrored</a> property reflects whether anchors have been mirrored.</p> <p>Note that application layouts and animations that are defined using <a href="qml-qtquick-item.html#x-prop">x</a> property values (as opposed to anchors or positioner types) are not affected by the <a href="qml-qtquick-layoutmirroring.html">LayoutMirroring</a> attached property. Therefore, adding right-to-left support to these types of layouts may require some code changes to your application, especially in views that rely on both the anchors and x coordinate-based positioning. Here is one way to use the <a href="qml-qtquick-layoutmirroring.html">LayoutMirroring</a> attached property to apply mirroring to an item that is positioned using <a href="qml-qtquick-item.html#x-prop">x</a> coordinates:</p> <pre data-language="qml">Rectangle {
    color: "black"
    height: 50; width: 50
    x: mirror(10)
    function mirror(value) {
        return LayoutMirroring.enabled ? (parent.width - width - value) : value;
    }
}</pre> <p>Not all layouts should necessarily be mirrored. There are cases where a visual type is positioned to the right side of the screen for improved one-handed use, because most people are right-handed, and not because of the reading direction. In the case that a child type should not be affected by mirroring, set the <a href="qml-qtquick-layoutmirroring.html#enabled-prop">LayoutMirroring.enabled</a> property for that type to false.</p> <p>Qt Quick is designed for developing animated, fluid user interfaces. When mirroring your application, remember to test that the animations and transitions continue to work as expected. If you do not have the resources to add right-to-left support for your application, it may be better to just keep the application layouts left aligned and just make sure that text is translated and aligned properly.</p> <h2 id="mirroring-icons">Mirroring Icons
</h2> <p>(This applies to <a href="qml-qtquick-image.html">Image</a>, <a href="qml-qtquick-borderimage.html">BorderImage</a> and <a href="qml-qtquick-animatedimage.html">AnimatedImage</a> types.)</p> <p>Most images do not need to be mirrored, but some directional icons, such as arrows, may need to be mirrored. The painting of these icons can be mirrored with a dedicated <code>mirror</code> property:</p> <pre data-language="qml">Image {
    source: "arrow.png"
    mirror: true
}</pre> <h2 id="default-layout-direction">Default Layout Direction
</h2> <p>The <a href="qml-qtqml-qt.html#application-prop">Qt.application.layoutDirection</a> property can be used to query the active layout direction of the application. It is based on <a href="qguiapplication.html#layoutDirection-prop">QGuiApplication::layoutDirection</a>(), which most commonly determines the layout direction from the active language translation file.</p> <p>To define the layout direction for a particular locale, declare the dedicated string literal <code>QT_LAYOUT_DIRECTION</code> in context <code>QGuiApplication</code> as either "LTR" or "RTL".</p> <p>You can do this by first introducing this line</p> <pre data-language="cpp">qsTr("QT_LAYOUT_DIRECTION","QGuiApplication");</pre> <p>somewhere in your QML source code and calling <code>lupdate</code> to generate the translation source file.</p> <pre data-language="cpp">lupdate myapp.qml -ts myapp.ts</pre> <p>This will append the following declaration to the translation file, where you can fill in either "LTR" or "RTL" as the translation for the locale.</p> <pre data-language="cpp">&lt;context&gt;
    &lt;name&gt;QGuiApplication&lt;/name&gt;
    &lt;message&gt;
        &lt;location filename="myapp.qml" line="33"/&gt;
        &lt;source&gt;QT_LAYOUT_DIRECTION&lt;/source&gt;
        &lt;translation type="unfinished"&gt;RTL&lt;/translation&gt;
    &lt;/message&gt;
&lt;/context&gt;</pre> <p>You can test that the layout direction works as expected by running your Qt Quick application with the compiled translation file:</p> <pre data-language="cpp">qml myapp.qml -translation myapp.qm</pre> <p>You can test your application in right-to-left layout direction by calling the static function <a href="qguiapplication.html#layoutDirection-prop">QGuiApplication::setLayoutDirection</a>():</p> <pre data-language="cpp">QGuiApplication app(argc, argv);
app.setLayoutDirection(Qt::RightToLeft);</pre><div class="_attribution">
  <p class="_attribution-p">
    &copy; The Qt Company Ltd<br>Licensed under the GNU Free Documentation License, Version 1.3.<br>
    <a href="https://doc.qt.io/qt-6.2/qtquick-positioning-righttoleft.html" class="_attribution-link">https://doc.qt.io/qt-6.2/qtquick-positioning-righttoleft.html</a>
  </p>
</div>
