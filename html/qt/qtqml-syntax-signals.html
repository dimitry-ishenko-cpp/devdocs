<h1 class="title">Signal and Handler Event System</h1>   <p>Application and user interface components need to communicate with each other. For example, a button needs to know that the user has clicked on it. The button may change colors to indicate its state or perform some logic. As well, application needs to know whether the user is clicking the button. The application may need to relay this clicking event to other applications.</p> <p>QML has a signal and handler mechanism, where the <i>signal</i> is the event and the signal is responded to through a <i>signal handler</i>. When a signal is emitted, the corresponding signal handler is invoked. Placing logic such as a script or other operations in the handler allows the component to respond to the event.</p> <h2 id="receiving-signals-with-signal-handlers">Receiving signals with signal handlers
</h2> <p>To receive a notification when a particular signal is emitted for a particular object, the object definition should declare a signal handler named <i>on&lt;Signal&gt;</i>, where <i>&lt;Signal&gt;</i> is the name of the signal, with the first letter capitalized. The signal handler should contain the JavaScript code to be executed when the signal handler is invoked.</p> <p>For example, the <a href="qml-qtquick-controls2-button.html">Button</a> type from the <a href="qtquickcontrols-index.html">Qt Quick Controls</a> module has a <code>clicked</code> signal, which is emitted whenever the button is clicked. In this case, the signal handler for receiving this signal should be <code>onClicked</code>. In the example below, whenever the button is clicked, the <code>onClicked</code> handler is invoked, applying a random color to the parent <a href="qml-qtquick-rectangle.html">Rectangle</a>:</p> <pre data-language="qml">import QtQuick
import QtQuick.Controls

Rectangle {
    id: rect
    width: 250; height: 250

    Button {
        anchors.bottom: parent.bottom
        anchors.horizontalCenter: parent.horizontalCenter
        text: "Change color!"
        onClicked: {
            rect.color = Qt.rgba(Math.random(), Math.random(), Math.random(), 1);
        }
    }
}</pre> <h3 id="property-change-signal-handlers">Property change signal handlers
</h3> <p>A signal is automatically emitted when the value of a QML property changes. This type of signal is a <i>property change signal</i> and signal handlers for these signals are written in the form <i>on&lt;Property&gt;Changed</i>, where <i>&lt;Property&gt;</i> is the name of the property, with the first letter capitalized.</p> <p>For example, the <a href="qml-qtquick-mousearea.html">MouseArea</a> type has a <a href="qml-qtquick-mousearea.html#pressed-signal">pressed</a> property. To receive a notification whenever this property changes, write a signal handler named <code>onPressedChanged</code>:</p> <pre data-language="qml">import QtQuick

Rectangle {
    id: rect
    width: 100; height: 100

    TapHandler {
        onPressedChanged: console.log("taphandler pressed?", pressed)
    }
}</pre> <p>Even though the <a href="qml-qtquick-taphandler.html">TapHandler</a> documentation does not document a signal handler named <code>onPressedChanged</code>, the signal is implicitly provided by the fact that the <code>pressed</code> property exists.</p> <h3 id="signal-parameters">Signal parameters
</h3> <p>Signals might have parameters. To access those, you should assign a function to the handler. Both arrow functions and anonymous functions work.</p> <p>For the following examples, consider a Status component with an errorOccurred signal (see <a href="qtqml-syntax-signals.html#adding-signals-to-custom-qml-types">Adding signals to custom QML types</a> for more information about how signals can be added to QML components).</p> <pre data-language="qml">// Status.qml
import QtQuick

Item {
    id: myitem
    signal errorOccurred(message: string, line: int, column: int)
}</pre> <pre data-language="qml">Status {
    onErrorOccurred: (mgs, line, col) =&gt; console.log(`${line}:${col}: ${msg}`)
}</pre> <div class="admonition note"> <p><b>Note: </b>The names of the formal parameters in the function do not have to match those in the signal.</p> </div> <p>If you do not need to handle all parameters, it is possible to omit trailing ones:</p> <pre data-language="qml">Status {
    onErrorOccurred: function (message) { console.log(message) }
}</pre> <p>It is not possible to leave out leading parameters you are interested in, however you can use some placeholder name to indicate to readers that they are not important:</p> <pre data-language="qml">Status {
    onErrorOccurred: (_, _, col) =&gt; console.log(`Error happened at column ${col}`)
}</pre> <div class="admonition note"> <p><b>Note: </b>Instead of using a function, it is possible, but discouraged, to use a plain code block. In that case all signal parameters get injected into the scope of the block. However, this can make code difficult to read as it's unclear where the parameters come from, and results in slower lookups in the QML engine. Injecting parameters in this way is deprecated, and will cause runtime warnings if the parameter is actually used.</p> </div> <h3 id="using-the-connections-type">Using the Connections type
</h3> <p>In some cases it may be desirable to access a signal outside of the object that emits it. For these purposes, the <code>QtQuick</code> module provides the <a href="qml-qtqml-connections.html">Connections</a> type for connecting to signals of arbitrary objects. A <a href="qml-qtqml-connections.html">Connections</a> object can receive any signal from its specified <a href="qml-qtqml-connections.html#target-prop">target</a>.</p> <p>For example, the <code>onClicked</code> handler in the earlier example could have been received by the root <a href="qml-qtquick-rectangle.html">Rectangle</a> instead, by placing the <code>onClicked</code> handler in a <a href="qml-qtqml-connections.html">Connections</a> object that has its <a href="qml-qtqml-connections.html#target-prop">target</a> set to the <code>button</code>:</p> <pre data-language="qml">import QtQuick
import QtQuick.Controls

Rectangle {
    id: rect
    width: 250; height: 250

    Button {
        id: button
        anchors.bottom: parent.bottom
        anchors.horizontalCenter: parent.horizontalCenter
        text: "Change color!"
    }

    Connections {
        target: button
        function onClicked() {
            rect.color = Qt.rgba(Math.random(), Math.random(), Math.random(), 1);
        }
    }
}</pre> <h3 id="attached-signal-handlers">Attached signal handlers
</h3> <p>An <a href="qtqml-syntax-objectattributes.html#attached-properties-and-attached-signal-handlers">attached signal handler</a> receives a signal from an <i>attaching type</i> rather than the object within which the handler is declared.</p> <p>For example, <a href="qml-qtqml-component.html#completed-signal">Component.onCompleted</a> is an attached signal handler. It is often used to execute some JavaScript code when its creation process is complete. Here is an example:</p> <pre data-language="qml">import QtQuick

Rectangle {
    width: 200; height: 200
    color: Qt.rgba(Qt.random(), Qt.random(), Qt.random(), 1)

    Component.onCompleted: {
        console.log("The rectangle's color is", color)
    }
}</pre> <p>The <code>onCompleted</code> handler is not responding to a <code>completed</code> signal from the <a href="qml-qtquick-rectangle.html">Rectangle</a> type. Instead, an object of the <code>Component</code> <i>attaching type</i> with a <code>completed</code> signal has automatically been <i>attached</i> to the <a href="qml-qtquick-rectangle.html">Rectangle</a> object by the QML engine. The engine emits this signal when the Rectangle object is created, thus triggering the <code>Component.onCompleted</code> signal handler.</p> <p>Attached signal handlers allow objects to be notified of particular signals that are significant to each individual object. If there was no <code>Component.onCompleted</code> attached signal handler, for example, an object could not receive this notification without registering for some special signal from some special object. The <i>attached signal handler</i> mechanism enables objects to receive particular signals without extra code.</p> <p>See <a href="qtqml-syntax-objectattributes.html#attached-properties-and-attached-signal-handlers">Attached properties and attached signal handlers</a> for more information on attached signal handlers.</p> <h2 id="adding-signals-to-custom-qml-types">Adding signals to custom QML types
</h2> <p>Signals can be added to custom QML types through the <code>signal</code> keyword.</p> <p>The syntax for defining a new signal is:</p> <p><code>signal &lt;name&gt;[([&lt;type&gt; &lt;parameter name&gt;[, ...]])]</code></p> <p>A signal is emitted by invoking the signal as a method.</p> <p>For example, the code below is defined in a file named <code>SquareButton.qml</code>. The root <a href="qml-qtquick-rectangle.html">Rectangle</a> object has an <code>activated</code> signal, which is emitted whenever the child <a href="qml-qtquick-taphandler.html">TapHandler</a> is <code>tapped</code>. In this particular example the activated signal is emitted with the x and y coordinates of the mouse click:</p> <pre data-language="qml">// SquareButton.qml
import QtQuick

Rectangle {
    id: root

    signal activated(real xPosition, real yPosition)
    property point mouseXY
    property int side: 100
    width: side; height: side

    TapHandler {
        id: handler
        onTapped: root.activated(root.mouseXY.x, root.mouseXY.y)
        onPressedChanged: root.mouseXY = handler.point.position
    }
}</pre> <p>Now any objects of the <code>SquareButton</code> can connect to the <code>activated</code> signal using an <code>onActivated</code> signal handler:</p> <pre data-language="qml">// myapplication.qml
SquareButton {
    onActivated: (xPosition, yPosition)=&gt; console.log("Activated at " + xPosition + "," + yPosition)
}</pre> <p>See <a href="qtqml-syntax-objectattributes.html#signal-attributes">Signal Attributes</a> for more details on writing signals for custom QML types.</p> <h2 id="connecting-signals-to-methods-and-signals">Connecting signals to methods and signals
</h2> <p>Signal objects have a <code>connect()</code> method to a connect a signal either to a method or another signal. When a signal is connected to a method, the method is automatically invoked whenever the signal is emitted. This mechanism enables a signal to be received by a method instead of a signal handler.</p> <p>Below, the <code>messageReceived</code> signal is connected to three methods using the <code>connect()</code> method:</p> <pre data-language="qml">import QtQuick

Rectangle {
    id: relay

    signal messageReceived(string person, string notice)

    Component.onCompleted: {
        relay.messageReceived.connect(sendToPost)
        relay.messageReceived.connect(sendToTelegraph)
        relay.messageReceived.connect(sendToEmail)
        relay.messageReceived("Tom", "Happy Birthday")
    }

    function sendToPost(person, notice) {
        console.log("Sending to post: " + person + ", " + notice)
    }
    function sendToTelegraph(person, notice) {
        console.log("Sending to telegraph: " + person + ", " + notice)
    }
    function sendToEmail(person, notice) {
        console.log("Sending to email: " + person + ", " + notice)
    }
}</pre> <p>In many cases it is sufficient to receive signals through signal handlers rather than using the connect() function. However, using the <code>connect</code> method allows a signal to be received by multiple methods as shown earlier, which would not be possible with signal handlers as they must be uniquely named. Also, the <code>connect</code> method is useful when connecting signals to <a href="qtqml-javascript-dynamicobjectcreation.html">dynamically created objects</a>.</p> <p>There is a corresponding <code>disconnect()</code> method for removing connected signals:</p> <pre data-language="qml">Rectangle {
    id: relay
    //...

    function removeTelegraphSignal() {
        relay.messageReceived.disconnect(sendToTelegraph)
    }
}</pre> <h4 id="signal-to-signal-connect">Signal to signal connect
</h4> <p>By connecting signals to other signals, the <code>connect()</code> method can form different signal chains.</p> <pre data-language="qml">import QtQuick

Rectangle {
    id: forwarder
    width: 100; height: 100

    signal send()
    onSend: console.log("Send clicked")

    TapHandler {
        id: mousearea
        anchors.fill: parent
        onTapped: console.log("Mouse clicked")
    }

    Component.onCompleted: {
        mousearea.tapped.connect(send)
    }
}</pre> <p>Whenever the <a href="qml-qtquick-taphandler.html">TapHandler</a>'s <code>tapped</code> signal is emitted, the <code>send</code> signal will automatically be emitted as well.</p> <pre data-language="cpp">output:
    MouseArea clicked
    Send clicked</pre><div class="_attribution">
  <p class="_attribution-p">
    &copy; The Qt Company Ltd<br>Licensed under the GNU Free Documentation License, Version 1.3.<br>
    <a href="https://doc.qt.io/qt-6.2/qtqml-syntax-signals.html" class="_attribution-link">https://doc.qt.io/qt-6.2/qtqml-syntax-signals.html</a>
  </p>
</div>
