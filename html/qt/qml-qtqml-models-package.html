<h1 class="title">Package QML Type</h1>  <p>Specifies a collection of named items. <a href="#details">More...</a></p>  <table class="alignedsummary"> <tr>
<td class="memItemLeft rightAlign topAlign"> Import Statement:</td>
<td class="memItemRight bottomAlign"> import QtQml.Models</td>
</tr>
</table>
<ul> <li><a href="https://doc.qt.io/qt-6.2/qml-qtqml-models-package-members.html">List of all members, including inherited members</a></li> </ul> <h2 id="attached-properties">Attached Properties
</h2> <ul> <li class="fn">
<b><a href="qml-qtqml-models-package.html#name-attached-prop">name</a></b> : string</li> </ul>  <h2 id="details">Detailed Description
</h2> <p>The Package type is used in conjunction with <a href="qml-qtqml-models-delegatemodel.html">DelegateModel</a> to enable delegates with a shared context to be provided to multiple views.</p> <p>Any item within a Package may be assigned a name via the <a href="qml-qtqml-models-package.html#name-attached-prop">Package.name</a> attached property.</p> <p>The example below creates a Package containing two named items; <i>list</i> and <i>grid</i>. The third item in the package (the <a href="qml-qtquick-rectangle.html">Rectangle</a>) is parented to whichever delegate it should appear in. This allows an item to move between views.</p> <pre data-language="qml">Package {
    Text { id: listDelegate; width: parent.width; height: 25; text: 'Empty'; Package.name: 'list' }
    Text { id: gridDelegate; width: parent.width / 2; height: 50; text: 'Empty'; Package.name: 'grid' }

    Rectangle {
        id: wrapper
        width: parent.width; height: 25
        color: 'lightsteelblue'

        Text { text: display; anchors.centerIn: parent }
        state: root.upTo &gt; index ? 'inGrid' : 'inList'
        states: [
            State {
                name: 'inList'
                ParentChange { target: wrapper; parent: listDelegate }
            },
            State {
                name: 'inGrid'
                ParentChange {
                    target: wrapper; parent: gridDelegate
                    x: 0; y: 0; width: gridDelegate.width; height: gridDelegate.height
                }
            }
        ]

        transitions: [
            Transition {
                ParentAnimation {
                    NumberAnimation { properties: 'x,y,width,height'; duration: 300 }
                }
            }
        ]
    }
}</pre> <p>These named items are used as the delegates by the two views who reference the special <a href="qml-qtqml-models-delegatemodel.html#parts-prop">DelegateModel::parts</a> property to select a model which provides the chosen delegate.</p> <pre data-language="qml">DelegateModel {
    id: visualModel
    delegate: Delegate {}
    model: myModel
}

ListView {
    id: lv
    height: parent.height/2
    width: parent.width

    model: visualModel.parts.list
}
GridView {
    y: parent.height/2
    height: parent.height/2
    width: parent.width
    cellWidth: width / 2
    cellHeight: 50
    model: visualModel.parts.grid
}</pre> <div class="admonition note"> <p><b>Note: </b>Package is part of <a href="https://doc.qt.io/qt-6.2/qtqml-module.html">QtQml</a>.Models since version 2.14 and part of <a href="https://doc.qt.io/qt-6.2/qtquick-module.html">QtQuick</a> since version 2.0. Importing Package via <a href="https://doc.qt.io/qt-6.2/qtquick-module.html">QtQuick</a> is deprecated since Qt 5.14.</p> </div> <p><b>See also </b><a href="https://doc.qt.io/qt-6.2/qtquick-views-example.html">Qt Quick Examples - Views</a>, <a href="https://doc.qt.io/qt-6.2/qtdoc-demos-photoviewer-example.html">Qt Quick Demo - Photo Viewer</a>, and <a href="qtqml-index.html">Qt QML</a>.</p>  <h2>Attached Property Documentation</h2>  <h3 class="qmlproto" id="name-attached-prop"><p> <span class="name">Package.name</span> : <span class="type"><a href="qml-string.html">string</a></span></p></h3> <p>This attached property holds the name of an item within a Package.</p><div class="_attribution">
  <p class="_attribution-p">
    &copy; The Qt Company Ltd<br>Licensed under the GNU Free Documentation License, Version 1.3.<br>
    <a href="https://doc.qt.io/qt-6.2/qml-qtqml-models-package.html" class="_attribution-link">https://doc.qt.io/qt-6.2/qml-qtqml-models-package.html</a>
  </p>
</div>
