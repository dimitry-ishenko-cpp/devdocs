<h1 class="title">Dynamic QML Object Creation from JavaScript</h1>   <p>QML supports the dynamic creation of objects from within JavaScript. This is useful to delay instantiation of objects until necessary, thereby improving application startup time. It also allows visual objects to be dynamically created and added to the scene in reaction to user input or other events.</p> <p>See the <a href="https://doc.qt.io/qt-6.2/qtqml-dynamicscene-example.html">Dynamic Scene example</a> for a demonstration of the concepts discussed on this page.</p> <h2 id="creating-objects-dynamically">Creating Objects Dynamically
</h2> <p>There are two ways to create objects dynamically from JavaScript. You can either call <a href="qml-qtqml-qt.html#createComponent-method">Qt.createComponent</a>() to dynamically create a <a href="qml-qtqml-component.html">Component</a> object, or use <a href="qml-qtqml-qt.html#createQmlObject-method">Qt.createQmlObject</a>() to create an object from a string of QML. Creating a component is better if you have an existing component defined in a QML document and you want to dynamically create instances of that component. Otherwise, creating an object from a string of QML is useful when the object QML itself is generated at runtime.</p> <h3 id="creating-a-component-dynamically">Creating a Component Dynamically
</h3> <p>To dynamically load a component defined in a QML file, call the <a href="qml-qtqml-qt.html#createComponent-method">Qt.createComponent</a>() function in the <a href="qml-qtqml-qt.html">Qt object</a>. This function takes the URL of the QML file as its only argument and creates a <a href="qml-qtqml-component.html">Component</a> object from this URL.</p> <p>Once you have a <a href="qml-qtqml-component.html">Component</a>, you can call its <a href="qml-qtqml-component.html#createObject-method">createObject</a>() method to create an instance of the component. This function can take one or two arguments:</p> <ul> <li>The first is the parent for the new object. The parent can be a graphical object (i.e. of the <a href="qml-qtquick-item.html">Item</a> type) or non-graphical object (i.e. of the <a href="qml-qtqml-qtobject.html">QtObject</a> or C++ <a href="qobject.html">QObject</a> type). Only graphical objects with graphical parent objects will be rendered to the <a href="qtquick-index.html">Qt Quick</a> visual canvas. If you wish to set the parent later you can safely pass <code>null</code> to this function.</li> <li>The second is optional and is a map of property-value pairs that define initial any property values for the object. Property values specified by this argument are applied to the object before its creation is finalized, avoiding binding errors that may occur if particular properties must be initialized to enable other property bindings. Additionally, there are small performance benefits when compared to defining property values and bindings after the object is created.</li> </ul> <p>Here is an example. First there is <code>Sprite.qml</code>, which defines a simple QML component:</p> <pre data-language="qml">import QtQuick 2.0

Rectangle { width: 80; height: 50; color: "red" }</pre> <p>Our main application file, <code>main.qml</code>, imports a <code>componentCreation.js</code> JavaScript file that will create <code>Sprite</code> objects:</p> <pre data-language="qml">import QtQuick 2.0
import "componentCreation.js" as MyScript

Rectangle {
    id: appWindow
    width: 300; height: 300

    Component.onCompleted: MyScript.createSpriteObjects();
}</pre> <p>Here is <code>componentCreation.js</code>. Notice it checks whether the component <a href="qml-qtqml-component.html#status-prop">status</a> is <code>Component.Ready</code> before calling <a href="qml-qtqml-component.html#createObject-method">createObject</a>() in case the QML file is loaded over a network and thus is not ready immediately.</p> <pre>var component;
var sprite;

function createSpriteObjects() {
    component = Qt.createComponent("Sprite.qml");
    if (component.status == Component.Ready)
        finishCreation();
    else
        component.statusChanged.connect(finishCreation);
}

function finishCreation() {
    if (component.status == Component.Ready) {
        sprite = component.createObject(appWindow, {x: 100, y: 100});
        if (sprite == null) {
            // Error Handling
            console.log("Error creating object");
        }
    } else if (component.status == Component.Error) {
        // Error Handling
        console.log("Error loading component:", component.errorString());
    }
}</pre> <p>If you are certain the QML file to be loaded is a local file, you could omit the <code>finishCreation()</code> function and call <a href="qml-qtqml-component.html#createObject-method">createObject</a>() immediately:</p> <pre>function createSpriteObjects() {
    component = Qt.createComponent("Sprite.qml");
    sprite = component.createObject(appWindow, {x: 100, y: 100});

    if (sprite == null) {
        // Error Handling
        console.log("Error creating object");
    }
}</pre> <p>Notice in both instances, <a href="qml-qtqml-component.html#createObject-method">createObject</a>() is called with <code>appWindow</code> passed as the parent argument, since the dynamically created object is a visual (Qt Quick) object. The created object will become a child of the <code>appWindow</code> object in <code>main.qml</code>, and appear in the scene.</p> <p>When using files with relative paths, the path should be relative to the file where <a href="qml-qtqml-qt.html#createComponent-method">Qt.createComponent</a>() is executed.</p> <p>To connect signals to (or receive signals from) dynamically created objects, use the signal <code>connect()</code> method. See <a href="qtqml-syntax-signals.html#connecting-signals-to-methods-and-signals">Connecting Signals to Methods and Signals</a> for more information.</p> <p>It is also possible to instantiate components without blocking via the <a href="qml-qtqml-component.html#incubateObject-method">incubateObject</a>() function.</p> <h3 id="creating-an-object-from-a-string-of-qml">Creating an Object from a String of QML
</h3> <p>If the QML is not defined until runtime, you can create a QML object from a string of QML using the <a href="qml-qtqml-qt.html#createQmlObject-method">Qt.createQmlObject</a>() function, as in the following example:</p> <pre data-language="qml">const newObject = Qt.createQmlObject(`
    import QtQuick 2.0

    Rectangle {
        color: "red"
        width: 20
        height: 20
    }
    `,
    parentItem,
    "myDynamicSnippet"
);</pre> <p>The first argument is the string of QML to create. Just like in a new file, you will need to import any types you wish to use. The second argument is the parent object for the new object, and the parent argument semantics which apply to components are similarly applicable for <code>createQmlObject()</code>. The third argument is the file path to associate with the new object; this is used for error reporting.</p> <p>If the string of QML imports files using relative paths, the path should be relative to the file in which the parent object (the second argument to the method) is defined.</p> <div class="admonition important"> <p><b>Important: </b>When building static QML applications, QML files are scanned to detect import dependencies. That way, all necessary plugins and resources are resolved at compile time. However, only explicit import statements are considered (those found at the top of a QML file), and not import statements enclosed within string literals. To support static builds, you therefore need to ensure that QML files using <a href="qml-qtqml-qt.html#createQmlObject-method">Qt.createQmlObject</a>(), explicitly contain all necessary imports at the top of the file in addition to inside the string literals.</p> </div> <h2 id="maintaining-dynamically-created-objects">Maintaining Dynamically Created Objects
</h2> <p>When managing dynamically created objects, you must ensure the creation context outlives the created object. Otherwise, if the creation context is destroyed first, the bindings and signal handlers in the dynamic object will no longer work.</p> <p>The actual creation context depends on how an object is created:</p> <ul> <li>If <a href="qml-qtqml-qt.html#createComponent-method">Qt.createComponent</a>() is used, the creation context is the <a href="qqmlcontext.html">QQmlContext</a> in which this method is called</li> <li>If <a href="qml-qtqml-qt.html#createQmlObject-method">Qt.createQmlObject</a>() is called, the creation context is the context of the parent object passed to this method</li> <li>If a <code>Component{}</code> object is defined and <a href="qml-qtqml-component.html#createObject-method">createObject</a>() or <a href="qml-qtqml-component.html#incubateObject-method">incubateObject</a>() is called on that object, the creation context is the context in which the <code>Component</code> is defined</li> </ul> <p>Also, note that while dynamically created objects may be used the same as other objects, they do not have an id in QML.</p> <h2 id="deleting-objects-dynamically">Deleting Objects Dynamically
</h2> <p>In many user interfaces, it is sufficient to set a visual object's opacity to 0 or to move the visual object off the screen instead of deleting it. If you have lots of dynamically created objects, however, you may receive a worthwhile performance benefit if unused objects are deleted.</p> <p>Note that you should never manually delete objects that were dynamically created by convenience QML object factories (such as <a href="qml-qtquick-loader.html">Loader</a> and <a href="qml-qtquick-repeater.html">Repeater</a>). Also, you should avoid deleting objects that you did not dynamically create yourself.</p> <p>Items can be deleted using the <code>destroy()</code> method. This method has an optional argument (which defaults to 0) that specifies the approximate delay in milliseconds before the object is to be destroyed.</p> <p>Here is an example. The <code>application.qml</code> creates five instances of the <code>SelfDestroyingRect.qml</code> component. Each instance runs a <a href="qml-qtquick-numberanimation.html">NumberAnimation</a>, and when the animation has finished, calls <code>destroy()</code> on its root object to destroy itself:</p> <table class="generic"> <tr class="odd" valign="top">
<td><code>application.qml</code></td>
<td>
<pre data-language="qml">import QtQuick 2.0

Item {
    id: container
    width: 500; height: 100

    Component.onCompleted: {
        var component = Qt.createComponent("SelfDestroyingRect.qml");
        for (var i=0; i&lt;5; i++) {
            var object = component.createObject(container);
            object.x = (object.width + 10) * i;
        }
    }
}</pre> </td>
</tr> <tr class="even" valign="top">
<td><code>SelfDestroyingRect.qml</code></td>
<td>
<pre data-language="qml">import QtQuick 2.0

Rectangle {
    id: rect
    width: 80; height: 80
    color: "red"

    NumberAnimation on opacity {
        to: 0
        duration: 1000

        onRunningChanged: {
            if (!running) {
                console.log("Destroying...")
                rect.destroy();
            }
        }
    }
}</pre> </td>
</tr> </table> <p>Alternatively, the <code>application.qml</code> could have destroyed the created object by calling <code>object.destroy()</code>.</p> <p>Note that it is safe to call destroy() on an object within that object. Objects are not destroyed the instant destroy() is called, but are cleaned up sometime between the end of that script block and the next frame (unless you specified a non-zero delay).</p> <p>Note also that if a <code>SelfDestroyingRect</code> instance was created statically like this:</p> <pre data-language="qml">Item {
    SelfDestroyingRect {
        // ...
    }
}</pre> <p>This would result in an error, since objects can only be dynamically destroyed if they were dynamically created.</p> <p>Objects created with <a href="qml-qtqml-qt.html#createQmlObject-method">Qt.createQmlObject</a>() can similarly be destroyed using <code>destroy()</code>:</p> <pre data-language="qml">const newObject = Qt.createQmlObject(`
    import QtQuick 2.0

    Rectangle {
        color: "red"
        width: 20
        height: 20
    }
    `,
    parentItem,
    "myDynamicSnippet"
);
newObject.destroy(1000);</pre><div class="_attribution">
  <p class="_attribution-p">
    &copy; The Qt Company Ltd<br>Licensed under the GNU Free Documentation License, Version 1.3.<br>
    <a href="https://doc.qt.io/qt-6.2/qtqml-javascript-dynamicobjectcreation.html" class="_attribution-link">https://doc.qt.io/qt-6.2/qtqml-javascript-dynamicobjectcreation.html</a>
  </p>
</div>
