<h1 class="title">SignalTransition QML Type</h1>  <p>The SignalTransition type provides a transition based on a Qt signal. <a href="#details">More...</a></p>  <table class="alignedsummary"> <tr>
<td class="memItemLeft rightAlign topAlign"> Import Statement:</td>
<td class="memItemRight bottomAlign"> import QtQml.StateMachine</td>
</tr>
<tr>
<td class="memItemLeft rightAlign topAlign"> Since:</td>
<td class="memItemRight bottomAlign"> Qt 5.4</td>
</tr>
<tr>
<td class="memItemLeft rightAlign topAlign"> Inherits:</td>
<td class="memItemRight bottomAlign"> <p><a href="qml-qtqml-statemachine-qsignaltransition.html">QSignalTransition</a></p> </td>
</tr>
</table>
<ul> <li><a href="https://doc.qt.io/qt-6.2/qml-qtqml-statemachine-signaltransition-members.html">List of all members, including inherited members</a></li> </ul> <h2 id="properties">Properties
</h2> <ul> <li class="fn">
<b><a href="qml-qtqml-statemachine-signaltransition.html#guard-prop">guard</a></b> : bool</li> <li class="fn">
<b><a href="qml-qtqml-statemachine-signaltransition.html#signal-prop">signal</a></b> : signal</li> </ul>  <h2 id="details">Detailed Description
</h2> <p>SignalTransition is part of <a href="qmlstatemachine-qml-guide.html">Qt State Machine QML API</a>.</p> <h2 id="example-usage">Example Usage
</h2> <pre data-language="qml">import QtQuick
import QtQml.StateMachine as DSM

Rectangle {
    DSM.StateMachine {
        id: stateMachine
        initialState: state
        running: true
        DSM.State {
            id: state
            DSM.SignalTransition {
                targetState: finalState
                signal: button.clicked
                guard: guardButton.checked
            }
        }
        DSM.FinalState {
            id: finalState
        }
        onFinished: Qt.quit()
    }
    Row {
        spacing: 2
        Button {
            id: button
            text: "Finish state"
        }

        Button {
            id: guardButton
            checkable: true
            text: checked ? "Press to block the SignalTransition" : "Press to unblock the SignalTransition"
        }
    }
}</pre> <p><b>See also </b><a href="qml-qtqml-statemachine-statemachine.html">StateMachine</a>, <a href="qml-qtqml-statemachine-finalstate.html">FinalState</a>, and <a href="qml-qtqml-statemachine-timeouttransition.html">TimeoutTransition</a>.</p>  <h2>Property Documentation</h2>  <h3 class="qmlproto" id="guard-prop"><p> <span class="name">guard</span> : <span class="type"><a href="qml-bool.html">bool</a></span></p></h3> <p>Guard conditions affect the behavior of a state machine by enabling transitions only when they evaluate to true and disabling them when they evaluate to false.</p> <p>When the signal associated with this signal transition is emitted the guard condition is evaluated. In the guard condition the arguments of the signal can be used as demonstrated in the example below.</p> <pre data-language="qml">import QtQuick
import QtQml.StateMachine as DSM

Rectangle {
    Button {
        anchors.fill: parent
        id: button
        DSM.StateMachine {
            DSM.State {
                DSM.SignalTransition {
                    targetState: finalState
                    signal: button.mysignal
                    // the guard condition uses the mystr string argument from mysignal
                    guard: mystr == "test"
                }
            }
            DSM.FinalState {
                id: finalState
            }
        }
        // define the signal the SignalTransition is connected with
        signal mysignal(mystr: string)
        // on clicking the button emit the signal with a single string argument
        onClicked: button.mysignal("test")
    }
}</pre> <p><b>See also </b><a href="qml-qtqml-statemachine-signaltransition.html#signal-prop">signal</a>.</p>    <h3 class="qmlproto" id="signal-prop"><p> <span class="name">signal</span> : <span class="type">signal</span></p></h3> <p>The signal which is associated with this signal transition.</p> <pre data-language="qml">import QtQuick
import QtQml.StateMachine as DSM

Rectangle {
    Button {
        anchors.fill: parent
        id: button
        DSM.StateMachine {
            DSM.State {
                DSM.SignalTransition {
                    targetState: finalState
                    signal: button.clicked
                }
            }
            DSM.FinalState {
                id: finalState
            }
        }
    }
}</pre><div class="_attribution">
  <p class="_attribution-p">
    &copy; The Qt Company Ltd<br>Licensed under the GNU Free Documentation License, Version 1.3.<br>
    <a href="https://doc.qt.io/qt-6.2/qml-qtqml-statemachine-signaltransition.html" class="_attribution-link">https://doc.qt.io/qt-6.2/qml-qtqml-statemachine-signaltransition.html</a>
  </p>
</div>
