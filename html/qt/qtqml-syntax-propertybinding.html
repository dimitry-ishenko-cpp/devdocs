<h1 class="title">Property Binding</h1>   <p>An object's property can be assigned a static value which stays constant until it is explicitly assigned a new value. However, to make the fullest use of QML and its built-in support for dynamic object behaviors, most QML objects use <i>property bindings</i>.</p> <p>Property bindings are a core feature of QML that lets developers specify relationships between different object properties. When a property's <i>dependencies</i> change in value, the property is automatically updated according to the specified relationship.</p> <p>Behind the scenes, the QML engine monitors the property's dependencies (that is, the variables in the binding expression). When a change is detected, the QML engine re-evaluates the binding expression and applies the new result to the property.</p> <h2 id="overview">Overview
</h2> <p>To create a property binding, a property is assigned a JavaScript expression that evaluates to the desired value. At its simplest, a binding may be a reference to another property. Take the following example, where the blue <a href="qml-qtquick-rectangle.html">Rectangle</a>'s height is bound to the height of its parent:</p> <pre data-language="qml">Rectangle {
    width: 200; height: 200

    Rectangle {
        width: 100
        height: parent.height
        color: "blue"
    }
}</pre> <p>Whenever the height of the parent rectangle changes, the height of the blue rectangle automatically updates to be of the same value.</p> <p>A binding can contain any valid JavaScript expression or statement, as QML uses a standards compliant JavaScript engine. Bindings can access object properties, call methods and use built-in JavaScript objects such as <code>Date</code> and <code>Math</code>. Below are other possible bindings for the previous example:</p> <pre data-language="cpp">height: parent.height / 2

height: Math.min(parent.width, parent.height)

height: parent.height &gt; 100 ? parent.height : parent.height/2

height: {
    if (parent.height &gt; 100)
        return parent.height
    else
        return parent.height / 2
}

height: someMethodThatReturnsHeight()</pre> <p>Below is a more complex example involving more objects and types:</p> <pre data-language="qml">Column {
    id: column
    width: 200
    height: 200

    Rectangle {
        id: topRect
        width: Math.max(bottomRect.width, parent.width/2)
        height: (parent.height / 3) + 10
        color: "yellow"

        TextInput {
            id: myTextInput
            text: "Hello QML!"
        }
    }

    Rectangle {
        id: bottomRect
        width: 100
        height: 50
        color: myTextInput.text.length &lt;= 10 ? "red" : "blue"
    }
}</pre> <p>In the previous example,</p> <ul> <li>
<code>topRect.width</code> depends on <code>bottomRect.width</code> and <code>column.width</code>
</li> <li>
<code>topRect.height</code> depends on <code>column.height</code>
</li> <li>
<code>bottomRect.color</code> depends on <code>myTextInput.text.length</code>
</li> </ul> <p>Syntactically, bindings are allowed to be of arbitrary complexity. However, if a binding is overly complex - such as involving multiple lines, or imperative loops - it could indicate that the binding is being used for more than describing property relationships. Complex bindings can reduce code performance, readability, and maintainability. It may be a good idea to redesign components that have complex bindings, or at least factor the binding out into a separate function. As a general rule, users should not rely on the evaluation order of bindings.</p> <h2 id="creating-property-bindings-from-javascript">Creating Property Bindings from JavaScript
</h2> <p>A property with a binding is automatically updated as necessary. However, if the property is later assigned a static value from a JavaScript statement, the binding will be removed.</p> <p>For example, the <a href="qml-qtquick-rectangle.html">Rectangle</a> below initially ensures that its <code>height</code> is always twice its <code>width</code>. However, when the space key is pressed, the current value of <code>width*3</code> will be assigned to <code>height</code> as a <i>static</i> value. After that, <i>the <code>height</code> will remain fixed at this value, even if the <code>width</code> changes</i>. The assignment of the static value removes the binding.</p> <pre data-language="qml">import QtQuick 2.0

Rectangle {
    width: 100
    height: width * 2

    focus: true
    Keys.onSpacePressed: {
        height = width * 3
    }
}</pre> <p>If the intention is to give the rectangle a fixed height and stop automatic updates, then this is not a problem. However, if the intention is to establish a new relationship between <code>width</code> and <code>height</code>, then the new binding expression must be wrapped in the Qt.binding() function instead:</p> <pre data-language="qml">import QtQuick 2.0

Rectangle {
    width: 100
    height: width * 2

    focus: true
    Keys.onSpacePressed: {
        height = Qt.binding(function() { return width * 3 })
    }
}</pre> <p>Now, after the space key is pressed, the rectangle's height will continue auto-updating to always be three times its width.</p> <h4 id="debugging-overwriting-of-bindings">Debugging overwriting of bindings
</h4> <p>A common cause of bugs in QML applications is accidentally overwriting bindings with static values from JavaScript statements. To help developers track down problems of this kind, the QML engine is able to emit messages whenever a binding is lost due to imperative assignments.</p> <p>In order to generate such messages, you need to enable the informational output for the <code>qt.qml.binding.removal</code> logging category, for instance by calling:</p> <pre data-language="cpp">QLoggingCategory::setFilterRules(QStringLiteral("qt.qml.binding.removal.info=true"));</pre> <p>Please refer to the <a href="qloggingcategory.html">QLoggingCategory</a> documentation for more information about enabling output from logging categories.</p> <p>Note that is perfectly reasonable in some circumstances to overwrite bindings. Any message generated by the QML engine should be treated as a diagnostic aid, and not necessarily as evidence of a problem without further investigation.</p> <h3 id="using-keyword-this-keyword-with-property-binding">Using <code>this</code> with Property Binding
</h3> <p>When creating a property binding from JavaScript, the <code>this</code> keyword can be used to refer to the object which receives the binding. This is helpful for resolving ambiguities with property names.</p> <p>For example, the <code>Component.onCompleted</code> handler below is defined within the scope of the <a href="qml-qtquick-item.html">Item</a>. In this scope, <code>width</code> refers to the <a href="qml-qtquick-item.html">Item</a>'s width, not the <a href="qml-qtquick-rectangle.html">Rectangle</a>'s width. To bind the <a href="qml-qtquick-rectangle.html">Rectangle</a>'s <code>height</code> to its own <code>width</code>, the binding expression must explicitly refer to <code>this.width</code> (or alternatively, <code>rect.width</code>):</p> <pre data-language="qml">Item {
    width: 500
    height: 500

    Rectangle {
        id: rect
        width: 100
        color: "yellow"
    }

    Component.onCompleted: {
        rect.height = Qt.binding(function() { return this.width * 2 })
        console.log("rect.height = " + rect.height) // prints 200, not 1000
    }
}</pre> <div class="admonition note"> <p><b>Note: </b>The value of <code>this</code> is not defined outside of property bindings. See <a href="qtqml-javascript-hostenvironment.html#javascript-environment-restrictions">JavaScript Environment Restrictions</a> for details.</p> </div>  <p><b>See also </b><a href="qtquick-positioning-anchors.html">Positioning with Anchors</a>.</p><div class="_attribution">
  <p class="_attribution-p">
    &copy; The Qt Company Ltd<br>Licensed under the GNU Free Documentation License, Version 1.3.<br>
    <a href="https://doc.qt.io/qt-6.2/qtqml-syntax-propertybinding.html" class="_attribution-link">https://doc.qt.io/qt-6.2/qtqml-syntax-propertybinding.html</a>
  </p>
</div>
