<h1 class="title">Transition QML Type</h1>  <p>Defines animated transitions that occur on state changes. <a href="#details">More...</a></p>  <table class="alignedsummary"> <tr>
<td class="memItemLeft rightAlign topAlign"> Import Statement:</td>
<td class="memItemRight bottomAlign"> import QtQuick</td>
</tr>
</table>
<ul> <li><a href="https://doc.qt.io/qt-6.2/qml-qtquick-transition-members.html">List of all members, including inherited members</a></li> </ul> <h2 id="properties">Properties
</h2> <ul> <li class="fn">
<b><a href="qml-qtquick-transition.html#animations-prop">animations</a></b> : list&lt;Animation&gt;</li> <li class="fn">
<b><a href="qml-qtquick-transition.html#enabled-prop">enabled</a></b> : bool</li> <li class="fn">
<b><a href="qml-qtquick-transition.html#from-prop">from</a></b> : string</li> <li class="fn">
<b><a href="qml-qtquick-transition.html#reversible-prop">reversible</a></b> : bool</li> <li class="fn">
<b><a href="qml-qtquick-transition.html#running-prop">running</a></b> : bool</li> <li class="fn">
<b><a href="qml-qtquick-transition.html#to-prop">to</a></b> : string</li> </ul>  <h2 id="details">Detailed Description
</h2> <p>A Transition defines the animations to be applied when a <a href="qml-qtquick-state.html">State</a> change occurs.</p> <p>For example, the following <a href="qml-qtquick-rectangle.html">Rectangle</a> has two states: the default state, and an added "moved" state. In the "moved state, the rectangle's position changes to (50, 50). The added Transition specifies that when the rectangle changes between the default and the "moved" state, any changes to the <code>x</code> and <code>y</code> properties should be animated, using an <code>Easing.InOutQuad</code>.</p> <pre data-language="qml">import QtQuick 2.0

Rectangle {
    id: rect
    width: 100; height: 100
    color: "red"

    MouseArea {
        id: mouseArea
        anchors.fill: parent
    }

    states: State {
        name: "moved"; when: mouseArea.pressed
        PropertyChanges { target: rect; x: 50; y: 50 }
    }

    transitions: Transition {
        NumberAnimation { properties: "x,y"; easing.type: Easing.InOutQuad }
    }
}</pre> <p>Notice the example does not require <a href="qml-qtquick-propertyanimation.html#to-prop">to</a> and <a href="qml-qtquick-propertyanimation.html#from-prop">from</a> values for the <a href="qml-qtquick-numberanimation.html">NumberAnimation</a>. As a convenience, these properties are automatically set to the values of <code>x</code> and <code>y</code> before and after the state change; the <code>from</code> values are provided by the current values of <code>x</code> and <code>y</code>, and the <code>to</code> values are provided by the <a href="qml-qtquick-propertychanges.html">PropertyChanges</a> object. If you wish, you can provide <a href="qml-qtquick-propertyanimation.html#to-prop">to</a> and <a href="qml-qtquick-propertyanimation.html#from-prop">from</a> values anyway to override the default values.</p> <p>By default, a Transition's animations are applied for any state change in the parent item. The Transition <a href="qml-qtquick-transition.html#from-prop">from</a> and <a href="qml-qtquick-transition.html#to-prop">to</a> values can be set to restrict the animations to only be applied when changing from one particular state to another.</p> <p>To define multiple Transitions, specify <a href="qml-qtquick-item.html#transitions-prop">Item::transitions</a> as a list:</p> <pre data-language="qml">    transitions: [
        Transition {
            from: "*"; to: "middleRight"
            NumberAnimation {
                properties: "x,y";
                easing.type: Easing.InOutQuad;
                duration: 2000;
            }
        },
        Transition {
            from: "*"; to: "bottomLeft";
            NumberAnimation {
                properties: "x,y";
                easing.type: Easing.InOutQuad;
                duration: 200;
            }
        },
        //If any other rectangle is clicked, the icon will return
        //to the start position at a slow speed and bounce.
        Transition {
            from: "*"; to: "*";
            NumberAnimation {
                easing.type: Easing.OutBounce;
                properties: "x,y";
                duration: 4000;
            }
        }
    ]</pre> <p>If multiple Transitions are specified, only a single (best-matching) Transition will be applied for any particular state change. In the example above, if the Rectangle enters a state other than <code>"middleRight"</code> or <code>"bottomLeft"</code>, the third Transition will be carried out, meaning the icon will be moved to the starting point.</p> <p>If a state change has a Transition that matches the same property as a <a href="qml-qtquick-behavior.html">Behavior</a>, the Transition animation overrides the <a href="qml-qtquick-behavior.html">Behavior</a> for that state change.</p> <p><b>See also </b><a href="qtquick-statesanimations-animations.html">Animation and Transitions in Qt Quick</a>, <a href="https://doc.qt.io/qt-6.2/qtquick-animation-example.html#states">States example</a>, <a href="qtquick-statesanimations-states.html">Qt Quick States</a>, and <a href="qtqml-index.html">Qt QML</a>.</p>  <h2>Property Documentation</h2>  <div class="fngroup"> <h3 class="qmlproto" id="from-prop"><p> <span class="name">from</span> : <span class="type"><a href="qml-string.html">string</a></span></p></h3> </div>
<p>These properties indicate the state changes that trigger the transition.</p> <p>The default values for these properties is "*" (that is, any state).</p> <p>For example, the following transition has not set the <code>to</code> and <code>from</code> properties, so the animation is always applied when changing between the two states (i.e. when the mouse is pressed and released).</p> <pre data-language="qml">Rectangle {
    id: rect
    width: 100; height: 100
    color: "red"

    MouseArea { id: mouseArea; anchors.fill: parent }

    states: State {
        name: "brighter"; when: mouseArea.pressed
        PropertyChanges { target: rect; color: "yellow" }
    }

    transitions: Transition {
        ColorAnimation { duration: 1000 }
    }
}</pre> <p>If the transition was changed to this:</p> <pre data-language="qml">transitions: Transition {
    to: "brighter"
    ColorAnimation { duration: 1000 }
}</pre> <p>The animation would only be applied when changing from the default state to the "brighter" state (i.e. when the mouse is pressed, but not on release).</p> <p>Multiple <code>to</code> and <code>from</code> values can be set by using a comma-separated string.</p> <p><b>See also </b><a href="qml-qtquick-transition.html#reversible-prop">reversible</a>.</p>    <h3 class="qmlproto" id="animations-prop"><p> <span class="qmlextra">[default] </span><span class="name">animations</span> : <span class="type"><a href="qml-list.html">list</a></span>&lt;<span class="type"><a href="qml-qtquick-animation.html">Animation</a></span>&gt;</p></h3> <p>This property holds a list of the animations to be run for this transition.</p> <pre data-language="qml">transitions: Transition {
    PropertyAnimation { duration: 3000 }
    ColorAnimation { duration: 3000 }
}</pre> <p>The top-level animations are run in parallel. To run them sequentially, define them within a <a href="qml-qtquick-sequentialanimation.html">SequentialAnimation</a>:</p> <pre data-language="qml">transitions: Transition {
    to: "brighter"
    reversible: true
    SequentialAnimation {
        PropertyAnimation { property: "x"; duration: 1000 }
        ColorAnimation { duration: 1000 }
    }
}</pre>    <h3 class="qmlproto" id="enabled-prop"><p> <span class="name">enabled</span> : <span class="type"><a href="qml-bool.html">bool</a></span></p></h3> <p>This property holds whether the Transition will be run when moving from the <code>from</code> state to the <code>to</code> state.</p> <p>By default a Transition is enabled.</p> <p>Note that in some circumstances disabling a Transition may cause an alternative Transition to be used in its place. In the following example, although the first Transition has been set to animate changes from "state1" to "state2", this transition has been disabled by setting <code>enabled</code> to <code>false</code>, so any such state change will actually be animated by the second Transition instead.</p> <pre data-language="qml">Item {
    states: [
        State { name: "state1" },
        State { name: "state2" }
    ]
    transitions: [
        Transition { from: "state1"; to: "state2"; enabled: false },
        Transition {
            // ...
        }
    ]
}</pre>    <h3 class="qmlproto" id="reversible-prop"><p> <span class="name">reversible</span> : <span class="type"><a href="qml-bool.html">bool</a></span></p></h3> <p>This property holds whether the transition should be automatically reversed when the conditions that triggered this transition are reversed.</p> <p>The default value is false.</p> <p>By default, transitions run in parallel and are applied to all state changes if the <a href="qml-qtquick-transition.html#from-prop">from</a> and <a href="qml-qtquick-transition.html#to-prop">to</a> states have not been set. In this situation, the transition is automatically applied when a state change is reversed, and it is not necessary to set this property to reverse the transition.</p> <p>However, if a <a href="qml-qtquick-sequentialanimation.html">SequentialAnimation</a> is used, or if the <a href="qml-qtquick-transition.html#from-prop">from</a> or <a href="qml-qtquick-transition.html#to-prop">to</a> properties have been set, this property will need to be set to reverse a transition when a state change is reverted. For example, the following transition applies a sequential animation when the mouse is pressed, and reverses the sequence of the animation when the mouse is released:</p> <pre data-language="qml">Rectangle {
    id: rect
    width: 100; height: 100
    color: "steelblue"

    TapHandler { id: tapHandler }

    states: State {
        name: "brighter"
        when: tapHandler.pressed
        PropertyChanges { target: rect; color: "lightsteelblue"; x: 50 }
    }

    transitions: Transition {
        to: "brighter"
        reversible: true
        SequentialAnimation {
            PropertyAnimation { property: "x"; duration: 1000 }
            ColorAnimation { duration: 1000 }
        }
    }
}</pre> <p>If the transition did not set the <code>to</code> and <code>reversible</code> values, then on the mouse release, the transition would play the <a href="qml-qtquick-propertyanimation.html">PropertyAnimation</a> before the <a href="qml-qtquick-coloranimation.html">ColorAnimation</a> instead of reversing the sequence.</p>    <h3 class="qmlproto" id="running-prop"><p> <span class="name">running</span> : <span class="type"><a href="qml-bool.html">bool</a></span></p></h3> <p>This property holds whether the transition is currently running.</p> <p>This property is read only.</p><div class="_attribution">
  <p class="_attribution-p">
    &copy; The Qt Company Ltd<br>Licensed under the GNU Free Documentation License, Version 1.3.<br>
    <a href="https://doc.qt.io/qt-6.2/qml-qtquick-transition.html" class="_attribution-link">https://doc.qt.io/qt-6.2/qml-qtquick-transition.html</a>
  </p>
</div>
