<h1 class="title">QML Coding Conventions</h1>   <p>This document contains the QML coding conventions that we follow in our documentation and examples and recommend that others follow.</p> <h2 id="qml-object-declarations">QML Object Declarations
</h2> <p>Throughout our documentation and examples, <a href="qtqml-syntax-objectattributes.html">QML object attributes</a> are always structured in the following order:</p> <ul> <li>id</li> <li>property declarations</li> <li>signal declarations</li> <li>JavaScript functions</li> <li>object properties</li> <li>child objects</li> </ul> <p>For better readability, we separate these different parts with an empty line.</p> <p>For example, a hypothetical <i>photo</i> QML object would look like this:</p> <pre data-language="qml">Rectangle {
    id: photo                                               // id on the first line makes it easy to find an object

    property bool thumbnail: false                          // property declarations
    property alias image: photoImage.source

    signal clicked                                          // signal declarations

    function doSomething(x)                                 // javascript functions
    {
        return x + photoImage.width
    }

    color: "gray"                                           // object properties
    x: 20                                                   // try to group related properties together
    y: 20
    height: 150
    width: {                                                // large bindings
        if (photoImage.width &gt; 200) {
            photoImage.width;
        } else {
            200;
        }
    }

    states: [
        State {
            name: "selected"
            PropertyChanges { target: border; color: "red" }
        }
    ]

    transitions: [
        Transition {
            from: ""
            to: "selected"
            ColorAnimation { target: border; duration: 200 }
        }
    ]

    Rectangle {                                             // child objects
        id: border
        anchors.centerIn: parent; color: "white"

        Image {
            id: photoImage
            anchors.centerIn: parent
        }
    }
}</pre> <h2 id="grouped-properties">Grouped Properties
</h2> <p>If using multiple properties from a group of properties, consider using <i>group notation</i> instead of <i>dot notation</i> if it improves readability.</p> <p>For example, this:</p> <pre data-language="qml">Rectangle {
    anchors.left: parent.left; anchors.top: parent.top; anchors.right: parent.right; anchors.leftMargin: 20
}

Text {
    text: "hello"
    font.bold: true; font.italic: true; font.pixelSize: 20; font.capitalization: Font.AllUppercase
}</pre> <p>could be written like this:</p> <pre data-language="qml">Rectangle {
    anchors { left: parent.left; top: parent.top; right: parent.right; leftMargin: 20 }
}

Text {
    text: "hello"
    font { bold: true; italic: true; pixelSize: 20; capitalization: Font.AllUppercase }
}</pre> <h2 id="unqualified-access">Unqualified access
</h2> <p>In order to improve readability and performance always reference properties of parent components by their id explicitly:</p> <pre data-language="qml">Item {
    id: root

    property int rectangleWidth: 50

    Rectangle {
        width: root.rectangleWidth
    }
}</pre> <h2 id="required-properties">Required properties
</h2> <p>When requiring data defined outside the component, make this explicit by using <a href="qml-codingconventions.html#required-properties">Required Properties</a>. Required properties must be set or else the creation of the component will fail. These are preferable to unqualified lookups because they are more performant and allow for both users and tooling to reason about an external property's type. Additionally they remove assumptions that a component otherwise has to make about the environment in which it is created.</p> <h2 id="signal-handlers">Signal handlers
</h2> <p>When handling parameters in signal handlers use functions which name them explicitly:</p> <pre data-language="qml">MouseArea {
    onClicked: (event) =&gt; { console.log(`${event.x},${event.y}`); }
}</pre> <h2 id="javascript-code">JavaScript Code
</h2> <p>If the script is a single expression, we recommend writing it inline:</p> <pre data-language="qml">Rectangle { color: "blue"; width: parent.width / 3 }</pre> <p>If the script is only a couple of lines long, we generally use a block:</p> <pre data-language="qml">Rectangle {
    color: "blue"
    width: {
        var w = parent.width / 3
        console.debug(w)
        return w
    }
}</pre> <p>If the script is more than a couple of lines long or can be used by different objects, we recommend creating a function and calling it like this:</p> <pre data-language="qml">function calculateWidth(object : Item) : double
{
    var w = object.width / 3
    // ...
    // more javascript code
    // ...
    console.debug(w)
    return w
}

Rectangle { color: "blue"; width: calculateWidth(parent) }</pre> <p>Also note that is recommended to add type annotations to your function in order to more easily reason about and refactor your application since both parameter and return types are immediately visible from the function signature.</p> <p>For long scripts, we will put the functions in their own JavaScript file and import it like this:</p> <pre data-language="qml">import "myscript.js" as Script

Rectangle { color: "blue"; width: Script.calculateWidth(parent) }</pre> <p>If the code is longer than one line and hence within a block, we use semicolons to indicate the end of each statement:</p> <pre data-language="qml">MouseArea {
    anchors.fill: parent
    onClicked: {
        var scenePos = mapToItem(null, mouseX, mouseY);
        console.log("MouseArea was clicked at scene pos " + scenePos);
    }
}</pre> <h2 id="related-information">Related Information
</h2> <ul> <li><a href="qtquick-bestpractices.html">Best Practices for QML and Qt Quick</a></li> </ul><div class="_attribution">
  <p class="_attribution-p">
    &copy; The Qt Company Ltd<br>Licensed under the GNU Free Documentation License, Version 1.3.<br>
    <a href="https://doc.qt.io/qt-6.2/qml-codingconventions.html" class="_attribution-link">https://doc.qt.io/qt-6.2/qml-codingconventions.html</a>
  </p>
</div>
