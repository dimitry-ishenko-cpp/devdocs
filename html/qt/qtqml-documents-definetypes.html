<h1 class="title">Defining Object Types through QML Documents</h1>   <p>One of the core features of QML is that it enables QML object types to be easily defined in a lightweight manner through QML documents to suit the needs of individual QML applications. The standard <a href="qtquick-index.html">Qt Quick</a> module provides various types like <a href="qml-qtquick-rectangle.html">Rectangle</a>, <a href="qml-qtquick-text.html">Text</a> and <a href="qml-qtquick-image.html">Image</a> for building a QML application; beyond these, you can easily define your own QML types to be reused within your application. This ability to create your own types forms the building blocks of any QML application.</p> <h2 id="defining-an-object-type-with-a-qml-file">Defining an Object Type with a QML File
</h2> <h3 id="naming-custom-qml-object-types">Naming Custom QML Object Types
</h3> <p>To create an object type, a QML document should be placed into a text file named as <i>&lt;TypeName&gt;.qml</i> where <i>&lt;TypeName&gt;</i> is the desired name of the type. The type name has the following requirements:</p> <ul> <li>It must be comprised of alphanumeric characters or underscores.</li> <li>It must begin with an uppercase letter.</li> </ul> <p>This document is then automatically recognized by the engine as a definition of a QML type. Additionally, a type defined in this manner is automatically made available to other QML files within the same directory as the engine searches within the immediate directory when resolving QML type names.</p> <h3 id="custom-qml-type-definition">Custom QML Type Definition
</h3> <p>For example, below is a document that declares a <a href="qml-qtquick-rectangle.html">Rectangle</a> with a child <a href="qml-qtquick-mousearea.html">MouseArea</a>. The document has been saved to file named <code>SquareButton.qml</code>:</p> <pre data-language="qml">// SquareButton.qml
import QtQuick 2.0

Rectangle {
    property int side: 100
    width: side; height: side
    color: "red"

    MouseArea {
        anchors.fill: parent
        onClicked: console.log("Button clicked!")
    }
}</pre> <p>Since the file is named <code>SquareButton.qml</code>, <b>this can now be used as a type named <code>SquareButton</code> by any other QML file within the same directory</b>. For example, if there was a <code>myapplication.qml</code> file in the same directory, it could refer to the <code>SquareButton</code> type:</p> <pre data-language="qml">// myapplication.qml
import QtQuick 2.0

SquareButton {}</pre> <p class="centerAlign"><img alt="" src="data:image/png;base64,iVBORw0KGgoAAAANSUhEUgAAAGQAAABkAQMAAABKLAcXAAAACXBIWXMAAAsTAAALEwEAmpwYAAAABlBMVEX/AACZmZmaMYvPAAAAGElEQVQ4y2P4jwQ+MIyCUTAKRsEooDEAAC6QDOVkUxqsAAAAAElFTkSuQmCC"></p>
<p>This creates a 100 x 100 red <a href="qml-qtquick-rectangle.html">Rectangle</a> with an inner <a href="qml-qtquick-mousearea.html">MouseArea</a>, as defined in <code>SquareButton.qml</code>. When this <code>myapplication.qml</code> document is loaded by the engine, it loads the SquareButton.qml document as a component and instantiates it to create a <code>SquareButton</code> object.</p> <p>The <code>SquareButton</code> type encapsulates the tree of QML objects declared in <code>SquareButton.qml</code>. When the QML engine instantiates a <code>SquareButton</code> object from this type, it is instantiating an object from the <a href="qml-qtquick-rectangle.html">Rectangle</a> tree declared in <code>SquareButton.qml</code>.</p> <div class="admonition note"> <p><b>Note: </b>the letter case of the file name is significant on some (notably UNIX) filesystems. It is recommended the file name case matches the case of the desired QML type name exactly - for example, <code>Box.qml</code> and not <code>BoX.qml</code> - regardless of the platform to which the QML type will be deployed.</p> </div> <h3 id="inline-components">Inline Components
</h3> <p>Sometimes, it can be inconvenient to create a new file for a type, for instance when reusing a small delegate in multiple views. If you don't actually need to expose the type, but only need to create an instance, <a href="qml-qtqml-component.html">Component</a> is an option. But if you want to declare properties with the component types, or if you want to use it in multiple files, <code>Component</code> is not an option. In that case, you can use <i>inline components</i>. Inline components declare a new component inside of a file. The syntax for that is</p> <pre data-language="cpp">component &lt;component name&gt; : BaseType {
    // declare properties and bindings here
}</pre> <p>Inside the file which declares the inline component, the type can be referenced simply by its name.</p> <pre data-language="qml">// Images.qml
import QtQuick 2.15

Item {
    component LabeledImage: Column {
        property alias source: image.source
        property alias caption: text.text

        Image {
            id: image
            width: 50
            height: 50
        }
        Text {
            id: text
            font.bold: true
        }
    }

    Row {
        LabeledImage {
            id: before
            source: "before.png"
            caption: "Before"
        }
        LabeledImage {
            id: after
            source: "after.png"
            caption: "After"
        }
    }
    property LabeledImage selectedImage: before
}</pre> <p>In other files, it has to be prefixed with the name of its containing component.</p> <pre data-language="qml">// LabeledImageBox.qml
import QtQuick 2.15

Rectangle {
    property alias caption: image.caption
    property alias source: image.source
    border.width: 2
    border.color: "black"
    Images.LabeledImage {
        id: image
    }
}</pre> <div class="admonition note"> <p><b>Note: </b>Inline components don't share their scope with the component they are declared in. In the following example, when <code>A.MyInlineComponent</code> in file B.qml gets created, a ReferenceError will occur, as <code>root</code> does not exist as an id in B.qml. It is therefore advisable not to reference objects in an inline component which are not part of it.</p> </div> <pre data-language="qml">// A.qml
import QtQuick 2.15

Item {
    id: root
    property string message: "From A"
    component MyInlineComponent : Item {
        Component.onCompleted: console.log(root.message)
    }
}
// B.qml
import QtQuick 2.15

Item {
    A.MyInlineComponent {}
}</pre> <div class="admonition note"> <p><b>Note: </b>Inline components cannot be nested.</p> </div> <h3 id="importing-types-defined-outside-the-current-directory">Importing Types Defined Outside the Current Directory
</h3> <p>If <code>SquareButton.qml</code> was not in the same directory as <code>myapplication.qml</code>, the <code>SquareButton</code> type would need to be specifically made available through an <i>import</i> statement in <code>myapplication.qml</code>. It could be imported from a relative path on the file system, or as an installed module; see <a href="https://doc.qt.io/qt-6.2/qtqml-modules-topic.html">module</a> for more details.</p> <h2 id="accessible-attributes-of-custom-types">Accessible Attributes of Custom Types
</h2> <p>The <b>root object</b> definition in a .qml file <b>defines the attributes that are available for a QML type</b>. All properties, signals and methods that belong to this root object - whether they are custom declared, or come from the QML type of the root object - are externally accessible and can be read and modified for objects of this type.</p> <p>For example, the root object type in the <code>SquareButton.qml</code> file above is <a href="qml-qtquick-rectangle.html">Rectangle</a>. This means any properties defined by the <a href="qml-qtquick-rectangle.html">Rectangle</a> type can be modified for a <code>SquareButton</code> object. The code below defines three <code>SquareButton</code> objects with customized values for some of the properties of the root <a href="qml-qtquick-rectangle.html">Rectangle</a> object of the <code>SquareButton</code> type:</p> <pre data-language="qml">// application.qml
import QtQuick 2.0

Column {
    SquareButton { side: 50 }
    SquareButton { x: 50; color: "blue" }
    SquareButton { radius: 10 }
}</pre> <p class="centerAlign"><img alt="" src="data:image/png;base64,iVBORw0KGgoAAAANSUhEUgAAAGQAAAD6AgMAAAAq4Ar0AAAACXBIWXMAAAsTAAALEwEAmpwYAAAACVBMVEUAAP//AAD////1Ly4uAAAAZUlEQVRYw+3WIQ4AIQxE0V6yCI6w98GvWQGnBFnRQaA24Y9rXuq/eVgdYYYgCHKfxOO1OARBEARBEORq6Z7tWQXZUinD8pf1ZJ+QupEmpGzE1RAEQRAEQX4oJ71z0lW60nTZyRqcSDMIXgLbeU4AAAAASUVORK5CYII="></p>
<p>The attributes that are accessible to objects of the custom QML type include any <a href="qtqml-syntax-objectattributes.html#defining-property-attributes">custom properties</a>, <a href="qtqml-syntax-objectattributes.html#defining-method-attributes">methods</a> and <a href="qtqml-syntax-objectattributes.html#defining-signal-attributes">signals</a> that have additionally been defined for an object. For example, suppose the <a href="qml-qtquick-rectangle.html">Rectangle</a> in <code>SquareButton.qml</code> had been defined as follows, with additional properties, methods and signals:</p> <pre data-language="qml">// SquareButton.qml
import QtQuick 2.0

Rectangle {
    id: root

    property bool pressed: mouseArea.pressed

    signal buttonClicked(real xPos, real yPos)

    function randomizeColor() {
        root.color = Qt.rgba(Math.random(), Math.random(), Math.random(), 1)
    }

    property int side: 100
    width: side; height: side
    color: "red"

    MouseArea {
        id: mouseArea
        anchors.fill: parent
        onClicked: (mouse)=&gt; root.buttonClicked(mouse.x, mouse.y)
    }
}</pre> <p>Any <code>SquareButton</code> object could make use of the <code>pressed</code> property, <code>buttonClicked</code> signal and <code>randomizeColor()</code> method that have been added to the root <a href="qml-qtquick-rectangle.html">Rectangle</a>:</p> <pre data-language="qml">// application.qml
import QtQuick 2.0

SquareButton {
    id: squareButton

    onButtonClicked: (xPos, yPos)=&gt; {
        console.log("Clicked", xPos, yPos)
        randomizeColor()
    }

    Text { text: squareButton.pressed ? "Down" : "Up" }
}</pre> <p>Note that any of the <code>id</code> values defined in <code>SquareButton.qml</code> are not accessible to <code>SquareButton</code> objects, as id values are only accessible from within the component scope in which a component is declared. The <code>SquareButton</code> object definition above cannot refer to <code>mouseArea</code> in order to refer to the <a href="qml-qtquick-mousearea.html">MouseArea</a> child, and if it had an <code>id</code> of <code>root</code> rather than <code>squareButton</code>, this would not conflict with the <code>id</code> of the same value for the root object defined in <code>SquareButton.qml</code> as the two would be declared within separate scopes.</p><div class="_attribution">
  <p class="_attribution-p">
    &copy; The Qt Company Ltd<br>Licensed under the GNU Free Documentation License, Version 1.3.<br>
    <a href="https://doc.qt.io/qt-6.2/qtqml-documents-definetypes.html" class="_attribution-link">https://doc.qt.io/qt-6.2/qtqml-documents-definetypes.html</a>
  </p>
</div>
