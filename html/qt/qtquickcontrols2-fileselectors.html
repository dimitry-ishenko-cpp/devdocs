<h1 class="title">Using File Selectors with Qt Quick Controls</h1>   <p><a href="qfileselector.html">File selectors</a> provide a convenient way of selecting file variants. Qt offers the platform name and the locale as built-in selectors. Qt Quick Controls extends the built-in selectors with the name (lowercase) of the style that an application is running with.</p> <p>By using file selectors, style-specific tweaks can be applied without creating a hard dependency to a style. From the available file variants, only the selected QML file is loaded by the QML engine. Each file variant can assume the context, that is, a specific style. This typically leads to some code duplication, but on the other hand, cuts the aforementioned hard dependency to the style, and leads to simpler and more efficient QML code.</p> <p>The following example demonstrates a custom rounded button that has a styled drop shadow in the <a href="qtquickcontrols2-material.html">Material style</a>, and looks flat in other styles. The files are organized so that the Material version of <code>CustomButton.qml</code> is placed into a <code>+Material</code> sub-directory.</p> <pre data-language="cpp">:/main.qml
:/CustomButton.qml
:/+Material/CustomButton.qml</pre> <p>By default, <code>main.qml</code> will use <code>CustomButton.qml</code> for the <code>CustomButton</code> type. However, when the application is run with the Material style, the <code>Material</code> selector will be present and the <code>+Material/CustomButton.qml</code> version will be used instead.</p> <pre data-language="cpp">// main.qml
import QtQuick
import QtQuick.Controls

ApplicationWindow {
    id: window
    visible: true

    CustomButton {
        text: "Button"
        anchors.centerIn: parent
    }
}</pre> <p>The base implementation of the custom button is a simple rounded flat button.</p> <pre data-language="cpp">// CustomButton.qml
import QtQuick
import QtQuick.Controls

Button {
    id: control

    background: Rectangle {
        radius: width / 2
        implicitWidth: 36
        implicitHeight: 36
        color: control.pressed ? "#ccc" : "#eee"
    }
}</pre> <p>The Material style's implementation of the custom button imports the Material style, requests a dark theme to get light text, and creates a drop shadow for the background.</p> <pre data-language="cpp">// +Material/CustomButton.qml
import QtQuick
import QtGraphicalEffects
import QtQuick.Controls
import QtQuick.Controls.Material

Button {
    id: control

    Material.theme: Material.Dark

    background: Rectangle {
        implicitWidth: 48
        implicitHeight: 48
        color: Material.accentColor
        radius: width / 2

        layer.enabled: control.enabled
        layer.effect: DropShadow {
            verticalOffset: 1
            color: Material.dropShadowColor
            samples: control.pressed ? 20 : 10
            spread: 0.5
        }
    }
}</pre> <div class="admonition note"> <p><b>Note: </b>It is recommended to use <a href="qqmlapplicationengine.html">QQmlApplicationEngine</a>, which internally creates a <a href="qqmlfileselector.html">QQmlFileSelector</a> instance. This is all that is needed to take QML file selectors into use.</p> </div> <h2 id="related-information">Related Information
</h2> <ul> <li><a href="qfileselector.html">QFileSelector</a></li> <li><a href="qqmlfileselector.html">QQmlFileSelector</a></li> <li><a href="qtquickcontrols2-styles.html">Styling Qt Quick Controls</a></li> </ul><div class="_attribution">
  <p class="_attribution-p">
    &copy; The Qt Company Ltd<br>Licensed under the GNU Free Documentation License, Version 1.3.<br>
    <a href="https://doc.qt.io/qt-6.2/qtquickcontrols2-fileselectors.html" class="_attribution-link">https://doc.qt.io/qt-6.2/qtquickcontrols2-fileselectors.html</a>
  </p>
</div>
