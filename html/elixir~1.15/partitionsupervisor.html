<h1>   <span translate="no">PartitionSupervisor</span>  </h1> <section id="moduledoc"> <p>A supervisor that starts multiple partitions of the same child.</p>
<p>Certain processes may become bottlenecks in large systems. If those processes can have their state trivially partitioned, in a way there is no dependency between them, then they can use the <a href="partitionsupervisor.html#content"><code class="inline">PartitionSupervisor</code></a> to create multiple isolated and independent partitions.</p>
<p>Once the <a href="partitionsupervisor.html#content"><code class="inline">PartitionSupervisor</code></a> starts, you can dispatch to its children using <code class="inline">{:via, PartitionSupervisor, {name, key}}</code>, where <code class="inline">name</code> is the name of the <a href="partitionsupervisor.html#content"><code class="inline">PartitionSupervisor</code></a> and key is used for routing.</p>
<h2 id="module-simple-example" class="section-heading"> <a href="#module-simple-example">  Simple Example </a> </h2> <p>Let's start with an example which is not useful per se, but shows how the partitions are started and how messages are routed to them.</p>
<p>Here's a toy GenServer that simply collects the messages it's given. It prints them for easy illustration.</p>
<pre data-language="elixir">defmodule Collector do
  use GenServer

  def start_link(args) do
    GenServer.start_link(__MODULE__, args)
  end

  def init(args) do
    IO.inspect [__MODULE__, " got args ", args, " in ", self()]
    {:ok, _initial_state = []}
  end

  def collect(server, msg) do
    GenServer.call(server, {:collect, msg})
  end

  def handle_call({:collect, msg}, _from, state) do
    new_state = [msg | state]
    IO.inspect ["current messages:", new_state, " in process", self()]
    {:reply, :ok, new_state}
  end
end</pre>
<p>To run multiple of these, we can start them under a <a href="partitionsupervisor.html#content"><code class="inline">PartitionSupervisor</code></a> by placing this in our supervision tree:</p>
<pre data-language="elixir">{PartitionSupervisor,
  child_spec: Collector.child_spec([some: :arg]),
  name: MyApp.PartitionSupervisor
}</pre>
<p>We can send messages to them using a "via tuple":</p>
<pre data-language="elixir"># The key is used to route our message to a particular instance.
key = 1
Collector.collect({:via, PartitionSupervisor, {MyApp.PartitionSupervisor, key}}, :hi)
# ["current messages:", [:hi], " in process", #PID&lt;0.602.0&gt;]
:ok
Collector.collect({:via, PartitionSupervisor, {MyApp.PartitionSupervisor, key}}, :ho)
# ["current messages:", [:ho, :hi], " in process", #PID&lt;0.602.0&gt;]
:ok

# With a different key, the message will be routed to a different instance.
key = 2
Collector.collect({:via, PartitionSupervisor, {MyApp.PartitionSupervisor, key}}, :a)
# ["current messages:", [:a], " in process", #PID&lt;0.603.0&gt;]
:ok
Collector.collect({:via, PartitionSupervisor, {MyApp.PartitionSupervisor, key}}, :b)
# ["current messages:", [:b, :a], " in process", #PID&lt;0.603.0&gt;]
:ok</pre>
<p>Now let's move on to a useful example.</p>
<h2 id="module-dynamicsupervisor-example" class="section-heading"> <a href="dynamicsupervisor.html"><code class="inline">DynamicSupervisor</code></a> Example </h2> <p>The <a href="dynamicsupervisor.html"><code class="inline">DynamicSupervisor</code></a> is a single process responsible for starting other processes. In some applications, the <a href="dynamicsupervisor.html"><code class="inline">DynamicSupervisor</code></a> may become a bottleneck. To address this, you can start multiple instances of the <a href="dynamicsupervisor.html"><code class="inline">DynamicSupervisor</code></a> through a <a href="partitionsupervisor.html#content"><code class="inline">PartitionSupervisor</code></a>, and then pick a "random" instance to start the child on.</p>
<p>Instead of starting a single <a href="dynamicsupervisor.html"><code class="inline">DynamicSupervisor</code></a>:</p>
<pre data-language="elixir">children = [
  {DynamicSupervisor, name: MyApp.DynamicSupervisor}
]

Supervisor.start_link(children, strategy: :one_for_one)</pre>
<p>and starting children on that dynamic supervisor directly:</p>
<pre data-language="elixir">DynamicSupervisor.start_child(MyApp.DynamicSupervisor, {Agent, fn -&gt; %{} end})</pre>
<p>You can do start the dynamic supervisors under a <a href="partitionsupervisor.html#content"><code class="inline">PartitionSupervisor</code></a>:</p>
<pre data-language="elixir">children = [
  {PartitionSupervisor,
   child_spec: DynamicSupervisor,
   name: MyApp.DynamicSupervisors}
]

Supervisor.start_link(children, strategy: :one_for_one)</pre>
<p>and then:</p>
<pre data-language="elixir">DynamicSupervisor.start_child(
  {:via, PartitionSupervisor, {MyApp.DynamicSupervisors, self()}},
  {Agent, fn -&gt; %{} end}
)</pre>
<p>In the code above, we start a partition supervisor that will by default start a dynamic supervisor for each core in your machine. Then, instead of calling the <a href="dynamicsupervisor.html"><code class="inline">DynamicSupervisor</code></a> by name, you call it through the partition supervisor using the <code class="inline">{:via, PartitionSupervisor, {name, key}}</code> format. We picked <code class="inline">self()</code> as the routing key, which means each process will be assigned one of the existing dynamic supervisors. See <a href="#start_link/1"><code class="inline">start_link/1</code></a> to see all options supported by the <a href="partitionsupervisor.html#content"><code class="inline">PartitionSupervisor</code></a>.</p>
<h2 id="module-implementation-notes" class="section-heading"> <a href="#module-implementation-notes">  Implementation notes </a> </h2> <p>The <a href="partitionsupervisor.html#content"><code class="inline">PartitionSupervisor</code></a> uses either an ETS table or a <a href="registry.html"><code class="inline">Registry</code></a> to manage all of the partitions. Under the hood, the <a href="partitionsupervisor.html#content"><code class="inline">PartitionSupervisor</code></a> generates a child spec for each partition and then acts as a regular supervisor. The ID of each child spec is the partition number.</p>
<p>For routing, two strategies are used. If <code class="inline">key</code> is an integer, it is routed using <code class="inline">rem(abs(key), partitions)</code> where <code class="inline">partitions</code> is the number of partitions. Otherwise it uses <code class="inline">:erlang.phash2(key, partitions)</code>. The particular routing may change in the future, and therefore must not be relied on. If you want to retrieve a particular PID for a certain key, you can use <code class="inline">GenServer.whereis({:via, PartitionSupervisor, {name, key}})</code>.</p> </section> <section id="summary" class="details-list">  <h2> Types </h2>
<dl class="summary-types summary">  <div class="summary-row"> <dt class="summary-signature"> <a href="#t:name/0" translate="no">name()</a> </dt> <dd class="summary-synopsis"><p>The name of the <a href="partitionsupervisor.html#content"><code class="inline">PartitionSupervisor</code></a>.</p></dd> </div> </dl> <h2> Functions </h2>
<dl class="summary-functions summary">  <div class="summary-row"> <dt class="summary-signature"> <a href="#count_children/1" translate="no">count_children(supervisor)</a> </dt> <dd class="summary-synopsis"><p>Returns a map containing count values for the supervisor.</p></dd> </div> <div class="summary-row"> <dt class="summary-signature"> <a href="#partitions/1" translate="no">partitions(name)</a> </dt> <dd class="summary-synopsis"><p>Returns the number of partitions for the partition supervisor.</p></dd> </div> <div class="summary-row"> <dt class="summary-signature"> <a href="#start_link/1" translate="no">start_link(opts)</a> </dt> <dd class="summary-synopsis"><p>Starts a partition supervisor with the given options.</p></dd> </div> <div class="summary-row"> <dt class="summary-signature"> <a href="#stop/3" translate="no">stop(supervisor, reason \\ :normal, timeout \\ :infinity)</a> </dt> <dd class="summary-synopsis"><p>Synchronously stops the given partition supervisor with the given <code class="inline">reason</code>.</p></dd> </div> <div class="summary-row"> <dt class="summary-signature"> <a href="#which_children/1" translate="no">which_children(name)</a> </dt> <dd class="summary-synopsis"><p>Returns a list with information about all children.</p></dd> </div> </dl> </section> <section id="types" class="details-list">  <div class="types-list"> <section class="detail"> <h3 class="detail-header" id="t:name/0">name()<a href="https://github.com/elixir-lang/elixir/blob/v1.15.4/lib/elixir/lib/partition_supervisor.ex#L144" class="source">Source</a>
</h3> <section class="docstring"> <div class="specs"> <pre translate="no" data-language="elixir">@type name() :: atom() | {:via, module(), term()}</pre> </div> <p>The name of the <a href="partitionsupervisor.html#content"><code class="inline">PartitionSupervisor</code></a>.</p> </section> </section> </div> </section> <section id="functions" class="details-list">  <div class="functions-list"> <section class="detail"> <h3 class="detail-header" id="count_children/1">count_children(supervisor)<a href="https://github.com/elixir-lang/elixir/blob/v1.15.4/lib/elixir/lib/partition_supervisor.ex#L402" class="source">Source</a>
</h3> <section class="docstring"> <div class="specs"> <pre translate="no" data-language="elixir">@spec count_children(name()) :: %{
  specs: non_neg_integer(),
  active: non_neg_integer(),
  supervisors: non_neg_integer(),
  workers: non_neg_integer()
}</pre> </div> <p>Returns a map containing count values for the supervisor.</p>
<p>The map contains the following keys:</p>
<ul>
<li><p><code class="inline">:specs</code> - the number of partitions (children processes)</p></li>
<li><p><code class="inline">:active</code> - the count of all actively running child processes managed by this supervisor</p></li>
<li><p><code class="inline">:supervisors</code> - the count of all supervisors whether or not the child process is still alive</p></li>
<li><p><code class="inline">:workers</code> - the count of all workers, whether or not the child process is still alive</p></li>
</ul> </section> </section> <section class="detail"> <h3 class="detail-header" id="partitions/1">partitions(name)<a href="https://github.com/elixir-lang/elixir/blob/v1.15.4/lib/elixir/lib/partition_supervisor.ex#L329" class="source">Source</a>
</h3> <section class="docstring"> <div class="specs"> <pre translate="no" data-language="elixir">@spec partitions(name()) :: pos_integer()</pre> </div> <p>Returns the number of partitions for the partition supervisor.</p> </section> </section> <section class="detail"> <h3 class="detail-header" id="start_link/1">start_link(opts)<a href="https://github.com/elixir-lang/elixir/blob/v1.15.4/lib/elixir/lib/partition_supervisor.ex#L223" class="source">Source</a>
</h3> <section class="docstring"> <div class="specs"> <pre translate="no" data-language="elixir">@spec start_link(keyword()) :: Supervisor.on_start()</pre> </div> <p>Starts a partition supervisor with the given options.</p>
<p>This function is typically not invoked directly, instead it is invoked when using a <a href="partitionsupervisor.html#content"><code class="inline">PartitionSupervisor</code></a> as a child of another supervisor:</p>
<pre data-language="elixir">children = [
  {PartitionSupervisor, child_spec: SomeChild, name: MyPartitionSupervisor}
]</pre>
<p>If the supervisor is successfully spawned, this function returns <code class="inline">{:ok, pid}</code>, where <code class="inline">pid</code> is the PID of the supervisor. If the given name for the partition supervisor is already assigned to a process, the function returns <code class="inline">{:error, {:already_started, pid}}</code>, where <code class="inline">pid</code> is the PID of that process.</p>
<p>Note that a supervisor started with this function is linked to the parent process and exits not only on crashes but also if the parent process exits with <code class="inline">:normal</code> reason.</p>
<h4 id="start_link/1-options" class="section-heading"> <a href="#start_link/1-options">  Options </a> </h4> <ul>
<li><p><code class="inline">:name</code> - an atom or via tuple representing the name of the partition supervisor (see <a href="#t:name/0"><code class="inline">name/0</code></a>).</p></li>
<li><p><code class="inline">:child_spec</code> - the child spec to be used when starting the partitions.</p></li>
<li><p><code class="inline">:partitions</code> - a positive integer with the number of partitions. Defaults to <code class="inline">System.schedulers_online()</code> (typically the number of cores).</p></li>
<li><p><code class="inline">:strategy</code> - the restart strategy option, defaults to <code class="inline">:one_for_one</code>. You can learn more about strategies in the <a href="supervisor.html"><code class="inline">Supervisor</code></a> module docs.</p></li>
<li><p><code class="inline">:max_restarts</code> - the maximum number of restarts allowed in a time frame. Defaults to <code class="inline">3</code>.</p></li>
<li><p><code class="inline">:max_seconds</code> - the time frame in which <code class="inline">:max_restarts</code> applies. Defaults to <code class="inline">5</code>.</p></li>
<li><p><code class="inline">:with_arguments</code> - a two-argument anonymous function that allows the partition to be given to the child starting function. See the <code class="inline">:with_arguments</code> section below.</p></li>
</ul>
<h4 id="start_link/1-with_arguments" class="section-heading"> <a href="#start_link/1-with_arguments">  <code class="inline">:with_arguments</code> </a> </h4> <p>Sometimes you want each partition to know their partition assigned number. This can be done with the <code class="inline">:with_arguments</code> option. This function receives the value of the <code class="inline">:child_spec</code> option and an integer for the partition number. It must return a new list of arguments that will be used to start the partition process.</p>
<p>For example, most processes are started by calling <code class="inline">start_link(opts)</code>, where <code class="inline">opts</code> is a keyword list. You could inject the partition into the options given to the child:</p>
<pre data-language="elixir">with_arguments: fn [opts], partition -&gt;
  [Keyword.put(opts, :partition, partition)]
end</pre> </section> </section> <section class="detail">   <h3 class="detail-header" id="stop/3">stop(supervisor, reason \\ :normal, timeout \\ :infinity)<a href="https://github.com/elixir-lang/elixir/blob/v1.15.4/lib/elixir/lib/partition_supervisor.ex#L418" class="source">Source</a>
</h3> <section class="docstring"> <div class="specs"> <pre translate="no" data-language="elixir">@spec stop(name(), reason :: term(), timeout()) :: :ok</pre> </div> <p>Synchronously stops the given partition supervisor with the given <code class="inline">reason</code>.</p>
<p>It returns <code class="inline">:ok</code> if the supervisor terminates with the given reason. If it terminates with another reason, the call exits.</p>
<p>This function keeps OTP semantics regarding error reporting. If the reason is any other than <code class="inline">:normal</code>, <code class="inline">:shutdown</code> or <code class="inline">{:shutdown, _}</code>, an error report is logged.</p> </section> </section> <section class="detail"> <h3 class="detail-header" id="which_children/1">which_children(name)<a href="https://github.com/elixir-lang/elixir/blob/v1.15.4/lib/elixir/lib/partition_supervisor.ex#L374" class="source">Source</a>
</h3> <section class="docstring"> <div class="specs"> <pre translate="no" data-language="elixir">@spec which_children(name()) :: [
  {:undefined, pid() | :restarting, :worker | :supervisor,
   [module()] | :dynamic}
]</pre> </div> <p>Returns a list with information about all children.</p>
<p>This function returns a list of tuples containing:</p>
<ul>
<li><p><code class="inline">id</code> - the partition number</p></li>
<li><p><code class="inline">child</code> - the PID of the corresponding child process or the atom <code class="inline">:restarting</code> if the process is about to be restarted</p></li>
<li><p><code class="inline">type</code> - <code class="inline">:worker</code> or <code class="inline">:supervisor</code> as defined in the child specification</p></li>
<li><p><code class="inline">modules</code> - as defined in the child specification</p></li>
</ul> </section> </section> </div> </section><div class="_attribution">
  <p class="_attribution-p">
    &copy; 2012 Plataformatec<br>Licensed under the Apache License, Version 2.0.<br>
    <a href="https://hexdocs.pm/elixir/1.15.4/PartitionSupervisor.html" class="_attribution-link">https://hexdocs.pm/elixir/1.15.4/PartitionSupervisor.html</a>
  </p>
</div>
