<div class="getting-started-title"> <small>Meta-programming in Elixir</small> <h1>Domain-specific languages</h1> </div>  <h2 id="foreword">Foreword</h2> <p><a href="https://en.wikipedia.org/wiki/Domain-specific_language">Domain-specific languages (DSL)</a> allow developers to tailor their application to a particular domain. You don’t need macros in order to have a DSL: every data structure and every function you define in your module is part of your Domain-specific language.</p> <p>For example, imagine we want to implement a Validator module which provides a data validation domain-specific language. We could implement it using data structures, functions or macros. Let’s see what those different DSLs would look like:</p> <div class="language-elixir highlighter-rouge"><pre class="highlight" data-language=""># 1. data structures
import Validator
validate user, name: [length: 1..100],
               email: [matches: ~r/@/]

# 2. functions
import Validator
user
|&gt; validate_length(:name, 1..100)
|&gt; validate_matches(:email, ~r/@/)

# 3. macros + modules
defmodule MyValidator do
  use Validator
  validate_length :name, 1..100
  validate_matches :email, ~r/@/
end

MyValidator.validate(user)
</pre></div> <p>Of all the approaches above, the first is definitely the most flexible. If our domain rules can be encoded with data structures, they are by far the easiest to compose and implement, as Elixir’s standard library is filled with functions for manipulating different data types.</p> <p>The second approach uses function calls which better suits more complex APIs (for example, if you need to pass many options) and reads nicely in Elixir thanks to the pipe operator.</p> <p>The third approach uses macros, and is by far the most complex. It will take more lines of code to implement, it is hard and expensive to test (compared to testing simple functions), and it limits how the user may use the library since all validations need to be defined inside a module.</p> <p>To drive the point home, imagine you want to validate a certain attribute only if a given condition is met. We could easily achieve it with the first solution, by manipulating the data structure accordingly, or with the second solution by using conditionals (if/else) before invoking the function. However, it is impossible to do so with the macros approach unless its DSL is augmented.</p> <p>In other words:</p> <div class="language-plaintext highlighter-rouge"><pre class="highlight" data-language="">data &gt; functions &gt; macros
</pre></div> <p>That said, there are still cases where using macros and modules to build domain-specific languages is useful. Since we have explored data structures and function definitions in the Getting Started guide, this chapter will explore how to use macros and module attributes to tackle more complex DSLs.</p> <h2 id="building-our-own-test-case">Building our own test case</h2> <p>The goal in this chapter is to build a module named <code class="language-plaintext highlighter-rouge">TestCase</code> that allows us to write the following:</p> <div class="language-elixir highlighter-rouge"><pre class="highlight" data-language="">defmodule MyTest do
  use TestCase

  test "arithmetic operations" do
    4 = 2 + 2
  end

  test "list operations" do
    [1, 2, 3] = [1, 2] ++ [3]
  end
end

MyTest.run
</pre></div> <p>In the example above, by using <code class="language-plaintext highlighter-rouge">TestCase</code>, we can write tests using the <code class="language-plaintext highlighter-rouge">test</code> macro, which defines a function named <code class="language-plaintext highlighter-rouge">run</code> to automatically run all tests for us. Our prototype will rely on the match operator (<code class="language-plaintext highlighter-rouge">=</code>) as a mechanism to do assertions.</p> <h2 id="the-test-macro">The <code class="language-plaintext highlighter-rouge">test</code> macro</h2> <p>Let’s start by creating a module that defines and imports the <code class="language-plaintext highlighter-rouge">test</code> macro when used:</p> <div class="language-elixir highlighter-rouge"><pre class="highlight" data-language="">defmodule TestCase do
  # Callback invoked by `use`.
  #
  # For now it returns a quoted expression that
  # imports the module itself into the user code.
  @doc false
  defmacro __using__(_opts) do
    quote do
      import TestCase
    end
  end

  @doc """
  Defines a test case with the given description.

  ## Examples

      test "arithmetic operations" do
        4 = 2 + 2
      end

  """
  defmacro test(description, do: block) do
    function_name = String.to_atom("test " &lt;&gt; description)
    quote do
      def unquote(function_name)(), do: unquote(block)
    end
  end
end
</pre></div> <p>Assuming we defined <code class="language-plaintext highlighter-rouge">TestCase</code> in a file named <code class="language-plaintext highlighter-rouge">tests.exs</code>, we can open it up by running <code class="language-plaintext highlighter-rouge">iex tests.exs</code> and define our first tests:</p> <div class="language-elixir highlighter-rouge"><pre class="highlight" data-language="">iex&gt; defmodule MyTest do
...&gt;   use TestCase
...&gt;
...&gt;   test "hello" do
...&gt;     "hello" = "world"
...&gt;   end
...&gt; end
</pre></div> <p>For now, we don’t have a mechanism to run tests, but we know that a function named “test hello” was defined behind the scenes. When we invoke it, it should fail:</p> <div class="language-elixir highlighter-rouge"><pre class="highlight" data-language="">iex&gt; MyTest."test hello"()
** (MatchError) no match of right hand side value: "world"
</pre></div> <h2 id="storing-information-with-attributes">Storing information with attributes</h2> <p>In order to finish our <code class="language-plaintext highlighter-rouge">TestCase</code> implementation, we need to be able to access all defined test cases. One way of doing this is by retrieving the tests at runtime via <code class="language-plaintext highlighter-rouge">__MODULE__.__info__(:functions)</code>, which returns a list of all functions in a given module. However, considering that we may want to store more information about each test besides the test name, a more flexible approach is required.</p> <p>When discussing module attributes in earlier chapters, we mentioned how they can be used as temporary storage. That’s exactly the property we will apply in this section.</p> <p>In the <code class="language-plaintext highlighter-rouge">__using__/1</code> implementation, we will initialize a module attribute named <code class="language-plaintext highlighter-rouge">@tests</code> to an empty list, then store the name of each defined test in this attribute so the tests can be invoked from the <code class="language-plaintext highlighter-rouge">run</code> function.</p> <p>Here is the updated code for the <code class="language-plaintext highlighter-rouge">TestCase</code> module:</p> <div class="language-elixir highlighter-rouge"><pre class="highlight" data-language="">defmodule TestCase do
  @doc false
  defmacro __using__(_opts) do
    quote do
      import TestCase

      # Initialize @tests to an empty list
      @tests []

      # Invoke TestCase.__before_compile__/1 before the module is compiled
      @before_compile TestCase
    end
  end

  @doc """
  Defines a test case with the given description.

  ## Examples

      test "arithmetic operations" do
        4 = 2 + 2
      end

  """
  defmacro test(description, do: block) do
    function_name = String.to_atom("test " &lt;&gt; description)
    quote do
      # Prepend the newly defined test to the list of tests
      @tests [unquote(function_name) | @tests]
      def unquote(function_name)(), do: unquote(block)
    end
  end

  # This will be invoked right before the target module is compiled
  # giving us the perfect opportunity to inject the `run/0` function
  @doc false
  defmacro __before_compile__(_env) do
    quote do
      def run do
        Enum.each @tests, fn name -&gt;
          IO.puts "Running #{name}"
          apply(__MODULE__, name, [])
        end
      end
    end
  end
end
</pre></div> <p>By starting a new IEx session, we can now define our tests and run them:</p> <div class="language-elixir highlighter-rouge"><pre class="highlight" data-language="">iex&gt; defmodule MyTest do
...&gt;   use TestCase
...&gt;
...&gt;   test "hello" do
...&gt;     "hello" = "world"
...&gt;   end
...&gt; end
iex&gt; MyTest.run
Running test hello
** (MatchError) no match of right hand side value: "world"
</pre></div> <p>Although we have overlooked some details, this is the main idea behind creating domain-specific modules in Elixir. Macros enable us to return quoted expressions that are executed in the caller, which we can then use to transform code and store relevant information in the target module via module attributes. Finally, callbacks such as <code class="language-plaintext highlighter-rouge">@before_compile</code> allow us to inject code into the module when its definition is complete.</p> <p>Besides <code class="language-plaintext highlighter-rouge">@before_compile</code>, there are other useful module attributes like <code class="language-plaintext highlighter-rouge">@on_definition</code> and <code class="language-plaintext highlighter-rouge">@after_compile</code>, which you can read more about in <a href="https://hexdocs.pm/elixir/Module.html">the docs for the <code class="language-plaintext highlighter-rouge">Module</code> module</a>. You can also find useful information about macros and the compilation environment in the documentation for the <a href="https://hexdocs.pm/elixir/Macro.html"><code class="language-plaintext highlighter-rouge">Macro</code> module</a> and <a href="https://hexdocs.pm/elixir/Macro.Env.html"><code class="language-plaintext highlighter-rouge">Macro.Env</code></a>.</p><div class="_attribution">
  <p class="_attribution-p">
    &copy; 2012 Plataformatec<br>Licensed under the Apache License, Version 2.0.<br>
    <a href="https://elixir-lang.org/getting-started/meta/domain-specific-languages.html" class="_attribution-link">https://elixir-lang.org/getting-started/meta/domain-specific-languages.html</a>
  </p>
</div>
