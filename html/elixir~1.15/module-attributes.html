<div class="getting-started-title"> <small>Getting Started</small> <h1>Module attributes</h1> </div>  <p>Module attributes in Elixir serve three purposes:</p> <ol> <li>They serve to annotate the module, often with information to be used by the user or the <abbr title="Virtual Machine">VM</abbr>.</li> <li>They work as constants.</li> <li>They work as a temporary module storage to be used during compilation.</li> </ol> <p>Let’s check each case, one by one.</p> <h2 id="as-annotations">As annotations</h2> <p>Elixir brings the concept of module attributes from Erlang. For example:</p> <div class="language-elixir highlighter-rouge"><pre class="highlight" data-language="">defmodule MyServer do
  @moduledoc "My server code."
end
</pre></div> <p>In the example above, we are defining the module documentation by using the module attribute syntax. Elixir has a handful of reserved attributes. Here are a few of them, the most commonly used ones:</p> <ul> <li>
<code class="language-plaintext highlighter-rouge">@moduledoc</code> - provides documentation for the current module.</li> <li>
<code class="language-plaintext highlighter-rouge">@doc</code> - provides documentation for the function or macro that follows the attribute.</li> <li>
<code class="language-plaintext highlighter-rouge">@spec</code> - provides a typespec for the function that follows the attribute.</li> <li>
<code class="language-plaintext highlighter-rouge">@behaviour</code> - (notice the British spelling) used for specifying an <abbr title="Open Telecom Platform">OTP</abbr> or user-defined behaviour.</li> </ul> <p><code class="language-plaintext highlighter-rouge">@moduledoc</code> and <code class="language-plaintext highlighter-rouge">@doc</code> are by far the most used attributes, and we expect you to use them a lot. Elixir treats documentation as first-class and provides many functions to access documentation. You can read more about <a href="https://hexdocs.pm/elixir/writing-documentation.html">writing documentation in Elixir in our official documentation</a>.</p> <p>Let’s go back to the <code class="language-plaintext highlighter-rouge">Math</code> module defined in the previous chapters, add some documentation and save it to the <code class="language-plaintext highlighter-rouge">math.ex</code> file:</p> <div class="language-elixir highlighter-rouge"><pre class="highlight" data-language="">defmodule Math do
  @moduledoc """
  Provides math-related functions.

  ## Examples

      iex&gt; Math.sum(1, 2)
      3

  """

  @doc """
  Calculates the sum of two numbers.
  """
  def sum(a, b), do: a + b
end
</pre></div> <p>Elixir promotes the use of Markdown with heredocs to write readable documentation. Heredocs are multi-line strings, they start and end with triple double-quotes, keeping the formatting of the inner text. We can access the documentation of any compiled module directly from IEx:</p> <div class="language-console highlighter-rouge"><pre class="highlight" data-language="">$ elixirc math.ex
$ iex
</pre></div> <div class="language-elixir highlighter-rouge"><pre class="highlight" data-language="">iex&gt; h Math # Access the docs for the module Math
...
iex&gt; h Math.sum # Access the docs for the sum function
...
</pre></div> <p>We also provide a tool called <a href="https://github.com/elixir-lang/ex_doc">ExDoc</a> which is used to generate HTML pages from the documentation.</p> <p>You can take a look at the docs for <a href="https://hexdocs.pm/elixir/Module.html">Module</a> for a complete list of supported attributes. Elixir also uses attributes to define <a href="typespecs-and-behaviours.html">typespecs</a>.</p> <p>This section covers built-in attributes. However, attributes can also be used by developers or extended by libraries to support custom behaviour.</p> <h2 id="as-constants">As “constants”</h2> <p>Elixir developers often use module attributes when they wish to make a value more visible or reusable:</p> <div class="language-elixir highlighter-rouge"><pre class="highlight" data-language="">defmodule MyServer do
  @initial_state %{host: "127.0.0.1", port: 3456}
  IO.inspect @initial_state
end
</pre></div> <p>Trying to access an attribute that was not defined will print a warning:</p> <div class="language-elixir highlighter-rouge"><pre class="highlight" data-language="">defmodule MyServer do
  @unknown
end
warning: undefined module attribute @unknown, please remove access to @unknown or explicitly set it before access
</pre></div> <p>Attributes can also be read inside functions:</p> <div class="language-elixir highlighter-rouge"><pre class="highlight" data-language="">defmodule MyServer do
  @my_data 14
  def first_data, do: @my_data
  @my_data 13
  def second_data, do: @my_data
end

MyServer.first_data #=&gt; 14
MyServer.second_data #=&gt; 13
</pre></div> <blockquote> <p>Note: do not add a newline between the attribute and its value, otherwise Elixir will assume you are reading the value, rather than setting it.</p> </blockquote> <p>Functions may be called when defining a module attribute:</p> <div class="language-elixir highlighter-rouge"><pre class="highlight" data-language="">defmodule MyApp.Status do
  @service URI.parse("https://example.com")
  def status(email) do
    SomeHttpClient.get(@service)
  end
end
</pre></div> <p>The function above will be called at compilation time and its <em>return value</em>, not the function call itself, is what will be substituted in for the attribute. So the above will effectively compile to this:</p> <div class="language-elixir highlighter-rouge"><pre class="highlight" data-language="">defmodule MyApp.Status do
  def status(email) do
    SomeHttpClient.get(%URI{
      authority: "example.com",
      host: "example.com",
      port: 443,
      scheme: "https"
    })
  end
end
</pre></div> <p>This can be useful for pre-computing constant values, but it can also cause problems if you’re expecting the function to be called at runtime. For example, if you are reading a value from a database or an environment variable inside an attribute, be aware that it will read that value only at compilation time. Be careful, however: <em>functions defined in the same module as the attribute itself cannot be called</em> because they have not yet been compiled when the attribute is being defined.</p> <p>Every time an attribute is read inside a function, Elixir takes a snapshot of its current value. Therefore if you read the same attribute multiple times inside multiple functions, you may end-up making multiple copies of it. That’s usually not an issue, but if you are using functions to compute large module attributes, that can slow down compilation. The solution is to move the attribute to shared function. For example, instead of this:</p> <div class="language-elixir highlighter-rouge"><pre class="highlight" data-language="">def some_function, do: do_something_with(@example)
def another_function, do: do_something_else_with(@example)
</pre></div> <p>Prefer this:</p> <div class="language-elixir highlighter-rouge"><pre class="highlight" data-language="">def some_function, do: do_something_with(example())
def another_function, do: do_something_else_with(example())
defp example, do: @example
</pre></div> <p>If <code class="language-plaintext highlighter-rouge">@example</code> is cheap to compute, it may be even better to skip the module attribute altogether, and compute its value inside the function.</p> <h3 id="accumulating-attributes">Accumulating attributes</h3> <p>Normally, repeating a module attribute will cause its value to be reassigned, but there are circumstances where you may want to <a href="https://hexdocs.pm/elixir/Module.html#register_attribute/3">configure the module attribute</a> so that its values are accumulated:</p> <div class="language-elixir highlighter-rouge"><pre class="highlight" data-language="">defmodule Foo do
  Module.register_attribute __MODULE__, :param, accumulate: true

  @param :foo
  @param :bar
  # here @param == [:bar, :foo]
end
</pre></div> <h2 id="as-temporary-storage">As temporary storage</h2> <p>To see an example of using module attributes as storage, look no further than Elixir’s unit test framework called <a href="https://hexdocs.pm/ex_unit/">ExUnit</a>. ExUnit uses module attributes for multiple different purposes:</p> <div class="language-elixir highlighter-rouge"><pre class="highlight" data-language="">defmodule MyTest do
  use ExUnit.Case, async: true

  @tag :external
  @tag os: :unix
  test "contacts external service" do
    # ...
  end
end
</pre></div> <p>In the example above, <code class="language-plaintext highlighter-rouge">ExUnit</code> stores the value of <code class="language-plaintext highlighter-rouge">async: true</code> in a module attribute to change how the module is compiled. Tags are also defined as <code class="language-plaintext highlighter-rouge">accumulate: true</code> attributes, and they store tags that can be used to setup and filter tests. For example, you can avoid running external tests on your machine because they are slow and dependent on other services, while they can still be enabled in your build system.</p> <p>In order to understand the underlying code, we’d need macros, so we will revisit this pattern in the meta-programming guide and learn how to use module attributes as storage to allow developers to create Domain Specific Languages (DSLs).</p> <p>In the next chapters, we’ll explore structs and protocols before moving to exception handling and other constructs like sigils and comprehensions.</p><div class="_attribution">
  <p class="_attribution-p">
    &copy; 2012 Plataformatec<br>Licensed under the Apache License, Version 2.0.<br>
    <a href="https://elixir-lang.org/getting-started/module-attributes.html" class="_attribution-link">https://elixir-lang.org/getting-started/module-attributes.html</a>
  </p>
</div>
