<h1 id="promises-package">Promises Package</h1> <p>A <code>Promise</code> represents a value that will be available at a later time. <code>Promise</code>s can either be fulfilled with a value or rejected. Any number of function handlers can be added to the <code>Promise</code>, to be called when the <code>Promise</code> is fulfilled or rejected. These handlers themselves are also wrapped in <code>Promise</code>s so that they can be chained together in order for the fulfilled value of one <code>Promise</code> to be used to compute a value which will be used to fulfill the next <code>Promise</code> in the chain, or so that if the <code>Promise</code> is rejected then the subsequent reject functions will also be called. The input and output types of a fulfill handler do not have to be the same, so a chain of fulfill handlers can transform the original value into something new.</p> <p>Fulfill and reject handlers can either be specified as classes that implment the <code>Fulfill</code> and <code>Reject</code> interfaces, or as functions with the same signatures as the <code>apply</code> methods in <code>Fulfill</code> and <code>Reject</code>.</p> <p>In the following code, the fulfillment of the <code>Promise</code> causes the execution of several fulfillment functions. The output is:</p> <pre data-language="pony">fulfilled + foo
fulfilled + bar
fulfilled + baz
</pre> <pre data-language="pony">use "promises"

class PrintFulfill is Fulfill[String, String]
  let _env: Env
  let _msg: String
  new create(env: Env, msg: String) =&gt;
    _env = env
    _msg = msg
  fun apply(s: String): String =&gt;
    _env.out.print(" + ".join([s; _msg].values()))
    s

actor Main
  new create(env: Env) =&gt;
     let promise = Promise[String]
     promise.next[String](recover PrintFulfill(env, "foo") end)
     promise.next[String](recover PrintFulfill(env, "bar") end)
     promise.next[String](recover PrintFulfill(env, "baz") end)
     promise("fulfilled")
</pre> <p>In the following code, the fulfill functions are chained together so that the fulfilled value of the first one is used to generate a value which fulfills the second one, which in turn is used to compute a value which fulfills the third one, which in turn is used to compute a value which fulfills the fourth one. The output is the average length of the words passed on the command line or <code>0</code> if there are no command line arguments.</p> <pre data-language="pony">use "promises"

primitive Computation
  fun tag string_to_strings(s: String): Array[String] val =&gt;
    recover s.split() end
  fun tag strings_to_sizes(sa: Array[String] val): Array[USize] val =&gt;
    recover
      let len = Array[USize]
      for s in sa.values() do
        len.push(s.size())
      end
      len
    end
  fun tag sizes_to_avg(sza: Array[USize] val): USize =&gt;
    var acc = USize(0)
    for sz in sza.values() do
      acc = acc + sz
    end
    acc / sza.size()
  fun tag output(env: Env, sz: USize): None =&gt;
    env.out.print(sz.string())

actor Main
  new create(env: Env) =&gt;
     let promise = Promise[String]
     promise.next[Array[String] val](recover Computation~string_to_strings() end)
            .next[Array[USize] val](recover Computation~strings_to_sizes() end)
            .next[USize](recover Computation~sizes_to_avg() end)
            .next[None](recover Computation~output(env) end)
     promise(" ".join(env.args.slice(1).values()))
</pre> <h2 id="public-types">Public Types</h2> <ul> <li><a href="promises-promise.html">actor Promise</a></li> <li><a href="promises-promises.html">primitive Promises</a></li> <li><a href="promises-fulfill.html">interface Fulfill</a></li> <li><a href="promises-reject.html">interface Reject</a></li> <li><a href="promises-fulfillidentity.html">class FulfillIdentity</a></li> <li><a href="promises-rejectalways.html">class RejectAlways</a></li> </ul><div class="_attribution">
  <p class="_attribution-p">
    &copy; 2016-2020, The Pony Developers<br>&copy; 2014-2015, Causality Ltd.<br>Licensed under the BSD 2-Clause License.<br>
    <a href="https://stdlib.ponylang.io/promises--index" class="_attribution-link">https://stdlib.ponylang.io/promises--index</a>
  </p>
</div>
