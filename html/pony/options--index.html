<h1 id="options-package">Options package</h1> <p>The Options package provides support for parsing command line arguments.</p> <h2 id="deprectation-warning">Deprectation warning</h2> <p>This package is deprecated and will be removed in a future release. Use the <a href="cli--index.html">cli</a> package instead.</p> <h2 id="example-program">Example program</h2> <pre data-language="pony">use "options"

actor Main
  let _env: Env
  // Some values we can set via command line options
  var _a_string: String = "default"
  var _a_number: USize = 0
  var _a_unumber: USize = 0
  var _a_float: Float = F64(0.0)

  new create(env: Env) =&gt;
    _env = env
    try
      arguments()?
    end

    _env.out.print("The String is " + _a_string)
    _env.out.print("The Number is " + _a_number.string())
    _env.out.print("The UNumber is " + _a_unumber.string())
    _env.out.print("The Float is " + _a_float.string())

  fun ref arguments() ? =&gt;
    var options = Options(_env.args)

    options
      .add("string", "t", StringArgument)
      .add("number", "i", I64Argument)
      .add("unumber", "u", U64Argument)
      .add("float", "c", F64Argument)

    for option in options do
      match option
      | ("string", let arg: String) =&gt; _a_string = arg
      | ("number", let arg: I64) =&gt; _a_number = arg.usize()
      | ("unumber", let arg: U64) =&gt; _a_unumber = arg.usize()
      | ("float", let arg: F64) =&gt; _a_float = arg
      | let err: ParseError =&gt; err.report(_env.out) ; usage() ; error
      end
    end

  fun ref usage() =&gt;
    // this exists inside a doc-string to create the docs you are reading
    // in real code, we would use a single string literal for this but
    // docstrings are themselves string literals and you can't put a
    // string literal in a string literal. That would lead to total
    // protonic reversal. In your own code, use a string literal instead
    // of string concatenation for this.
    _env.out.print(
      "program [OPTIONS]\n" +
      "  --string      N   a string argument. Defaults to 'default'.\n" +
      "  --number      N   a number argument. Defaults to 0.\n" +
      "  --unumber     N   a unsigned number argument. Defaults to 0.\n" +
      "  --float       N   a floating point argument. Defaults to 0.0.\n"
      )
</pre> <h2 id="public-types">Public Types</h2> <ul> <li><a href="options-stringargument.html">primitive StringArgument</a></li> <li><a href="options-i64argument.html">primitive I64Argument</a></li> <li><a href="options-u64argument.html">primitive U64Argument</a></li> <li><a href="options-f64argument.html">primitive F64Argument</a></li> <li><a href="options-required.html">primitive Required</a></li> <li><a href="options-optional.html">primitive Optional</a></li> <li><a href="options-unrecognisedoption.html">primitive UnrecognisedOption</a></li> <li><a href="options-ambiguousmatch.html">primitive AmbiguousMatch</a></li> <li><a href="options-missingargument.html">primitive MissingArgument</a></li> <li><a href="options-invalidargument.html">primitive InvalidArgument</a></li> <li><a href="options-argumenttype.html">type ArgumentType</a></li> <li><a href="options-errorreason.html">type ErrorReason</a></li> <li><a href="options-parsedoption.html">type ParsedOption</a></li> <li><a href="options-parseerror.html">interface ParseError</a></li> <li><a href="options-options.html">class Options</a></li> <li><a href="options-envvars.html">primitive EnvVars</a></li> </ul><div class="_attribution">
  <p class="_attribution-p">
    &copy; 2016-2020, The Pony Developers<br>&copy; 2014-2015, Causality Ltd.<br>Licensed under the BSD 2-Clause License.<br>
    <a href="https://stdlib.ponylang.io/options--index" class="_attribution-link">https://stdlib.ponylang.io/options--index</a>
  </p>
</div>
