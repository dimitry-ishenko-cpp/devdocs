<h1 id="process-package">Process package</h1> <p>The Process package provides support for handling Unix style processes. For each external process that you want to handle, you need to create a <code>ProcessMonitor</code> and a corresponding <code>ProcessNotify</code> object. Each ProcessMonitor runs as it own actor and upon receiving data will call its corresponding <code>ProcessNotify</code>'s method.</p> <h2 id="example-program">Example program</h2> <p>The following program will spawn an external program and write to it's STDIN. Output received on STDOUT of the child process is forwarded to the ProcessNotify client and printed.</p> <pre data-language="pony">use "process"
use "files"

actor Main
  new create(env: Env) =&gt;
    // create a notifier
    let client = ProcessClient(env)
    let notifier: ProcessNotify iso = consume client
    // define the binary to run
    try
      let path = FilePath(env.root as AmbientAuth, "/bin/cat")?
      // define the arguments; first arg is always the binary name
      let args: Array[String] val = ["cat"]
      // define the environment variable for the execution
      let vars: Array[String] val = ["HOME=/"; "PATH=/bin"]
      // create a ProcessMonitor and spawn the child process
      let auth = env.root as AmbientAuth
      let pm: ProcessMonitor = ProcessMonitor(auth, auth, consume notifier,
      path, args, vars)
      // write to STDIN of the child process
      pm.write("one, two, three")
      pm.done_writing() // closing stdin allows cat to terminate
    else
      env.out.print("Could not create FilePath!")
    end

// define a client that implements the ProcessNotify interface
class ProcessClient is ProcessNotify
  let _env: Env

  new iso create(env: Env) =&gt;
    _env = env

  fun ref stdout(process: ProcessMonitor ref, data: Array[U8] iso) =&gt;
    let out = String.from_array(consume data)
    _env.out.print("STDOUT: " + out)

  fun ref stderr(process: ProcessMonitor ref, data: Array[U8] iso) =&gt;
    let err = String.from_array(consume data)
    _env.out.print("STDERR: " + err)

  fun ref failed(process: ProcessMonitor ref, err: ProcessError) =&gt;
    _env.out.print(err.string())

  fun ref dispose(process: ProcessMonitor ref, child_exit_status: ProcessExitStatus) =&gt;
    let code: I32 = consume child_exit_code
    match child_exit_status
    | let exited: Exited =&gt;
      _env.out.print("Child exit code: " + exited.exit_code().string())
    | let signaled: Signaled =&gt;
      _env.out.print("Child terminated by signal: " + signaled.signal().string())
    end
</pre> <h2 id="process-portability">Process portability</h2> <p>The ProcessMonitor supports spawning processes on Linux, FreeBSD, OSX and Windows.</p> <h2 id="shutting-down-processmonitor-and-external-process">Shutting down ProcessMonitor and external process</h2> <p>When a process is spawned using ProcessMonitor, and it is not necessary to communicate to it any further using <code>stdin</code> and <code>stdout</code> or <code>stderr</code>, calling <a href="process-processmonitor.html#done_writing">done_writing()</a> will close stdin to the child process. Processes expecting input will be notified of an <code>EOF</code> on their <code>stdin</code> and can terminate.</p> <p>If a running program needs to be canceled and the <a href="process-processmonitor.html">ProcessMonitor</a> should be shut down, calling <a href="process-processmonitor.html#dispose">dispose</a> will terminate the child process and clean up all resources.</p> <p>Once the child process is detected to be closed, the process exit status is retrieved and <a href="process-processnotify.html#dispose">ProcessNotify.dispose</a> is called.</p> <p>The process exit status can be either an instance of <a href="process-exited.html">Exited</a> containing the process exit code in case the program exited on its own, or (only on posix systems like linux, osx or bsd) an instance of <a href="process-signaled.html">Signaled</a> containing the signal number that terminated the process.</p> <h2 id="public-types">Public Types</h2> <ul> <li><a href="process-processnotify.html">interface ProcessNotify</a></li> <li><a href="process-processmonitorauth.html">type ProcessMonitorAuth</a></li> <li><a href="process-processmonitor.html">actor ProcessMonitor</a></li> <li><a href="process-processerror.html">class ProcessError</a></li> <li><a href="process-processerrortype.html">type ProcessErrorType</a></li> <li><a href="process-execveerror.html">primitive ExecveError</a></li> <li><a href="process-pipeerror.html">primitive PipeError</a></li> <li><a href="process-forkerror.html">primitive ForkError</a></li> <li><a href="process-waitpiderror.html">primitive WaitpidError</a></li> <li><a href="process-writeerror.html">primitive WriteError</a></li> <li><a href="process-killerror.html">primitive KillError</a></li> <li><a href="process-caperror.html">primitive CapError</a></li> <li><a href="process-chdirerror.html">primitive ChdirError</a></li> <li><a href="process-unknownerror.html">primitive UnknownError</a></li> <li><a href="process-startprocessauth.html">primitive StartProcessAuth</a></li> <li><a href="process-exited.html">class Exited</a></li> <li><a href="process-signaled.html">class Signaled</a></li> <li><a href="process-processexitstatus.html">type ProcessExitStatus</a></li> </ul><div class="_attribution">
  <p class="_attribution-p">
    &copy; 2016-2020, The Pony Developers<br>&copy; 2014-2015, Causality Ltd.<br>Licensed under the BSD 2-Clause License.<br>
    <a href="https://stdlib.ponylang.io/process--index" class="_attribution-link">https://stdlib.ponylang.io/process--index</a>
  </p>
</div>
