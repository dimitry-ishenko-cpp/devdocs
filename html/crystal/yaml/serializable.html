<h1 class="type-name"> module YAML::<wbr>Serializable </wbr>
</h1> <h2>  Overview </h2> <p>The <code><a href="serializable.html">YAML::Serializable</a></code> module automatically generates methods for YAML serialization when included.</p> <h3>
Example</h3> <pre data-language="crystal">require "yaml"

class Location
  include YAML::Serializable

  @[YAML::Field(key: "lat")]
  property latitude : Float64

  @[YAML::Field(key: "lng")]
  property longitude : Float64
end

class House
  include YAML::Serializable
  property address : String
  property location : Location?
end

house = House.from_yaml(%({"address": "Crystal Road 1234", "location": {"lat": 12.3, "lng": 34.5}}))
house.address  # =&gt; "Crystal Road 1234"
house.location # =&gt; #&lt;Location:0x10cd93d80 @latitude=12.3, @longitude=34.5&gt;
house.to_yaml  # =&gt; "---\naddress: Crystal Road 1234\nlocation:\n  lat: 12.3\n  lng: 34.5\n"

houses = Array(House).from_yaml("---\n- address: Crystal Road 1234\n  location:\n    lat: 12.3\n    lng: 34.5\n")
houses.size    # =&gt; 1
houses.to_yaml # =&gt; "---\n- address: Crystal Road 1234\n  location:\n    lat: 12.3\n    lng: 34.5\n"</pre> <h3>
Usage</h3> <p>Including <code><a href="serializable.html">YAML::Serializable</a></code> will create <code><a href="serializable.html#to_yaml%28yaml%3AYAML%3A%3ANodes%3A%3ABuilder%29-instance-method">#to_yaml</a></code> and <code>self.from_yaml</code> methods on the current class, and a constructor which takes a <code><a href="pullparser.html">YAML::PullParser</a></code>. By default, these methods serialize into a yaml object containing the value of every instance variable, the keys being the instance variable name. Most primitives and collections supported as instance variable values (string, integer, array, hash, etc.), along with objects which define to_yaml and a constructor taking a <code><a href="pullparser.html">YAML::PullParser</a></code>. Union types are also supported, including unions with nil. If multiple types in a union parse correctly, it is undefined which one will be chosen.</p> <p>To change how individual instance variables are parsed and serialized, the annotation <code><a href="field.html">YAML::Field</a></code> can be placed on the instance variable. Annotating property, getter and setter macros is also allowed.</p> <pre data-language="crystal">require "yaml"

class A
  include YAML::Serializable

  @[YAML::Field(key: "my_key", emit_null: true)]
  getter a : Int32?
end</pre> <p><code><a href="field.html">YAML::Field</a></code> properties:</p> <ul> <li>
<strong>ignore</strong>: if <code>true</code> skip this field in serialization and deserialization (by default false)</li> <li>
<strong>ignore_serialize</strong>: If truthy, skip this field in serialization (default: <code>false</code>). The value can be any Crystal expression and is evaluated at runtime.</li> <li>
<strong>ignore_deserialize</strong>: if <code>true</code> skip this field in deserialization (by default false)</li> <li>
<strong>key</strong>: the value of the key in the yaml object (by default the name of the instance variable)</li> <li>
<strong>converter</strong>: specify an alternate type for parsing and generation. The converter must define <code>from_yaml(<a href="parsecontext.html">YAML::ParseContext</a>, <a href="nodes/node.html">YAML::Nodes::Node</a>)</code> and <code>to_yaml(value, <a href="nodes/builder.html">YAML::Nodes::Builder</a>)</code>. Examples of converters are a <code><a href="../time/format.html">Time::Format</a></code> instance and <code><a href="../time/epochconverter.html">Time::EpochConverter</a></code> for <code><a href="../time.html">Time</a></code>.</li> <li>
<strong>presence</strong>: if <code>true</code>, a <code>@{{key}}_present</code> instance variable will be generated when the key was present (even if it has a <code>null</code> value), <code>false</code> by default</li> <li>
<strong>emit_null</strong>: if <code>true</code>, emits a <code>null</code> value for nilable property (by default nulls are not emitted)</li> </ul> <p>Deserialization also respects default values of variables:</p> <pre data-language="crystal">require "yaml"

struct A
  include YAML::Serializable
  @a : Int32
  @b : Float64 = 1.0
end

A.from_yaml("---\na: 1\n") # =&gt; A(@a=1, @b=1.0)</pre> <h3>
Extensions: <code><a href="serializable/strict.html">YAML::Serializable::Strict</a></code> and <code><a href="serializable/unmapped.html">YAML::Serializable::Unmapped</a></code>.</h3> <p>If the <code><a href="serializable/strict.html">YAML::Serializable::Strict</a></code> module is included, unknown properties in the YAML document will raise a parse exception. By default the unknown properties are silently ignored. If the <code><a href="serializable/unmapped.html">YAML::Serializable::Unmapped</a></code> module is included, unknown properties in the YAML document will be stored in a <code><a href="../hash.html">Hash</a>(<a href="../string.html">String</a>, <a href="any.html">YAML::Any</a>)</code>. On serialization, any keys inside yaml_unmapped will be serialized appended to the current yaml object.</p> <pre data-language="crystal">require "yaml"

struct A
  include YAML::Serializable
  include YAML::Serializable::Unmapped
  @a : Int32
end

a = A.from_yaml("---\na: 1\nb: 2\n") # =&gt; A(@yaml_unmapped={"b" =&gt; 2}, @a=1)
a.yaml_unmapped["b"].raw.class       # =&gt; Int64
a.to_yaml                            # =&gt; "---\na: 1\nb: 2\n"</pre> <h3>
Class annotation <code><a href="serializable/options.html">YAML::Serializable::Options</a></code>
</h3> <p>supported properties:</p> <ul> <li>
<strong>emit_nulls</strong>: if <code>true</code>, emits a <code>null</code> value for all nilable properties (by default nulls are not emitted)</li> </ul> <pre data-language="crystal">require "yaml"

@[YAML::Serializable::Options(emit_nulls: true)]
class A
  include YAML::Serializable
  @a : Int32?
end</pre> <h3>
Discriminator field</h3> <p>A very common YAML serialization strategy for handling different objects under a same hierarchy is to use a discriminator field. For example in <a href="https://tools.ietf.org/html/rfc7946">GeoJSON</a> each object has a "type" field, and the rest of the fields, and their meaning, depend on its value.</p> <p>You can use <code><a href="serializable.html#use_yaml_discriminator%28field%2Cmapping%29-macro">YAML::Serializable.use_yaml_discriminator</a></code> for this use case.</p> <h3>
<code>after_initialize</code> method</h3> <p><code>#after_initialize</code> is a method that runs after an instance is deserialized from YAML. It can be used as a hook to post-process the initialized object.</p> <p>Example:</p> <pre data-language="crystal">require "yaml"

class Person
  include YAML::Serializable
  getter name : String

  def after_initialize
    @name = @name.upcase
  end
end

person = Person.from_yaml "---\nname: Jane\n"
person.name # =&gt; "JANE"</pre> <h2>  Defined in: </h2> <a href="https://github.com/crystal-lang/crystal/blob/0cef61e51/src/yaml/serialization.cr#L151" target="_blank"> yaml/serialization.cr </a> <br> <h2>  Constructors </h2> <ul class="list-summary"> <li class="entry-summary"> <a href="#new%28%2A%2C__context_for_yaml_serializablectx%3AYAML%3A%3AParseContext%2C__node_for_yaml_serializablenode%3AYAML%3A%3ANodes%3A%3ANode%29-class-method" class="signature"><strong>.new</strong>(*, __context_for_yaml_serializable ctx : YAML::ParseContext, __node_for_yaml_serializable node : YAML::Nodes::Node)</a> </li> </ul> <h2>  Macro Summary </h2> <ul class="list-summary"> <li class="entry-summary"> <a href="#use_yaml_discriminator%28field%2Cmapping%29-macro" class="signature"><strong>use_yaml_discriminator</strong>(field, mapping)</a> <div class="summary"><p>Tells this class to decode YAML by using a field as a discriminator.</p></div> </li> </ul> <h2>  Instance Method Summary </h2> <ul class="list-summary"> <li class="entry-summary"> <a href="#to_yaml%28yaml%3AYAML%3A%3ANodes%3A%3ABuilder%29-instance-method" class="signature"><strong>#to_yaml</strong>(yaml : YAML::Nodes::Builder)</a> </li> </ul>  <h2>  Constructor Detail </h2> <div class="entry-detail" id="new(*,__context_for_yaml_serializablectx:YAML::ParseContext,__node_for_yaml_serializablenode:YAML::Nodes::Node)-class-method"> <h3 class="signature">def self.<strong>new</strong>(*, __context_for_yaml_serializable ctx : <a href="parsecontext.html">YAML::ParseContext</a>, __node_for_yaml_serializable node : <a href="nodes/node.html">YAML::Nodes::Node</a>)<a href="https://github.com/crystal-lang/crystal/blob/0cef61e51/src/yaml/serialization.cr#L187" target="_blank" class="view-source">Source</a>
</h3>   </div> <h2>  Macro Detail </h2> <div class="entry-detail" id="use_yaml_discriminator(field,mapping)-macro"> <h3 class="signature">macro <strong>use_yaml_discriminator</strong>(field, mapping)<a href="https://github.com/crystal-lang/crystal/blob/0cef61e51/src/yaml/serialization.cr#L407" target="_blank" class="view-source">Source</a>
</h3> <div class="doc"> <p>Tells this class to decode YAML by using a field as a discriminator.</p> <ul> <li>
<em>field</em> must be the field name to use as a discriminator</li> <li>
<em>mapping</em> must be a hash or named tuple where each key-value pair maps a discriminator value to a class to deserialize</li> </ul> <p>For example:</p> <pre data-language="crystal">require "yaml"

abstract class Shape
  include YAML::Serializable

  use_yaml_discriminator "type", {point: Point, circle: Circle}

  property type : String
end

class Point &lt; Shape
  property x : Int32
  property y : Int32
end

class Circle &lt; Shape
  property x : Int32
  property y : Int32
  property radius : Int32
end

Shape.from_yaml(%(
  type: point
  x: 1
  y: 2
)) # =&gt; #&lt;Point:0x10373ae20 @type="point", @x=1, @y=2&gt;

Shape.from_yaml(%(
  type: circle
  x: 1
  y: 2
  radius: 3
)) # =&gt; #&lt;Circle:0x106a4cea0 @type="circle", @x=1, @y=2, @radius=3&gt;</pre> </div>   </div> <h2>  Instance Method Detail </h2> <div class="entry-detail" id="to_yaml(yaml:YAML::Nodes::Builder)-instance-method"> <h3 class="signature">def <strong>to_yaml</strong>(yaml : <a href="nodes/builder.html">YAML::Nodes::Builder</a>)<a href="https://github.com/crystal-lang/crystal/blob/0cef61e51/src/yaml/serialization.cr#L286" target="_blank" class="view-source">Source</a>
</h3>   </div><div class="_attribution">
  <p class="_attribution-p">
    &copy; 2012&ndash;2024 Manas Technology Solutions.<br>Licensed under the Apache License, Version 2.0.<br>
    <a href="https://crystal-lang.org/api/1.13.1/YAML/Serializable.html" class="_attribution-link">https://crystal-lang.org/api/1.13.1/YAML/Serializable.html</a>
  </p>
</div>
