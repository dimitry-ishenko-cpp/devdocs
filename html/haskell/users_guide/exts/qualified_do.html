<h1 id="id1">6.2.5. Qualified do-notation</h1>
<div class="_sphinx"> <div itemprop="articleBody"> <section id="qualified-do-notation">  <dl class="std extension" id="index-0"> <dt class="sig sig-object std" id="extension-QualifiedDo">
<code>QualifiedDo</code> </dt> <dd>
<dl class="field-list simple"> <dt class="field-odd">Since<span class="colon">:</span>
</dt> <dd class="field-odd">
<p>9.0.1</p> </dd> </dl> <p>Allow the use of qualified <code>do</code> notation.</p> </dd>
</dl> <p><code>QualifiedDo</code> enables qualifying a <code>do</code> block with a module name, to control which operations to use for the monadic combinators that the <code>do</code> notation desugars to. When <code>-XQualifiedDo</code> is enabled, you can <em>qualify</em> the <code>do</code> notation by writing <code>modid.do</code>, where <code>modid</code> is a module name in scope:</p> <pre data-language="haskell">{-# LANGUAGE QualifiedDo #-}
import qualified Some.Module.Monad as M

action :: M.SomeType a
action = M.do x &lt;- u
              res
              M.return x
</pre> <p>The additional module name (here <code>M</code>) is called the qualifier of the do-expression.</p> <p>The unqualified <code>do</code> syntax is convenient for writing monadic code, but it only works for data types that provide an instance of the <code>Monad</code> type class. There are other types which are “monad-like” but can’t provide an instance of <code>Monad</code> (e.g. indexed monads, graded monads or relative monads), yet they could still use the <code>do</code> syntax if it weren’t hardwired to the methods of the <code>Monad</code> type class. <code>-XQualifiedDo</code> comes to make the do syntax customizable in this respect. It allows you to mix and match <code>do</code> blocks of different types with suitable operations to use on each case:</p> <pre data-language="haskell">{-# LANGUAGE QualifiedDo #-}
import qualified Control.Monad.Linear as L

import MAC (label, box, runMAC)
import qualified MAC as MAC

f :: IO ()
f = do
  x &lt;- runMAC $           -- (Prelude.&gt;&gt;=)
                          --   (runMAC $
    MAC.do                --
      d &lt;- label "y"      --     label "y" MAC.&gt;&gt;= \d -&gt;
      box $               --
                          --       (box $
        L.do              --
          r &lt;- L.f d      --         L.f d L.&gt;&gt;= \r -&gt;
          L.g r           --         L.g r L.&gt;&gt;
          L.return r      --         L.return r
                          --       ) MAC.&gt;&gt;
      MAC.return d        --       (MAC.return d)
                          --   )
  print x                 --   (\x -&gt; print x)
</pre> <p>The semantics of <code>do</code> notation statements with <code>-XQualifiedDo</code> is as follows:</p> <ul> <li>
<p>The <code>x &lt;- u</code> statement uses <code>(M.&gt;&gt;=)</code></p> <pre data-language="haskell">M.do { x &lt;- u; stmts }  =  u M.&gt;&gt;= \x -&gt; M.do { stmts }
</pre> </li> <li>
<p>The <code>u</code> statement uses <code>(M.&gt;&gt;)</code></p> <pre data-language="haskell">M.do { u; stmts }  =  u M.&gt;&gt; M.do { stmts }
</pre> </li> <li>
<p>The a <code>pat &lt;- u</code> statement uses <code>M.fail</code> for the failing case, if such a case is needed</p> <pre data-language="haskell">M.do { pat &lt;- u; stmts }  =  u M.&gt;&gt;= \case
  { pat -&gt; M.do { stmts }
  ; _ -&gt; M.fail "…"
  }
</pre> <p>If the pattern cannot fail, then we don’t need to use <code>M.fail</code>.</p> <pre data-language="haskell">M.do { pat &lt;- u; stmts }  =  u M.&gt;&gt;= \case pat -&gt; M.do { stmts }
</pre> </li> <li>
<p>The desugaring of <code>-XApplicativeDo</code> uses <code>M.fmap</code>, <code>(M.&lt;*&gt;)</code>, and <code>M.join</code> (after the applicative-do grouping has been performed)</p> <pre data-language="haskell">M.do { (x1 &lt;- u1 | … | xn &lt;- un); M.return e }  =
  (\x1 … xn -&gt; e) `M.fmap` u1 M.&lt;*&gt; … M.&lt;*&gt; un

M.do { (x1 &lt;- u1 | … | xn &lt;- un); stmts }  =
  M.join ((\x1 … xn -&gt; M.do { stmts }) `M.fmap` u1 M.&lt;*&gt; … M.&lt;*&gt; un)
</pre> </li> </ul>  <p>Note that <code>M.join</code> is only needed if the final expression is not identifiably a <code>return</code>. With <code>-XQualifiedDo</code> enabled, <code>-XApplicativeDo</code> looks only for the qualified <code>return</code>/<code>pure</code> in a qualified do-block.</p>  <ul> <li>
<p>With <code>-XRecursiveDo</code>, <code>rec</code> and <code>mdo</code> blocks use <code>M.mfix</code> and <code>M.return</code>:</p> <pre data-language="haskell">M.do { rec { x1 &lt;- u1; … ; xn &lt;- un }; stmts }  =
  M.do
  { (x1, …, xn) &lt;- M.mfix (\~(x1, …, xn) -&gt; M.do { x1 &lt;- u1; …; xn &lt;- un; M.return (x1, …, xn)})
  ; stmts
  }
</pre> </li> </ul> <p>If a name <code>M.op</code> is required by the desugaring process (and only if it’s required!) but the name is not in scope, it is reported as an error.</p> <p>The types of the operations picked for desugaring must produce an expression which is accepted by the typechecker. But other than that, there are no specific requirements on the types.</p> <p>If no qualifier is specified with <code>-XQualifiedDo</code> enabled, it defaults to the operations defined in the Prelude, or, if <code>-XRebindableSyntax</code> is enabled, to whatever operations are in scope.</p> <p>Note that the operations to be qualified must be in scope for QualifiedDo to work. I.e. <code>import MAC (label)</code> in the example above would result in an error, since <code>MAC.&gt;&gt;=</code> and <code>MAC.&gt;&gt;</code> would not be in scope.</p> <section id="examples"> <h2>
<span class="section-number">6.2.5.1. </span>Examples</h2> <p><code>-XQualifiedDo</code> does not affect <code>return</code> in the monadic <code>do</code> notation.</p> <pre data-language="haskell">import qualified Some.Monad.M as M

boolM :: (a -&gt; M.M Bool) -&gt; b -&gt; b -&gt; a -&gt; M.M b
boolM p a b x = M.do
    px &lt;- p x     -- M.&gt;&gt;=
    if px then
      return b    -- Prelude.return
    else
      M.return a  -- M.return
</pre> <p><code>-XQualifiedDo</code> does not affect explicit <code>(&gt;&gt;=)</code> in the monadic <code>do</code> notation.</p> <pre data-language="haskell">import qualified Some.Monad.M as M
import Data.Bool (bool)

boolMM :: (a -&gt; M.M Bool) -&gt; M b -&gt; M b -&gt; a -&gt; M.M b
boolMM p ma mb x = M.do
    p x &gt;&gt;= bool ma mb   -- Prelude.&gt;&gt;=
</pre> <p>Nested <code>do</code> blocks do not affect each other’s meanings.</p> <pre data-language="haskell">import qualified Some.Monad.M as M

f :: M.M SomeType
f = M.do
    x &lt;- f1                 -- M.&gt;&gt;=
    f2 (do y &lt;- g1          -- Prelude.&gt;&gt;=
           g2 x y)
  where
    f1 = ...
    f2 m = ...
    g1 = ...
    g2 x y = ...
</pre> <p>The type of <code>(&gt;&gt;=)</code> can also be modified, as seen here for a graded monad:</p> <pre data-language="haskell">{-# LANGUAGE ConstraintKinds #-}
{-# LANGUAGE PolyKinds #-}
{-# LANGUAGE TypeFamilies #-}
module Control.Monad.Graded (GradedMonad(..)) where

import Data.Kind (Constraint)

class GradedMonad (m :: k -&gt; * -&gt; *) where
  type Unit m :: k
  type Plus m (i :: k) (j :: k) :: k
  type Inv  m (i :: k) (j :: k) :: Constraint
  (&gt;&gt;=) :: Inv m i j =&gt; m i a -&gt; (a -&gt; m j b) -&gt; m (Plus m i j) b
  return :: a -&gt; m (Unit m) a

-----------------

module M where

import Control.Monad.Graded as Graded

g :: GradedMonad m =&gt; a -&gt; m SomeTypeIndex b
g a = Graded.do
  b &lt;- someGradedFunction a Graded.&gt;&gt;= someOtherGradedFunction
  c &lt;- anotherGradedFunction b
  Graded.return c
</pre> </section> </section> </div> </div><div class="_attribution">
  <p class="_attribution-p">
    &copy; 2002&ndash;2007 The University Court of the University of Glasgow. All rights reserved.<br>Licensed under the Glasgow Haskell Compiler License.<br>
    <a href="https://downloads.haskell.org/~ghc/9.12.1/docs/users_guide/exts/qualified_do.html" class="_attribution-link">https://downloads.haskell.org/~ghc/9.12.1/docs/users_guide/exts/qualified_do.html</a>
  </p>
</div>
