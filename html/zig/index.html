<h1>Zig Language Reference</h1> <h2 id="Introduction">Introduction </h2> <p> <a href="https://ziglang.org">Zig</a> is a general-purpose programming language and toolchain for maintaining <strong>robust</strong>, <strong>optimal</strong>, and <strong>reusable</strong> software. </p> <dl> <dt>Robust</dt>
<dd>Behavior is correct even for edge cases such as out of memory.</dd> <dt>Optimal</dt>
<dd>Write programs the best way they can behave and perform.</dd> <dt>Reusable</dt>
<dd>The same code works in many environments which have different constraints.</dd> <dt>Maintainable</dt>
<dd>Precisely communicate intent to the compiler and other programmers. The language imposes a low overhead to reading code and is resilient to changing requirements and environments.</dd> </dl> <p> Often the most efficient way to learn something new is to see examples, so this documentation shows how to use each of Zig's features. It is all on one page so you can search with your browser's search tool. </p> <p> The code samples in this document are compiled and tested as part of the main test suite of Zig. </p> <p> This HTML document depends on no external files, so you can use it offline. </p> <h2 id="Zig-Standard-Library">Zig Standard Library </h2> <p> The <a href="https://ziglang.org/documentation/master/std/">Zig Standard Library</a> has its own documentation. </p> <p> Zig's Standard Library contains commonly used algorithms, data structures, and definitions to help you build programs or libraries. You will see many examples of Zig's Standard Library used in this documentation. To learn more about the Zig Standard Library, visit the link above. </p> <h2 id="Hello-World">Hello World </h2> <figure><figcaption class="zig-cap"><cite class="file">hello.zig</cite></figcaption><pre data-language="zig">const std = @import("std");

pub fn main() !void {
    const stdout = std.io.getStdOut().writer();
    try stdout.print("Hello, {s}!\n", .{"world"});
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig build-exe hello.zig</kbd>
$ <kbd>./hello</kbd>
Hello, world!
</samp></pre></figure> <p> The Zig code sample above demonstrates one way to create a program that will output: <samp>Hello, world!</samp>. </p> <p> The code sample shows the contents of a file named <code class="file">hello.zig</code>. Files storing Zig source code are <a href="#Source-Encoding">UTF-8 encoded</a> text files. The files storing Zig source code must be named with the <code class="file"><em>.zig</em></code> extension. </p> <p> Following the <code class="file">hello.zig</code> Zig code sample, the <a href="#Zig-Build-System">Zig Build System</a> is used to build an executable program from the <code class="file">hello.zig</code> source code. Then, the <code class="file">hello</code> program is executed showing its output <samp>Hello, world!</samp>. The lines beginning with <samp>$</samp> represent command line prompts and a command. Everything else is program output. </p> <p> The code sample begins by adding the <a href="#Zig-Standard-Library">Zig Standard Library</a> to the build using the <a href="#import">@import</a> builtin function. The <code><span class="line"><span class="tok-builtin">@import</span>(<span class="tok-str">"std"</span>)</span></code> function call creates a structure that represents the Zig Standard Library. The code then <a href="#Container-Level-Variables">declares</a> a <a href="#Assignment">constant identifier</a>, named <code><span class="line">std</span></code>, that gives access to the features of the Zig Standard Library. </p> <p> Next, a <a href="#Functions">public function</a>, <code><span class="line"><span class="tok-kw">pub</span> <span class="tok-kw">fn</span></span></code>, named <code><span class="line">main</span></code> is declared. The <code><span class="line">main</span></code> function is necessary because it tells the Zig compiler where the start of the program exists. Programs designed to be executed will need a <code><span class="line"><span class="tok-kw">pub</span> <span class="tok-kw">fn</span> <span class="tok-fn">main</span></span></code> function. </p> <aside role="note" aria-label="Note about main function"> <p> For more advanced use cases, Zig offers other features to inform the compiler where the start of the program exists. Also, libraries do not need a <code><span class="line"><span class="tok-kw">pub</span> <span class="tok-kw">fn</span> <span class="tok-fn">main</span></span></code> function because library code is called by other programs or libraries. </p> </aside> <p> A function is a block of any number of statements and expressions that, as a whole, perform a task. Functions may or may not return data after they are done performing their task. If a function cannot perform its task, it might return an error. Zig makes all of this explicit. </p> <p> In the <code class="file">hello.zig</code> code sample, the <code>main</code> function is declared with the <code><span class="line">!<span class="tok-type">void</span></span></code> return type. This return type is known as an <a href="#Error-Union-Type">Error Union Type</a>. This syntax tells the Zig compiler that the function will either return an error or a value. An error union type combines an <a href="#Error-Set-Type">Error Set Type</a> and any other data type (e.g. a <a href="#Primitive-Types">Primitive Type</a> or a user-defined type such as a <a href="#struct">struct</a>, <a href="#enum">enum</a>, or <a href="#union">union</a>). The full form of an error union type is <code>&lt;error set type&gt;</code><code><span class="line">!</span></code><code>&lt;any data type&gt;</code>. In the code sample, the error set type is not explicitly written on the left side of the <code><span class="line">!</span></code> operator. When written this way, the error set type is an <a href="#Inferred-Error-Sets">inferred error set type</a>. The <code><span class="line"><span class="tok-type">void</span></span></code> after the <code><span class="line">!</span></code> operator tells the compiler that the function will not return a value under normal circumstances (i.e. when no errors occur). </p> <aside role="note" aria-label="Note to disambiguate exclamation mark operator"> <p> Note to experienced programmers: Zig also has the boolean <a href="#Operators">operator</a> <code><span class="line">!a</span></code> where <code><span class="line">a</span></code> is a value of type <code><span class="line"><span class="tok-type">bool</span></span></code>. Error union types contain the name of the type in the syntax: <code><span class="line">!</span></code><code>&lt;any data type&gt;</code>. </p> </aside> <p> In Zig, a function's block of statements and expressions are surrounded by an open curly-brace <code>{</code> and close curly-brace <code>}</code>. Inside of the <code><span class="line">main</span></code> function are expressions that perform the task of outputting <samp>Hello, world!</samp> to standard output. </p> <p> First, a constant identifier, <code><span class="line">stdout</span></code>, is initialized to represent standard output's writer. Then, the program tries to print the <samp>Hello, world!</samp> message to standard output. </p> <p> Functions sometimes need information to perform their task. In Zig, information is passed to functions between an open parenthesis <code><span class="line">(</span></code> and a close parenthesis <code><span class="line">)</span></code> placed after the function's name. This information is also known as arguments. When there are multiple arguments passed to a function, they are separated by commas <code><span class="line">,</span></code>. </p> <p> The two arguments passed to the <code><span class="line">stdout.print()</span></code> function, <code><span class="line"><span class="tok-str">"Hello, {s}!\n"</span></span></code> and <code><span class="line">.{<span class="tok-str">"world"</span>}</span></code>, are evaluated at <a href="#comptime">compile-time</a>. The code sample is purposely written to show how to perform <a href="#String-Literals-and-Unicode-Code-Point-Literals">string</a> substitution in the <code><span class="line">print</span></code> function. The curly-braces inside of the first argument are substituted with the compile-time known value inside of the second argument (known as a <a href="#Tuples">tuple</a>). The <code>\n</code> inside of the double-quotes of the first argument is the <a href="#Escape-Sequences">escape sequence</a> for the newline character. The <a href="#try">try</a> expression evaluates the result of <code><span class="line">stdout.print</span></code>. If the result is an error, then the <code><span class="line"><span class="tok-kw">try</span></span></code> expression will return from <code><span class="line">main</span></code> with the error. Otherwise, the program will continue. In this case, there are no more statements or expressions left to execute in the <code><span class="line">main</span></code> function, so the program exits. </p> <p> In Zig, the standard output writer's <code><span class="line">print</span></code> function is allowed to fail because it is actually a function defined as part of a generic Writer. Consider a generic Writer that represents writing data to a file. When the disk is full, a write to the file will fail. However, we typically do not expect writing text to the standard output to fail. To avoid having to handle the failure case of printing to standard output, you can use alternate functions: the functions in <code><span class="line">std.log</span></code> for proper logging or the <code><span class="line">std.debug.print</span></code> function. This documentation will use the latter option to print to standard error (stderr) and silently return on failure. The next code sample, <code class="file">hello_again.zig</code> demonstrates the use of <code><span class="line">std.debug.print</span></code>. </p> <figure><figcaption class="zig-cap"><cite class="file">hello_again.zig</cite></figcaption><pre data-language="zig">const print = @import("std").debug.print;

pub fn main() void {
    print("Hello, world!\n", .{});
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig build-exe hello_again.zig</kbd>
$ <kbd>./hello_again</kbd>
Hello, world!
</samp></pre></figure> <p> Note that you can leave off the <code><span class="line">!</span></code> from the return type because <code><span class="line">std.debug.print</span></code> cannot fail. </p> <p>See also:</p>
<ul> <li><a href="#Values">Values</a></li> <li><a href="#import">@import</a></li> <li><a href="#Errors">Errors</a></li> <li><a href="#Root-Source-File">Root Source File</a></li> <li><a href="#Source-Encoding">Source Encoding</a></li> </ul> <h2 id="Comments">Comments </h2> <p> Zig supports 3 types of comments. Normal comments are ignored, but doc comments and top-level doc comments are used by the compiler to generate the package documentation. </p> <p> The generated documentation is still experimental, and can be produced with: </p> <figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>zig test -femit-docs main.zig
</samp></pre></figure> <figure><figcaption class="zig-cap"><cite class="file">comments.zig</cite></figcaption><pre data-language="zig">const print = @import("std").debug.print;

pub fn main() void {
    // Comments in Zig start with "//" and end at the next LF byte (end of line).
    // The line below is a comment and won't be executed.

    //print("Hello?", .{});

    print("Hello, world!\n", .{}); // another comment
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig build-exe comments.zig</kbd>
$ <kbd>./comments</kbd>
Hello, world!
</samp></pre></figure> <p> There are no multiline comments in Zig (e.g. like <code class="c">/* */</code> comments in C). This allows Zig to have the property that each line of code can be tokenized out of context. </p> <h3 id="Doc-Comments">Doc Comments </h3> <p> A doc comment is one that begins with exactly three slashes (i.e. <code><span class="line"><span class="tok-comment">///</span></span></code> but not <code><span class="line"><span class="tok-comment">////</span></span></code>); multiple doc comments in a row are merged together to form a multiline doc comment. The doc comment documents whatever immediately follows it. </p> <figure><figcaption class="zig-cap"><cite class="file">doc_comments.zig</cite></figcaption><pre data-language="zig">/// A structure for storing a timestamp, with nanosecond precision (this is a
/// multiline doc comment).
const Timestamp = struct {
    /// The number of seconds since the epoch (this is also a doc comment).
    seconds: i64,  // signed so we can represent pre-1970 (not a doc comment)
    /// The number of nanoseconds past the second (doc comment again).
    nanos: u32,

    /// Returns a `Timestamp` struct representing the Unix epoch; that is, the
    /// moment of 1970 Jan 1 00:00:00 UTC (this is a doc comment too).
    pub fn unixEpoch() Timestamp {
        return Timestamp{
            .seconds = 0,
            .nanos = 0,
        };
    }
};</pre></figure> <p> Doc comments are only allowed in certain places; it is a compile error to have a doc comment in an unexpected place, such as in the middle of an expression, or just before a non-doc comment. </p> <figure><figcaption class="zig-cap"><cite class="file">invalid_doc-comment.zig</cite></figcaption><pre data-language="zig">/// doc-comment
//! top-level doc-comment
const std = @import("std");</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig build-obj invalid_doc-comment.zig</kbd>
<span class="sgr-1m">docgen_tmp/invalid_doc-comment.zig:1:16: </span><span class="sgr-31m">error: </span><span class="sgr-1m">expected type expression, found 'a document comment'
</span>/// doc-comment
               <span class="sgr-32m">^
</span>
</samp></pre></figure> <figure><figcaption class="zig-cap"><cite class="file">unattached_doc-comment.zig</cite></figcaption><pre data-language="zig">pub fn main() void {}

/// End of file</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig build-obj unattached_doc-comment.zig</kbd>
<span class="sgr-1m">docgen_tmp/unattached_doc-comment.zig:3:1: </span><span class="sgr-31m">error: </span><span class="sgr-1m">unattached documentation comment
</span>/// End of file
<span class="sgr-32m">^~~~~~~~~~~~~~~
</span>
</samp></pre></figure> <p> Doc comments can be interleaved with normal comments. Currently, when producing the package documentation, normal comments are merged with doc comments. </p> <h3 id="Top-Level-Doc-Comments">Top-Level Doc Comments </h3> <p> A top-level doc comment is one that begins with two slashes and an exclamation point: <code><span class="line"><span class="tok-comment">//!</span></span></code>; it documents the current module. </p> <p> It is a compile error if a top-level doc comment is not placed at the start of a <a href="#Containers">container</a>, before any expressions. </p> <figure><figcaption class="zig-cap"><cite class="file">tldoc_comments.zig</cite></figcaption><pre data-language="zig">//! This module provides functions for retrieving the current date and
//! time with varying degrees of precision and accuracy. It does not
//! depend on libc, but will use functions from it if available.

const S = struct {
    //! Top level comments are allowed inside a container other than a module,
    //! but it is not very useful.  Currently, when producing the package
    //! documentation, these comments are ignored.
};</pre></figure> <h2 id="Values">Values </h2> <figure><figcaption class="zig-cap"><cite class="file">values.zig</cite></figcaption><pre data-language="zig">// Top-level declarations are order-independent:
const print = std.debug.print;
const std = @import("std");
const os = std.os;
const assert = std.debug.assert;

pub fn main() void {
    // integers
    const one_plus_one: i32 = 1 + 1;
    print("1 + 1 = {}\n", .{one_plus_one});

    // floats
    const seven_div_three: f32 = 7.0 / 3.0;
    print("7.0 / 3.0 = {}\n", .{seven_div_three});

    // boolean
    print("{}\n{}\n{}\n", .{
        true and false,
        true or false,
        !true,
    });

    // optional
    var optional_value: ?[]const u8 = null;
    assert(optional_value == null);

    print("\noptional 1\ntype: {}\nvalue: {?s}\n", .{
        @TypeOf(optional_value), optional_value,
    });

    optional_value = "hi";
    assert(optional_value != null);

    print("\noptional 2\ntype: {}\nvalue: {?s}\n", .{
        @TypeOf(optional_value), optional_value,
    });

    // error union
    var number_or_error: anyerror!i32 = error.ArgNotFound;

    print("\nerror union 1\ntype: {}\nvalue: {!}\n", .{
        @TypeOf(number_or_error), number_or_error, });

    number_or_error = 1234;

    print("\nerror union 2\ntype: {}\nvalue: {!}\n", .{
        @TypeOf(number_or_error), number_or_error,
    });
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig build-exe values.zig</kbd>
$ <kbd>./values</kbd>
1 + 1 = 2
7.0 / 3.0 = 2.33333325e+00
false
true
false

optional 1
type: ?[]const u8
value: null

optional 2
type: ?[]const u8
value: hi

error union 1
type: anyerror!i32
value: error.ArgNotFound

error union 2
type: anyerror!i32
value: 1234
</samp></pre></figure> <h3 id="Primitive-Types">Primitive Types </h3> <div class="table-wrapper"> <table> <caption>Primitive Types</caption> <thead> <tr> <th scope="col">Type</th> <th scope="col">C Equivalent</th> <th scope="col">Description</th> </tr> </thead> <tbody> <tr> <th scope="row"><code><span class="line"><span class="tok-type">i8</span></span></code></th> <td><code class="c">int8_t</code></td> <td>signed 8-bit integer</td> </tr> <tr> <th scope="row"><code><span class="line"><span class="tok-type">u8</span></span></code></th> <td><code class="c">uint8_t</code></td> <td>unsigned 8-bit integer</td> </tr> <tr> <th scope="row"><code><span class="line"><span class="tok-type">i16</span></span></code></th> <td><code class="c">int16_t</code></td> <td>signed 16-bit integer</td> </tr> <tr> <th scope="row"><code><span class="line"><span class="tok-type">u16</span></span></code></th> <td><code class="c">uint16_t</code></td> <td>unsigned 16-bit integer</td> </tr> <tr> <th scope="row"><code><span class="line"><span class="tok-type">i32</span></span></code></th> <td><code class="c">int32_t</code></td> <td>signed 32-bit integer</td> </tr> <tr> <th scope="row"><code><span class="line"><span class="tok-type">u32</span></span></code></th> <td><code class="c">uint32_t</code></td> <td>unsigned 32-bit integer</td> </tr> <tr> <th scope="row"><code><span class="line"><span class="tok-type">i64</span></span></code></th> <td><code class="c">int64_t</code></td> <td>signed 64-bit integer</td> </tr> <tr> <th scope="row"><code><span class="line"><span class="tok-type">u64</span></span></code></th> <td><code class="c">uint64_t</code></td> <td>unsigned 64-bit integer</td> </tr> <tr> <th scope="row"><code><span class="line"><span class="tok-type">i128</span></span></code></th> <td><code class="c">__int128</code></td> <td>signed 128-bit integer</td> </tr> <tr> <th scope="row"><code><span class="line"><span class="tok-type">u128</span></span></code></th> <td><code class="c">unsigned __int128</code></td> <td>unsigned 128-bit integer</td> </tr> <tr> <th scope="row"><code><span class="line"><span class="tok-type">isize</span></span></code></th> <td><code class="c">intptr_t</code></td> <td>signed pointer sized integer</td> </tr> <tr> <th scope="row"><code><span class="line"><span class="tok-type">usize</span></span></code></th> <td>
<code class="c">uintptr_t</code>, <code class="c">size_t</code>
</td> <td>unsigned pointer sized integer. Also see <a href="https://github.com/ziglang/zig/issues/5185">#5185</a>
</td> </tr> <tr> <th scope="row"><code><span class="line"><span class="tok-type">c_char</span></span></code></th> <td><code class="c">char</code></td> <td>for ABI compatibility with C</td> </tr> <tr> <th scope="row"><code><span class="line"><span class="tok-type">c_short</span></span></code></th> <td><code class="c">short</code></td> <td>for ABI compatibility with C</td> </tr> <tr> <th scope="row"><code><span class="line"><span class="tok-type">c_ushort</span></span></code></th> <td><code class="c">unsigned short</code></td> <td>for ABI compatibility with C</td> </tr> <tr> <th scope="row"><code><span class="line"><span class="tok-type">c_int</span></span></code></th> <td><code class="c">int</code></td> <td>for ABI compatibility with C</td> </tr> <tr> <th scope="row"><code><span class="line"><span class="tok-type">c_uint</span></span></code></th> <td><code class="c">unsigned int</code></td> <td>for ABI compatibility with C</td> </tr> <tr> <th scope="row"><code><span class="line"><span class="tok-type">c_long</span></span></code></th> <td><code class="c">long</code></td> <td>for ABI compatibility with C</td> </tr> <tr> <th scope="row"><code><span class="line"><span class="tok-type">c_ulong</span></span></code></th> <td><code class="c">unsigned long</code></td> <td>for ABI compatibility with C</td> </tr> <tr> <th scope="row"><code><span class="line"><span class="tok-type">c_longlong</span></span></code></th> <td><code class="c">long long</code></td> <td>for ABI compatibility with C</td> </tr> <tr> <th scope="row"><code><span class="line"><span class="tok-type">c_ulonglong</span></span></code></th> <td><code class="c">unsigned long long</code></td> <td>for ABI compatibility with C</td> </tr> <tr> <th scope="row"><code><span class="line"><span class="tok-type">c_longdouble</span></span></code></th> <td><code class="c">long double</code></td> <td>for ABI compatibility with C</td> </tr> <tr> <th scope="row"><code><span class="line"><span class="tok-type">f16</span></span></code></th> <td><code class="c">_Float16</code></td> <td>16-bit floating point (10-bit mantissa) IEEE-754-2008 binary16</td> </tr> <tr> <th scope="row"><code><span class="line"><span class="tok-type">f32</span></span></code></th> <td><code class="c">float</code></td> <td>32-bit floating point (23-bit mantissa) IEEE-754-2008 binary32</td> </tr> <tr> <th scope="row"><code><span class="line"><span class="tok-type">f64</span></span></code></th> <td><code class="c">double</code></td> <td>64-bit floating point (52-bit mantissa) IEEE-754-2008 binary64</td> </tr> <tr> <th scope="row"><code><span class="line"><span class="tok-type">f80</span></span></code></th> <td><code class="c">double</code></td> <td>80-bit floating point (64-bit mantissa) IEEE-754-2008 80-bit extended precision</td> </tr> <tr> <th scope="row"><code><span class="line"><span class="tok-type">f128</span></span></code></th> <td><code class="c">_Float128</code></td> <td>128-bit floating point (112-bit mantissa) IEEE-754-2008 binary128</td> </tr> <tr> <th scope="row"><code><span class="line"><span class="tok-type">bool</span></span></code></th> <td><code class="c">bool</code></td> <td>
<code><span class="line"><span class="tok-null">true</span></span></code> or <code><span class="line"><span class="tok-null">false</span></span></code>
</td> </tr> <tr> <th scope="row"><code><span class="line"><span class="tok-type">anyopaque</span></span></code></th> <td><code class="c">void</code></td> <td>Used for type-erased pointers.</td> </tr> <tr> <th scope="row"><code><span class="line"><span class="tok-type">void</span></span></code></th> <td>(none)</td> <td>Always the value <code><span class="line"><span class="tok-type">void</span>{}</span></code>
</td> </tr> <tr> <th scope="row"><code><span class="line"><span class="tok-type">noreturn</span></span></code></th> <td>(none)</td> <td>the type of <code><span class="line"><span class="tok-kw">break</span></span></code>, <code><span class="line"><span class="tok-kw">continue</span></span></code>, <code><span class="line"><span class="tok-kw">return</span></span></code>, <code><span class="line"><span class="tok-kw">unreachable</span></span></code>, and <code><span class="line"><span class="tok-kw">while</span> (<span class="tok-null">true</span>) {}</span></code>
</td> </tr> <tr> <th scope="row"><code><span class="line"><span class="tok-type">type</span></span></code></th> <td>(none)</td> <td>the type of types</td> </tr> <tr> <th scope="row"><code><span class="line"><span class="tok-type">anyerror</span></span></code></th> <td>(none)</td> <td>an error code</td> </tr> <tr> <th scope="row"><code><span class="line"><span class="tok-type">comptime_int</span></span></code></th> <td>(none)</td> <td>Only allowed for <a href="#comptime">comptime</a>-known values. The type of integer literals.</td> </tr> <tr> <th scope="row"><code><span class="line"><span class="tok-type">comptime_float</span></span></code></th> <td>(none)</td> <td>Only allowed for <a href="#comptime">comptime</a>-known values. The type of float literals.</td> </tr> </tbody> </table> </div> <p> In addition to the integer types above, arbitrary bit-width integers can be referenced by using an identifier of <code>i</code> or <code>u</code> followed by digits. For example, the identifier <code><span class="line"><span class="tok-type">i7</span></span></code> refers to a signed 7-bit integer. The maximum allowed bit-width of an integer type is <code><span class="line"><span class="tok-number">65535</span></span></code>. </p> <p>See also:</p>
<ul> <li><a href="#Integers">Integers</a></li> <li><a href="#Floats">Floats</a></li> <li><a href="#void">void</a></li> <li><a href="#Errors">Errors</a></li> <li><a href="#Type">@Type</a></li> </ul> <h3 id="Primitive-Values">Primitive Values </h3> <div class="table-wrapper"> <table> <caption>Primitive Values</caption> <thead> <tr> <th scope="col">Name</th> <th scope="col">Description</th> </tr> </thead> <tbody> <tr> <th scope="row">
<code><span class="line"><span class="tok-null">true</span></span></code> and <code><span class="line"><span class="tok-null">false</span></span></code>
</th> <td>
<code><span class="line"><span class="tok-type">bool</span></span></code> values</td> </tr> <tr> <th scope="row"><code><span class="line"><span class="tok-null">null</span></span></code></th> <td>used to set an optional type to <code><span class="line"><span class="tok-null">null</span></span></code>
</td> </tr> <tr> <th scope="row"><code><span class="line"><span class="tok-null">undefined</span></span></code></th> <td>used to leave a value unspecified</td> </tr> </tbody> </table> </div> <p>See also:</p>
<ul> <li><a href="#Optionals">Optionals</a></li> <li><a href="#undefined">undefined</a></li> </ul> <h3 id="String-Literals-and-Unicode-Code-Point-Literals">String Literals and Unicode Code Point Literals </h3> <p> String literals are constant single-item <a href="#Pointers">Pointers</a> to null-terminated byte arrays. The type of string literals encodes both the length, and the fact that they are null-terminated, and thus they can be <a href="#Type-Coercion">coerced</a> to both <a href="#Slices">Slices</a> and <a href="#Sentinel-Terminated-Pointers">Null-Terminated Pointers</a>. Dereferencing string literals converts them to <a href="#Arrays">Arrays</a>. </p> <p> The encoding of a string in Zig is de-facto assumed to be UTF-8. Because Zig source code is <a href="#Source-Encoding">UTF-8 encoded</a>, any non-ASCII bytes appearing within a string literal in source code carry their UTF-8 meaning into the content of the string in the Zig program; the bytes are not modified by the compiler. However, it is possible to embed non-UTF-8 bytes into a string literal using <code>\xNN</code> notation. </p> <p> Indexing into a string containing non-ASCII bytes will return individual bytes, whether valid UTF-8 or not. The <a href="#Zig-Standard-Library">Zig Standard Library</a> provides routines for checking the validity of UTF-8 encoded strings, accessing their code points and other encoding/decoding related tasks in <code><span class="line">std.unicode</span></code>. </p> <p> Unicode code point literals have type <code><span class="line"><span class="tok-type">comptime_int</span></span></code>, the same as <a href="#Integer-Literals">Integer Literals</a>. All <a href="#Escape-Sequences">Escape Sequences</a> are valid in both string literals and Unicode code point literals. </p> <p> In many other programming languages, a Unicode code point literal is called a "character literal". However, there is <a href="https://unicode.org/glossary">no precise technical definition of a "character"</a> in recent versions of the Unicode specification (as of Unicode 13.0). In Zig, a Unicode code point literal corresponds to the Unicode definition of a code point. </p> <figure><figcaption class="zig-cap"><cite class="file">string_literals.zig</cite></figcaption><pre data-language="zig">const print = @import("std").debug.print;
const mem = @import("std").mem; // will be used to compare bytes

pub fn main() void {
    const bytes = "hello";
    print("{}\n", .{@TypeOf(bytes)});                   // *const [5:0]u8
    print("{d}\n", .{bytes.len});                       // 5
    print("{c}\n", .{bytes[1]});                        // 'e'
    print("{d}\n", .{bytes[5]});                        // 0
    print("{}\n", .{'e' == '\x65'});                    // true
    print("{d}\n", .{'\u{1f4a9}'});                     // 128169
    print("{d}\n", .{'ðŸ’¯'});                            // 128175
    print("{u}\n", .{'âš¡'});
    print("{}\n", .{mem.eql(u8, "hello", "h\x65llo")});      // true
    print("{}\n", .{mem.eql(u8, "ðŸ’¯", "\xf0\x9f\x92\xaf")}); // also true
    const invalid_utf8 = "\xff\xfe";      // non-UTF-8 strings are possible with \xNN notation.
    print("0x{x}\n", .{invalid_utf8[1]}); // indexing them returns individual bytes...
    print("0x{x}\n", .{"ðŸ’¯"[1]});    // ...as does indexing part-way through non-ASCII characters
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig build-exe string_literals.zig</kbd>
$ <kbd>./string_literals</kbd>
*const [5:0]u8
5
e
0
true
128169
128175
âš¡
true
true
0xfe
0x9f
</samp></pre></figure> <p>See also:</p>
<ul> <li><a href="#Arrays">Arrays</a></li> <li><a href="#Source-Encoding">Source Encoding</a></li> </ul> <h4 id="Escape-Sequences">
<a href="#toc-Escape-Sequences">Escape Sequences</a> </h4> <div class="table-wrapper"> <table> <caption>Escape Sequences</caption> <thead> <tr> <th scope="col">Escape Sequence</th> <th scope="col">Name</th> </tr> </thead> <tbody> <tr> <th scope="row"><code>\n</code></th> <td>Newline</td> </tr> <tr> <th scope="row"><code>\r</code></th> <td>Carriage Return</td> </tr> <tr> <th scope="row"><code>\t</code></th> <td>Tab</td> </tr> <tr> <th scope="row"><code>\\</code></th> <td>Backslash</td> </tr> <tr> <th scope="row"><code>\'</code></th> <td>Single Quote</td> </tr> <tr> <th scope="row"><code>\"</code></th> <td>Double Quote</td> </tr> <tr> <th scope="row"><code>\xNN</code></th> <td>hexadecimal 8-bit byte value (2 digits)</td> </tr> <tr> <th scope="row"><code>\u{NNNNNN}</code></th> <td>hexadecimal Unicode code point UTF-8 encoded (1 or more digits)</td> </tr> </tbody> </table> </div> <p>Note that the maximum valid Unicode point is <code><span class="line"><span class="tok-number">0x10ffff</span></span></code>.</p> <h4 id="Multiline-String-Literals">
<a href="#toc-Multiline-String-Literals">Multiline String Literals</a> </h4> <p> Multiline string literals have no escapes and can span across multiple lines. To start a multiline string literal, use the <code><span class="line"><span class="tok-str">\\</span></span></code> token. Just like a comment, the string literal goes until the end of the line. The end of the line is not included in the string literal. However, if the next line begins with <code><span class="line"><span class="tok-str">\\</span></span></code> then a newline is appended and the string literal continues. </p> <figure><figcaption class="zig-cap"><cite class="file">multiline_string_literals.zig</cite></figcaption><pre data-language="zig">const hello_world_in_c =
    \\#include &lt;stdio.h&gt;
    \\
    \\int main(int argc, char **argv) {
    \\    printf("hello world\n");
    \\    return 0;
    \\}
;</pre></figure> <p>See also:</p>
<ul> <li><a href="#embedFile">@embedFile</a></li> </ul> <h3 id="Assignment">Assignment </h3> <p>Use the <code><span class="line"><span class="tok-kw">const</span></span></code> keyword to assign a value to an identifier:</p> <figure><figcaption class="zig-cap"><cite class="file">constant_identifier_cannot_change.zig</cite></figcaption><pre data-language="zig">const x = 1234;

fn foo() void {
    // It works at file scope as well as inside functions.
    const y = 5678;

    // Once assigned, an identifier cannot be changed.
    y += 1;
}

pub fn main() void {
    foo();
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig build-exe constant_identifier_cannot_change.zig</kbd>
<span class="sgr-1m">constant_identifier_cannot_change.zig:8:7: </span><span class="sgr-31m">error: </span><span class="sgr-1m">cannot assign to constant
</span>    y += 1;
    <span class="sgr-32m">~~^~~~
</span><span class="sgr-2m">referenced by:
    main: constant_identifier_cannot_change.zig:12:5
    callMain: /home/ci/actions-runner/_work/zig-bootstrap/out/host/lib/zig/std/start.zig:564:17
    remaining reference traces hidden; use '-freference-trace' to see all reference traces
</span>
</samp></pre></figure> <p><code><span class="line"><span class="tok-kw">const</span></span></code> applies to all of the bytes that the identifier immediately addresses. <a href="#Pointers">Pointers</a> have their own const-ness.</p> <p>If you need a variable that you can modify, use the <code><span class="line"><span class="tok-kw">var</span></span></code> keyword:</p> <figure><figcaption class="zig-cap"><cite class="file">mutable_var.zig</cite></figcaption><pre data-language="zig">const print = @import("std").debug.print;

pub fn main() void {
    var y: i32 = 5678;

    y += 1;

    print("{d}", .{y});
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig build-exe mutable_var.zig</kbd>
$ <kbd>./mutable_var</kbd>
5679
</samp></pre></figure> <p>Variables must be initialized:</p> <figure><figcaption class="zig-cap"><cite class="file">var_must_be_initialized.zig</cite></figcaption><pre data-language="zig">pub fn main() void {
    var x: i32;

    x = 1;
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig build-exe var_must_be_initialized.zig</kbd>
<span class="sgr-1m">var_must_be_initialized.zig:2:5: </span><span class="sgr-31m">error: </span><span class="sgr-1m">variables must be initialized
</span>    var x: i32;
    <span class="sgr-32m">^~~~~~~~~~
</span>
</samp></pre></figure> <h4 id="undefined">
<a href="#toc-undefined">undefined</a> </h4> <p>Use <code><span class="line"><span class="tok-null">undefined</span></span></code> to leave variables uninitialized:</p> <figure><figcaption class="zig-cap"><cite class="file">assign_undefined.zig</cite></figcaption><pre data-language="zig">const print = @import("std").debug.print;

pub fn main() void {
    var x: i32 = undefined;
    x = 1;
    print("{d}", .{x});
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig build-exe assign_undefined.zig</kbd>
$ <kbd>./assign_undefined</kbd>
1
</samp></pre></figure> <p> <code><span class="line"><span class="tok-null">undefined</span></span></code> can be <a href="#Type-Coercion">coerced</a> to any type. Once this happens, it is no longer possible to detect that the value is <code><span class="line"><span class="tok-null">undefined</span></span></code>. <code><span class="line"><span class="tok-null">undefined</span></span></code> means the value could be anything, even something that is nonsense according to the type. Translated into English, <code><span class="line"><span class="tok-null">undefined</span></span></code> means "Not a meaningful value. Using this value would be a bug. The value will be unused, or overwritten before being used." </p> <p> In <a href="#Debug">Debug</a> mode, Zig writes <code><span class="line"><span class="tok-number">0xaa</span></span></code> bytes to undefined memory. This is to catch bugs early, and to help detect use of undefined memory in a debugger. However, this behavior is only an implementation feature, not a language semantic, so it is not guaranteed to be observable to code. </p> <h2 id="Zig-Test">Zig Test </h2> <p> Code written within one or more <code><span class="line"><span class="tok-kw">test</span></span></code> declarations can be used to ensure behavior meets expectations: </p> <figure><figcaption class="zig-cap"><cite class="file">testing_introduction.zig</cite></figcaption><pre data-language="zig">const std = @import("std");

test "expect addOne adds one to 41" {

    // The Standard Library contains useful functions to help create tests.
    // `expect` is a function that verifies its argument is true.
    // It will return an error if its argument is false to indicate a failure.
    // `try` is used to return an error to the test runner to notify it that the test failed.
    try std.testing.expect(addOne(41) == 42);
}

test addOne {
    // A test name can also be written using an identifier.
    try std.testing.expect(addOne(41) == 42);
}

/// The function `addOne` adds one to the number given as its argument.
fn addOne(number: i32) i32 {
    return number + 1;
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test testing_introduction.zig</kbd>
1/2 test.expect addOne adds one to 41... OK
2/2 decltest.addOne... OK
All 2 tests passed.
</samp></pre></figure> <p> The <code class="file">testing_introduction.zig</code> code sample tests the <a href="#Functions">function</a> <code><span class="line">addOne</span></code> to ensure that it returns <code><span class="line"><span class="tok-number">42</span></span></code> given the input <code><span class="line"><span class="tok-number">41</span></span></code>. From this test's perspective, the <code><span class="line">addOne</span></code> function is said to be <em>code under test</em>. </p> <p> <kbd>zig test</kbd> is a tool that creates and runs a test build. By default, it builds and runs an executable program using the <em>default test runner</em> provided by the <a href="#Zig-Standard-Library">Zig Standard Library</a> as its main entry point. During the build, <code><span class="line"><span class="tok-kw">test</span></span></code> declarations found while <a href="#Root-Source-File">resolving</a> the given Zig source file are included for the default test runner to run and report on. </p> <aside> This documentation discusses the features of the default test runner as provided by the Zig Standard Library. Its source code is located in <code class="file">lib/test_runner.zig</code>. </aside> <p> The shell output shown above displays two lines after the <kbd>zig test</kbd> command. These lines are printed to standard error by the default test runner: </p> <dl> <dt><samp>Test [1/2] test.expect addOne adds one to 41...</samp></dt> <dd>Lines like this indicate which test, out of the total number of tests, is being run. In this case, <samp>[1/2]</samp> indicates that the first test, out of a total of two test, is being run. Note that, when the test runner program's standard error is output to the terminal, these lines are cleared when a test succeeds. </dd> <dt><samp>Test [2/2] decltest.addOne...</samp></dt> <dd>When the test name is an identifier, the default test runner uses the text decltest instead of test. </dd> <dt><samp>All 2 tests passed.</samp></dt> <dd>This line indicates the total number of tests that have passed.</dd> </dl> <h3 id="Test-Declarations">Test Declarations </h3> <p> Test declarations contain the <a href="#Keyword-Reference">keyword</a> <code><span class="line"><span class="tok-kw">test</span></span></code>, followed by an optional name written as a <a href="#String-Literals-and-Unicode-Code-Point-Literals">string literal</a> or an <a href="#Identifiers">identifier</a>, followed by a <a href="#Blocks">block</a> containing any valid Zig code that is allowed in a <a href="#Functions">function</a>. </p> <aside> By convention, non-named tests should only be used to <a href="#Nested-Container-Tests">make other tests run</a>. Non-named tests cannot be <a href="#Skip-Tests">filtered</a>. </aside> <p> Test declarations are similar to <a href="#Functions">Functions</a>: they have a return type and a block of code. The implicit return type of <code><span class="line"><span class="tok-kw">test</span></span></code> is the <a href="#Error-Union-Type">Error Union Type</a> <code><span class="line"><span class="tok-type">anyerror</span>!<span class="tok-type">void</span></span></code>, and it cannot be changed. When a Zig source file is not built using the <kbd>zig test</kbd> tool, the test declarations are omitted from the build. </p> <p> Test declarations can be written in the same file, where code under test is written, or in a separate Zig source file. Since test declarations are top-level declarations, they are order-independent and can be written before or after the code under test. </p> <p>See also:</p>
<ul> <li><a href="#The-Global-Error-Set">The Global Error Set</a></li> <li><a href="#Grammar">Grammar</a></li> </ul> <h3 id="Nested-Container-Tests">Nested Container Tests </h3> <p> When the <kbd>zig test</kbd> tool is building a test runner, only resolved <code><span class="line"><span class="tok-kw">test</span></span></code> declarations are included in the build. Initially, only the given Zig source file's top-level declarations are resolved. Unless nested <a href="#Containers">containers</a> are referenced from a top-level test declaration, nested container tests will not be resolved. </p> <p> The code sample below uses the <code><span class="line">std.testing.refAllDecls(<span class="tok-builtin">@This</span>())</span></code> function call to reference all of the containers that are in the file including the imported Zig source file. The code sample also shows an alternative way to reference containers using the <code><span class="line">_ = C;</span></code> syntax. This syntax tells the compiler to ignore the result of the expression on the right side of the assignment operator. </p> <figure><figcaption class="zig-cap"><cite class="file">testing_nested_container_tests.zig</cite></figcaption><pre data-language="zig">const std = @import("std");
const expect = std.testing.expect;

// Imported source file tests will run when referenced from a top-level test declaration.
// The next line alone does not cause "testing_introduction.zig" tests to run.
const imported_file = @import("testing_introduction.zig");

test {
    // To run nested container tests, either, call `refAllDecls` which will
    // reference all declarations located in the given argument.
    // `@This()` is a builtin function that returns the innermost container it is called from.
    // In this example, the innermost container is this file (implicitly a struct).
    std.testing.refAllDecls(@This());

    // or, reference each container individually from a top-level test declaration.
    // The `_ = C;` syntax is a no-op reference to the identifier `C`.
    _ = S;
    _ = U;
    _ = @import("testing_introduction.zig");
}

const S = struct {
    test "S demo test" {
        try expect(true);
    }

    const SE = enum {
        V,

        // This test won't run because its container (SE) is not referenced.
        test "This Test Won't Run" {
            try expect(false);
        }
    };
};

const U = union { // U is referenced by the file's top-level test declaration
    s: US,        // and US is referenced here; therefore, "U.Us demo test" will run

    const US = struct {
        test "U.US demo test" {
            // This test is a top-level test declaration for the struct.
            // The struct is nested (declared) inside of a union.
            try expect(true);
        }
    };

    test "U demo test" {
        try expect(true);
    }
};</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test testing_nested_container_tests.zig</kbd>
1/5 test_0... OK
2/5 test.S demo test... OK
3/5 test.U demo test... OK
4/5 test.expect addOne adds one to 41... OK
5/5 decltest.addOne... OK
All 5 tests passed.
</samp></pre></figure> <h3 id="Test-Failure">Test Failure </h3> <p> The default test runner checks for an <a href="#Errors">error</a> returned from a test. When a test returns an error, the test is considered a failure and its <a href="#Error-Return-Traces">error return trace</a> is output to standard error. The total number of failures will be reported after all tests have run. </p> <figure><figcaption class="zig-cap"><cite class="file">testing_failure.zig</cite></figcaption><pre data-language="zig">const std = @import("std");

test "expect this to fail" {
    try std.testing.expect(false);
}

test "expect this to succeed" {
    try std.testing.expect(true);
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test testing_failure.zig</kbd>
1/2 test.expect this to fail... FAIL (TestUnexpectedResult)
/home/ci/actions-runner/_work/zig-bootstrap/out/host/lib/zig/std/testing.zig:515:14: 0x22425f in expect (test)
    if (!ok) return error.TestUnexpectedResult;
             ^
/home/ci/actions-runner/_work/zig-bootstrap/zig/docgen_tmp/testing_failure.zig:4:5: 0x224375 in test.expect this to fail (test)
    try std.testing.expect(false);
    ^
2/2 test.expect this to succeed... OK
1 passed; 0 skipped; 1 failed.
error: the following test command failed with exit code 1:
/home/ci/actions-runner/_work/zig-bootstrap/out/zig-local-cache/o/d4ffa468bf575e495decf1a50d3bbe44/test
</samp></pre></figure> <h3 id="Skip-Tests">Skip Tests </h3> <p> One way to skip tests is to filter them out by using the <kbd>zig test</kbd> command line parameter <kbd>--test-filter [text]</kbd>. This makes the test build only include tests whose name contains the supplied filter text. Note that non-named tests are run even when using the <kbd>--test-filter [text]</kbd> command line parameter. </p> <p> To programmatically skip a test, make a <code><span class="line"><span class="tok-kw">test</span></span></code> return the error <code><span class="line"><span class="tok-kw">error</span>.SkipZigTest</span></code> and the default test runner will consider the test as being skipped. The total number of skipped tests will be reported after all tests have run. </p> <figure><figcaption class="zig-cap"><cite class="file">testing_skip.zig</cite></figcaption><pre data-language="zig">test "this will be skipped" {
    return error.SkipZigTest;
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test testing_skip.zig</kbd>
1/1 test.this will be skipped... SKIP
0 passed; 1 skipped; 0 failed.
</samp></pre></figure> <h3 id="Report-Memory-Leaks">Report Memory Leaks </h3> <p> When code allocates <a href="#Memory">Memory</a> using the <a href="#Zig-Standard-Library">Zig Standard Library</a>'s testing allocator, <code><span class="line">std.testing.allocator</span></code>, the default test runner will report any leaks that are found from using the testing allocator: </p> <figure><figcaption class="zig-cap"><cite class="file">testing_detect_leak.zig</cite></figcaption><pre data-language="zig">const std = @import("std");

test "detect leak" {
    var list = std.ArrayList(u21).init(std.testing.allocator);
    // missing `defer list.deinit();`
    try list.append('â˜”');

    try std.testing.expect(list.items.len == 1);
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test testing_detect_leak.zig</kbd>
1/1 test.detect leak... OK
[gpa] (err): memory address 0x7f2a48e13000 leaked:
/home/ci/actions-runner/_work/zig-bootstrap/out/host/lib/zig/std/array_list.zig:403:67: 0x234cde in ensureTotalCapacityPrecise (test)
                const new_memory = try self.allocator.alignedAlloc(T, alignment, new_capacity);
                                                                  ^
/home/ci/actions-runner/_work/zig-bootstrap/out/host/lib/zig/std/array_list.zig:379:51: 0x22b0e4 in ensureTotalCapacity (test)
            return self.ensureTotalCapacityPrecise(better_capacity);
                                                  ^
/home/ci/actions-runner/_work/zig-bootstrap/out/host/lib/zig/std/array_list.zig:426:41: 0x228890 in addOne (test)
            try self.ensureTotalCapacity(self.items.len + 1);
                                        ^
/home/ci/actions-runner/_work/zig-bootstrap/out/host/lib/zig/std/array_list.zig:207:49: 0x22464d in append (test)
            const new_item_ptr = try self.addOne();
                                                ^
/home/ci/actions-runner/_work/zig-bootstrap/zig/docgen_tmp/testing_detect_leak.zig:6:20: 0x224572 in test.detect leak (test)
    try list.append('â˜”');
                   ^
/home/ci/actions-runner/_work/zig-bootstrap/out/host/lib/zig/test_runner.zig:176:28: 0x232319 in mainTerminal (test)
        } else test_fn.func();
                           ^
/home/ci/actions-runner/_work/zig-bootstrap/out/host/lib/zig/test_runner.zig:36:28: 0x22917a in main (test)
        return mainTerminal();
                           ^
/home/ci/actions-runner/_work/zig-bootstrap/out/host/lib/zig/std/start.zig:564:22: 0x224b62 in posixCallMainAndExit (test)
            root.main();
                     ^

All 1 tests passed.
1 errors were logged.
1 tests leaked memory.
error: the following test command failed with exit code 1:
/home/ci/actions-runner/_work/zig-bootstrap/out/zig-local-cache/o/54fbcd2f75b6aeb5afd27aeac12a6446/test
</samp></pre></figure> <p>See also:</p>
<ul> <li><a href="#defer">defer</a></li> <li><a href="#Memory">Memory</a></li> </ul> <h3 id="Detecting-Test-Build">Detecting Test Build </h3> <p> Use the <a href="#Compile-Variables">compile variable</a> <code><span class="line"><span class="tok-builtin">@import</span>(<span class="tok-str">"builtin"</span>).is_test</span></code> to detect a test build: </p> <figure><figcaption class="zig-cap"><cite class="file">testing_detect_test.zig</cite></figcaption><pre data-language="zig">const std = @import("std");
const builtin = @import("builtin");
const expect = std.testing.expect;

test "builtin.is_test" {
    try expect(isATest());
}

fn isATest() bool {
    return builtin.is_test;
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test testing_detect_test.zig</kbd>
1/1 test.builtin.is_test... OK
All 1 tests passed.
</samp></pre></figure> <h3 id="Test-Output-and-Logging">Test Output and Logging </h3> <p> The default test runner and the Zig Standard Library's testing namespace output messages to standard error. </p> <h3 id="The-Testing-Namespace">The Testing Namespace </h3> <p> The Zig Standard Library's <code>testing</code> namespace contains useful functions to help you create tests. In addition to the <code>expect</code> function, this document uses a couple of more functions as exemplified here: </p> <figure><figcaption class="zig-cap"><cite class="file">testing_namespace.zig</cite></figcaption><pre data-language="zig">const std = @import("std");

test "expectEqual demo" {
    const expected: i32 = 42;
    const actual = 42;

    // The first argument to `expectEqual` is the known, expected, result.
    // The second argument is the result of some expression.
    // The actual's type is casted to the type of expected.
    try std.testing.expectEqual(expected, actual);
}

test "expectError demo" {
    const expected_error = error.DemoError;
    const actual_error_union: anyerror!void = error.DemoError;

    // `expectError` will fail when the actual error is different than
    // the expected error.
    try std.testing.expectError(expected_error, actual_error_union);
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test testing_namespace.zig</kbd>
1/2 test.expectEqual demo... OK
2/2 test.expectError demo... OK
All 2 tests passed.
</samp></pre></figure> <p>The Zig Standard Library also contains functions to compare <a href="#Slices">Slices</a>, strings, and more. See the rest of the <code><span class="line">std.testing</span></code> namespace in the <a href="#Zig-Standard-Library">Zig Standard Library</a> for more available functions.</p> <h3 id="Test-Tool-Documentation">Test Tool Documentation </h3> <p> <kbd>zig test</kbd> has a few command line parameters which affect the compilation. See <kbd>zig test --help</kbd> for a full list. </p> <h2 id="Variables">Variables </h2> <p> A variable is a unit of <a href="#Memory">Memory</a> storage. </p> <p> It is generally preferable to use <code><span class="line"><span class="tok-kw">const</span></span></code> rather than <code><span class="line"><span class="tok-kw">var</span></span></code> when declaring a variable. This causes less work for both humans and computers to do when reading code, and creates more optimization opportunities. </p> <h3 id="Identifiers">Identifiers </h3> <p> Variable identifiers are never allowed to shadow identifiers from an outer scope. </p> <p> Identifiers must start with an alphabetic character or underscore and may be followed by any number of alphanumeric characters or underscores. They must not overlap with any keywords. See <a href="#Keyword-Reference">Keyword Reference</a>. </p> <p> If a name that does not fit these requirements is needed, such as for linking with external libraries, the <code><span class="line">@""</span></code> syntax may be used. </p> <figure><figcaption class="zig-cap"><cite class="file">identifiers.zig</cite></figcaption><pre data-language="zig">const @"identifier with spaces in it" = 0xff;
const @"1SmallStep4Man" = 112358;

const c = @import("std").c;
pub extern "c" fn @"error"() void;
pub extern "c" fn @"fstat$INODE64"(fd: c.fd_t, buf: *c.Stat) c_int;

const Color = enum {
  red,
  @"really red",
};
const color: Color = .@"really red";</pre></figure> <h3 id="Container-Level-Variables">Container Level Variables </h3> <p> <a href="#Containers">Container</a> level variables have static lifetime and are order-independent and lazily analyzed. The initialization value of container level variables is implicitly <a href="#comptime">comptime</a>. If a container level variable is <code><span class="line"><span class="tok-kw">const</span></span></code> then its value is <code><span class="line"><span class="tok-kw">comptime</span></span></code>-known, otherwise it is runtime-known. </p> <figure><figcaption class="zig-cap"><cite class="file">test_container_level_variables.zig</cite></figcaption><pre data-language="zig">var y: i32 = add(10, x);
const x: i32 = add(12, 34);

test "container level variables" {
    try expect(x == 46);
    try expect(y == 56);
}

fn add(a: i32, b: i32) i32 {
    return a + b;
}

const std = @import("std");
const expect = std.testing.expect;</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_container_level_variables.zig</kbd>
1/1 test.container level variables... OK
All 1 tests passed.
</samp></pre></figure> <p> Container level variables may be declared inside a <a href="#struct">struct</a>, <a href="#union">union</a>, <a href="#enum">enum</a>, or <a href="#opaque">opaque</a>: </p> <figure><figcaption class="zig-cap"><cite class="file">test_namespaced_container_level_variable.zig</cite></figcaption><pre data-language="zig">const std = @import("std");
const expect = std.testing.expect;

test "namespaced container level variable" {
    try expect(foo() == 1235);
    try expect(foo() == 1236);
}

const S = struct {
    var x: i32 = 1234;
};

fn foo() i32 {
    S.x += 1;
    return S.x;
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_namespaced_container_level_variable.zig</kbd>
1/1 test.namespaced container level variable... OK
All 1 tests passed.
</samp></pre></figure> <h3 id="Static-Local-Variables">Static Local Variables </h3> <p> It is also possible to have local variables with static lifetime by using containers inside functions. </p> <figure><figcaption class="zig-cap"><cite class="file">test_static_local_variable.zig</cite></figcaption><pre data-language="zig">const std = @import("std");
const expect = std.testing.expect;

test "static local variable" {
    try expect(foo() == 1235);
    try expect(foo() == 1236);
}

fn foo() i32 {
    const S = struct {
        var x: i32 = 1234;
    };
    S.x += 1;
    return S.x;
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_static_local_variable.zig</kbd>
1/1 test.static local variable... OK
All 1 tests passed.
</samp></pre></figure> <p> The <code><span class="line"><span class="tok-kw">extern</span></span></code> keyword or <a href="#extern">@extern</a> builtin function can be used to link against a variable that is exported from another object. The <code><span class="line"><span class="tok-kw">export</span></span></code> keyword or <a href="#export">@export</a> builtin function can be used to make a variable available to other objects at link time. In both cases, the type of the variable must be C ABI compatible. </p> <p>See also:</p>
<ul> <li><a href="#Exporting-a-C-Library">Exporting a C Library</a></li> </ul> <h3 id="Thread-Local-Variables">Thread Local Variables </h3> <p>A variable may be specified to be a thread-local variable using the <code><span class="line"><span class="tok-kw">threadlocal</span></span></code> keyword, which makes each thread work with a separate instance of the variable:</p> <figure><figcaption class="zig-cap"><cite class="file">test_thread_local_variables.zig</cite></figcaption><pre data-language="zig">const std = @import("std");
const assert = std.debug.assert;

threadlocal var x: i32 = 1234;

test "thread local storage" {
    const thread1 = try std.Thread.spawn(.{}, testTls, .{});
    const thread2 = try std.Thread.spawn(.{}, testTls, .{});
    testTls();
    thread1.join();
    thread2.join();
}

fn testTls() void {
    assert(x == 1234);
    x += 1;
    assert(x == 1235);
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_thread_local_variables.zig</kbd>
1/1 test.thread local storage... OK
All 1 tests passed.
</samp></pre></figure> <p> For <a href="#Single-Threaded-Builds">Single Threaded Builds</a>, all thread local variables are treated as regular <a href="#Container-Level-Variables">Container Level Variables</a>. </p> <p> Thread local variables may not be <code><span class="line"><span class="tok-kw">const</span></span></code>. </p> <h3 id="Local-Variables">Local Variables </h3> <p> Local variables occur inside <a href="#Functions">Functions</a>, <a href="#comptime">comptime</a> blocks, and <a href="#cImport">@cImport</a> blocks. </p> <p> When a local variable is <code><span class="line"><span class="tok-kw">const</span></span></code>, it means that after initialization, the variable's value will not change. If the initialization value of a <code><span class="line"><span class="tok-kw">const</span></span></code> variable is <a href="#comptime">comptime</a>-known, then the variable is also <code><span class="line"><span class="tok-kw">comptime</span></span></code>-known. </p> <p> A local variable may be qualified with the <code><span class="line"><span class="tok-kw">comptime</span></span></code> keyword. This causes the variable's value to be <code><span class="line"><span class="tok-kw">comptime</span></span></code>-known, and all loads and stores of the variable to happen during semantic analysis of the program, rather than at runtime. All variables declared in a <code><span class="line"><span class="tok-kw">comptime</span></span></code> expression are implicitly <code><span class="line"><span class="tok-kw">comptime</span></span></code> variables. </p> <figure><figcaption class="zig-cap"><cite class="file">test_comptime_variables.zig</cite></figcaption><pre data-language="zig">const std = @import("std");
const expect = std.testing.expect;

test "comptime vars" {
    var x: i32 = 1;
    comptime var y: i32 = 1;

    x += 1;
    y += 1;

    try expect(x == 2);
    try expect(y == 2);

    if (y != 2) {
        // This compile error never triggers because y is a comptime variable,
        // and so `y != 2` is a comptime value, and this if is statically evaluated.
        @compileError("wrong y value");
    }
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_comptime_variables.zig</kbd>
1/1 test.comptime vars... OK
All 1 tests passed.
</samp></pre></figure> <h2 id="Integers">Integers </h2> <h3 id="Integer-Literals">Integer Literals </h3> <figure><figcaption class="zig-cap"><cite class="file">integer_literals.zig</cite></figcaption><pre data-language="zig">const decimal_int = 98222;
const hex_int = 0xff;
const another_hex_int = 0xFF;
const octal_int = 0o755;
const binary_int = 0b11110000;

// underscores may be placed between two digits as a visual separator
const one_billion = 1_000_000_000;
const binary_mask = 0b1_1111_1111;
const permissions = 0o7_5_5;
const big_address = 0xFF80_0000_0000_0000;</pre></figure> <h3 id="Runtime-Integer-Values">Runtime Integer Values </h3> <p> Integer literals have no size limitation, and if any undefined behavior occurs, the compiler catches it. </p> <p> However, once an integer value is no longer known at compile-time, it must have a known size, and is vulnerable to undefined behavior. </p> <figure><figcaption class="zig-cap"><cite class="file">runtime_vs_comptime.zig</cite></figcaption><pre data-language="zig">fn divide(a: i32, b: i32) i32 {
    return a / b;
}</pre></figure> <p> In this function, values <code><span class="line">a</span></code> and <code><span class="line">b</span></code> are known only at runtime, and thus this division operation is vulnerable to both <a href="#Integer-Overflow">Integer Overflow</a> and <a href="#Division-by-Zero">Division by Zero</a>. </p> <p> Operators such as <code><span class="line">+</span></code> and <code><span class="line">-</span></code> cause undefined behavior on integer overflow. Alternative operators are provided for wrapping and saturating arithmetic on all targets. <code><span class="line">+%</span></code> and <code><span class="line">-%</span></code> perform wrapping arithmetic while <code><span class="line">+|</span></code> and <code><span class="line">-|</span></code> perform saturating arithmetic. </p> <p> Zig supports arbitrary bit-width integers, referenced by using an identifier of <code>i</code> or <code>u</code> followed by digits. For example, the identifier <code><span class="line"><span class="tok-type">i7</span></span></code> refers to a signed 7-bit integer. The maximum allowed bit-width of an integer type is <code><span class="line"><span class="tok-number">65535</span></span></code>. For signed integer types, Zig uses a <a href="https://en.wikipedia.org/wiki/Two's_complement">two's complement</a> representation. </p> <p>See also:</p>
<ul> <li><a href="#Wrapping-Operations">Wrapping Operations</a></li> </ul> <h2 id="Floats">Floats </h2> <p>Zig has the following floating point types:</p> <ul> <li>
<code><span class="line"><span class="tok-type">f16</span></span></code> - IEEE-754-2008 binary16</li> <li>
<code><span class="line"><span class="tok-type">f32</span></span></code> - IEEE-754-2008 binary32</li> <li>
<code><span class="line"><span class="tok-type">f64</span></span></code> - IEEE-754-2008 binary64</li> <li>
<code><span class="line"><span class="tok-type">f80</span></span></code> - IEEE-754-2008 80-bit extended precision</li> <li>
<code><span class="line"><span class="tok-type">f128</span></span></code> - IEEE-754-2008 binary128</li> <li>
<code><span class="line"><span class="tok-type">c_longdouble</span></span></code> - matches <code class="c">long double</code> for the target C ABI</li> </ul> <h3 id="Float-Literals">Float Literals </h3> <p> Float literals have type <code><span class="line"><span class="tok-type">comptime_float</span></span></code> which is guaranteed to have the same precision and operations of the largest other floating point type, which is <code><span class="line"><span class="tok-type">f128</span></span></code>. </p> <p> Float literals <a href="#Type-Coercion">coerce</a> to any floating point type, and to any <a href="#Integers">integer</a> type when there is no fractional component. </p> <figure><figcaption class="zig-cap"><cite class="file">float_literals.zig</cite></figcaption><pre data-language="zig">const floating_point = 123.0E+77;
const another_float = 123.0;
const yet_another = 123.0e+77;

const hex_floating_point = 0x103.70p-5;
const another_hex_float = 0x103.70;
const yet_another_hex_float = 0x103.70P-5;

// underscores may be placed between two digits as a visual separator
const lightspeed = 299_792_458.000_000;
const nanosecond = 0.000_000_001;
const more_hex = 0x1234_5678.9ABC_CDEFp-10;</pre></figure> <p> There is no syntax for NaN, infinity, or negative infinity. For these special values, one must use the standard library: </p> <figure><figcaption class="zig-cap"><cite class="file">float_special_values.zig</cite></figcaption><pre data-language="zig">const std = @import("std");

const inf = std.math.inf(f32);
const negative_inf = -std.math.inf(f64);
const nan = std.math.nan(f128);</pre></figure> <h3 id="Floating-Point-Operations">Floating Point Operations </h3> <p>By default floating point operations use <code><span class="line">Strict</span></code> mode, but you can switch to <code><span class="line">Optimized</span></code> mode on a per-block basis:</p> <figure><figcaption class="zig-cap"><cite class="file">float_mode_obj.zig</cite></figcaption><pre data-language="zig">const std = @import("std");
const big = @as(f64, 1 &lt;&lt; 40);

export fn foo_strict(x: f64) f64 {
    return x + big - big;
}

export fn foo_optimized(x: f64) f64 {
    @setFloatMode(.Optimized);
    return x + big - big;
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig build-obj float_mode_obj.zig -O ReleaseFast</kbd>
</samp></pre></figure> <p>For this test we have to separate code into two object files - otherwise the optimizer figures out all the values at compile-time, which operates in strict mode.</p> <figure><figcaption class="zig-cap"><cite class="file">float_mode_exe.zig</cite></figcaption><pre data-language="zig">const print = @import("std").debug.print;

extern fn foo_strict(x: f64) f64;
extern fn foo_optimized(x: f64) f64;

pub fn main() void {
    const x = 0.001;
    print("optimized = {}\n", .{foo_optimized(x)});
    print("strict = {}\n", .{foo_strict(x)});
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig build-exe float_mode_exe.zig float_mode_obj.o</kbd>
$ <kbd>./float_mode_exe</kbd>
optimized = 9.765625e-04
strict = 9.765625e-04
</samp></pre></figure> <p>See also:</p>
<ul> <li><a href="#setFloatMode">@setFloatMode</a></li> <li><a href="#Division-by-Zero">Division by Zero</a></li> </ul> <h2 id="Operators">Operators </h2> <p> There is no operator overloading. When you see an operator in Zig, you know that it is doing something from this table, and nothing else. </p> <h3 id="Table-of-Operators">Table of Operators </h3> <div class="table-wrapper"> <table> <caption>Table of Operators</caption> <thead> <tr> <th scope="col">Syntax</th> <th scope="col">Relevant Types</th> <th scope="col">Description</th> <th scope="col">Example</th> </tr> </thead> <tbody> <tr> <th scope="row"><pre data-language="zig">a + b
a += b</pre></th> <td> <ul> <li><a href="#Integers">Integers</a></li> <li><a href="#Floats">Floats</a></li> </ul> </td> <td>Addition. <ul> <li>Can cause <a href="#Default-Operations">overflow</a> for integers.</li> <li>Invokes <a href="#Peer-Type-Resolution">Peer Type Resolution</a> for the operands.</li> <li>See also <a href="#addWithOverflow">@addWithOverflow</a>.</li> </ul> </td> <td> <pre data-language="zig">2 + 5 == 7</pre> </td> </tr> <tr> <th scope="row"><pre data-language="zig">a +% b
a +%= b</pre></th> <td> <ul> <li><a href="#Integers">Integers</a></li> </ul> </td> <td>Wrapping Addition. <ul> <li>Guaranteed to have twos-complement wrapping behavior.</li> <li>Invokes <a href="#Peer-Type-Resolution">Peer Type Resolution</a> for the operands.</li> <li>See also <a href="#addWithOverflow">@addWithOverflow</a>.</li> </ul> </td> <td> <pre data-language="zig">@as(u32, std.math.maxInt(u32)) +% 1 == 0</pre> </td> </tr> <tr> <th scope="row"><pre data-language="zig">a +| b
a +|= b</pre></th> <td> <ul> <li><a href="#Integers">Integers</a></li> </ul> </td> <td>Saturating Addition. <ul> <li>Invokes <a href="#Peer-Type-Resolution">Peer Type Resolution</a> for the operands.</li> </ul> </td> <td> <pre data-language="zig">@as(u32, std.math.maxInt(u32)) +| 1 == @as(u32, std.math.maxInt(u32))</pre> </td> </tr> <tr> <th scope="row"><pre data-language="zig">a - b
a -= b</pre></th> <td> <ul> <li><a href="#Integers">Integers</a></li> <li><a href="#Floats">Floats</a></li> </ul> </td> <td>Subtraction. <ul> <li>Can cause <a href="#Default-Operations">overflow</a> for integers.</li> <li>Invokes <a href="#Peer-Type-Resolution">Peer Type Resolution</a> for the operands.</li> <li>See also <a href="#subWithOverflow">@subWithOverflow</a>.</li> </ul> </td> <td> <pre data-language="zig">2 - 5 == -3</pre> </td> </tr> <tr> <th scope="row"><pre data-language="zig">a -% b
a -%= b</pre></th> <td> <ul> <li><a href="#Integers">Integers</a></li> </ul> </td> <td>Wrapping Subtraction. <ul> <li>Guaranteed to have twos-complement wrapping behavior.</li> <li>Invokes <a href="#Peer-Type-Resolution">Peer Type Resolution</a> for the operands.</li> <li>See also <a href="#subWithOverflow">@subWithOverflow</a>.</li> </ul> </td> <td> <pre data-language="zig">@as(u32, 0) -% 1 == std.math.maxInt(u32)</pre> </td> </tr> <tr> <th scope="row"><pre data-language="zig">a -| b
a -|= b</pre></th> <td> <ul> <li><a href="#Integers">Integers</a></li> </ul> </td> <td>Saturating Subtraction. <ul> <li>Invokes <a href="#Peer-Type-Resolution">Peer Type Resolution</a> for the operands.</li> </ul> </td> <td> <pre data-language="zig">@as(u32, 0) -| 1 == 0</pre> </td> </tr> <tr> <th scope="row"><pre data-language="zig">-a</pre></th> <td> <ul> <li><a href="#Integers">Integers</a></li> <li><a href="#Floats">Floats</a></li> </ul> </td> <td> Negation. <ul> <li>Can cause <a href="#Default-Operations">overflow</a> for integers.</li> </ul> </td> <td> <pre data-language="zig">-1 == 0 - 1</pre> </td> </tr> <tr> <th scope="row"><pre data-language="zig">-%a</pre></th> <td> <ul> <li><a href="#Integers">Integers</a></li> </ul> </td> <td> Wrapping Negation. <ul> <li>Guaranteed to have twos-complement wrapping behavior.</li> </ul> </td> <td> <pre data-language="zig">-%@as(i32, std.math.minInt(i32)) == std.math.minInt(i32)</pre> </td> </tr> <tr> <th scope="row"><pre data-language="zig">a * b
a *= b</pre></th> <td> <ul> <li><a href="#Integers">Integers</a></li> <li><a href="#Floats">Floats</a></li> </ul> </td> <td>Multiplication. <ul> <li>Can cause <a href="#Default-Operations">overflow</a> for integers.</li> <li>Invokes <a href="#Peer-Type-Resolution">Peer Type Resolution</a> for the operands.</li> <li>See also <a href="#mulWithOverflow">@mulWithOverflow</a>.</li> </ul> </td> <td> <pre data-language="zig">2 * 5 == 10</pre> </td> </tr> <tr> <th scope="row"><pre data-language="zig">a *% b
a *%= b</pre></th> <td> <ul> <li><a href="#Integers">Integers</a></li> </ul> </td> <td>Wrapping Multiplication. <ul> <li>Guaranteed to have twos-complement wrapping behavior.</li> <li>Invokes <a href="#Peer-Type-Resolution">Peer Type Resolution</a> for the operands.</li> <li>See also <a href="#mulWithOverflow">@mulWithOverflow</a>.</li> </ul> </td> <td> <pre data-language="zig">@as(u8, 200) *% 2 == 144</pre> </td> </tr> <tr> <th scope="row"><pre data-language="zig">a *| b
a *|= b</pre></th> <td> <ul> <li><a href="#Integers">Integers</a></li> </ul> </td> <td>Saturating Multiplication. <ul> <li>Invokes <a href="#Peer-Type-Resolution">Peer Type Resolution</a> for the operands.</li> </ul> </td> <td> <pre data-language="zig">@as(u8, 200) *| 2 == 255</pre> </td> </tr> <tr> <th scope="row"><pre data-language="zig">a / b
a /= b</pre></th> <td> <ul> <li><a href="#Integers">Integers</a></li> <li><a href="#Floats">Floats</a></li> </ul> </td> <td>Division. <ul> <li>Can cause <a href="#Default-Operations">overflow</a> for integers.</li> <li>Can cause <a href="#Division-by-Zero">Division by Zero</a> for integers.</li> <li>Can cause <a href="#Division-by-Zero">Division by Zero</a> for floats in <a href="#Floating-Point-Operations">FloatMode.Optimized Mode</a>.</li> <li>Signed integer operands must be comptime-known and positive. In other cases, use <a href="#divTrunc">@divTrunc</a>, <a href="#divFloor">@divFloor</a>, or <a href="#divExact">@divExact</a> instead. </li> <li>Invokes <a href="#Peer-Type-Resolution">Peer Type Resolution</a> for the operands.</li> </ul> </td> <td> <pre data-language="zig">10 / 5 == 2</pre> </td> </tr> <tr> <th scope="row"><pre data-language="zig">a % b
a %= b</pre></th> <td> <ul> <li><a href="#Integers">Integers</a></li> <li><a href="#Floats">Floats</a></li> </ul> </td> <td>Remainder Division. <ul> <li>Can cause <a href="#Division-by-Zero">Division by Zero</a> for integers.</li> <li>Can cause <a href="#Division-by-Zero">Division by Zero</a> for floats in <a href="#Floating-Point-Operations">FloatMode.Optimized Mode</a>.</li> <li>Signed or floating-point operands must be comptime-known and positive. In other cases, use <a href="#rem">@rem</a> or <a href="#mod">@mod</a> instead. </li> <li>Invokes <a href="#Peer-Type-Resolution">Peer Type Resolution</a> for the operands.</li> </ul> </td> <td> <pre data-language="zig">10 % 3 == 1</pre> </td> </tr> <tr> <th scope="row"><pre data-language="zig">a &lt;&lt; b
a &lt;&lt;= b</pre></th> <td> <ul> <li><a href="#Integers">Integers</a></li> </ul> </td> <td>Bit Shift Left. <ul> <li>
<code><span class="line">b</span></code> must be <a href="#comptime">comptime-known</a> or have a type with log2 number of bits as <code><span class="line">a</span></code>.</li> <li>See also <a href="#shlExact">@shlExact</a>.</li> <li>See also <a href="#shlWithOverflow">@shlWithOverflow</a>.</li> </ul> </td> <td> <pre data-language="zig">1 &lt;&lt; 8 == 256</pre> </td> </tr> <tr> <th scope="row"><pre data-language="zig">a &lt;&lt;| b
a &lt;&lt;|= b</pre></th> <td> <ul> <li><a href="#Integers">Integers</a></li> </ul> </td> <td>Saturating Bit Shift Left. <ul> <li>See also <a href="#shlExact">@shlExact</a>.</li> <li>See also <a href="#shlWithOverflow">@shlWithOverflow</a>.</li> </ul> </td> <td> <pre data-language="zig">@as(u8, 1) &lt;&lt;| 8 == 255</pre> </td> </tr> <tr> <th scope="row"><pre data-language="zig">a &gt;&gt; b
a &gt;&gt;= b</pre></th> <td> <ul> <li><a href="#Integers">Integers</a></li> </ul> </td> <td>Bit Shift Right. <ul> <li>
<code><span class="line">b</span></code> must be <a href="#comptime">comptime-known</a> or have a type with log2 number of bits as <code><span class="line">a</span></code>.</li> <li>See also <a href="#shrExact">@shrExact</a>.</li> </ul> </td> <td> <pre data-language="zig">10 &gt;&gt; 1 == 5</pre> </td> </tr> <tr> <th scope="row"><pre data-language="zig">a &amp; b
a &amp;= b</pre></th> <td> <ul> <li><a href="#Integers">Integers</a></li> </ul> </td> <td>Bitwise AND. <ul> <li>Invokes <a href="#Peer-Type-Resolution">Peer Type Resolution</a> for the operands.</li> </ul> </td> <td> <pre data-language="zig">0b011 &amp; 0b101 == 0b001</pre> </td> </tr> <tr> <th scope="row"><pre data-language="zig">a | b
a |= b</pre></th> <td> <ul> <li><a href="#Integers">Integers</a></li> </ul> </td> <td>Bitwise OR. <ul> <li>Invokes <a href="#Peer-Type-Resolution">Peer Type Resolution</a> for the operands.</li> </ul> </td> <td> <pre data-language="zig">0b010 | 0b100 == 0b110</pre> </td> </tr> <tr> <th scope="row"><pre data-language="zig">a ^ b
a ^= b</pre></th> <td> <ul> <li><a href="#Integers">Integers</a></li> </ul> </td> <td>Bitwise XOR. <ul> <li>Invokes <a href="#Peer-Type-Resolution">Peer Type Resolution</a> for the operands.</li> </ul> </td> <td> <pre data-language="zig">0b011 ^ 0b101 == 0b110</pre> </td> </tr> <tr> <th scope="row"><pre data-language="zig">~a</pre></th> <td> <ul> <li><a href="#Integers">Integers</a></li> </ul> </td> <td> Bitwise NOT. </td> <td> <pre data-language="zig">~@as(u8, 0b10101111) == 0b01010000</pre> </td> </tr> <tr> <th scope="row"><pre data-language="zig">a orelse b</pre></th> <td> <ul> <li><a href="#Optionals">Optionals</a></li> </ul> </td> <td>If <code><span class="line">a</span></code> is <code><span class="line"><span class="tok-null">null</span></span></code>, returns <code><span class="line">b</span></code> ("default value"), otherwise returns the unwrapped value of <code><span class="line">a</span></code>. Note that <code><span class="line">b</span></code> may be a value of type <a href="#noreturn">noreturn</a>. </td> <td> <pre data-language="zig">const value: ?u32 = null;
const unwrapped = value orelse 1234;
unwrapped == 1234</pre> </td> </tr> <tr> <th scope="row"><pre data-language="zig">a.?</pre></th> <td> <ul> <li><a href="#Optionals">Optionals</a></li> </ul> </td> <td> Equivalent to: <pre data-language="zig">a orelse unreachable</pre> </td> <td> <pre data-language="zig">const value: ?u32 = 5678;
value.? == 5678</pre> </td> </tr> <tr> <th scope="row"><pre data-language="zig">a catch b
a catch |err| b</pre></th> <td> <ul> <li><a href="#Errors">Error Unions</a></li> </ul> </td> <td>If <code><span class="line">a</span></code> is an <code><span class="line"><span class="tok-kw">error</span></span></code>, returns <code><span class="line">b</span></code> ("default value"), otherwise returns the unwrapped value of <code><span class="line">a</span></code>. Note that <code><span class="line">b</span></code> may be a value of type <a href="#noreturn">noreturn</a>. <code><span class="line">err</span></code> is the <code><span class="line"><span class="tok-kw">error</span></span></code> and is in scope of the expression <code><span class="line">b</span></code>. </td> <td> <pre data-language="zig">const value: anyerror!u32 = error.Broken;
const unwrapped = value catch 1234;
unwrapped == 1234</pre> </td> </tr> <tr> <th scope="row"><pre data-language="zig">a and b</pre></th> <td> <ul> <li><a href="#Primitive-Types">bool</a></li> </ul> </td> <td> If <code><span class="line">a</span></code> is <code><span class="line"><span class="tok-null">false</span></span></code>, returns <code><span class="line"><span class="tok-null">false</span></span></code> without evaluating <code><span class="line">b</span></code>. Otherwise, returns <code><span class="line">b</span></code>. </td> <td> <pre data-language="zig">(false and true) == false</pre> </td> </tr> <tr> <th scope="row"><pre data-language="zig">a or b</pre></th> <td> <ul> <li><a href="#Primitive-Types">bool</a></li> </ul> </td> <td> If <code><span class="line">a</span></code> is <code><span class="line"><span class="tok-null">true</span></span></code>, returns <code><span class="line"><span class="tok-null">true</span></span></code> without evaluating <code><span class="line">b</span></code>. Otherwise, returns <code><span class="line">b</span></code>. </td> <td> <pre data-language="zig">(false or true) == true</pre> </td> </tr> <tr> <th scope="row"><pre data-language="zig">!a</pre></th> <td> <ul> <li><a href="#Primitive-Types">bool</a></li> </ul> </td> <td> Boolean NOT. </td> <td> <pre data-language="zig">!false == true</pre> </td> </tr> <tr> <th scope="row"><pre data-language="zig">a == b</pre></th> <td> <ul> <li><a href="#Integers">Integers</a></li> <li><a href="#Floats">Floats</a></li> <li><a href="#Primitive-Types">bool</a></li> <li><a href="#Primitive-Types">type</a></li> </ul> </td> <td> Returns <code><span class="line"><span class="tok-null">true</span></span></code> if a and b are equal, otherwise returns <code><span class="line"><span class="tok-null">false</span></span></code>. Invokes <a href="#Peer-Type-Resolution">Peer Type Resolution</a> for the operands. </td> <td> <pre data-language="zig">(1 == 1) == true</pre> </td> </tr> <tr> <th scope="row"><pre data-language="zig">a == null</pre></th> <td> <ul> <li><a href="#Optionals">Optionals</a></li> </ul> </td> <td> Returns <code><span class="line"><span class="tok-null">true</span></span></code> if a is <code><span class="line"><span class="tok-null">null</span></span></code>, otherwise returns <code><span class="line"><span class="tok-null">false</span></span></code>. </td> <td> <pre data-language="zig">const value: ?u32 = null;
value == null</pre> </td> </tr> <tr> <th scope="row"><pre data-language="zig">a != b</pre></th> <td> <ul> <li><a href="#Integers">Integers</a></li> <li><a href="#Floats">Floats</a></li> <li><a href="#Primitive-Types">bool</a></li> <li><a href="#Primitive-Types">type</a></li> </ul> </td> <td> Returns <code><span class="line"><span class="tok-null">false</span></span></code> if a and b are equal, otherwise returns <code><span class="line"><span class="tok-null">true</span></span></code>. Invokes <a href="#Peer-Type-Resolution">Peer Type Resolution</a> for the operands. </td> <td> <pre data-language="zig">(1 != 1) == false</pre> </td> </tr> <tr> <th scope="row"><pre data-language="zig">a &gt; b</pre></th> <td> <ul> <li><a href="#Integers">Integers</a></li> <li><a href="#Floats">Floats</a></li> </ul> </td> <td> Returns <code><span class="line"><span class="tok-null">true</span></span></code> if a is greater than b, otherwise returns <code><span class="line"><span class="tok-null">false</span></span></code>. Invokes <a href="#Peer-Type-Resolution">Peer Type Resolution</a> for the operands. </td> <td> <pre data-language="zig">(2 &gt; 1) == true</pre> </td> </tr> <tr> <th scope="row"><pre data-language="zig">a &gt;= b</pre></th> <td> <ul> <li><a href="#Integers">Integers</a></li> <li><a href="#Floats">Floats</a></li> </ul> </td> <td> Returns <code><span class="line"><span class="tok-null">true</span></span></code> if a is greater than or equal to b, otherwise returns <code><span class="line"><span class="tok-null">false</span></span></code>. Invokes <a href="#Peer-Type-Resolution">Peer Type Resolution</a> for the operands. </td> <td> <pre data-language="zig">(2 &gt;= 1) == true</pre> </td> </tr> <tr> <th scope="row"><pre data-language="zig">a &lt; b</pre></th> <td> <ul> <li><a href="#Integers">Integers</a></li> <li><a href="#Floats">Floats</a></li> </ul> </td> <td> Returns <code><span class="line"><span class="tok-null">true</span></span></code> if a is less than b, otherwise returns <code><span class="line"><span class="tok-null">false</span></span></code>. Invokes <a href="#Peer-Type-Resolution">Peer Type Resolution</a> for the operands. </td> <td> <pre data-language="zig">(1 &lt; 2) == true&gt;</pre> </td> </tr> <tr> <th scope="row"><pre data-language="zig">a &lt;= b</pre></th> <td> <ul> <li><a href="#Integers">Integers</a></li> <li><a href="#Floats">Floats</a></li> </ul> </td> <td> Returns <code><span class="line"><span class="tok-null">true</span></span></code> if a is less than or equal to b, otherwise returns <code><span class="line"><span class="tok-null">false</span></span></code>. Invokes <a href="#Peer-Type-Resolution">Peer Type Resolution</a> for the operands. </td> <td> <pre data-language="zig">(1 &lt;= 2) == true</pre> </td> </tr> <tr> <th scope="row"><pre data-language="zig">a ++ b</pre></th> <td> <ul> <li><a href="#Arrays">Arrays</a></li> </ul> </td> <td> Array concatenation. <ul> <li>Only available when the lengths of both <code><span class="line">a</span></code> and <code><span class="line">b</span></code> are <a href="#comptime">compile-time known</a>.</li> </ul> </td> <td> <pre data-language="zig">const mem = @import("std").mem;
const array1 = [_]u32{1,2};
const array2 = [_]u32{3,4};
const together = array1 ++ array2;
mem.eql(u32, &amp;together, &amp;[_]u32{1,2,3,4})</pre> </td> </tr> <tr> <th scope="row"><pre data-language="zig">a ** b</pre></th> <td> <ul> <li><a href="#Arrays">Arrays</a></li> </ul> </td> <td> Array multiplication. <ul> <li>Only available when the length of <code><span class="line">a</span></code> and <code><span class="line">b</span></code> are <a href="#comptime">compile-time known</a>.</li> </ul> </td> <td> <pre data-language="zig">const mem = @import("std").mem;
const pattern = "ab" ** 3;
mem.eql(u8, pattern, "ababab")</pre> </td> </tr> <tr> <th scope="row"><pre data-language="zig">a.*</pre></th> <td> <ul> <li><a href="#Pointers">Pointers</a></li> </ul> </td> <td> Pointer dereference. </td> <td> <pre data-language="zig">const x: u32 = 1234;
const ptr = &amp;x;
ptr.* == 1234</pre> </td> </tr> <tr> <th scope="row"><pre data-language="zig">&amp;a</pre></th> <td> All types </td> <td> Address of. </td> <td> <pre data-language="zig">const x: u32 = 1234;
const ptr = &amp;x;
ptr.* == 1234</pre> </td> </tr> <tr> <th scope="row"><pre data-language="zig">a || b</pre></th> <td> <ul> <li><a href="#Error-Set-Type">Error Set Type</a></li> </ul> </td> <td> <a href="#Merging-Error-Sets">Merging Error Sets</a> </td> <td> <pre data-language="zig">const A = error{One};
const B = error{Two};
(A || B) == error{One, Two}</pre> </td> </tr> </tbody> </table> </div> <h3 id="Precedence">Precedence </h3> <pre data-language="zig">x() x[] x.y x.* x.?
a!b
x{}
!x -x -%x ~x &amp;x ?x
* / % ** *% *| ||
+ - ++ +% -% +| -|
&lt;&lt; &gt;&gt; &lt;&lt;|
&amp; ^ | orelse catch
== != &lt; &gt; &lt;= &gt;=
and
or
= *= *%= *|= /= %= += +%= +|= -= -%= -|= &lt;&lt;= &lt;&lt;|= &gt;&gt;= &amp;= ^= |=</pre> <h2 id="Arrays">Arrays </h2> <figure><figcaption class="zig-cap"><cite class="file">test_arrays.zig</cite></figcaption><pre data-language="zig">const expect = @import("std").testing.expect;
const assert = @import("std").debug.assert;
const mem = @import("std").mem;

// array literal
const message = [_]u8{ 'h', 'e', 'l', 'l', 'o' };

// get the size of an array
comptime {
    assert(message.len == 5);
}

// A string literal is a single-item pointer to an array.
const same_message = "hello";

comptime {
    assert(mem.eql(u8, &amp;message, same_message));
}

test "iterate over an array" {
    var sum: usize = 0;
    for (message) |byte| {
        sum += byte;
    }
    try expect(sum == 'h' + 'e' + 'l' * 2 + 'o');
}

// modifiable array
var some_integers: [100]i32 = undefined;

test "modify an array" {
    for (&amp;some_integers, 0..) |*item, i| {
        item.* = @intCast(i);
    }
    try expect(some_integers[10] == 10);
    try expect(some_integers[99] == 99);
}

// array concatenation works if the values are known
// at compile time
const part_one = [_]i32{ 1, 2, 3, 4 };
const part_two = [_]i32{ 5, 6, 7, 8 };
const all_of_it = part_one ++ part_two;
comptime {
    assert(mem.eql(i32, &amp;all_of_it, &amp;[_]i32{ 1, 2, 3, 4, 5, 6, 7, 8 }));
}

// remember that string literals are arrays
const hello = "hello";
const world = "world";
const hello_world = hello ++ " " ++ world;
comptime {
    assert(mem.eql(u8, hello_world, "hello world"));
}

// ** does repeating patterns
const pattern = "ab" ** 3;
comptime {
    assert(mem.eql(u8, pattern, "ababab"));
}

// initialize an array to zero
const all_zero = [_]u16{0} ** 10;

comptime {
    assert(all_zero.len == 10);
    assert(all_zero[5] == 0);
}

// use compile-time code to initialize an array
var fancy_array = init: {
    var initial_value: [10]Point = undefined;
    for (&amp;initial_value, 0..) |*pt, i| {
        pt.* = Point{
            .x = @intCast(i),
            .y = @intCast(i * 2),
        };
    }
    break :init initial_value;
};
const Point = struct {
    x: i32,
    y: i32,
};

test "compile-time array initialization" {
    try expect(fancy_array[4].x == 4);
    try expect(fancy_array[4].y == 8);
}

// call a function to initialize an array
var more_points = [_]Point{makePoint(3)} ** 10;
fn makePoint(x: i32) Point {
    return Point{
        .x = x,
        .y = x * 2,
    };
}
test "array initialization with function calls" {
    try expect(more_points[4].x == 3);
    try expect(more_points[4].y == 6);
    try expect(more_points.len == 10);
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_arrays.zig</kbd>
1/4 test.iterate over an array... OK
2/4 test.modify an array... OK
3/4 test.compile-time array initialization... OK
4/4 test.array initialization with function calls... OK
All 4 tests passed.
</samp></pre></figure> <p>See also:</p>
<ul> <li><a href="#for">for</a></li> <li><a href="#Slices">Slices</a></li> </ul> <h3 id="Multidimensional-Arrays">Multidimensional Arrays </h3> <p> Multidimensional arrays can be created by nesting arrays: </p> <figure><figcaption class="zig-cap"><cite class="file">test_multidimensional_arrays.zig</cite></figcaption><pre data-language="zig">const std = @import("std");
const expect = std.testing.expect;

const mat4x4 = [4][4]f32{
    [_]f32{ 1.0, 0.0, 0.0, 0.0 },
    [_]f32{ 0.0, 1.0, 0.0, 1.0 },
    [_]f32{ 0.0, 0.0, 1.0, 0.0 },
    [_]f32{ 0.0, 0.0, 0.0, 1.0 },
};
test "multidimensional arrays" {
    // Access the 2D array by indexing the outer array, and then the inner array.
    try expect(mat4x4[1][1] == 1.0);

    // Here we iterate with for loops.
    for (mat4x4, 0..) |row, row_index| {
        for (row, 0..) |cell, column_index| {
            if (row_index == column_index) {
                try expect(cell == 1.0);
            }
        }
    }
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_multidimensional_arrays.zig</kbd>
1/1 test.multidimensional arrays... OK
All 1 tests passed.
</samp></pre></figure> <h3 id="Sentinel-Terminated-Arrays">Sentinel-Terminated Arrays </h3> <p> The syntax <code><span class="line">[N:x]T</span></code> describes an array which has a sentinel element of value <code><span class="line">x</span></code> at the index corresponding to <code><span class="line">len</span></code>. </p> <figure><figcaption class="zig-cap"><cite class="file">test_null_terminated_array.zig</cite></figcaption><pre data-language="zig">const std = @import("std");
const expect = std.testing.expect;

test "null terminated array" {
    const array = [_:0]u8 {1, 2, 3, 4};

    try expect(@TypeOf(array) == [4:0]u8);
    try expect(array.len == 4);
    try expect(array[4] == 0);
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_null_terminated_array.zig</kbd>
1/1 test.null terminated array... OK
All 1 tests passed.
</samp></pre></figure> <p>See also:</p>
<ul> <li><a href="#Sentinel-Terminated-Pointers">Sentinel-Terminated Pointers</a></li> <li><a href="#Sentinel-Terminated-Slices">Sentinel-Terminated Slices</a></li> </ul> <h2 id="Vectors">Vectors </h2> <p> A vector is a group of booleans, <a href="#Integers">Integers</a>, <a href="#Floats">Floats</a>, or <a href="#Pointers">Pointers</a> which are operated on in parallel, using SIMD instructions if possible. Vector types are created with the builtin function <a href="#Vector">@Vector</a>. </p> <p> Vectors support the same builtin operators as their underlying base types. These operations are performed element-wise, and return a vector of the same length as the input vectors. This includes: </p> <ul> <li>Arithmetic (<code><span class="line">+</span></code>, <code><span class="line">-</span></code>, <code><span class="line">/</span></code>, <code><span class="line">*</span></code>, <code><span class="line"><span class="tok-builtin">@divFloor</span></span></code>, <code><span class="line"><span class="tok-builtin">@sqrt</span></span></code>, <code><span class="line"><span class="tok-builtin">@ceil</span></span></code>, <code><span class="line"><span class="tok-builtin">@log</span></span></code>, etc.)</li> <li>Bitwise operators (<code><span class="line">&gt;&gt;</span></code>, <code><span class="line">&lt;&lt;</span></code>, <code><span class="line">&amp;</span></code>, <code><span class="line">|</span></code>, <code><span class="line">~</span></code>, etc.)</li> <li>Comparison operators (<code><span class="line">&lt;</span></code>, <code><span class="line">&gt;</span></code>, <code><span class="line">==</span></code>, etc.)</li> </ul> <p> It is prohibited to use a math operator on a mixture of scalars (individual numbers) and vectors. Zig provides the <a href="#splat">@splat</a> builtin to easily convert from scalars to vectors, and it supports <a href="#reduce">@reduce</a> and array indexing syntax to convert from vectors to scalars. Vectors also support assignment to and from fixed-length arrays with comptime-known length. </p> <p> For rearranging elements within and between vectors, Zig provides the <a href="#shuffle">@shuffle</a> and <a href="#select">@select</a> functions. </p> <p> Operations on vectors shorter than the target machine's native SIMD size will typically compile to single SIMD instructions, while vectors longer than the target machine's native SIMD size will compile to multiple SIMD instructions. If a given operation doesn't have SIMD support on the target architecture, the compiler will default to operating on each vector element one at a time. Zig supports any comptime-known vector length up to 2^32-1, although small powers of two (2-64) are most typical. Note that excessively long vector lengths (e.g. 2^20) may result in compiler crashes on current versions of Zig. </p> <figure><figcaption class="zig-cap"><cite class="file">test_vector.zig</cite></figcaption><pre data-language="zig">const std = @import("std");
const expectEqual = std.testing.expectEqual;

test "Basic vector usage" {
    // Vectors have a compile-time known length and base type.
    const a = @Vector(4, i32){ 1, 2, 3, 4 };
    const b = @Vector(4, i32){ 5, 6, 7, 8 };

    // Math operations take place element-wise.
    const c = a + b;

    // Individual vector elements can be accessed using array indexing syntax.
    try expectEqual(6, c[0]);
    try expectEqual(8, c[1]);
    try expectEqual(10, c[2]);
    try expectEqual(12, c[3]);
}

test "Conversion between vectors, arrays, and slices" {
    // Vectors and fixed-length arrays can be automatically assigned back and forth
    var arr1: [4]f32 = [_]f32{ 1.1, 3.2, 4.5, 5.6 };
    var vec: @Vector(4, f32) = arr1;
    var arr2: [4]f32 = vec;
    try expectEqual(arr1, arr2);

    // You can also assign from a slice with comptime-known length to a vector using .*
    const vec2: @Vector(2, f32) = arr1[1..3].*;

    var slice: []const f32 = &amp;arr1;
    var offset: u32 = 1;
    // To extract a comptime-known length from a runtime-known offset,
    // first extract a new slice from the starting offset, then an array of
    // comptime-known length
    const vec3: @Vector(2, f32) = slice[offset..][0..2].*;
    try expectEqual(slice[offset], vec2[0]);
    try expectEqual(slice[offset + 1], vec2[1]);
    try expectEqual(vec2, vec3);
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_vector.zig</kbd>
1/2 test.Basic vector usage... OK
2/2 test.Conversion between vectors, arrays, and slices... OK
All 2 tests passed.
</samp></pre></figure> <p> TODO talk about C ABI interop<br> TODO consider suggesting std.MultiArrayList </p> <p>See also:</p>
<ul> <li><a href="#splat">@splat</a></li> <li><a href="#shuffle">@shuffle</a></li> <li><a href="#select">@select</a></li> <li><a href="#reduce">@reduce</a></li> </ul> <h2 id="Pointers">Pointers </h2> <p> Zig has two kinds of pointers: single-item and many-item. </p> <ul> <li>
<code><span class="line">*T</span></code> - single-item pointer to exactly one item. <ul> <li>Supports deref syntax: <code><span class="line">ptr.*</span></code>
</li> </ul> </li> <li>
<code><span class="line">[*]T</span></code> - many-item pointer to unknown number of items. <ul> <li>Supports index syntax: <code><span class="line">ptr[i]</span></code>
</li> <li>Supports slice syntax: <code><span class="line">ptr[start..end]</span></code>
</li> <li>Supports pointer arithmetic: <code><span class="line">ptr + x</span></code>, <code><span class="line">ptr - x</span></code>
</li> <li>
<code><span class="line">T</span></code> must have a known size, which means that it cannot be <code><span class="line"><span class="tok-type">anyopaque</span></span></code> or any other <a href="#opaque">opaque type</a>.</li> </ul> </li> </ul> <p>These types are closely related to <a href="#Arrays">Arrays</a> and <a href="#Slices">Slices</a>:</p> <ul> <li>
<code><span class="line">*[N]T</span></code> - pointer to N items, same as single-item pointer to an array. <ul> <li>Supports index syntax: <code><span class="line">array_ptr[i]</span></code>
</li> <li>Supports slice syntax: <code><span class="line">array_ptr[start..end]</span></code>
</li> <li>Supports len property: <code><span class="line">array_ptr.len</span></code>
</li> </ul> </li> </ul> <ul> <li>
<code><span class="line">[]T</span></code> - is a slice (a fat pointer, which contains a pointer of type <code><span class="line">[*]T</span></code> and a length). <ul> <li>Supports index syntax: <code><span class="line">slice[i]</span></code>
</li> <li>Supports slice syntax: <code><span class="line">slice[start..end]</span></code>
</li> <li>Supports len property: <code><span class="line">slice.len</span></code>
</li> </ul> </li> </ul> <p>Use <code><span class="line">&amp;x</span></code> to obtain a single-item pointer:</p> <figure><figcaption class="zig-cap"><cite class="file">test_single_item_pointer.zig</cite></figcaption><pre data-language="zig">const expect = @import("std").testing.expect;

test "address of syntax" {
    // Get the address of a variable:
    const x: i32 = 1234;
    const x_ptr = &amp;x;

    // Dereference a pointer:
    try expect(x_ptr.* == 1234);

    // When you get the address of a const variable, you get a const single-item pointer.
    try expect(@TypeOf(x_ptr) == *const i32);

    // If you want to mutate the value, you'd need an address of a mutable variable:
    var y: i32 = 5678;
    const y_ptr = &amp;y;
    try expect(@TypeOf(y_ptr) == *i32);
    y_ptr.* += 1;
    try expect(y_ptr.* == 5679);
}

test "pointer array access" {
    // Taking an address of an individual element gives a
    // single-item pointer. This kind of pointer
    // does not support pointer arithmetic.
    var array = [_]u8{ 1, 2, 3, 4, 5, 6, 7, 8, 9, 10 };
    const ptr = &amp;array[2];
    try expect(@TypeOf(ptr) == *u8);

    try expect(array[2] == 3);
    ptr.* += 1;
    try expect(array[2] == 4);
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_single_item_pointer.zig</kbd>
1/2 test.address of syntax... OK
2/2 test.pointer array access... OK
All 2 tests passed.
</samp></pre></figure> <p> Zig supports pointer arithmetic. It's better to assign the pointer to <code><span class="line">[*]T</span></code> and increment that variable. For example, directly incrementing the pointer from a slice will corrupt it. </p> <figure><figcaption class="zig-cap"><cite class="file">test_pointer_arithmetic.zig</cite></figcaption><pre data-language="zig">const expect = @import("std").testing.expect;

test "pointer arithmetic with many-item pointer" {
    const array = [_]i32{ 1, 2, 3, 4 };
    var ptr: [*]const i32 = &amp;array;

    try expect(ptr[0] == 1);
    ptr += 1;
    try expect(ptr[0] == 2);
}

test "pointer arithmetic with slices" {
    var array = [_]i32{ 1, 2, 3, 4 };
    var length: usize = 0;
    var slice = array[length..array.len];

    try expect(slice[0] == 1);
    try expect(slice.len == 4);

    slice.ptr += 1;
    // now the slice is in an bad state since len has not been updated

    try expect(slice[0] == 2);
    try expect(slice.len == 4);
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_pointer_arithmetic.zig</kbd>
1/2 test.pointer arithmetic with many-item pointer... OK
2/2 test.pointer arithmetic with slices... OK
All 2 tests passed.
</samp></pre></figure> <p> In Zig, we generally prefer <a href="#Slices">Slices</a> rather than <a href="#Sentinel-Terminated-Pointers">Sentinel-Terminated Pointers</a>. You can turn an array or pointer into a slice using slice syntax. </p> <p> Slices have bounds checking and are therefore protected against this kind of undefined behavior. This is one reason we prefer slices to pointers. </p> <figure><figcaption class="zig-cap"><cite class="file">test_slice_bounds.zig</cite></figcaption><pre data-language="zig">const expect = @import("std").testing.expect;

test "pointer slicing" {
    var array = [_]u8{ 1, 2, 3, 4, 5, 6, 7, 8, 9, 10 };
    var start: usize = 2;
    const slice = array[start..4];
    try expect(slice.len == 2);

    try expect(array[3] == 4);
    slice[1] += 1;
    try expect(array[3] == 5);
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_slice_bounds.zig</kbd>
1/1 test.pointer slicing... OK
All 1 tests passed.
</samp></pre></figure> <p>Pointers work at compile-time too, as long as the code does not depend on an undefined memory layout:</p> <figure><figcaption class="zig-cap"><cite class="file">test_comptime_pointers.zig</cite></figcaption><pre data-language="zig">const expect = @import("std").testing.expect;

test "comptime pointers" {
    comptime {
        var x: i32 = 1;
        const ptr = &amp;x;
        ptr.* += 1;
        x += 1;
        try expect(ptr.* == 3);
    }
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_comptime_pointers.zig</kbd>
1/1 test.comptime pointers... OK
All 1 tests passed.
</samp></pre></figure> <p>To convert an integer address into a pointer, use <code><span class="line"><span class="tok-builtin">@ptrFromInt</span></span></code>. To convert a pointer to an integer, use <code><span class="line"><span class="tok-builtin">@intFromPtr</span></span></code>:</p> <figure><figcaption class="zig-cap"><cite class="file">test_integer_pointer_conversion.zig</cite></figcaption><pre data-language="zig">const expect = @import("std").testing.expect;

test "@intFromPtr and @ptrFromInt" {
    const ptr: *i32 = @ptrFromInt(0xdeadbee0);
    const addr = @intFromPtr(ptr);
    try expect(@TypeOf(addr) == usize);
    try expect(addr == 0xdeadbee0);
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_integer_pointer_conversion.zig</kbd>
1/1 test.@intFromPtr and @ptrFromInt... OK
All 1 tests passed.
</samp></pre></figure> <p>Zig is able to preserve memory addresses in comptime code, as long as the pointer is never dereferenced:</p> <figure><figcaption class="zig-cap"><cite class="file">test_comptime_pointer_conversion.zig</cite></figcaption><pre data-language="zig">const expect = @import("std").testing.expect;

test "comptime @ptrFromInt" {
    comptime {
        // Zig is able to do this at compile-time, as long as
        // ptr is never dereferenced.
        const ptr: *i32 = @ptrFromInt(0xdeadbee0);
        const addr = @intFromPtr(ptr);
        try expect(@TypeOf(addr) == usize);
        try expect(addr == 0xdeadbee0);
    }
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_comptime_pointer_conversion.zig</kbd>
1/1 test.comptime @ptrFromInt... OK
All 1 tests passed.
</samp></pre></figure> <p>See also:</p>
<ul> <li><a href="#Optional-Pointers">Optional Pointers</a></li> <li><a href="#ptrFromInt">@ptrFromInt</a></li> <li><a href="#intFromPtr">@intFromPtr</a></li> <li><a href="#C-Pointers">C Pointers</a></li> </ul> <h3 id="volatile">volatile </h3> <p>Loads and stores are assumed to not have side effects. If a given load or store should have side effects, such as Memory Mapped Input/Output (MMIO), use <code><span class="line"><span class="tok-kw">volatile</span></span></code>. In the following code, loads and stores with <code><span class="line">mmio_ptr</span></code> are guaranteed to all happen and in the same order as in source code:</p> <figure><figcaption class="zig-cap"><cite class="file">test_volatile.zig</cite></figcaption><pre data-language="zig">const expect = @import("std").testing.expect;

test "volatile" {
    const mmio_ptr: *volatile u8 = @ptrFromInt(0x12345678);
    try expect(@TypeOf(mmio_ptr) == *volatile u8);
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_volatile.zig</kbd>
1/1 test.volatile... OK
All 1 tests passed.
</samp></pre></figure> <p> Note that <code><span class="line"><span class="tok-kw">volatile</span></span></code> is unrelated to concurrency and <a href="#Atomics">Atomics</a>. If you see code that is using <code><span class="line"><span class="tok-kw">volatile</span></span></code> for something other than Memory Mapped Input/Output, it is probably a bug. </p> <p> To convert one pointer type to another, use <a href="#ptrCast">@ptrCast</a>. This is an unsafe operation that Zig cannot protect you against. Use <code><span class="line"><span class="tok-builtin">@ptrCast</span></span></code> only when other conversions are not possible. </p> <figure><figcaption class="zig-cap"><cite class="file">test_pointer_casting.zig</cite></figcaption><pre data-language="zig">const std = @import("std");
const expect = std.testing.expect;

test "pointer casting" {
    const bytes align(@alignOf(u32)) = [_]u8{ 0x12, 0x12, 0x12, 0x12 };
    const u32_ptr: *const u32 = @ptrCast(&amp;bytes);
    try expect(u32_ptr.* == 0x12121212);

    // Even this example is contrived - there are better ways to do the above than
    // pointer casting. For example, using a slice narrowing cast:
    const u32_value = std.mem.bytesAsSlice(u32, bytes[0..])[0];
    try expect(u32_value == 0x12121212);

    // And even another way, the most straightforward way to do it:
    try expect(@as(u32, @bitCast(bytes)) == 0x12121212);
}

test "pointer child type" {
    // pointer types have a `child` field which tells you the type they point to.
    try expect(@typeInfo(*u32).Pointer.child == u32);
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_pointer_casting.zig</kbd>
1/2 test.pointer casting... OK
2/2 test.pointer child type... OK
All 2 tests passed.
</samp></pre></figure> <h3 id="Alignment">Alignment </h3> <p> Each type has an <strong>alignment</strong> - a number of bytes such that, when a value of the type is loaded from or stored to memory, the memory address must be evenly divisible by this number. You can use <a href="#alignOf">@alignOf</a> to find out this value for any type. </p> <p> Alignment depends on the CPU architecture, but is always a power of two, and less than <code><span class="line"><span class="tok-number">1</span> &lt;&lt; <span class="tok-number">29</span></span></code>. </p> <p> In Zig, a pointer type has an alignment value. If the value is equal to the alignment of the underlying type, it can be omitted from the type: </p> <figure><figcaption class="zig-cap"><cite class="file">test_variable_alignment.zig</cite></figcaption><pre data-language="zig">const std = @import("std");
const builtin = @import("builtin");
const expect = std.testing.expect;

test "variable alignment" {
    var x: i32 = 1234;
    const align_of_i32 = @alignOf(@TypeOf(x));
    try expect(@TypeOf(&amp;x) == *i32);
    try expect(*i32 == *align(align_of_i32) i32);
    if (builtin.target.cpu.arch == .x86_64) {
        try expect(@typeInfo(*i32).Pointer.alignment == 4);
    }
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_variable_alignment.zig</kbd>
1/1 test.variable alignment... OK
All 1 tests passed.
</samp></pre></figure> <p>In the same way that a <code><span class="line">*<span class="tok-type">i32</span></span></code> can be <a href="#Type-Coercion">coerced</a> to a <code><span class="line">*<span class="tok-kw">const</span> <span class="tok-type">i32</span></span></code>, a pointer with a larger alignment can be implicitly cast to a pointer with a smaller alignment, but not vice versa. </p> <p> You can specify alignment on variables and functions. If you do this, then pointers to them get the specified alignment: </p> <figure><figcaption class="zig-cap"><cite class="file">test_variable_func_alignment.zig</cite></figcaption><pre data-language="zig">const expect = @import("std").testing.expect;

var foo: u8 align(4) = 100;

test "global variable alignment" {
    try expect(@typeInfo(@TypeOf(&amp;foo)).Pointer.alignment == 4);
    try expect(@TypeOf(&amp;foo) == *align(4) u8);
    const as_pointer_to_array: *align(4) [1]u8 = &amp;foo;
    const as_slice: []align(4) u8 = as_pointer_to_array;
    const as_unaligned_slice: []u8 = as_slice;
    try expect(as_unaligned_slice[0] == 100);
}

fn derp() align(@sizeOf(usize) * 2) i32 {
    return 1234;
}
fn noop1() align(1) void {}
fn noop4() align(4) void {}

test "function alignment" {
    try expect(derp() == 1234);
    try expect(@TypeOf(noop1) == fn () align(1) void);
    try expect(@TypeOf(noop4) == fn () align(4) void);
    noop1();
    noop4();
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_variable_func_alignment.zig</kbd>
1/2 test.global variable alignment... OK
2/2 test.function alignment... OK
All 2 tests passed.
</samp></pre></figure> <p> If you have a pointer or a slice that has a small alignment, but you know that it actually has a bigger alignment, use <a href="#alignCast">@alignCast</a> to change the pointer into a more aligned pointer. This is a no-op at runtime, but inserts a <a href="#Incorrect-Pointer-Alignment">safety check</a>: </p> <figure><figcaption class="zig-cap"><cite class="file">test_incorrect_pointer_alignment.zig</cite></figcaption><pre data-language="zig">const std = @import("std");

test "pointer alignment safety" {
    var array align(4) = [_]u32{ 0x11111111, 0x11111111 };
    const bytes = std.mem.sliceAsBytes(array[0..]);
    try std.testing.expect(foo(bytes) == 0x11111111);
}
fn foo(bytes: []u8) u32 {
    const slice4 = bytes[1..5];
    const int_slice = std.mem.bytesAsSlice(u32, @as([]align(4) u8, @alignCast(slice4)));
    return int_slice[0];
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_incorrect_pointer_alignment.zig</kbd>
1/1 test.pointer alignment safety... thread 2451744 panic: incorrect alignment
/home/ci/actions-runner/_work/zig-bootstrap/zig/docgen_tmp/test_incorrect_pointer_alignment.zig:10:68: 0x2244df in foo (test)
    const int_slice = std.mem.bytesAsSlice(u32, @as([]align(4) u8, @alignCast(slice4)));
                                                                   ^
/home/ci/actions-runner/_work/zig-bootstrap/zig/docgen_tmp/test_incorrect_pointer_alignment.zig:6:31: 0x2243cf in test.pointer alignment safety (test)
    try std.testing.expect(foo(bytes) == 0x11111111);
                              ^
/home/ci/actions-runner/_work/zig-bootstrap/out/host/lib/zig/test_runner.zig:176:28: 0x22d1c9 in mainTerminal (test)
        } else test_fn.func();
                           ^
/home/ci/actions-runner/_work/zig-bootstrap/out/host/lib/zig/test_runner.zig:36:28: 0x22542a in main (test)
        return mainTerminal();
                           ^
/home/ci/actions-runner/_work/zig-bootstrap/out/host/lib/zig/std/start.zig:564:22: 0x2249c2 in posixCallMainAndExit (test)
            root.main();
                     ^
/home/ci/actions-runner/_work/zig-bootstrap/out/host/lib/zig/std/start.zig:243:5: 0x224511 in _start (test)
    asm volatile (switch (native_arch) {
    ^
???:?:?: 0x0 in ??? (???)
error: the following test command crashed:
/home/ci/actions-runner/_work/zig-bootstrap/out/zig-local-cache/o/d8194fb80c1fbfe244b9f4470745a9ae/test
</samp></pre></figure> <h3 id="allowzero">allowzero </h3> <p> This pointer attribute allows a pointer to have address zero. This is only ever needed on the freestanding OS target, where the address zero is mappable. If you want to represent null pointers, use <a href="#Optional-Pointers">Optional Pointers</a> instead. <a href="#Optional-Pointers">Optional Pointers</a> with <code><span class="line"><span class="tok-kw">allowzero</span></span></code> are not the same size as pointers. In this code example, if the pointer did not have the <code><span class="line"><span class="tok-kw">allowzero</span></span></code> attribute, this would be a <a href="#Pointer-Cast-Invalid-Null">Pointer Cast Invalid Null</a> panic: </p> <figure><figcaption class="zig-cap"><cite class="file">test_allowzero.zig</cite></figcaption><pre data-language="zig">const std = @import("std");
const expect = std.testing.expect;

test "allowzero" {
    var zero: usize = 0;
    var ptr: *allowzero i32 = @ptrFromInt(zero);
    try expect(@intFromPtr(ptr) == 0);
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_allowzero.zig</kbd>
1/1 test.allowzero... OK
All 1 tests passed.
</samp></pre></figure> <h3 id="Sentinel-Terminated-Pointers">Sentinel-Terminated Pointers </h3> <p> The syntax <code><span class="line">[*:x]T</span></code> describes a pointer that has a length determined by a sentinel value. This provides protection against buffer overflow and overreads. </p> <figure><figcaption class="zig-cap"><cite class="file">sentinel-terminated_pointer.zig</cite></figcaption><pre data-language="zig">const std = @import("std");

// This is also available as `std.c.printf`.
pub extern "c" fn printf(format: [*:0]const u8, ...) c_int;

pub fn main() anyerror!void {
    _ = printf("Hello, world!\n"); // OK

    const msg = "Hello, world!\n";
    const non_null_terminated_msg: [msg.len]u8 = msg.*;
    _ = printf(&amp;non_null_terminated_msg);
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig build-exe sentinel-terminated_pointer.zig -lc</kbd>
<span class="sgr-1m">sentinel-terminated_pointer.zig:11:16: </span><span class="sgr-31m">error: </span><span class="sgr-1m">expected type '[*:0]const u8', found '*const [14]u8'
</span>    _ = printf(&amp;non_null_terminated_msg);
               <span class="sgr-32m">^~~~~~~~~~~~~~~~~~~~~~~~
</span><span class="sgr-1m">sentinel-terminated_pointer.zig:11:16: </span><span class="sgr-36m">note: </span><span class="sgr-1m">destination pointer requires '0' sentinel
</span><span class="sgr-1m">sentinel-terminated_pointer.zig:4:35: </span><span class="sgr-36m">note: </span><span class="sgr-1m">parameter type declared here
</span>pub extern "c" fn printf(format: [*:0]const u8, ...) c_int;
                                 <span class="sgr-32m">~^~~~~~~~~~~~
</span><span class="sgr-2m">referenced by:
    callMain: /home/ci/actions-runner/_work/zig-bootstrap/out/host/lib/zig/std/start.zig:574:32
    initEventLoopAndCallMain: /home/ci/actions-runner/_work/zig-bootstrap/out/host/lib/zig/std/start.zig:508:34
    remaining reference traces hidden; use '-freference-trace' to see all reference traces
</span>
</samp></pre></figure> <p>See also:</p>
<ul> <li><a href="#Sentinel-Terminated-Slices">Sentinel-Terminated Slices</a></li> <li><a href="#Sentinel-Terminated-Arrays">Sentinel-Terminated Arrays</a></li> </ul> <h2 id="Slices">Slices </h2> <figure><figcaption class="zig-cap"><cite class="file">test_basic_slices.zig</cite></figcaption><pre data-language="zig">const expect = @import("std").testing.expect;

test "basic slices" {
    var array = [_]i32{ 1, 2, 3, 4 };
    // A slice is a pointer and a length. The difference between an array and
    // a slice is that the array's length is part of the type and known at
    // compile-time, whereas the slice's length is known at runtime.
    // Both can be accessed with the `len` field.
    var known_at_runtime_zero: usize = 0;
    const slice = array[known_at_runtime_zero..array.len];
    try expect(@TypeOf(slice) == []i32);
    try expect(&amp;slice[0] == &amp;array[0]);
    try expect(slice.len == array.len);

    // If you slice with comptime-known start and end positions, the result is
    // a pointer to an array, rather than a slice.
    const array_ptr = array[0..array.len];
    try expect(@TypeOf(array_ptr) == *[array.len]i32);

    // You can perform a slice-by-length by slicing twice. This allows the compiler
    // to perform some optimisations like recognising a comptime-known length when
    // the start position is only known at runtime.
    var runtime_start: usize = 1;
    const length = 2;
    const array_ptr_len = array[runtime_start..][0..length];
    try expect(@TypeOf(array_ptr_len) == *[length]i32);

    // Using the address-of operator on a slice gives a single-item pointer,
    // while using the `ptr` field gives a many-item pointer.
    try expect(@TypeOf(slice.ptr) == [*]i32);
    try expect(@TypeOf(&amp;slice[0]) == *i32);
    try expect(@intFromPtr(slice.ptr) == @intFromPtr(&amp;slice[0]));

    // Slices have array bounds checking. If you try to access something out
    // of bounds, you'll get a safety check failure:
    slice[10] += 1;

    // Note that `slice.ptr` does not invoke safety checking, while `&amp;slice[0]`
    // asserts that the slice has len &gt; 0.
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_basic_slices.zig</kbd>
1/1 test.basic slices... thread 2451814 panic: index out of bounds: index 10, len 4
/home/ci/actions-runner/_work/zig-bootstrap/zig/docgen_tmp/test_basic_slices.zig:36:10: 0x224695 in test.basic slices (test)
    slice[10] += 1;
         ^
/home/ci/actions-runner/_work/zig-bootstrap/out/host/lib/zig/test_runner.zig:176:28: 0x22da69 in mainTerminal (test)
        } else test_fn.func();
                           ^
/home/ci/actions-runner/_work/zig-bootstrap/out/host/lib/zig/test_runner.zig:36:28: 0x225bfa in main (test)
        return mainTerminal();
                           ^
/home/ci/actions-runner/_work/zig-bootstrap/out/host/lib/zig/std/start.zig:564:22: 0x224c92 in posixCallMainAndExit (test)
            root.main();
                     ^
/home/ci/actions-runner/_work/zig-bootstrap/out/host/lib/zig/std/start.zig:243:5: 0x2247e1 in _start (test)
    asm volatile (switch (native_arch) {
    ^
???:?:?: 0x0 in ??? (???)
error: the following test command crashed:
/home/ci/actions-runner/_work/zig-bootstrap/out/zig-local-cache/o/5549974cc24ed8e4b563851bcf6ec010/test
</samp></pre></figure> <p>This is one reason we prefer slices to pointers.</p> <figure><figcaption class="zig-cap"><cite class="file">test_slices.zig</cite></figcaption><pre data-language="zig">const std = @import("std");
const expect = std.testing.expect;
const mem = std.mem;
const fmt = std.fmt;

test "using slices for strings" {
    // Zig has no concept of strings. String literals are const pointers
    // to null-terminated arrays of u8, and by convention parameters
    // that are "strings" are expected to be UTF-8 encoded slices of u8.
    // Here we coerce *const [5:0]u8 and *const [6:0]u8 to []const u8
    const hello: []const u8 = "hello";
    const world: []const u8 = "ä¸–ç•Œ";

    var all_together: [100]u8 = undefined;
    // You can use slice syntax with at least one runtime-known index on an
    // array to convert an array into a slice.
    var start : usize = 0;
    const all_together_slice = all_together[start..];
    // String concatenation example.
    const hello_world = try fmt.bufPrint(all_together_slice, "{s} {s}", .{ hello, world });

    // Generally, you can use UTF-8 and not worry about whether something is a
    // string. If you don't need to deal with individual characters, no need
    // to decode.
    try expect(mem.eql(u8, hello_world, "hello ä¸–ç•Œ"));
}

test "slice pointer" {
    var a: []u8 = undefined;
    try expect(@TypeOf(a) == []u8);
    var array: [10]u8 = undefined;
    const ptr = &amp;array;
    try expect(@TypeOf(ptr) == *[10]u8);

    // A pointer to an array can be sliced just like an array:
    var start: usize = 0;
    var end: usize = 5;
    const slice = ptr[start..end];
    slice[2] = 3;
    try expect(slice[2] == 3);
    // The slice is mutable because we sliced a mutable pointer.
    try expect(@TypeOf(slice) == []u8);

    // Again, slicing with comptime-known indexes will produce another pointer
    // to an array:
    const ptr2 = slice[2..3];
    try expect(ptr2.len == 1);
    try expect(ptr2[0] == 3);
    try expect(@TypeOf(ptr2) == *[1]u8);
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_slices.zig</kbd>
1/2 test.using slices for strings... OK
2/2 test.slice pointer... OK
All 2 tests passed.
</samp></pre></figure> <p>See also:</p>
<ul> <li><a href="#Pointers">Pointers</a></li> <li><a href="#for">for</a></li> <li><a href="#Arrays">Arrays</a></li> </ul> <h3 id="Sentinel-Terminated-Slices">Sentinel-Terminated Slices </h3> <p> The syntax <code><span class="line">[:x]T</span></code> is a slice which has a runtime-known length and also guarantees a sentinel value at the element indexed by the length. The type does not guarantee that there are no sentinel elements before that. Sentinel-terminated slices allow element access to the <code><span class="line">len</span></code> index. </p> <figure><figcaption class="zig-cap"><cite class="file">test_null_terminated_slice.zig</cite></figcaption><pre data-language="zig">const std = @import("std");
const expect = std.testing.expect;

test "null terminated slice" {
    const slice: [:0]const u8 = "hello";

    try expect(slice.len == 5);
    try expect(slice[5] == 0);
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_null_terminated_slice.zig</kbd>
1/1 test.null terminated slice... OK
All 1 tests passed.
</samp></pre></figure> <p> Sentinel-terminated slices can also be created using a variation of the slice syntax <code><span class="line">data[start..end :x]</span></code>, where <code><span class="line">data</span></code> is a many-item pointer, array or slice and <code><span class="line">x</span></code> is the sentinel value. </p> <figure><figcaption class="zig-cap"><cite class="file">test_null_terminated_slicing.zig</cite></figcaption><pre data-language="zig">const std = @import("std");
const expect = std.testing.expect;

test "null terminated slicing" {
    var array = [_]u8{ 3, 2, 1, 0, 3, 2, 1, 0 };
    var runtime_length: usize = 3;
    const slice = array[0..runtime_length :0];

    try expect(@TypeOf(slice) == [:0]u8);
    try expect(slice.len == 3);
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_null_terminated_slicing.zig</kbd>
1/1 test.null terminated slicing... OK
All 1 tests passed.
</samp></pre></figure> <p> Sentinel-terminated slicing asserts that the element in the sentinel position of the backing data is actually the sentinel value. If this is not the case, safety-protected <a href="#Undefined-Behavior">Undefined Behavior</a> results. </p> <figure><figcaption class="zig-cap"><cite class="file">test_sentinel_mismatch.zig</cite></figcaption><pre data-language="zig">const std = @import("std");
const expect = std.testing.expect;

test "sentinel mismatch" {
    var array = [_]u8{ 3, 2, 1, 0 };

    // Creating a sentinel-terminated slice from the array with a length of 2
    // will result in the value `1` occupying the sentinel element position.
    // This does not match the indicated sentinel value of `0` and will lead
    // to a runtime panic.
    var runtime_length: usize = 2;
    const slice = array[0..runtime_length :0];

    _ = slice;
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_sentinel_mismatch.zig</kbd>
1/1 test.sentinel mismatch... thread 2451922 panic: sentinel mismatch: expected 0, found 1
/home/ci/actions-runner/_work/zig-bootstrap/zig/docgen_tmp/test_sentinel_mismatch.zig:12:24: 0x224246 in test.sentinel mismatch (test)
    const slice = array[0..runtime_length :0];
                       ^
/home/ci/actions-runner/_work/zig-bootstrap/out/host/lib/zig/test_runner.zig:176:28: 0x22da49 in mainTerminal (test)
        } else test_fn.func();
                           ^
/home/ci/actions-runner/_work/zig-bootstrap/out/host/lib/zig/test_runner.zig:36:28: 0x225a0a in main (test)
        return mainTerminal();
                           ^
/home/ci/actions-runner/_work/zig-bootstrap/out/host/lib/zig/std/start.zig:564:22: 0x224842 in posixCallMainAndExit (test)
            root.main();
                     ^
/home/ci/actions-runner/_work/zig-bootstrap/out/host/lib/zig/std/start.zig:243:5: 0x224391 in _start (test)
    asm volatile (switch (native_arch) {
    ^
???:?:?: 0x0 in ??? (???)
error: the following test command crashed:
/home/ci/actions-runner/_work/zig-bootstrap/out/zig-local-cache/o/3510ea03a08182d3c72b6b3a42d629a3/test
</samp></pre></figure> <p>See also:</p>
<ul> <li><a href="#Sentinel-Terminated-Pointers">Sentinel-Terminated Pointers</a></li> <li><a href="#Sentinel-Terminated-Arrays">Sentinel-Terminated Arrays</a></li> </ul> <h2 id="struct">struct </h2> <figure><figcaption class="zig-cap"><cite class="file">test_structs.zig</cite></figcaption><pre data-language="zig">// Declare a struct.
// Zig gives no guarantees about the order of fields and the size of
// the struct but the fields are guaranteed to be ABI-aligned.
const Point = struct {
    x: f32,
    y: f32,
};

// Maybe we want to pass it to OpenGL so we want to be particular about
// how the bytes are arranged.
const Point2 = packed struct {
    x: f32,
    y: f32,
};


// Declare an instance of a struct.
const p = Point {
    .x = 0.12,
    .y = 0.34,
};

// Maybe we're not ready to fill out some of the fields.
var p2 = Point {
    .x = 0.12,
    .y = undefined,
};

// Structs can have methods
// Struct methods are not special, they are only namespaced
// functions that you can call with dot syntax.
const Vec3 = struct {
    x: f32,
    y: f32,
    z: f32,

    pub fn init(x: f32, y: f32, z: f32) Vec3 {
        return Vec3 {
            .x = x,
            .y = y,
            .z = z,
        };
    }

    pub fn dot(self: Vec3, other: Vec3) f32 {
        return self.x * other.x + self.y * other.y + self.z * other.z;
    }
};

const expect = @import("std").testing.expect;
test "dot product" {
    const v1 = Vec3.init(1.0, 0.0, 0.0);
    const v2 = Vec3.init(0.0, 1.0, 0.0);
    try expect(v1.dot(v2) == 0.0);

    // Other than being available to call with dot syntax, struct methods are
    // not special. You can reference them as any other declaration inside
    // the struct:
    try expect(Vec3.dot(v1, v2) == 0.0);
}

// Structs can have declarations.
// Structs can have 0 fields.
const Empty = struct {
    pub const PI = 3.14;
};
test "struct namespaced variable" {
    try expect(Empty.PI == 3.14);
    try expect(@sizeOf(Empty) == 0);

    // you can still instantiate an empty struct
    const does_nothing = Empty {};

    _ = does_nothing;
}

// struct field order is determined by the compiler for optimal performance.
// however, you can still calculate a struct base pointer given a field pointer:
fn setYBasedOnX(x: *f32, y: f32) void {
    const point = @fieldParentPtr(Point, "x", x);
    point.y = y;
}
test "field parent pointer" {
    var point = Point {
        .x = 0.1234,
        .y = 0.5678,
    };
    setYBasedOnX(&amp;point.x, 0.9);
    try expect(point.y == 0.9);
}

// You can return a struct from a function. This is how we do generics
// in Zig:
fn LinkedList(comptime T: type) type {
    return struct {
        pub const Node = struct {
            prev: ?*Node,
            next: ?*Node,
            data: T,
        };

        first: ?*Node,
        last:  ?*Node,
        len:   usize,
    };
}

test "linked list" {
    // Functions called at compile-time are memoized. This means you can
    // do this:
    try expect(LinkedList(i32) == LinkedList(i32));

    var list = LinkedList(i32) {
        .first = null,
        .last = null,
        .len = 0,
    };
    try expect(list.len == 0);

    // Since types are first class values you can instantiate the type
    // by assigning it to a variable:
    const ListOfInts = LinkedList(i32);
    try expect(ListOfInts == LinkedList(i32));

    var node = ListOfInts.Node {
        .prev = null,
        .next = null,
        .data = 1234,
    };
    var list2 = LinkedList(i32) {
        .first = &amp;node,
        .last = &amp;node,
        .len = 1,
    };

    // When using a pointer to a struct, fields can be accessed directly,
    // without explicitly dereferencing the pointer.
    // So you can do
    try expect(list2.first.?.data == 1234);
    // instead of try expect(list2.first.?.*.data == 1234);
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_structs.zig</kbd>
1/4 test.dot product... OK
2/4 test.struct namespaced variable... OK
3/4 test.field parent pointer... OK
4/4 test.linked list... OK
All 4 tests passed.
</samp></pre></figure> <h3 id="Default-Field-Values">Default Field Values </h3> <p> Each struct field may have an expression indicating the default field value. Such expressions are executed at <a href="#comptime">comptime</a>, and allow the field to be omitted in a struct literal expression: </p> <figure><figcaption class="zig-cap"><cite class="file">test_struct_default_field_values.zig</cite></figcaption><pre data-language="zig">const Foo = struct {
    a: i32 = 1234,
    b: i32,
};

test "default struct initialization fields" {
    const x = Foo{
        .b = 5,
    };
    if (x.a + x.b != 1239) {
        @compileError("it's even comptime-known!");
    }
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_struct_default_field_values.zig</kbd>
1/1 test.default struct initialization fields... OK
All 1 tests passed.
</samp></pre></figure> <h3 id="extern-struct">extern struct </h3> <p>An <code><span class="line"><span class="tok-kw">extern</span> <span class="tok-kw">struct</span></span></code> has in-memory layout guaranteed to match the C ABI for the target.</p> <p>This kind of struct should only be used for compatibility with the C ABI. Every other use case should be solved with <a href="#packed-struct">packed struct</a> or normal <a href="#struct">struct</a>.</p> <p>See also:</p>
<ul> <li><a href="#extern-union">extern union</a></li> <li><a href="#extern-enum">extern enum</a></li> </ul> <h3 id="packed-struct">packed struct </h3> <p> Unlike normal structs, <code><span class="line"><span class="tok-kw">packed</span></span></code> structs have guaranteed in-memory layout: </p> <ul> <li>Fields remain in the order declared, least to most significant.</li> <li>There is no padding between fields.</li> <li>Zig supports arbitrary width <a href="#Integers">Integers</a> and although normally, integers with fewer than 8 bits will still use 1 byte of memory, in packed structs, they use exactly their bit width. </li> <li>
<code><span class="line"><span class="tok-type">bool</span></span></code> fields use exactly 1 bit.</li> <li>An <a href="#enum">enum</a> field uses exactly the bit width of its integer tag type.</li> <li>A <a href="#packed-union">packed union</a> field uses exactly the bit width of the union field with the largest bit width.</li> <li>Non-ABI-aligned fields are packed into the smallest possible ABI-aligned integers in accordance with the target endianness. </li> </ul> <p> This means that a <code><span class="line"><span class="tok-kw">packed</span> <span class="tok-kw">struct</span></span></code> can participate in a <a href="#bitCast">@bitCast</a> or a <a href="#ptrCast">@ptrCast</a> to reinterpret memory. This even works at <a href="#comptime">comptime</a>: </p> <figure><figcaption class="zig-cap"><cite class="file">test_packed_structs.zig</cite></figcaption><pre data-language="zig">const std = @import("std");
const native_endian = @import("builtin").target.cpu.arch.endian();
const expect = std.testing.expect;

const Full = packed struct {
    number: u16,
};
const Divided = packed struct {
    half1: u8,
    quarter3: u4,
    quarter4: u4,
};

test "@bitCast between packed structs" {
    try doTheTest();
    try comptime doTheTest();
}

fn doTheTest() !void {
    try expect(@sizeOf(Full) == 2);
    try expect(@sizeOf(Divided) == 2);
    var full = Full{ .number = 0x1234 };
    var divided: Divided = @bitCast(full);
    try expect(divided.half1 == 0x34);
    try expect(divided.quarter3 == 0x2);
    try expect(divided.quarter4 == 0x1);

    var ordered: [2]u8 = @bitCast(full);
    switch (native_endian) {
        .Big =&gt; {
            try expect(ordered[0] == 0x12);
            try expect(ordered[1] == 0x34);
        },
        .Little =&gt; {
            try expect(ordered[0] == 0x34);
            try expect(ordered[1] == 0x12);
        },
    }
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_packed_structs.zig</kbd>
1/1 test.@bitCast between packed structs... OK
All 1 tests passed.
</samp></pre></figure> <p> Zig allows the address to be taken of a non-byte-aligned field: </p> <figure><figcaption class="zig-cap"><cite class="file">test_pointer_to_non-byte_aligned_field.zig</cite></figcaption><pre data-language="zig">const std = @import("std");
const expect = std.testing.expect;

const BitField = packed struct {
    a: u3,
    b: u3,
    c: u2,
};

var foo = BitField{
    .a = 1,
    .b = 2,
    .c = 3,
};

test "pointer to non-byte-aligned field" {
    const ptr = &amp;foo.b;
    try expect(ptr.* == 2);
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_pointer_to_non-byte_aligned_field.zig</kbd>
1/1 test.pointer to non-byte-aligned field... OK
All 1 tests passed.
</samp></pre></figure> <p> However, the pointer to a non-byte-aligned field has special properties and cannot be passed when a normal pointer is expected: </p> <figure><figcaption class="zig-cap"><cite class="file">test_misaligned_pointer.zig</cite></figcaption><pre data-language="zig">const std = @import("std");
const expect = std.testing.expect;

const BitField = packed struct {
    a: u3,
    b: u3,
    c: u2,
};

var bit_field = BitField{
    .a = 1,
    .b = 2,
    .c = 3,
};

test "pointer to non-bit-aligned field" {
    try expect(bar(&amp;bit_field.b) == 2);
}

fn bar(x: *const u3) u3 {
    return x.*;
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_misaligned_pointer.zig</kbd>
<span class="sgr-1m">docgen_tmp/test_misaligned_pointer.zig:17:20: </span><span class="sgr-31m">error: </span><span class="sgr-1m">expected type '*const u3', found '*align(1:3:1) u3'
</span>    try expect(bar(&amp;bit_field.b) == 2);
                   <span class="sgr-32m">^~~~~~~~~~~~
</span><span class="sgr-1m">docgen_tmp/test_misaligned_pointer.zig:17:20: </span><span class="sgr-36m">note: </span><span class="sgr-1m">pointer host size '1' cannot cast into pointer host size '0'
</span><span class="sgr-1m">docgen_tmp/test_misaligned_pointer.zig:17:20: </span><span class="sgr-36m">note: </span><span class="sgr-1m">pointer bit offset '3' cannot cast into pointer bit offset '0'
</span><span class="sgr-1m">docgen_tmp/test_misaligned_pointer.zig:20:11: </span><span class="sgr-36m">note: </span><span class="sgr-1m">parameter type declared here
</span>fn bar(x: *const u3) u3 {
          <span class="sgr-32m">^~~~~~~~~
</span>
</samp></pre></figure> <p> In this case, the function <code><span class="line">bar</span></code> cannot be called because the pointer to the non-ABI-aligned field mentions the bit offset, but the function expects an ABI-aligned pointer. </p> <p> Pointers to non-ABI-aligned fields share the same address as the other fields within their host integer: </p> <figure><figcaption class="zig-cap"><cite class="file">test_packed_struct_field_address.zig</cite></figcaption><pre data-language="zig">const std = @import("std");
const expect = std.testing.expect;

const BitField = packed struct {
    a: u3,
    b: u3,
    c: u2,
};

var bit_field = BitField{
    .a = 1,
    .b = 2,
    .c = 3,
};

test "pointers of sub-byte-aligned fields share addresses" {
    try expect(@intFromPtr(&amp;bit_field.a) == @intFromPtr(&amp;bit_field.b));
    try expect(@intFromPtr(&amp;bit_field.a) == @intFromPtr(&amp;bit_field.c));
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_packed_struct_field_address.zig</kbd>
1/1 test.pointers of sub-byte-aligned fields share addresses... OK
All 1 tests passed.
</samp></pre></figure> <p> This can be observed with <a href="#bitOffsetOf">@bitOffsetOf</a> and <a href="#offsetOf">offsetOf</a>: </p> <figure><figcaption class="zig-cap"><cite class="file">test_bitOffsetOf_offsetOf.zig</cite></figcaption><pre data-language="zig">const std = @import("std");
const expect = std.testing.expect;

const BitField = packed struct {
    a: u3,
    b: u3,
    c: u2,
};

test "pointer to non-bit-aligned field" {
    comptime {
        try expect(@bitOffsetOf(BitField, "a") == 0);
        try expect(@bitOffsetOf(BitField, "b") == 3);
        try expect(@bitOffsetOf(BitField, "c") == 6);

        try expect(@offsetOf(BitField, "a") == 0);
        try expect(@offsetOf(BitField, "b") == 0);
        try expect(@offsetOf(BitField, "c") == 0);
    }
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_bitOffsetOf_offsetOf.zig</kbd>
1/1 test.pointer to non-bit-aligned field... OK
All 1 tests passed.
</samp></pre></figure> <p> Packed structs have the same alignment as their backing integer, however, overaligned pointers to packed structs can override this: </p> <figure><figcaption class="zig-cap"><cite class="file">test_overaligned_packed_struct.zig</cite></figcaption><pre data-language="zig">const std = @import("std");
const expect = std.testing.expect;

const S = packed struct {
    a: u32,
    b: u32,
};
test "overaligned pointer to packed struct" {
    var foo: S align(4) = .{ .a = 1, .b = 2 };
    const ptr: *align(4) S = &amp;foo;
    const ptr_to_b: *u32 = &amp;ptr.b;
    try expect(ptr_to_b.* == 2);
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_overaligned_packed_struct.zig</kbd>
1/1 test.overaligned pointer to packed struct... OK
All 1 tests passed.
</samp></pre></figure> <p> It's also possible to set alignment of struct fields: </p> <figure><figcaption class="zig-cap"><cite class="file">test_aligned_struct_fields.zig</cite></figcaption><pre data-language="zig">const std = @import("std");
const expectEqual = std.testing.expectEqual;

test "aligned struct fields" {
    const S = struct {
        a: u32 align(2),
        b: u32 align(64),
    };
    var foo = S{ .a = 1, .b = 2 };

    try expectEqual(64, @alignOf(S));
    try expectEqual(*align(2) u32, @TypeOf(&amp;foo.a));
    try expectEqual(*align(64) u32, @TypeOf(&amp;foo.b));
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_aligned_struct_fields.zig</kbd>
1/1 test.aligned struct fields... OK
All 1 tests passed.
</samp></pre></figure> <p> Using packed structs with <a href="#volatile">volatile</a> is problematic, and may be a compile error in the future. For details on this subscribe to <a href="https://github.com/ziglang/zig/issues/1761">this issue</a>. TODO update these docs with a recommendation on how to use packed structs with MMIO (the use case for volatile packed structs) once this issue is resolved. Don't worry, there will be a good solution for this use case in zig. </p> <h3 id="Struct-Naming">Struct Naming </h3> <p>Since all structs are anonymous, Zig infers the type name based on a few rules.</p> <ul> <li>If the struct is in the initialization expression of a variable, it gets named after that variable.</li> <li>If the struct is in the <code><span class="line"><span class="tok-kw">return</span></span></code> expression, it gets named after the function it is returning from, with the parameter values serialized.</li> <li>Otherwise, the struct gets a name such as <code>(filename.funcname.__struct_ID)</code>.</li> <li>If the struct is declared inside another struct, it gets named after both the parent struct and the name inferred by the previous rules, separated by a dot.</li> </ul> <figure><figcaption class="zig-cap"><cite class="file">struct_name.zig</cite></figcaption><pre data-language="zig">const std = @import("std");

pub fn main() void {
    const Foo = struct {};
    std.debug.print("variable: {s}\n", .{@typeName(Foo)});
    std.debug.print("anonymous: {s}\n", .{@typeName(struct {})});
    std.debug.print("function: {s}\n", .{@typeName(List(i32))});
}

fn List(comptime T: type) type {
    return struct {
        x: T,
    };
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig build-exe struct_name.zig</kbd>
$ <kbd>./struct_name</kbd>
variable: struct_name.main.Foo
anonymous: struct_name.main__struct_3509
function: struct_name.List(i32)
</samp></pre></figure> <h3 id="Anonymous-Struct-Literals">Anonymous Struct Literals </h3> <p> Zig allows omitting the struct type of a literal. When the result is <a href="#Type-Coercion">coerced</a>, the struct literal will directly instantiate the <a href="#Result-Location-Semantics">result location</a>, with no copy: </p> <figure><figcaption class="zig-cap"><cite class="file">test_struct_result.zig</cite></figcaption><pre data-language="zig">const std = @import("std");
const expect = std.testing.expect;

const Point = struct {x: i32, y: i32};

test "anonymous struct literal" {
    var pt: Point = .{
        .x = 13,
        .y = 67,
    };
    try expect(pt.x == 13);
    try expect(pt.y == 67);
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_struct_result.zig</kbd>
1/1 test.anonymous struct literal... OK
All 1 tests passed.
</samp></pre></figure> <p> The struct type can be inferred. Here the <a href="#Result-Location-Semantics">result location</a> does not include a type, and so Zig infers the type: </p> <figure><figcaption class="zig-cap"><cite class="file">test_anonymous_struct.zig</cite></figcaption><pre data-language="zig">const std = @import("std");
const expect = std.testing.expect;

test "fully anonymous struct" {
    try dump(.{
        .int = @as(u32, 1234),
        .float = @as(f64, 12.34),
        .b = true,
        .s = "hi",
    });
}

fn dump(args: anytype) !void {
    try expect(args.int == 1234);
    try expect(args.float == 12.34);
    try expect(args.b);
    try expect(args.s[0] == 'h');
    try expect(args.s[1] == 'i');
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_anonymous_struct.zig</kbd>
1/1 test.fully anonymous struct... OK
All 1 tests passed.
</samp></pre></figure> <h3 id="Tuples">Tuples </h3> <p> Anonymous structs can be created without specifying field names, and are referred to as "tuples". </p> <p> The fields are implicitly named using numbers starting from 0. Because their names are integers, they cannot be accessed with <code><span class="line">.</span></code> syntax without also wrapping them in <code><span class="line">@""</span></code>. Names inside <code><span class="line">@""</span></code> are always recognised as <a href="#Identifiers">identifiers</a>. </p> <p> Like arrays, tuples have a .len field, can be indexed (provided the index is comptime-known) and work with the ++ and ** operators. They can also be iterated over with <a href="#inline-for">inline for</a>. </p> <figure><figcaption class="zig-cap"><cite class="file">test_tuples.zig</cite></figcaption><pre data-language="zig">const std = @import("std");
const expect = std.testing.expect;

test "tuple" {
    const values = .{
        @as(u32, 1234),
        @as(f64, 12.34),
        true,
        "hi",
    } ++ .{false} ** 2;
    try expect(values[0] == 1234);
    try expect(values[4] == false);
    inline for (values, 0..) |v, i| {
        if (i != 2) continue;
        try expect(v);
    }
    try expect(values.len == 6);
    try expect(values.@"3"[0] == 'h');
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_tuples.zig</kbd>
1/1 test.tuple... OK
All 1 tests passed.
</samp></pre></figure> <p>See also:</p>
<ul> <li><a href="#comptime">comptime</a></li> <li><a href="#fieldParentPtr">@fieldParentPtr</a></li> </ul> <h2 id="enum">enum </h2> <figure><figcaption class="zig-cap"><cite class="file">test_enums.zig</cite></figcaption><pre data-language="zig">const expect = @import("std").testing.expect;
const mem = @import("std").mem;

// Declare an enum.
const Type = enum {
    ok,
    not_ok,
};

// Declare a specific enum field.
const c = Type.ok;

// If you want access to the ordinal value of an enum, you
// can specify the tag type.
const Value = enum(u2) {
    zero,
    one,
    two,
};
// Now you can cast between u2 and Value.
// The ordinal value starts from 0, counting up by 1 from the previous member.
test "enum ordinal value" {
    try expect(@intFromEnum(Value.zero) == 0);
    try expect(@intFromEnum(Value.one) == 1);
    try expect(@intFromEnum(Value.two) == 2);
}

// You can override the ordinal value for an enum.
const Value2 = enum(u32) {
    hundred = 100,
    thousand = 1000,
    million = 1000000,
};
test "set enum ordinal value" {
    try expect(@intFromEnum(Value2.hundred) == 100);
    try expect(@intFromEnum(Value2.thousand) == 1000);
    try expect(@intFromEnum(Value2.million) == 1000000);
}

// You can also override only some values.
const Value3 = enum(u4) {
    a,
    b = 8,
    c,
    d = 4,
    e,
};
test "enum implicit ordinal values and overridden values" {
    try expect(@intFromEnum(Value3.a) == 0);
    try expect(@intFromEnum(Value3.b) == 8);
    try expect(@intFromEnum(Value3.c) == 9);
    try expect(@intFromEnum(Value3.d) == 4);
    try expect(@intFromEnum(Value3.e) == 5);
}

// Enums can have methods, the same as structs and unions.
// Enum methods are not special, they are only namespaced
// functions that you can call with dot syntax.
const Suit = enum {
    clubs,
    spades,
    diamonds,
    hearts,

    pub fn isClubs(self: Suit) bool {
        return self == Suit.clubs;
    }
};
test "enum method" {
    const p = Suit.spades;
    try expect(!p.isClubs());
}

// An enum can be switched upon.
const Foo = enum {
    string,
    number,
    none,
};
test "enum switch" {
    const p = Foo.number;
    const what_is_it = switch (p) {
        Foo.string =&gt; "this is a string",
        Foo.number =&gt; "this is a number",
        Foo.none =&gt; "this is a none",
    };
    try expect(mem.eql(u8, what_is_it, "this is a number"));
}

// @typeInfo can be used to access the integer tag type of an enum.
const Small = enum {
    one,
    two,
    three,
    four,
};
test "std.meta.Tag" {
    try expect(@typeInfo(Small).Enum.tag_type == u2);
}

// @typeInfo tells us the field count and the fields names:
test "@typeInfo" {
    try expect(@typeInfo(Small).Enum.fields.len == 4);
    try expect(mem.eql(u8, @typeInfo(Small).Enum.fields[1].name, "two"));
}

// @tagName gives a [:0]const u8 representation of an enum value:
test "@tagName" {
    try expect(mem.eql(u8, @tagName(Small.three), "three"));
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_enums.zig</kbd>
1/8 test.enum ordinal value... OK
2/8 test.set enum ordinal value... OK
3/8 test.enum implicit ordinal values and overridden values... OK
4/8 test.enum method... OK
5/8 test.enum switch... OK
6/8 test.std.meta.Tag... OK
7/8 test.@typeInfo... OK
8/8 test.@tagName... OK
All 8 tests passed.
</samp></pre></figure> <p>See also:</p>
<ul> <li><a href="#typeInfo">@typeInfo</a></li> <li><a href="#tagName">@tagName</a></li> <li><a href="#sizeOf">@sizeOf</a></li> </ul> <h3 id="extern-enum">extern enum </h3> <p> By default, enums are not guaranteed to be compatible with the C ABI: </p> <figure><figcaption class="zig-cap"><cite class="file">enum_export_error.zig</cite></figcaption><pre data-language="zig">const Foo = enum { a, b, c };
export fn entry(foo: Foo) void { _ = foo; }</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig build-obj enum_export_error.zig</kbd>
<span class="sgr-1m">docgen_tmp/enum_export_error.zig:2:17: </span><span class="sgr-31m">error: </span><span class="sgr-1m">parameter of type 'enum_export_error.Foo' not allowed in function with calling convention 'C'
</span>export fn entry(foo: Foo) void { _ = foo; }
                <span class="sgr-32m">^~~~~~~~
</span><span class="sgr-1m">docgen_tmp/enum_export_error.zig:2:17: </span><span class="sgr-36m">note: </span><span class="sgr-1m">enum tag type 'u2' is not extern compatible
</span><span class="sgr-1m">docgen_tmp/enum_export_error.zig:2:17: </span><span class="sgr-36m">note: </span><span class="sgr-1m">only integers with 0, 8, 16, 32, 64 and 128 bits are extern compatible
</span><span class="sgr-1m">docgen_tmp/enum_export_error.zig:1:13: </span><span class="sgr-36m">note: </span><span class="sgr-1m">enum declared here
</span>const Foo = enum { a, b, c };
            <span class="sgr-32m">^~~~~~~~~~~~~~~~
</span>
</samp></pre></figure> <p> For a C-ABI-compatible enum, provide an explicit tag type to the enum: </p> <figure><figcaption class="zig-cap"><cite class="file">enum_export.zig</cite></figcaption><pre data-language="zig">const Foo = enum(c_int) { a, b, c };
export fn entry(foo: Foo) void { _ = foo; }</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig build-obj enum_export.zig</kbd>
</samp></pre></figure> <h3 id="Enum-Literals">Enum Literals </h3> <p> Enum literals allow specifying the name of an enum field without specifying the enum type: </p> <figure><figcaption class="zig-cap"><cite class="file">test_enum_literals.zig</cite></figcaption><pre data-language="zig">const std = @import("std");
const expect = std.testing.expect;

const Color = enum {
    auto,
    off,
    on,
};

test "enum literals" {
    const color1: Color = .auto;
    const color2 = Color.auto;
    try expect(color1 == color2);
}

test "switch using enum literals" {
    const color = Color.on;
    const result = switch (color) {
        .auto =&gt; false,
        .on =&gt; true,
        .off =&gt; false,
    };
    try expect(result);
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_enum_literals.zig</kbd>
1/2 test.enum literals... OK
2/2 test.switch using enum literals... OK
All 2 tests passed.
</samp></pre></figure> <h3 id="Non-exhaustive-enum">Non-exhaustive enum </h3> <p> A Non-exhaustive enum can be created by adding a trailing '_' field. It must specify a tag type and cannot consume every enumeration value. </p> <p> <a href="#enumFromInt">@enumFromInt</a> on a non-exhaustive enum involves the safety semantics of <a href="#intCast">@intCast</a> to the integer tag type, but beyond that always results in a well-defined enum value. </p> <p> A switch on a non-exhaustive enum can include a '_' prong as an alternative to an <code><span class="line"><span class="tok-kw">else</span></span></code> prong with the difference being that it makes it a compile error if all the known tag names are not handled by the switch. </p> <figure><figcaption class="zig-cap"><cite class="file">test_switch_non-exhaustive.zig</cite></figcaption><pre data-language="zig">const std = @import("std");
const expect = std.testing.expect;

const Number = enum(u8) {
    one,
    two,
    three,
    _,
};

test "switch on non-exhaustive enum" {
    const number = Number.one;
    const result = switch (number) {
        .one =&gt; true,
        .two,
        .three =&gt; false,
        _ =&gt; false,
    };
    try expect(result);
    const is_one = switch (number) {
        .one =&gt; true,
        else =&gt; false,
    };
    try expect(is_one);
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_switch_non-exhaustive.zig</kbd>
1/1 test.switch on non-exhaustive enum... OK
All 1 tests passed.
</samp></pre></figure> <h2 id="union">union </h2> <p> A bare <code><span class="line"><span class="tok-kw">union</span></span></code> defines a set of possible types that a value can be as a list of fields. Only one field can be active at a time. The in-memory representation of bare unions is not guaranteed. Bare unions cannot be used to reinterpret memory. For that, use <a href="#ptrCast">@ptrCast</a>, or use an <a href="#extern-union">extern union</a> or a <a href="#packed-union">packed union</a> which have guaranteed in-memory layout. <a href="#Wrong-Union-Field-Access">Accessing the non-active field</a> is safety-checked <a href="#Undefined-Behavior">Undefined Behavior</a>: </p> <figure><figcaption class="zig-cap"><cite class="file">test_wrong_union_access.zig</cite></figcaption><pre data-language="zig">const Payload = union {
    int: i64,
    float: f64,
    boolean: bool,
};
test "simple union" {
    var payload = Payload{ .int = 1234 };
    payload.float = 12.34;
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_wrong_union_access.zig</kbd>
1/1 test.simple union... thread 2452406 panic: access of union field 'float' while field 'int' is active
/home/ci/actions-runner/_work/zig-bootstrap/zig/docgen_tmp/test_wrong_union_access.zig:8:12: 0x224257 in test.simple union (test)
    payload.float = 12.34;
           ^
/home/ci/actions-runner/_work/zig-bootstrap/out/host/lib/zig/test_runner.zig:176:28: 0x22d429 in mainTerminal (test)
        } else test_fn.func();
                           ^
/home/ci/actions-runner/_work/zig-bootstrap/out/host/lib/zig/test_runner.zig:36:28: 0x22547a in main (test)
        return mainTerminal();
                           ^
/home/ci/actions-runner/_work/zig-bootstrap/out/host/lib/zig/std/start.zig:564:22: 0x224812 in posixCallMainAndExit (test)
            root.main();
                     ^
/home/ci/actions-runner/_work/zig-bootstrap/out/host/lib/zig/std/start.zig:243:5: 0x224361 in _start (test)
    asm volatile (switch (native_arch) {
    ^
???:?:?: 0x0 in ??? (???)
error: the following test command crashed:
/home/ci/actions-runner/_work/zig-bootstrap/out/zig-local-cache/o/4c8d493e90b10d80885d62728b7b9734/test
</samp></pre></figure> <p>You can activate another field by assigning the entire union:</p> <figure><figcaption class="zig-cap"><cite class="file">test_simple_union.zig</cite></figcaption><pre data-language="zig">const std = @import("std");
const expect = std.testing.expect;

const Payload = union {
    int: i64,
    float: f64,
    boolean: bool,
};
test "simple union" {
    var payload = Payload{ .int = 1234 };
    try expect(payload.int == 1234);
    payload = Payload{ .float = 12.34 };
    try expect(payload.float == 12.34);
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_simple_union.zig</kbd>
1/1 test.simple union... OK
All 1 tests passed.
</samp></pre></figure> <p> In order to use <a href="#switch">switch</a> with a union, it must be a <a href="#Tagged-union">Tagged union</a>. </p> <p> To initialize a union when the tag is a <a href="#comptime">comptime</a>-known name, see <a href="#unionInit">@unionInit</a>. </p> <h3 id="Tagged-union">Tagged union </h3> <p>Unions can be declared with an enum tag type. This turns the union into a <em>tagged</em> union, which makes it eligible to use with <a href="#switch">switch</a> expressions. Tagged unions coerce to their tag type: <a href="#Type-Coercion-Unions-and-Enums">Type Coercion: Unions and Enums</a>. </p> <figure><figcaption class="zig-cap"><cite class="file">test_tagged_union.zig</cite></figcaption><pre data-language="zig">const std = @import("std");
const expect = std.testing.expect;

const ComplexTypeTag = enum {
    ok,
    not_ok,
};
const ComplexType = union(ComplexTypeTag) {
    ok: u8,
    not_ok: void,
};

test "switch on tagged union" {
    const c = ComplexType{ .ok = 42 };
    try expect(@as(ComplexTypeTag, c) == ComplexTypeTag.ok);

    switch (c) {
        ComplexTypeTag.ok =&gt; |value| try expect(value == 42),
        ComplexTypeTag.not_ok =&gt; unreachable,
    }
}

test "get tag type" {
    try expect(std.meta.Tag(ComplexType) == ComplexTypeTag);
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_tagged_union.zig</kbd>
1/2 test.switch on tagged union... OK
2/2 test.get tag type... OK
All 2 tests passed.
</samp></pre></figure> <p>In order to modify the payload of a tagged union in a switch expression, place a <code><span class="line">*</span></code> before the variable name to make it a pointer: </p> <figure><figcaption class="zig-cap"><cite class="file">test_switch_modify_tagged_union.zig</cite></figcaption><pre data-language="zig">const std = @import("std");
const expect = std.testing.expect;

const ComplexTypeTag = enum {
    ok,
    not_ok,
};
const ComplexType = union(ComplexTypeTag) {
    ok: u8,
    not_ok: void,
};

test "modify tagged union in switch" {
    var c = ComplexType{ .ok = 42 };

    switch (c) {
        ComplexTypeTag.ok =&gt; |*value| value.* += 1,
        ComplexTypeTag.not_ok =&gt; unreachable,
    }

    try expect(c.ok == 43);
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_switch_modify_tagged_union.zig</kbd>
1/1 test.modify tagged union in switch... OK
All 1 tests passed.
</samp></pre></figure> <p> Unions can be made to infer the enum tag type. Further, unions can have methods just like structs and enums. </p> <figure><figcaption class="zig-cap"><cite class="file">test_union_method.zig</cite></figcaption><pre data-language="zig">const std = @import("std");
const expect = std.testing.expect;

const Variant = union(enum) {
    int: i32,
    boolean: bool,

    // void can be omitted when inferring enum tag type.
    none,

    fn truthy(self: Variant) bool {
        return switch (self) {
            Variant.int =&gt; |x_int| x_int != 0,
            Variant.boolean =&gt; |x_bool| x_bool,
            Variant.none =&gt; false,
        };
    }
};

test "union method" {
    var v1 = Variant{ .int = 1 };
    var v2 = Variant{ .boolean = false };

    try expect(v1.truthy());
    try expect(!v2.truthy());
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_union_method.zig</kbd>
1/1 test.union method... OK
All 1 tests passed.
</samp></pre></figure> <p> <a href="#tagName">@tagName</a> can be used to return a <a href="#comptime">comptime</a> <code><span class="line">[:<span class="tok-number">0</span>]<span class="tok-kw">const</span> <span class="tok-type">u8</span></span></code> value representing the field name: </p> <figure><figcaption class="zig-cap"><cite class="file">test_tagName.zig</cite></figcaption><pre data-language="zig">const std = @import("std");
const expect = std.testing.expect;

const Small2 = union(enum) {
    a: i32,
    b: bool,
    c: u8,
};
test "@tagName" {
    try expect(std.mem.eql(u8, @tagName(Small2.a), "a"));
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_tagName.zig</kbd>
1/1 test.@tagName... OK
All 1 tests passed.
</samp></pre></figure> <h3 id="extern-union">extern union </h3> <p> An <code><span class="line"><span class="tok-kw">extern</span> <span class="tok-kw">union</span></span></code> has memory layout guaranteed to be compatible with the target C ABI. </p> <p>See also:</p>
<ul> <li><a href="#extern-struct">extern struct</a></li> </ul> <h3 id="packed-union">packed union </h3> <p>A <code><span class="line"><span class="tok-kw">packed</span> <span class="tok-kw">union</span></span></code> has well-defined in-memory layout and is eligible to be in a <a href="#packed-struct">packed struct</a>.</p> <h3 id="Anonymous-Union-Literals">Anonymous Union Literals </h3> <p><a href="#Anonymous-Struct-Literals">Anonymous Struct Literals</a> syntax can be used to initialize unions without specifying the type:</p> <figure><figcaption class="zig-cap"><cite class="file">test_anonymous_union.zig</cite></figcaption><pre data-language="zig">const std = @import("std");
const expect = std.testing.expect;

const Number = union {
    int: i32,
    float: f64,
};

test "anonymous union literal syntax" {
    var i: Number = .{.int = 42};
    var f = makeNumber();
    try expect(i.int == 42);
    try expect(f.float == 12.34);
}

fn makeNumber() Number {
    return .{.float = 12.34};
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_anonymous_union.zig</kbd>
1/1 test.anonymous union literal syntax... OK
All 1 tests passed.
</samp></pre></figure> <h2 id="opaque">opaque </h2> <p> <code><span class="line"><span class="tok-kw">opaque</span> {}</span></code> declares a new type with an unknown (but non-zero) size and alignment. It can contain declarations the same as <a href="#struct">structs</a>, <a href="#union">unions</a>, and <a href="#enum">enums</a>. </p> <p> This is typically used for type safety when interacting with C code that does not expose struct details. Example: </p> <figure><figcaption class="zig-cap"><cite class="file">test_opaque.zig</cite></figcaption><pre data-language="zig">const Derp = opaque {};
const Wat = opaque {};

extern fn bar(d: *Derp) void;
fn foo(w: *Wat) callconv(.C) void {
    bar(w);
}

test "call foo" {
    foo(undefined);
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_opaque.zig</kbd>
<span class="sgr-1m">docgen_tmp/test_opaque.zig:6:9: </span><span class="sgr-31m">error: </span><span class="sgr-1m">expected type '*test_opaque.Derp', found '*test_opaque.Wat'
</span>    bar(w);
        <span class="sgr-32m">^
</span><span class="sgr-1m">docgen_tmp/test_opaque.zig:6:9: </span><span class="sgr-36m">note: </span><span class="sgr-1m">pointer type child 'test_opaque.Wat' cannot cast into pointer type child 'test_opaque.Derp'
</span><span class="sgr-1m">docgen_tmp/test_opaque.zig:2:13: </span><span class="sgr-36m">note: </span><span class="sgr-1m">opaque declared here
</span>const Wat = opaque {};
            <span class="sgr-32m">^~~~~~~~~
</span><span class="sgr-1m">docgen_tmp/test_opaque.zig:1:14: </span><span class="sgr-36m">note: </span><span class="sgr-1m">opaque declared here
</span>const Derp = opaque {};
             <span class="sgr-32m">^~~~~~~~~
</span><span class="sgr-1m">docgen_tmp/test_opaque.zig:4:18: </span><span class="sgr-36m">note: </span><span class="sgr-1m">parameter type declared here
</span>extern fn bar(d: *Derp) void;
                 <span class="sgr-32m">^~~~~
</span><span class="sgr-2m">referenced by:
    test.call foo: docgen_tmp/test_opaque.zig:10:5
    remaining reference traces hidden; use '-freference-trace' to see all reference traces
</span>
</samp></pre></figure> <h2 id="Blocks">Blocks </h2> <p> Blocks are used to limit the scope of variable declarations: </p> <figure><figcaption class="zig-cap"><cite class="file">test_blocks.zig</cite></figcaption><pre data-language="zig">test "access variable after block scope" {
    {
        var x: i32 = 1;
        _ = x;
    }
    x += 1;
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_blocks.zig</kbd>
<span class="sgr-1m">docgen_tmp/test_blocks.zig:6:5: </span><span class="sgr-31m">error: </span><span class="sgr-1m">use of undeclared identifier 'x'
</span>    x += 1;
    <span class="sgr-32m">^
</span>
</samp></pre></figure> <p>Blocks are expressions. When labeled, <code><span class="line"><span class="tok-kw">break</span></span></code> can be used to return a value from the block: </p> <figure><figcaption class="zig-cap"><cite class="file">test_labeled_break.zig</cite></figcaption><pre data-language="zig">const std = @import("std");
const expect = std.testing.expect;

test "labeled break from labeled block expression" {
    var y: i32 = 123;

    const x = blk: {
        y += 1;
        break :blk y;
    };
    try expect(x == 124);
    try expect(y == 124);
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_labeled_break.zig</kbd>
1/1 test.labeled break from labeled block expression... OK
All 1 tests passed.
</samp></pre></figure> <p>Here, <code><span class="line">blk</span></code> can be any name.</p> <p>See also:</p>
<ul> <li><a href="#Labeled-while">Labeled while</a></li> <li><a href="#Labeled-for">Labeled for</a></li> </ul> <h3 id="Shadowing">Shadowing </h3> <p><a href="#Identifiers">Identifiers</a> are never allowed to "hide" other identifiers by using the same name:</p> <figure><figcaption class="zig-cap"><cite class="file">test_shadowing.zig</cite></figcaption><pre data-language="zig">const pi = 3.14;

test "inside test block" {
    // Let's even go inside another block
    {
        var pi: i32 = 1234;
    }
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_shadowing.zig</kbd>
<span class="sgr-1m">docgen_tmp/test_shadowing.zig:6:13: </span><span class="sgr-31m">error: </span><span class="sgr-1m">local variable shadows declaration of 'pi'
</span>        var pi: i32 = 1234;
            <span class="sgr-32m">^~
</span><span class="sgr-1m">docgen_tmp/test_shadowing.zig:1:1: </span><span class="sgr-36m">note: </span><span class="sgr-1m">declared here
</span>const pi = 3.14;
<span class="sgr-32m">^~~~~~~~~~~~~~~
</span>
</samp></pre></figure> <p> Because of this, when you read Zig code you can always rely on an identifier to consistently mean the same thing within the scope it is defined. Note that you can, however, use the same name if the scopes are separate: </p> <figure><figcaption class="zig-cap"><cite class="file">test_scopes.zig</cite></figcaption><pre data-language="zig">test "separate scopes" {
    {
        const pi = 3.14;
        _ = pi;
    }
    {
        var pi: bool = true;
        _ = pi;
    }
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_scopes.zig</kbd>
1/1 test.separate scopes... OK
All 1 tests passed.
</samp></pre></figure> <h3 id="Empty-Blocks">Empty Blocks </h3> <p>An empty block is equivalent to <code><span class="line"><span class="tok-type">void</span>{}</span></code>:</p> <figure><figcaption class="zig-cap"><cite class="file">test_empty_block.zig</cite></figcaption><pre data-language="zig">const std = @import("std");
const expect = std.testing.expect;

test {
    const a = {};
    const b = void{};
    try expect(@TypeOf(a) == void);
    try expect(@TypeOf(b) == void);
    try expect(a == b);
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_empty_block.zig</kbd>
1/1 test_0... OK
All 1 tests passed.
</samp></pre></figure> <h2 id="switch">switch </h2> <figure><figcaption class="zig-cap"><cite class="file">test_switch.zig</cite></figcaption><pre data-language="zig">const std = @import("std");
const builtin = @import("builtin");
const expect = std.testing.expect;

test "switch simple" {
    const a: u64 = 10;
    const zz: u64 = 103;

    // All branches of a switch expression must be able to be coerced to a
    // common type.
    //
    // Branches cannot fallthrough. If fallthrough behavior is desired, combine
    // the cases and use an if.
    const b = switch (a) {
        // Multiple cases can be combined via a ','
        1, 2, 3 =&gt; 0,

        // Ranges can be specified using the ... syntax. These are inclusive
        // of both ends.
        5...100 =&gt; 1,

        // Branches can be arbitrarily complex.
        101 =&gt; blk: {
            const c: u64 = 5;
            break :blk c * 2 + 1;
        },

        // Switching on arbitrary expressions is allowed as long as the
        // expression is known at compile-time.
        zz =&gt; zz,
        blk: {
            const d: u32 = 5;
            const e: u32 = 100;
            break :blk d + e;
        } =&gt; 107,

        // The else branch catches everything not already captured.
        // Else branches are mandatory unless the entire range of values
        // is handled.
        else =&gt; 9,
    };

    try expect(b == 1);
}

// Switch expressions can be used outside a function:
const os_msg = switch (builtin.target.os.tag) {
    .linux =&gt; "we found a linux user",
    else =&gt; "not a linux user",
};

// Inside a function, switch statements implicitly are compile-time
// evaluated if the target expression is compile-time known.
test "switch inside function" {
    switch (builtin.target.os.tag) {
        .fuchsia =&gt; {
            // On an OS other than fuchsia, block is not even analyzed,
            // so this compile error is not triggered.
            // On fuchsia this compile error would be triggered.
            @compileError("fuchsia not supported");
        },
        else =&gt; {},
    }
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_switch.zig</kbd>
1/2 test.switch simple... OK
2/2 test.switch inside function... OK
All 2 tests passed.
</samp></pre></figure> <p> <code><span class="line"><span class="tok-kw">switch</span></span></code> can be used to capture the field values of a <a href="#Tagged-union">Tagged union</a>. Modifications to the field values can be done by placing a <code><span class="line">*</span></code> before the capture variable name, turning it into a pointer. </p> <figure><figcaption class="zig-cap"><cite class="file">test_switch_tagged_union.zig</cite></figcaption><pre data-language="zig">const expect = @import("std").testing.expect;

test "switch on tagged union" {
    const Point = struct {
        x: u8,
        y: u8,
    };
    const Item = union(enum) {
        a: u32,
        c: Point,
        d,
        e: u32,
    };

    var a = Item{ .c = Point{ .x = 1, .y = 2 } };

    // Switching on more complex enums is allowed.
    const b = switch (a) {
        // A capture group is allowed on a match, and will return the enum
        // value matched. If the payload types of both cases are the same
        // they can be put into the same switch prong.
        Item.a, Item.e =&gt; |item| item,

        // A reference to the matched value can be obtained using `*` syntax.
        Item.c =&gt; |*item| blk: {
            item.*.x += 1;
            break :blk 6;
        },

        // No else is required if the types cases was exhaustively handled
        Item.d =&gt; 8,
    };

    try expect(b == 6);
    try expect(a.c.x == 2);
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_switch_tagged_union.zig</kbd>
1/1 test.switch on tagged union... OK
All 1 tests passed.
</samp></pre></figure> <p>See also:</p>
<ul> <li><a href="#comptime">comptime</a></li> <li><a href="#enum">enum</a></li> <li><a href="#compileError">@compileError</a></li> <li><a href="#Compile-Variables">Compile Variables</a></li> </ul> <h3 id="Exhaustive-Switching">Exhaustive Switching </h3> <p> When a <code><span class="line"><span class="tok-kw">switch</span></span></code> expression does not have an <code><span class="line"><span class="tok-kw">else</span></span></code> clause, it must exhaustively list all the possible values. Failure to do so is a compile error: </p> <figure><figcaption class="zig-cap"><cite class="file">test_unhandled_enumeration_value.zig</cite></figcaption><pre data-language="zig">const Color = enum {
    auto,
    off,
    on,
};

test "exhaustive switching" {
    const color = Color.off;
    switch (color) {
        Color.auto =&gt; {},
        Color.on =&gt; {},
    }
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_unhandled_enumeration_value.zig</kbd>
<span class="sgr-1m">docgen_tmp/test_unhandled_enumeration_value.zig:9:5: </span><span class="sgr-31m">error: </span><span class="sgr-1m">switch must handle all possibilities
</span>    switch (color) {
    <span class="sgr-32m">^~~~~~
</span><span class="sgr-1m">docgen_tmp/test_unhandled_enumeration_value.zig:3:5: </span><span class="sgr-36m">note: </span><span class="sgr-1m">unhandled enumeration value: 'off'
</span>    off,
    <span class="sgr-32m">^~~
</span><span class="sgr-1m">docgen_tmp/test_unhandled_enumeration_value.zig:1:15: </span><span class="sgr-36m">note: </span><span class="sgr-1m">enum 'test_unhandled_enumeration_value.Color' declared here
</span>const Color = enum {
              <span class="sgr-32m">^~~~
</span>
</samp></pre></figure> <h3 id="Switching-with-Enum-Literals">Switching with Enum Literals </h3> <p> <a href="#Enum-Literals">Enum Literals</a> can be useful to use with <code><span class="line"><span class="tok-kw">switch</span></span></code> to avoid repetitively specifying <a href="#enum">enum</a> or <a href="#union">union</a> types: </p> <figure><figcaption class="zig-cap"><cite class="file">test_exhaustive_switch.zig</cite></figcaption><pre data-language="zig">const std = @import("std");
const expect = std.testing.expect;

const Color = enum {
    auto,
    off,
    on,
};

test "enum literals with switch" {
    const color = Color.off;
    const result = switch (color) {
        .auto =&gt; false,
        .on =&gt; false,
        .off =&gt; true,
    };
    try expect(result);
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_exhaustive_switch.zig</kbd>
1/1 test.enum literals with switch... OK
All 1 tests passed.
</samp></pre></figure> <h3 id="Inline-switch">Inline switch </h3> <p> Switch prongs can be marked as <code><span class="line"><span class="tok-kw">inline</span></span></code> to generate the prong's body for each possible value it could have: </p> <figure><figcaption class="zig-cap"><cite class="file">test_inline_switch.zig</cite></figcaption><pre data-language="zig">const std = @import("std");
const expect = std.testing.expect;
const expectError = std.testing.expectError;

fn isFieldOptional(comptime T: type, field_index: usize) !bool {
    const fields = @typeInfo(T).Struct.fields;
    return switch (field_index) {
        // This prong is analyzed `fields.len - 1` times with `idx` being a
        // unique comptime-known value each time.
        inline 0...fields.len - 1 =&gt; |idx| @typeInfo(fields[idx].type) == .Optional,
        else =&gt; return error.IndexOutOfBounds,
    };
}

const Struct1 = struct { a: u32, b: ?u32 };

test "using @typeInfo with runtime values" {
    var index: usize = 0;
    try expect(!try isFieldOptional(Struct1, index));
    index += 1;
    try expect(try isFieldOptional(Struct1, index));
    index += 1;
    try expectError(error.IndexOutOfBounds, isFieldOptional(Struct1, index));
}

// Calls to `isFieldOptional` on `Struct1` get unrolled to an equivalent
// of this function:
fn isFieldOptionalUnrolled(field_index: usize) !bool {
    return switch (field_index) {
        0 =&gt; false,
        1 =&gt; true,
        else =&gt; return error.IndexOutOfBounds,
    };
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_inline_switch.zig</kbd>
1/1 test.using @typeInfo with runtime values... OK
All 1 tests passed.
</samp></pre></figure> <p> <code><span class="line"><span class="tok-kw">inline</span> <span class="tok-kw">else</span></span></code> prongs can be used as a type safe alternative to <code><span class="line"><span class="tok-kw">inline</span> <span class="tok-kw">for</span></span></code> loops: </p> <figure><figcaption class="zig-cap"><cite class="file">test_inline_else.zig</cite></figcaption><pre data-language="zig">const std = @import("std");
const expect = std.testing.expect;

const SliceTypeA = extern struct {
    len: usize,
    ptr: [*]u32,
};
const SliceTypeB = extern struct {
    ptr: [*]SliceTypeA,
    len: usize,
};
const AnySlice = union(enum) {
    a: SliceTypeA,
    b: SliceTypeB,
    c: []const u8,
    d: []AnySlice,
};

fn withFor(any: AnySlice) usize {
    const Tag = @typeInfo(AnySlice).Union.tag_type.?;
    inline for (@typeInfo(Tag).Enum.fields) |field| {
        // With `inline for` the function gets generated as
        // a series of `if` statements relying on the optimizer
        // to convert it to a switch.
        if (field.value == @intFromEnum(any)) {
            return @field(any, field.name).len;
        }
    }
    // When using `inline for` the compiler doesn't know that every
    // possible case has been handled requiring an explicit `unreachable`.
    unreachable;
}

fn withSwitch(any: AnySlice) usize {
    return switch (any) {
        // With `inline else` the function is explicitly generated
        // as the desired switch and the compiler can check that
        // every possible case is handled.
        inline else =&gt; |slice| slice.len,
    };
}

test "inline for and inline else similarity" {
    var any = AnySlice{ .c = "hello" };
    try expect(withFor(any) == 5);
    try expect(withSwitch(any) == 5);
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_inline_else.zig</kbd>
1/1 test.inline for and inline else similarity... OK
All 1 tests passed.
</samp></pre></figure> <p> When using an inline prong switching on an union an additional capture can be used to obtain the union's enum tag value. </p> <figure><figcaption class="zig-cap"><cite class="file">test_inline_switch_union_tag.zig</cite></figcaption><pre data-language="zig">const std = @import("std");
const expect = std.testing.expect;

const U = union(enum) {
    a: u32,
    b: f32,
};

fn getNum(u: U) u32 {
    switch (u) {
        // Here `num` is a runtime-known value that is either
        // `u.a` or `u.b` and `tag` is `u`'s comptime-known tag value.
        inline else =&gt; |num, tag| {
            if (tag == .b) {
                return @intFromFloat(num);
            }
            return num;
        }
    }
}

test "test" {
    var u = U{ .b = 42 };
    try expect(getNum(u) == 42);
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_inline_switch_union_tag.zig</kbd>
1/1 test.test... OK
All 1 tests passed.
</samp></pre></figure> <p>See also:</p>
<ul> <li><a href="#inline-while">inline while</a></li> <li><a href="#inline-for">inline for</a></li> </ul> <h2 id="while">while </h2> <p> A while loop is used to repeatedly execute an expression until some condition is no longer true. </p> <figure><figcaption class="zig-cap"><cite class="file">test_while.zig</cite></figcaption><pre data-language="zig">const expect = @import("std").testing.expect;

test "while basic" {
    var i: usize = 0;
    while (i &lt; 10) {
        i += 1;
    }
    try expect(i == 10);
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_while.zig</kbd>
1/1 test.while basic... OK
All 1 tests passed.
</samp></pre></figure> <p> Use <code><span class="line"><span class="tok-kw">break</span></span></code> to exit a while loop early. </p> <figure><figcaption class="zig-cap"><cite class="file">test_while_break.zig</cite></figcaption><pre data-language="zig">const expect = @import("std").testing.expect;

test "while break" {
    var i: usize = 0;
    while (true) {
        if (i == 10)
            break;
        i += 1;
    }
    try expect(i == 10);
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_while_break.zig</kbd>
1/1 test.while break... OK
All 1 tests passed.
</samp></pre></figure> <p> Use <code><span class="line"><span class="tok-kw">continue</span></span></code> to jump back to the beginning of the loop. </p> <figure><figcaption class="zig-cap"><cite class="file">test_while_continue.zig</cite></figcaption><pre data-language="zig">const expect = @import("std").testing.expect;

test "while continue" {
    var i: usize = 0;
    while (true) {
        i += 1;
        if (i &lt; 10)
            continue;
        break;
    }
    try expect(i == 10);
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_while_continue.zig</kbd>
1/1 test.while continue... OK
All 1 tests passed.
</samp></pre></figure> <p> While loops support a continue expression which is executed when the loop is continued. The <code><span class="line"><span class="tok-kw">continue</span></span></code> keyword respects this expression. </p> <figure><figcaption class="zig-cap"><cite class="file">test_while_continue_expression.zig</cite></figcaption><pre data-language="zig">const expect = @import("std").testing.expect;

test "while loop continue expression" {
    var i: usize = 0;
    while (i &lt; 10) : (i += 1) {}
    try expect(i == 10);
}

test "while loop continue expression, more complicated" {
    var i: usize = 1;
    var j: usize = 1;
    while (i * j &lt; 2000) : ({ i *= 2; j *= 3; }) {
        const my_ij = i * j;
        try expect(my_ij &lt; 2000);
    }
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_while_continue_expression.zig</kbd>
1/2 test.while loop continue expression... OK
2/2 test.while loop continue expression, more complicated... OK
All 2 tests passed.
</samp></pre></figure> <p> While loops are expressions. The result of the expression is the result of the <code><span class="line"><span class="tok-kw">else</span></span></code> clause of a while loop, which is executed when the condition of the while loop is tested as false. </p> <p> <code><span class="line"><span class="tok-kw">break</span></span></code>, like <code><span class="line"><span class="tok-kw">return</span></span></code>, accepts a value parameter. This is the result of the <code><span class="line"><span class="tok-kw">while</span></span></code> expression. When you <code><span class="line"><span class="tok-kw">break</span></span></code> from a while loop, the <code><span class="line"><span class="tok-kw">else</span></span></code> branch is not evaluated. </p> <figure><figcaption class="zig-cap"><cite class="file">test_while_else.zig</cite></figcaption><pre data-language="zig">const expect = @import("std").testing.expect;

test "while else" {
    try expect(rangeHasNumber(0, 10, 5));
    try expect(!rangeHasNumber(0, 10, 15));
}

fn rangeHasNumber(begin: usize, end: usize, number: usize) bool {
    var i = begin;
    return while (i &lt; end) : (i += 1) {
        if (i == number) {
            break true;
        }
    } else false;
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_while_else.zig</kbd>
1/1 test.while else... OK
All 1 tests passed.
</samp></pre></figure> <h3 id="Labeled-while">Labeled while </h3> <p>When a <code><span class="line"><span class="tok-kw">while</span></span></code> loop is labeled, it can be referenced from a <code><span class="line"><span class="tok-kw">break</span></span></code> or <code><span class="line"><span class="tok-kw">continue</span></span></code> from within a nested loop:</p> <figure><figcaption class="zig-cap"><cite class="file">test_while_nested_break.zig</cite></figcaption><pre data-language="zig">test "nested break" {
    outer: while (true) {
        while (true) {
            break :outer;
        }
    }
}

test "nested continue" {
    var i: usize = 0;
    outer: while (i &lt; 10) : (i += 1) {
        while (true) {
            continue :outer;
        }
    }
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_while_nested_break.zig</kbd>
1/2 test.nested break... OK
2/2 test.nested continue... OK
All 2 tests passed.
</samp></pre></figure> <h3 id="while-with-Optionals">while with Optionals </h3> <p> Just like <a href="#if">if</a> expressions, while loops can take an optional as the condition and capture the payload. When <a href="#null">null</a> is encountered the loop exits. </p> <p> When the <code><span class="line">|x|</span></code> syntax is present on a <code><span class="line"><span class="tok-kw">while</span></span></code> expression, the while condition must have an <a href="#Optional-Type">Optional Type</a>. </p> <p> The <code><span class="line"><span class="tok-kw">else</span></span></code> branch is allowed on optional iteration. In this case, it will be executed on the first null value encountered. </p> <figure><figcaption class="zig-cap"><cite class="file">test_while_null_capture.zig</cite></figcaption><pre data-language="zig">const expect = @import("std").testing.expect;

test "while null capture" {
    var sum1: u32 = 0;
    numbers_left = 3;
    while (eventuallyNullSequence()) |value| {
        sum1 += value;
    }
    try expect(sum1 == 3);

    var sum2: u32 = 0;
    numbers_left = 3;
    while (eventuallyNullSequence()) |value| {
        sum2 += value;
    } else {
        try expect(sum2 == 3);
    }
}

var numbers_left: u32 = undefined;
fn eventuallyNullSequence() ?u32 {
    return if (numbers_left == 0) null else blk: {
        numbers_left -= 1;
        break :blk numbers_left;
    };
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_while_null_capture.zig</kbd>
1/1 test.while null capture... OK
All 1 tests passed.
</samp></pre></figure> <h3 id="while-with-Error-Unions">while with Error Unions </h3> <p> Just like <a href="#if">if</a> expressions, while loops can take an error union as the condition and capture the payload or the error code. When the condition results in an error code the else branch is evaluated and the loop is finished. </p> <p> When the <code><span class="line"><span class="tok-kw">else</span> |x|</span></code> syntax is present on a <code><span class="line"><span class="tok-kw">while</span></span></code> expression, the while condition must have an <a href="#Error-Union-Type">Error Union Type</a>. </p> <figure><figcaption class="zig-cap"><cite class="file">test_while_error_capture.zig</cite></figcaption><pre data-language="zig">const expect = @import("std").testing.expect;

test "while error union capture" {
    var sum1: u32 = 0;
    numbers_left = 3;
    while (eventuallyErrorSequence()) |value| {
        sum1 += value;
    } else |err| {
        try expect(err == error.ReachedZero);
    }
}

var numbers_left: u32 = undefined;

fn eventuallyErrorSequence() anyerror!u32 {
    return if (numbers_left == 0) error.ReachedZero else blk: {
        numbers_left -= 1;
        break :blk numbers_left;
    };
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_while_error_capture.zig</kbd>
1/1 test.while error union capture... OK
All 1 tests passed.
</samp></pre></figure> <h3 id="inline-while">inline while </h3> <p> While loops can be inlined. This causes the loop to be unrolled, which allows the code to do some things which only work at compile time, such as use types as first class values. </p> <figure><figcaption class="zig-cap"><cite class="file">test_inline_while.zig</cite></figcaption><pre data-language="zig">const expect = @import("std").testing.expect;

test "inline while loop" {
    comptime var i = 0;
    var sum: usize = 0;
    inline while (i &lt; 3) : (i += 1) {
        const T = switch (i) {
            0 =&gt; f32,
            1 =&gt; i8,
            2 =&gt; bool,
            else =&gt; unreachable,
        };
        sum += typeNameLength(T);
    }
    try expect(sum == 9);
}

fn typeNameLength(comptime T: type) usize {
    return @typeName(T).len;
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_inline_while.zig</kbd>
1/1 test.inline while loop... OK
All 1 tests passed.
</samp></pre></figure> <p> It is recommended to use <code><span class="line"><span class="tok-kw">inline</span></span></code> loops only for one of these reasons: </p> <ul> <li>You need the loop to execute at <a href="#comptime">comptime</a> for the semantics to work.</li> <li> You have a benchmark to prove that forcibly unrolling the loop in this way is measurably faster. </li> </ul> <p>See also:</p>
<ul> <li><a href="#if">if</a></li> <li><a href="#Optionals">Optionals</a></li> <li><a href="#Errors">Errors</a></li> <li><a href="#comptime">comptime</a></li> <li><a href="#unreachable">unreachable</a></li> </ul> <h2 id="for">for </h2> <figure><figcaption class="zig-cap"><cite class="file">test_for.zig</cite></figcaption><pre data-language="zig">const expect = @import("std").testing.expect;

test "for basics" {
    const items = [_]i32 { 4, 5, 3, 4, 0 };
    var sum: i32 = 0;

    // For loops iterate over slices and arrays.
    for (items) |value| {
        // Break and continue are supported.
        if (value == 0) {
            continue;
        }
        sum += value;
    }
    try expect(sum == 16);

    // To iterate over a portion of a slice, reslice.
    for (items[0..1]) |value| {
        sum += value;
    }
    try expect(sum == 20);

    // To access the index of iteration, specify a second condition as well
    // as a second capture value.
    var sum2: i32 = 0;
    for (items, 0..) |_, i| {
        try expect(@TypeOf(i) == usize);
        sum2 += @as(i32, @intCast(i));
    }
    try expect(sum2 == 10);

    // To iterate over consecutive integers, use the range syntax.
    // Unbounded range is always a compile error.
    var sum3 : usize = 0;
    for (0..5) |i| {
        sum3 += i;
    }
    try expect(sum3 == 10);
}

test "multi object for" {
    const items = [_]usize{ 1, 2, 3 };
    const items2 = [_]usize{ 4, 5, 6 };
    var count: usize = 0;

    // Iterate over multiple objects.
    // All lengths must be equal at the start of the loop, otherwise detectable
    // illegal behavior occurs.
    for (items, items2) |i, j| {
        count += i + j;
    }

    try expect(count == 21);
}

test "for reference" {
    var items = [_]i32 { 3, 4, 2 };

    // Iterate over the slice by reference by
    // specifying that the capture value is a pointer.
    for (&amp;items) |*value| {
        value.* += 1;
    }

    try expect(items[0] == 4);
    try expect(items[1] == 5);
    try expect(items[2] == 3);
}

test "for else" {
    // For allows an else attached to it, the same as a while loop.
    var items = [_]?i32 { 3, 4, null, 5 };

    // For loops can also be used as expressions.
    // Similar to while loops, when you break from a for loop, the else branch is not evaluated.
    var sum: i32 = 0;
    const result = for (items) |value| {
        if (value != null) {
            sum += value.?;
        }
    } else blk: {
        try expect(sum == 12);
        break :blk sum;
    };
    try expect(result == 12);
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_for.zig</kbd>
1/4 test.for basics... OK
2/4 test.multi object for... OK
3/4 test.for reference... OK
4/4 test.for else... OK
All 4 tests passed.
</samp></pre></figure> <h3 id="Labeled-for">Labeled for </h3> <p>When a <code><span class="line"><span class="tok-kw">for</span></span></code> loop is labeled, it can be referenced from a <code><span class="line"><span class="tok-kw">break</span></span></code> or <code><span class="line"><span class="tok-kw">continue</span></span></code> from within a nested loop:</p> <figure><figcaption class="zig-cap"><cite class="file">test_for_nested_break.zig</cite></figcaption><pre data-language="zig">const std = @import("std");
const expect = std.testing.expect;

test "nested break" {
    var count: usize = 0;
    outer: for (1..6) |_| {
        for (1..6) |_| {
            count += 1;
            break :outer;
        }
    }
    try expect(count == 1);
}

test "nested continue" {
    var count: usize = 0;
    outer: for (1..9) |_| {
        for (1..6) |_| {
            count += 1;
            continue :outer;
        }
    }

    try expect(count == 8);
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_for_nested_break.zig</kbd>
1/2 test.nested break... OK
2/2 test.nested continue... OK
All 2 tests passed.
</samp></pre></figure> <h3 id="inline-for">inline for </h3> <p> For loops can be inlined. This causes the loop to be unrolled, which allows the code to do some things which only work at compile time, such as use types as first class values. The capture value and iterator value of inlined for loops are compile-time known. </p> <figure><figcaption class="zig-cap"><cite class="file">test_inline_for.zig</cite></figcaption><pre data-language="zig">const expect = @import("std").testing.expect;

test "inline for loop" {
    const nums = [_]i32{2, 4, 6};
    var sum: usize = 0;
    inline for (nums) |i| {
        const T = switch (i) {
            2 =&gt; f32,
            4 =&gt; i8,
            6 =&gt; bool,
            else =&gt; unreachable,
        };
        sum += typeNameLength(T);
    }
    try expect(sum == 9);
}

fn typeNameLength(comptime T: type) usize {
    return @typeName(T).len;
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_inline_for.zig</kbd>
1/1 test.inline for loop... OK
All 1 tests passed.
</samp></pre></figure> <p> It is recommended to use <code><span class="line"><span class="tok-kw">inline</span></span></code> loops only for one of these reasons: </p> <ul> <li>You need the loop to execute at <a href="#comptime">comptime</a> for the semantics to work.</li> <li> You have a benchmark to prove that forcibly unrolling the loop in this way is measurably faster. </li> </ul> <p>See also:</p>
<ul> <li><a href="#while">while</a></li> <li><a href="#comptime">comptime</a></li> <li><a href="#Arrays">Arrays</a></li> <li><a href="#Slices">Slices</a></li> </ul> <h2 id="if">if </h2> <figure><figcaption class="zig-cap"><cite class="file">test_if.zig</cite></figcaption><pre data-language="zig">// If expressions have three uses, corresponding to the three types:
// * bool
// * ?T
// * anyerror!T

const expect = @import("std").testing.expect;

test "if expression" {
    // If expressions are used instead of a ternary expression.
    const a: u32 = 5;
    const b: u32 = 4;
    const result = if (a != b) 47 else 3089;
    try expect(result == 47);
}

test "if boolean" {
    // If expressions test boolean conditions.
    const a: u32 = 5;
    const b: u32 = 4;
    if (a != b) {
        try expect(true);
    } else if (a == 9) {
        unreachable;
    } else {
        unreachable;
    }
}

test "if optional" {
    // If expressions test for null.

    const a: ?u32 = 0;
    if (a) |value| {
        try expect(value == 0);
    } else {
        unreachable;
    }

    const b: ?u32 = null;
    if (b) |_| {
        unreachable;
    } else {
        try expect(true);
    }

    // The else is not required.
    if (a) |value| {
        try expect(value == 0);
    }

    // To test against null only, use the binary equality operator.
    if (b == null) {
        try expect(true);
    }

    // Access the value by reference using a pointer capture.
    var c: ?u32 = 3;
    if (c) |*value| {
        value.* = 2;
    }

    if (c) |value| {
        try expect(value == 2);
    } else {
        unreachable;
    }
}

test "if error union" {
    // If expressions test for errors.
    // Note the |err| capture on the else.

    const a: anyerror!u32 = 0;
    if (a) |value| {
        try expect(value == 0);
    } else |err| {
        _ = err;
        unreachable;
    }

    const b: anyerror!u32 = error.BadValue;
    if (b) |value| {
        _ = value;
        unreachable;
    } else |err| {
        try expect(err == error.BadValue);
    }

    // The else and |err| capture is strictly required.
    if (a) |value| {
        try expect(value == 0);
    } else |_| {}

    // To check only the error value, use an empty block expression.
    if (b) |_| {} else |err| {
        try expect(err == error.BadValue);
    }

    // Access the value by reference using a pointer capture.
    var c: anyerror!u32 = 3;
    if (c) |*value| {
        value.* = 9;
    } else |_| {
        unreachable;
    }

    if (c) |value| {
        try expect(value == 9);
    } else |_| {
        unreachable;
    }
}

test "if error union with optional" {
    // If expressions test for errors before unwrapping optionals.
    // The |optional_value| capture's type is ?u32.

    const a: anyerror!?u32 = 0;
    if (a) |optional_value| {
        try expect(optional_value.? == 0);
    } else |err| {
        _ = err;
        unreachable;
    }

    const b: anyerror!?u32 = null;
    if (b) |optional_value| {
        try expect(optional_value == null);
    } else |_| {
        unreachable;
    }

    const c: anyerror!?u32 = error.BadValue;
    if (c) |optional_value| {
        _ = optional_value;
        unreachable;
    } else |err| {
        try expect(err == error.BadValue);
    }

    // Access the value by reference by using a pointer capture each time.
    var d: anyerror!?u32 = 3;
    if (d) |*optional_value| {
        if (optional_value.*) |*value| {
            value.* = 9;
        }
    } else |_| {
        unreachable;
    }

    if (d) |optional_value| {
        try expect(optional_value.? == 9);
    } else |_| {
        unreachable;
    }
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_if.zig</kbd>
1/5 test.if expression... OK
2/5 test.if boolean... OK
3/5 test.if optional... OK
4/5 test.if error union... OK
5/5 test.if error union with optional... OK
All 5 tests passed.
</samp></pre></figure> <p>See also:</p>
<ul> <li><a href="#Optionals">Optionals</a></li> <li><a href="#Errors">Errors</a></li> </ul> <h2 id="defer">defer </h2> <figure><figcaption class="zig-cap"><cite class="file">test_defer.zig</cite></figcaption><pre data-language="zig">const std = @import("std");
const expect = std.testing.expect;
const print = std.debug.print;

// defer will execute an expression at the end of the current scope.
fn deferExample() !usize {
    var a: usize = 1;

    {
        defer a = 2;
        a = 1;
    }
    try expect(a == 2);

    a = 5;
    return a;
}

test "defer basics" {
    try expect((try deferExample()) == 5);
}

// If multiple defer statements are specified, they will be executed in
// the reverse order they were run.
fn deferUnwindExample() void {
    print("\n", .{});

    defer {
        print("1 ", .{});
    }
    defer {
        print("2 ", .{});
    }
    if (false) {
        // defers are not run if they are never executed.
        defer {
            print("3 ", .{});
        }
    }
}

test "defer unwinding" {
    deferUnwindExample();
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_defer.zig</kbd>
1/2 test.defer basics... OK
2/2 test.defer unwinding...
2 1 OK
All 2 tests passed.
</samp></pre></figure> <figure><figcaption class="zig-cap"><cite class="file">test_invalid_defer.zig</cite></figcaption><pre data-language="zig">// Inside a defer expression the return statement is not allowed.
fn deferInvalidExample() !void {
    defer {
        return error.DeferError;
    }

    return error.DeferError;
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_invalid_defer.zig</kbd>
<span class="sgr-1m">docgen_tmp/test_invalid_defer.zig:4:9: </span><span class="sgr-31m">error: </span><span class="sgr-1m">cannot return from defer expression
</span>        return error.DeferError;
        <span class="sgr-32m">^~~~~~~~~~~~~~~~~~~~~~~
</span><span class="sgr-1m">docgen_tmp/test_invalid_defer.zig:3:5: </span><span class="sgr-36m">note: </span><span class="sgr-1m">defer expression here
</span>    defer {
    <span class="sgr-32m">^~~~~
</span>
</samp></pre></figure> <figure><figcaption class="zig-cap"><cite class="file">test_errdefer.zig</cite></figcaption><pre data-language="zig">const std = @import("std");
const print = std.debug.print;

// The errdefer keyword is similar to defer, but will only execute if the
// scope returns with an error.
//
// This is especially useful in allowing a function to clean up properly
// on error, and replaces goto error handling tactics as seen in c.
fn deferErrorExample(is_error: bool) !void {
    print("\nstart of function\n", .{});

    // This will always be executed on exit
    defer {
        print("end of function\n", .{});
    }

    errdefer {
        print("encountered an error!\n", .{});
    }

    if (is_error) {
        return error.DeferError;
    }
}

// The errdefer keyword also supports an alternative syntax to capture the
// generated error.
//
// This is useful for printing an additional error message during clean up.
fn deferErrorCaptureExample() !void {
    errdefer |err| {
        std.debug.print("the error is {s}\n", .{@errorName(err)});
    }

    return error.DeferError;
}

test "errdefer unwinding" {
    deferErrorExample(false) catch {};
    deferErrorExample(true) catch {};
    deferErrorCaptureExample() catch {};
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_errdefer.zig</kbd>
1/1 test.errdefer unwinding...
start of function
end of function

start of function
encountered an error!
end of function
the error is DeferError
OK
All 1 tests passed.
</samp></pre></figure> <p>See also:</p>
<ul> <li><a href="#Errors">Errors</a></li> </ul> <h2 id="unreachable">unreachable </h2> <p> In <a href="#Debug">Debug</a> and <a href="#ReleaseSafe">ReleaseSafe</a> mode <code><span class="line"><span class="tok-kw">unreachable</span></span></code> emits a call to <code><span class="line">panic</span></code> with the message <code>reached unreachable code</code>. </p> <p> In <a href="#ReleaseFast">ReleaseFast</a> and <a href="#ReleaseSmall">ReleaseSmall</a> mode, the optimizer uses the assumption that <code><span class="line"><span class="tok-kw">unreachable</span></span></code> code will never be hit to perform optimizations. </p> <h3 id="Basics">Basics </h3> <figure><figcaption class="zig-cap"><cite class="file">test_unreachable.zig</cite></figcaption><pre data-language="zig">// unreachable is used to assert that control flow will never reach a
// particular location:
test "basic math" {
    const x = 1;
    const y = 2;
    if (x + y != 3) {
        unreachable;
    }
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_unreachable.zig</kbd>
1/1 test.basic math... OK
All 1 tests passed.
</samp></pre></figure> <p>In fact, this is how <code><span class="line">std.debug.assert</span></code> is implemented:</p> <figure><figcaption class="zig-cap"><cite class="file">test_assertion_failure.zig</cite></figcaption><pre data-language="zig">// This is how std.debug.assert is implemented
fn assert(ok: bool) void {
    if (!ok) unreachable; // assertion failure
}

// This test will fail because we hit unreachable.
test "this will fail" {
    assert(false);
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_assertion_failure.zig</kbd>
1/1 test.this will fail... thread 2453337 panic: reached unreachable code
/home/ci/actions-runner/_work/zig-bootstrap/zig/docgen_tmp/test_assertion_failure.zig:3:14: 0x2241e2 in assert (test)
    if (!ok) unreachable; // assertion failure
             ^
/home/ci/actions-runner/_work/zig-bootstrap/zig/docgen_tmp/test_assertion_failure.zig:8:11: 0x22419a in test.this will fail (test)
    assert(false);
          ^
/home/ci/actions-runner/_work/zig-bootstrap/out/host/lib/zig/test_runner.zig:176:28: 0x22ceb9 in mainTerminal (test)
        } else test_fn.func();
                           ^
/home/ci/actions-runner/_work/zig-bootstrap/out/host/lib/zig/test_runner.zig:36:28: 0x22503a in main (test)
        return mainTerminal();
                           ^
/home/ci/actions-runner/_work/zig-bootstrap/out/host/lib/zig/std/start.zig:564:22: 0x2246b2 in posixCallMainAndExit (test)
            root.main();
                     ^
/home/ci/actions-runner/_work/zig-bootstrap/out/host/lib/zig/std/start.zig:243:5: 0x224201 in _start (test)
    asm volatile (switch (native_arch) {
    ^
???:?:?: 0x0 in ??? (???)
error: the following test command crashed:
/home/ci/actions-runner/_work/zig-bootstrap/out/zig-local-cache/o/3f6575b81fc83074e649e9b167fa28e1/test
</samp></pre></figure> <h3 id="At-Compile-Time">At Compile-Time </h3> <figure><figcaption class="zig-cap"><cite class="file">test_comptime_unreachable.zig</cite></figcaption><pre data-language="zig">const assert = @import("std").debug.assert;

test "type of unreachable" {
    comptime {
        // The type of unreachable is noreturn.

        // However this assertion will still fail to compile because
        // unreachable expressions are compile errors.

        assert(@TypeOf(unreachable) == noreturn);
    }
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_comptime_unreachable.zig</kbd>
<span class="sgr-1m">docgen_tmp/test_comptime_unreachable.zig:10:16: </span><span class="sgr-31m">error: </span><span class="sgr-1m">unreachable code
</span>        assert(@TypeOf(unreachable) == noreturn);
               <span class="sgr-32m">^~~~~~~~~~~~~~~~~~~~
</span><span class="sgr-1m">docgen_tmp/test_comptime_unreachable.zig:10:24: </span><span class="sgr-36m">note: </span><span class="sgr-1m">control flow is diverted here
</span>        assert(@TypeOf(unreachable) == noreturn);
                       <span class="sgr-32m">^~~~~~~~~~~
</span>
</samp></pre></figure> <p>See also:</p>
<ul> <li><a href="#Zig-Test">Zig Test</a></li> <li><a href="#Build-Mode">Build Mode</a></li> <li><a href="#comptime">comptime</a></li> </ul> <h2 id="noreturn">noreturn </h2> <p> <code><span class="line"><span class="tok-type">noreturn</span></span></code> is the type of: </p> <ul> <li><code><span class="line"><span class="tok-kw">break</span></span></code></li> <li><code><span class="line"><span class="tok-kw">continue</span></span></code></li> <li><code><span class="line"><span class="tok-kw">return</span></span></code></li> <li><code><span class="line"><span class="tok-kw">unreachable</span></span></code></li> <li><code><span class="line"><span class="tok-kw">while</span> (<span class="tok-null">true</span>) {}</span></code></li> </ul> <p>When resolving types together, such as <code><span class="line"><span class="tok-kw">if</span></span></code> clauses or <code><span class="line"><span class="tok-kw">switch</span></span></code> prongs, the <code><span class="line"><span class="tok-type">noreturn</span></span></code> type is compatible with every other type. Consider: </p> <figure><figcaption class="zig-cap"><cite class="file">test_noreturn.zig</cite></figcaption><pre data-language="zig">fn foo(condition: bool, b: u32) void {
    const a = if (condition) b else return;
    _ = a;
    @panic("do something with a");
}
test "noreturn" {
    foo(false, 1);
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_noreturn.zig</kbd>
1/1 test.noreturn... OK
All 1 tests passed.
</samp></pre></figure> <p>Another use case for <code><span class="line"><span class="tok-type">noreturn</span></span></code> is the <code><span class="line">exit</span></code> function:</p> <figure><figcaption class="zig-cap"><cite class="file">test_noreturn_from_exit.zig</cite></figcaption><pre data-language="zig">const std = @import("std");
const builtin = @import("builtin");
const native_arch = builtin.cpu.arch;
const expect = std.testing.expect;

const WINAPI: std.builtin.CallingConvention = if (native_arch == .x86) .Stdcall else .C;
extern "kernel32" fn ExitProcess(exit_code: c_uint) callconv(WINAPI) noreturn;

test "foo" {
    const value = bar() catch ExitProcess(1);
    try expect(value == 1234);
}

fn bar() anyerror!u32 {
    return 1234;
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_noreturn_from_exit.zig -target x86_64-windows --test-no-exec</kbd>
</samp></pre></figure> <h2 id="Functions">Functions </h2> <figure><figcaption class="zig-cap"><cite class="file">test_functions.zig</cite></figcaption><pre data-language="zig">const std = @import("std");
const builtin = @import("builtin");
const native_arch = builtin.cpu.arch;
const expect = std.testing.expect;

// Functions are declared like this
fn add(a: i8, b: i8) i8 {
    if (a == 0) {
        return b;
    }

    return a + b;
}

// The export specifier makes a function externally visible in the generated
// object file, and makes it use the C ABI.
export fn sub(a: i8, b: i8) i8 { return a - b; }

// The extern specifier is used to declare a function that will be resolved
// at link time, when linking statically, or at runtime, when linking
// dynamically. The quoted identifier after the extern keyword specifies
// the library that has the function. (e.g. "c" -&gt; libc.so)
// The callconv specifier changes the calling convention of the function.
const WINAPI: std.builtin.CallingConvention = if (native_arch == .x86) .Stdcall else .C;
extern "kernel32" fn ExitProcess(exit_code: u32) callconv(WINAPI) noreturn;
extern "c" fn atan2(a: f64, b: f64) f64;

// The @setCold builtin tells the optimizer that a function is rarely called.
fn abort() noreturn {
    @setCold(true);
    while (true) {}
}

// The naked calling convention makes a function not have any function prologue or epilogue.
// This can be useful when integrating with assembly.
fn _start() callconv(.Naked) noreturn {
    abort();
}

// The inline calling convention forces a function to be inlined at all call sites.
// If the function cannot be inlined, it is a compile-time error.
fn shiftLeftOne(a: u32) callconv(.Inline) u32 {
    return a &lt;&lt; 1;
}

// The pub specifier allows the function to be visible when importing.
// Another file can use @import and call sub2
pub fn sub2(a: i8, b: i8) i8 { return a - b; }

// Function pointers are prefixed with `*const `.
const call2_op = *const fn (a: i8, b: i8) i8;
fn do_op(fn_call: call2_op, op1: i8, op2: i8) i8 {
    return fn_call(op1, op2);
}

test "function" {
    try expect(do_op(add, 5, 6) == 11);
    try expect(do_op(sub2, 5, 6) == -1);
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_functions.zig</kbd>
1/1 test.function... OK
All 1 tests passed.
</samp></pre></figure> <p>There is a difference between a function <em>body</em> and a function <em>pointer</em>. Function bodies are <a href="#comptime">comptime</a>-only types while function <a href="#Pointers">Pointers</a> may be runtime-known.</p> <h3 id="Pass-by-value-Parameters">Pass-by-value Parameters </h3> <p> Primitive types such as <a href="#Integers">Integers</a> and <a href="#Floats">Floats</a> passed as parameters are copied, and then the copy is available in the function body. This is called "passing by value". Copying a primitive type is essentially free and typically involves nothing more than setting a register. </p> <p> Structs, unions, and arrays can sometimes be more efficiently passed as a reference, since a copy could be arbitrarily expensive depending on the size. When these types are passed as parameters, Zig may choose to copy and pass by value, or pass by reference, whichever way Zig decides will be faster. This is made possible, in part, by the fact that parameters are immutable. </p> <figure><figcaption class="zig-cap"><cite class="file">test_pass_by_reference_or_value.zig</cite></figcaption><pre data-language="zig">const Point = struct {
    x: i32,
    y: i32,
};

fn foo(point: Point) i32 {
    // Here, `point` could be a reference, or a copy. The function body
    // can ignore the difference and treat it as a value. Be very careful
    // taking the address of the parameter - it should be treated as if
    // the address will become invalid when the function returns.
    return point.x + point.y;
}

const expect = @import("std").testing.expect;

test "pass struct to function" {
    try expect(foo(Point{ .x = 1, .y = 2 }) == 3);
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_pass_by_reference_or_value.zig</kbd>
1/1 test.pass struct to function... OK
All 1 tests passed.
</samp></pre></figure> <p> For extern functions, Zig follows the C ABI for passing structs and unions by value. </p> <h3 id="Function-Parameter-Type-Inference">Function Parameter Type Inference </h3> <p> Function parameters can be declared with <code><span class="line"><span class="tok-kw">anytype</span></span></code> in place of the type. In this case the parameter types will be inferred when the function is called. Use <a href="#TypeOf">@TypeOf</a> and <a href="#typeInfo">@typeInfo</a> to get information about the inferred type. </p> <figure><figcaption class="zig-cap"><cite class="file">test_fn_type_inference.zig</cite></figcaption><pre data-language="zig">const expect = @import("std").testing.expect;

fn addFortyTwo(x: anytype) @TypeOf(x) {
    return x + 42;
}

test "fn type inference" {
    try expect(addFortyTwo(1) == 43);
    try expect(@TypeOf(addFortyTwo(1)) == comptime_int);
    var y: i64 = 2;
    try expect(addFortyTwo(y) == 44);
    try expect(@TypeOf(addFortyTwo(y)) == i64);
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_fn_type_inference.zig</kbd>
1/1 test.fn type inference... OK
All 1 tests passed.
</samp></pre></figure> <h3 id="Function-Reflection">Function Reflection </h3> <figure><figcaption class="zig-cap"><cite class="file">test_fn_reflection.zig</cite></figcaption><pre data-language="zig">const std = @import("std");
const math = std.math;
const testing = std.testing;

test "fn reflection" {
    try testing.expect(@typeInfo(@TypeOf(testing.expect)).Fn.params[0].type.? == bool);
    try testing.expect(@typeInfo(@TypeOf(testing.tmpDir)).Fn.return_type.? == testing.TmpDir);

    try testing.expect(@typeInfo(@TypeOf(math.Log2Int)).Fn.is_generic);
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_fn_reflection.zig</kbd>
1/1 test.fn reflection... OK
All 1 tests passed.
</samp></pre></figure> <h2 id="Errors">Errors </h2> <h3 id="Error-Set-Type">Error Set Type </h3> <p> An error set is like an <a href="#enum">enum</a>. However, each error name across the entire compilation gets assigned an unsigned integer greater than 0. You are allowed to declare the same error name more than once, and if you do, it gets assigned the same integer value. </p> <p> The number of unique error values across the entire compilation should determine the size of the error set type. However right now it is hard coded to be a <code><span class="line"><span class="tok-type">u16</span></span></code>. See <a href="https://github.com/ziglang/zig/issues/786">#786</a>. </p> <p> You can <a href="#Type-Coercion">coerce</a> an error from a subset to a superset: </p> <figure><figcaption class="zig-cap"><cite class="file">test_coerce_error_subset_to_superset.zig</cite></figcaption><pre data-language="zig">const std = @import("std");

const FileOpenError = error {
    AccessDenied,
    OutOfMemory,
    FileNotFound,
};

const AllocationError = error {
    OutOfMemory,
};

test "coerce subset to superset" {
    const err = foo(AllocationError.OutOfMemory);
    try std.testing.expect(err == FileOpenError.OutOfMemory);
}

fn foo(err: AllocationError) FileOpenError {
    return err;
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_coerce_error_subset_to_superset.zig</kbd>
1/1 test.coerce subset to superset... OK
All 1 tests passed.
</samp></pre></figure> <p> But you cannot <a href="#Type-Coercion">coerce</a> an error from a superset to a subset: </p> <figure><figcaption class="zig-cap"><cite class="file">test_coerce_error_superset_to_subset.zig</cite></figcaption><pre data-language="zig">const FileOpenError = error {
    AccessDenied,
    OutOfMemory,
    FileNotFound,
};

const AllocationError = error {
    OutOfMemory,
};

test "coerce superset to subset" {
    foo(FileOpenError.OutOfMemory) catch {};
}

fn foo(err: FileOpenError) AllocationError {
    return err;
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_coerce_error_superset_to_subset.zig</kbd>
<span class="sgr-1m">docgen_tmp/test_coerce_error_superset_to_subset.zig:16:12: </span><span class="sgr-31m">error: </span><span class="sgr-1m">expected type 'error{OutOfMemory}', found 'error{AccessDenied,OutOfMemory,FileNotFound}'
</span>    return err;
           <span class="sgr-32m">^~~
</span><span class="sgr-1m">docgen_tmp/test_coerce_error_superset_to_subset.zig:16:12: </span><span class="sgr-36m">note: </span><span class="sgr-1m">'error.AccessDenied' not a member of destination error set
</span><span class="sgr-1m">docgen_tmp/test_coerce_error_superset_to_subset.zig:16:12: </span><span class="sgr-36m">note: </span><span class="sgr-1m">'error.FileNotFound' not a member of destination error set
</span><span class="sgr-1m">docgen_tmp/test_coerce_error_superset_to_subset.zig:15:28: </span><span class="sgr-36m">note: </span><span class="sgr-1m">function return type declared here
</span>fn foo(err: FileOpenError) AllocationError {
                           <span class="sgr-32m">^~~~~~~~~~~~~~~
</span><span class="sgr-2m">referenced by:
    test.coerce superset to subset: docgen_tmp/test_coerce_error_superset_to_subset.zig:12:5
    remaining reference traces hidden; use '-freference-trace' to see all reference traces
</span>
</samp></pre></figure> <p> There is a shortcut for declaring an error set with only 1 value, and then getting that value: </p> <figure><figcaption class="zig-cap"><cite class="file">single_value_error_set_shortcut.zig</cite></figcaption><pre data-language="zig">const err = error.FileNotFound;</pre></figure> <p>This is equivalent to:</p> <figure><figcaption class="zig-cap"><cite class="file">single_value_error_set.zig</cite></figcaption><pre data-language="zig">const err = (error {FileNotFound}).FileNotFound;</pre></figure> <p> This becomes useful when using <a href="#Inferred-Error-Sets">Inferred Error Sets</a>. </p> <h4 id="The-Global-Error-Set">
<a href="#toc-The-Global-Error-Set">The Global Error Set</a> </h4> <p><code><span class="line"><span class="tok-type">anyerror</span></span></code> refers to the global error set. This is the error set that contains all errors in the entire compilation unit. It is a superset of all other error sets and a subset of none of them. </p> <p> You can <a href="#Type-Coercion">coerce</a> any error set to the global one, and you can explicitly cast an error of the global error set to a non-global one. This inserts a language-level assert to make sure the error value is in fact in the destination error set. </p> <p> The global error set should generally be avoided because it prevents the compiler from knowing what errors are possible at compile-time. Knowing the error set at compile-time is better for generated documentation and helpful error messages, such as forgetting a possible error value in a <a href="#switch">switch</a>. </p> <h3 id="Error-Union-Type">Error Union Type </h3> <p> An error set type and normal type can be combined with the <code><span class="line">!</span></code> binary operator to form an error union type. You are likely to use an error union type more often than an error set type by itself. </p> <p> Here is a function to parse a string into a 64-bit integer: </p> <figure><figcaption class="zig-cap"><cite class="file">error_union_parsing_u64.zig</cite></figcaption><pre data-language="zig">const std = @import("std");
const maxInt = std.math.maxInt;

pub fn parseU64(buf: []const u8, radix: u8) !u64 {
    var x: u64 = 0;

    for (buf) |c| {
        const digit = charToDigit(c);

        if (digit &gt;= radix) {
            return error.InvalidChar;
        }

        // x *= radix
        var ov = @mulWithOverflow(x, radix);
        if (ov[1] != 0) return error.OverFlow;

        // x += digit
        ov = @addWithOverflow(ov[0], digit);
        if (ov[1] != 0) return error.OverFlow;
        x = ov[0];
    }

    return x;
}

fn charToDigit(c: u8) u8 {
    return switch (c) {
        '0' ... '9' =&gt; c - '0',
        'A' ... 'Z' =&gt; c - 'A' + 10,
        'a' ... 'z' =&gt; c - 'a' + 10,
        else =&gt; maxInt(u8),
    };
}

test "parse u64" {
    const result = try parseU64("1234", 10);
    try std.testing.expect(result == 1234);
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test error_union_parsing_u64.zig</kbd>
1/1 test.parse u64... OK
All 1 tests passed.
</samp></pre></figure> <p> Notice the return type is <code><span class="line">!<span class="tok-type">u64</span></span></code>. This means that the function either returns an unsigned 64 bit integer, or an error. We left off the error set to the left of the <code><span class="line">!</span></code>, so the error set is inferred. </p> <p> Within the function definition, you can see some return statements that return an error, and at the bottom a return statement that returns a <code><span class="line"><span class="tok-type">u64</span></span></code>. Both types <a href="#Type-Coercion">coerce</a> to <code><span class="line"><span class="tok-type">anyerror</span>!<span class="tok-type">u64</span></span></code>. </p> <p> What it looks like to use this function varies depending on what you're trying to do. One of the following: </p> <ul> <li>You want to provide a default value if it returned an error.</li> <li>If it returned an error then you want to return the same error.</li> <li>You know with complete certainty it will not return an error, so want to unconditionally unwrap it.</li> <li>You want to take a different action for each possible error.</li> </ul> <h4 id="catch">
<a href="#toc-catch">catch</a> </h4> <p>If you want to provide a default value, you can use the <code><span class="line"><span class="tok-kw">catch</span></span></code> binary operator:</p> <figure><figcaption class="zig-cap"><cite class="file">catch.zig</cite></figcaption><pre data-language="zig">const parseU64 = @import("error_union_parsing_u64.zig").parseU64;

fn doAThing(str: []u8) void {
    const number = parseU64(str, 10) catch 13;
    _ = number; // ...
}</pre></figure> <p> In this code, <code><span class="line">number</span></code> will be equal to the successfully parsed string, or a default value of 13. The type of the right hand side of the binary <code><span class="line"><span class="tok-kw">catch</span></span></code> operator must match the unwrapped error union type, or be of type <code><span class="line"><span class="tok-type">noreturn</span></span></code>. </p> <p> If you want to provide a default value with <code><span class="line"><span class="tok-kw">catch</span></span></code> after performing some logic, you can combine <code><span class="line"><span class="tok-kw">catch</span></span></code> with named <a href="#Blocks">Blocks</a>: </p> <figure><figcaption class="zig-cap"><cite class="file">handle_error_with_catch_block.zig.zig</cite></figcaption><pre data-language="zig">const parseU64 = @import("error_union_parsing_u64.zig").parseU64;

fn doAThing(str: []u8) void {
    const number = parseU64(str, 10) catch blk: {
        // do things
        break :blk 13;
    };
    _ = number; // number is now initialized
}</pre></figure> <h4 id="try">
<a href="#toc-try">try</a> </h4> <p>Let's say you wanted to return the error if you got one, otherwise continue with the function logic:</p> <figure><figcaption class="zig-cap"><cite class="file">catch_err_return.zig</cite></figcaption><pre data-language="zig">const parseU64 = @import("error_union_parsing_u64.zig").parseU64;

fn doAThing(str: []u8) !void {
    const number = parseU64(str, 10) catch |err| return err;
    _ = number; // ...
}</pre></figure> <p> There is a shortcut for this. The <code><span class="line"><span class="tok-kw">try</span></span></code> expression: </p> <figure><figcaption class="zig-cap"><cite class="file">try.zig</cite></figcaption><pre data-language="zig">const parseU64 = @import("error_union_parsing_u64.zig").parseU64;

fn doAThing(str: []u8) !void {
    const number = try parseU64(str, 10);
    _ = number; // ...
}</pre></figure> <p> <code><span class="line"><span class="tok-kw">try</span></span></code> evaluates an error union expression. If it is an error, it returns from the current function with the same error. Otherwise, the expression results in the unwrapped value. </p> <p> Maybe you know with complete certainty that an expression will never be an error. In this case you can do this: </p> <code><span class="line"><span class="tok-kw">const</span> number = parseU64(<span class="tok-str">"1234"</span>, <span class="tok-number">10</span>) <span class="tok-kw">catch</span> <span class="tok-kw">unreachable</span>;</span></code> <p> Here we know for sure that "1234" will parse successfully. So we put the <code><span class="line"><span class="tok-kw">unreachable</span></span></code> value on the right hand side. <code><span class="line"><span class="tok-kw">unreachable</span></span></code> generates a panic in <a href="#Debug">Debug</a> and <a href="#ReleaseSafe">ReleaseSafe</a> modes and undefined behavior in <a href="#ReleaseFast">ReleaseFast</a> and <a href="#ReleaseSmall">ReleaseSmall</a> modes. So, while we're debugging the application, if there <em>was</em> a surprise error here, the application would crash appropriately. </p> <p> You may want to take a different action for every situation. For that, we combine the <a href="#if">if</a> and <a href="#switch">switch</a> expression: </p> <figure><figcaption class="zig-cap"><cite class="file">handle_all_error_scenarios.zig</cite></figcaption><pre data-language="zig">fn doAThing(str: []u8) void {
    if (parseU64(str, 10)) |number| {
        doSomethingWithNumber(number);
    } else |err| switch (err) {
        error.Overflow =&gt; {
            // handle overflow...
        },
        // we promise that InvalidChar won't happen (or crash in debug mode if it does)
        error.InvalidChar =&gt; unreachable,
    }
}</pre></figure> <p> Finally, you may want to handle only some errors. For that, you can capture the unhandled errors in the <code><span class="line"><span class="tok-kw">else</span></span></code> case, which now contains a narrower error set: </p> <figure><figcaption class="zig-cap"><cite class="file">handle_some_error_scenarios.zig</cite></figcaption><pre data-language="zig">fn doAnotherThing(str: []u8) error{InvalidChar}!void {
    if (parseU64(str, 10)) |number| {
        doSomethingWithNumber(number);
    } else |err| switch (err) {
        error.Overflow =&gt; {
            // handle overflow...
        },
        else =&gt; |leftover_err| return leftover_err,
    }
}</pre></figure> <p> You must use the variable capture syntax. If you don't need the variable, you can capture with <code><span class="line">_</span></code> and avoid the <code><span class="line"><span class="tok-kw">switch</span></span></code>. </p> <figure><figcaption class="zig-cap"><cite class="file">handle_no_error_scenarios.zig</cite></figcaption><pre data-language="zig">fn doADifferentThing(str: []u8) void {
    if (parseU64(str, 10)) |number| {
        doSomethingWithNumber(number);
    } else |_| {
        // do as you'd like
    }
}</pre></figure> <h4 id="errdefer">
<a href="#toc-errdefer">errdefer</a> </h4> <p> The other component to error handling is defer statements. In addition to an unconditional <a href="#defer">defer</a>, Zig has <code><span class="line"><span class="tok-kw">errdefer</span></span></code>, which evaluates the deferred expression on block exit path if and only if the function returned with an error from the block. </p> <p> Example: </p> <figure><figcaption class="zig-cap"><cite class="file">errdefer_example.zig</cite></figcaption><pre data-language="zig">fn createFoo(param: i32) !Foo {
    const foo = try tryToAllocateFoo();
    // now we have allocated foo. we need to free it if the function fails.
    // but we want to return it if the function succeeds.
    errdefer deallocateFoo(foo);

    const tmp_buf = allocateTmpBuffer() orelse return error.OutOfMemory;
    // tmp_buf is truly a temporary resource, and we for sure want to clean it up
    // before this block leaves scope
    defer deallocateTmpBuffer(tmp_buf);

    if (param &gt; 1337) return error.InvalidParam;

    // here the errdefer will not run since we're returning success from the function.
    // but the defer will run!
    return foo;
}</pre></figure> <p> The neat thing about this is that you get robust error handling without the verbosity and cognitive overhead of trying to make sure every exit path is covered. The deallocation code is always directly following the allocation code. </p> <h4 id="Common-errdefer-Slip-Ups">
<a href="#toc-Common-errdefer-Slip-Ups">Common errdefer Slip-Ups</a> </h4> <p> It should be noted that <code><span class="line"><span class="tok-kw">errdefer</span></span></code> statements only last until the end of the block they are written in, and therefore are not run if an error is returned outside of that block: </p> <figure><figcaption class="zig-cap"><cite class="file">test_errdefer_slip_ups.zig</cite></figcaption><pre data-language="zig">const std = @import("std");
const Allocator = std.mem.Allocator;

const Foo = struct {
    data: u32,
};

fn tryToAllocateFoo(allocator: Allocator) !*Foo {
    return allocator.create(Foo);
}

fn deallocateFoo(allocator: Allocator, foo: *Foo) void {
    allocator.destroy(foo);
}

fn getFooData() !u32 {
    return 666;
}

fn createFoo(allocator: Allocator, param: i32) !*Foo {
    const foo = getFoo: {
        var foo = try tryToAllocateFoo(allocator);
        errdefer deallocateFoo(allocator, foo); // Only lasts until the end of getFoo

        // Calls deallocateFoo on error
        foo.data = try getFooData();

        break :getFoo foo;
    };

    // Outside of the scope of the errdefer, so
    // deallocateFoo will not be called here
    if (param &gt; 1337) return error.InvalidParam;

    return foo;
}

test "createFoo" {
    try std.testing.expectError(error.InvalidParam, createFoo(std.testing.allocator, 2468));
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_errdefer_slip_ups.zig</kbd>
1/1 test.createFoo... OK
[gpa] (err): memory address 0x7f263e4fb000 leaked:
/home/ci/actions-runner/_work/zig-bootstrap/zig/docgen_tmp/test_errdefer_slip_ups.zig:9:28: 0x22489f in tryToAllocateFoo (test)
    return allocator.create(Foo);
                           ^
/home/ci/actions-runner/_work/zig-bootstrap/zig/docgen_tmp/test_errdefer_slip_ups.zig:22:39: 0x224ab5 in createFoo (test)
        var foo = try tryToAllocateFoo(allocator);
                                      ^
/home/ci/actions-runner/_work/zig-bootstrap/zig/docgen_tmp/test_errdefer_slip_ups.zig:39:62: 0x224cdd in test.createFoo (test)
    try std.testing.expectError(error.InvalidParam, createFoo(std.testing.allocator, 2468));
                                                             ^
/home/ci/actions-runner/_work/zig-bootstrap/out/host/lib/zig/test_runner.zig:176:28: 0x232c99 in mainTerminal (test)
        } else test_fn.func();
                           ^
/home/ci/actions-runner/_work/zig-bootstrap/out/host/lib/zig/test_runner.zig:36:28: 0x229b8a in main (test)
        return mainTerminal();
                           ^
/home/ci/actions-runner/_work/zig-bootstrap/out/host/lib/zig/std/start.zig:564:22: 0x225202 in posixCallMainAndExit (test)
            root.main();
                     ^
/home/ci/actions-runner/_work/zig-bootstrap/out/host/lib/zig/std/start.zig:243:5: 0x224d51 in _start (test)
    asm volatile (switch (native_arch) {
    ^

All 1 tests passed.
1 errors were logged.
1 tests leaked memory.
error: the following test command failed with exit code 1:
/home/ci/actions-runner/_work/zig-bootstrap/out/zig-local-cache/o/10666e5d8b320aa9e03f2764057c3609/test
</samp></pre></figure> <p> To ensure that <code><span class="line">deallocateFoo</span></code> is properly called when returning an error, you must add an <code><span class="line"><span class="tok-kw">errdefer</span></span></code> outside of the block: </p> <figure><figcaption class="zig-cap"><cite class="file">test_errdefer_block.zig</cite></figcaption><pre data-language="zig">const std = @import("std");
const Allocator = std.mem.Allocator;

const Foo = struct {
    data: u32,
};

fn tryToAllocateFoo(allocator: Allocator) !*Foo {
    return allocator.create(Foo);
}

fn deallocateFoo(allocator: Allocator, foo: *Foo) void {
    allocator.destroy(foo);
}

fn getFooData() !u32 {
    return 666;
}

fn createFoo(allocator: Allocator, param: i32) !*Foo {
    const foo = getFoo: {
        var foo = try tryToAllocateFoo(allocator);
        errdefer deallocateFoo(allocator, foo);

        foo.data = try getFooData();

        break :getFoo foo;
    };
    // This lasts for the rest of the function
    errdefer deallocateFoo(allocator, foo);

    // Error is now properly handled by errdefer
    if (param &gt; 1337) return error.InvalidParam;

    return foo;
}

test "createFoo" {
    try std.testing.expectError(error.InvalidParam, createFoo(std.testing.allocator, 2468));
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_errdefer_block.zig</kbd>
1/1 test.createFoo... OK
All 1 tests passed.
</samp></pre></figure> <p> The fact that errdefers only last for the block they are declared in is especially important when using loops: </p> <figure><figcaption class="zig-cap"><cite class="file">test_errdefer_loop_leak.zig</cite></figcaption><pre data-language="zig">const std = @import("std");
const Allocator = std.mem.Allocator;

const Foo = struct {
    data: *u32
};

fn getData() !u32 {
    return 666;
}

fn genFoos(allocator: Allocator, num: usize) ![]Foo {
    var foos = try allocator.alloc(Foo, num);
    errdefer allocator.free(foos);

    for (foos, 0..) |*foo, i| {
        foo.data = try allocator.create(u32);
        // This errdefer does not last between iterations
        errdefer allocator.destroy(foo.data);

        // The data for the first 3 foos will be leaked
        if(i &gt;= 3) return error.TooManyFoos;

        foo.data.* = try getData();
    }

    return foos;
}

test "genFoos" {
    try std.testing.expectError(error.TooManyFoos, genFoos(std.testing.allocator, 5));
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_errdefer_loop_leak.zig</kbd>
1/1 test.genFoos... OK
[gpa] (err): memory address 0x7ff22a6a7000 leaked:
/home/ci/actions-runner/_work/zig-bootstrap/zig/docgen_tmp/test_errdefer_loop_leak.zig:17:40: 0x224d07 in genFoos (test)
        foo.data = try allocator.create(u32);
                                       ^
/home/ci/actions-runner/_work/zig-bootstrap/zig/docgen_tmp/test_errdefer_loop_leak.zig:31:59: 0x2255fd in test.genFoos (test)
    try std.testing.expectError(error.TooManyFoos, genFoos(std.testing.allocator, 5));
                                                          ^
/home/ci/actions-runner/_work/zig-bootstrap/out/host/lib/zig/test_runner.zig:176:28: 0x233a59 in mainTerminal (test)
        } else test_fn.func();
                           ^
/home/ci/actions-runner/_work/zig-bootstrap/out/host/lib/zig/test_runner.zig:36:28: 0x22a68a in main (test)
        return mainTerminal();
                           ^
/home/ci/actions-runner/_work/zig-bootstrap/out/host/lib/zig/std/start.zig:564:22: 0x225b22 in posixCallMainAndExit (test)
            root.main();
                     ^
/home/ci/actions-runner/_work/zig-bootstrap/out/host/lib/zig/std/start.zig:243:5: 0x225671 in _start (test)
    asm volatile (switch (native_arch) {
    ^

[gpa] (err): memory address 0x7ff22a6a7004 leaked:
/home/ci/actions-runner/_work/zig-bootstrap/zig/docgen_tmp/test_errdefer_loop_leak.zig:17:40: 0x224d07 in genFoos (test)
        foo.data = try allocator.create(u32);
                                       ^
/home/ci/actions-runner/_work/zig-bootstrap/zig/docgen_tmp/test_errdefer_loop_leak.zig:31:59: 0x2255fd in test.genFoos (test)
    try std.testing.expectError(error.TooManyFoos, genFoos(std.testing.allocator, 5));
                                                          ^
/home/ci/actions-runner/_work/zig-bootstrap/out/host/lib/zig/test_runner.zig:176:28: 0x233a59 in mainTerminal (test)
        } else test_fn.func();
                           ^
/home/ci/actions-runner/_work/zig-bootstrap/out/host/lib/zig/test_runner.zig:36:28: 0x22a68a in main (test)
        return mainTerminal();
                           ^
/home/ci/actions-runner/_work/zig-bootstrap/out/host/lib/zig/std/start.zig:564:22: 0x225b22 in posixCallMainAndExit (test)
            root.main();
                     ^
/home/ci/actions-runner/_work/zig-bootstrap/out/host/lib/zig/std/start.zig:243:5: 0x225671 in _start (test)
    asm volatile (switch (native_arch) {
    ^

[gpa] (err): memory address 0x7ff22a6a7008 leaked:
/home/ci/actions-runner/_work/zig-bootstrap/zig/docgen_tmp/test_errdefer_loop_leak.zig:17:40: 0x224d07 in genFoos (test)
        foo.data = try allocator.create(u32);
                                       ^
/home/ci/actions-runner/_work/zig-bootstrap/zig/docgen_tmp/test_errdefer_loop_leak.zig:31:59: 0x2255fd in test.genFoos (test)
    try std.testing.expectError(error.TooManyFoos, genFoos(std.testing.allocator, 5));
                                                          ^
/home/ci/actions-runner/_work/zig-bootstrap/out/host/lib/zig/test_runner.zig:176:28: 0x233a59 in mainTerminal (test)
        } else test_fn.func();
                           ^
/home/ci/actions-runner/_work/zig-bootstrap/out/host/lib/zig/test_runner.zig:36:28: 0x22a68a in main (test)
        return mainTerminal();
                           ^
/home/ci/actions-runner/_work/zig-bootstrap/out/host/lib/zig/std/start.zig:564:22: 0x225b22 in posixCallMainAndExit (test)
            root.main();
                     ^
/home/ci/actions-runner/_work/zig-bootstrap/out/host/lib/zig/std/start.zig:243:5: 0x225671 in _start (test)
    asm volatile (switch (native_arch) {
    ^

All 1 tests passed.
3 errors were logged.
1 tests leaked memory.
error: the following test command failed with exit code 1:
/home/ci/actions-runner/_work/zig-bootstrap/out/zig-local-cache/o/e094ad5e16ced3e70d4b87c42993e682/test
</samp></pre></figure> <p> Special care must be taken with code that allocates in a loop to make sure that no memory is leaked when returning an error: </p> <figure><figcaption class="zig-cap"><cite class="file">test_errdefer_loop.zig</cite></figcaption><pre data-language="zig">const std = @import("std");
const Allocator = std.mem.Allocator;

const Foo = struct {
    data: *u32
};

fn getData() !u32 {
    return 666;
}

fn genFoos(allocator: Allocator, num: usize) ![]Foo {
    var foos = try allocator.alloc(Foo, num);
    errdefer allocator.free(foos);

    // Used to track how many foos have been initialized
    // (including their data being allocated)
    var num_allocated: usize = 0;
    errdefer for (foos[0..num_allocated]) |foo| {
        allocator.destroy(foo.data);
    };
    for (foos, 0..) |*foo, i| {
        foo.data = try allocator.create(u32);
        num_allocated += 1;

        if (i &gt;= 3) return error.TooManyFoos;

        foo.data.* = try getData();
    }

    return foos;
}

test "genFoos" {
    try std.testing.expectError(error.TooManyFoos, genFoos(std.testing.allocator, 5));
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_errdefer_loop.zig</kbd>
1/1 test.genFoos... OK
All 1 tests passed.
</samp></pre></figure> <p> A couple of other tidbits about error handling: </p> <ul> <li>These primitives give enough expressiveness that it's completely practical to have failing to check for an error be a compile error. If you really want to ignore the error, you can add <code><span class="line"><span class="tok-kw">catch</span> <span class="tok-kw">unreachable</span></span></code> and get the added benefit of crashing in Debug and ReleaseSafe modes if your assumption was wrong. </li> <li> Since Zig understands error types, it can pre-weight branches in favor of errors not occurring. Just a small optimization benefit that is not available in other languages. </li> </ul> <p>See also:</p>
<ul> <li><a href="#defer">defer</a></li> <li><a href="#if">if</a></li> <li><a href="#switch">switch</a></li> </ul> <p>An error union is created with the <code><span class="line">!</span></code> binary operator. You can use compile-time reflection to access the child type of an error union:</p> <figure><figcaption class="zig-cap"><cite class="file">test_error_union.zig</cite></figcaption><pre data-language="zig">const expect = @import("std").testing.expect;

test "error union" {
    var foo: anyerror!i32 = undefined;

    // Coerce from child type of an error union:
    foo = 1234;

    // Coerce from an error set:
    foo = error.SomeError;

    // Use compile-time reflection to access the payload type of an error union:
    try comptime expect(@typeInfo(@TypeOf(foo)).ErrorUnion.payload == i32);

    // Use compile-time reflection to access the error set type of an error union:
    try comptime expect(@typeInfo(@TypeOf(foo)).ErrorUnion.error_set == anyerror);
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_error_union.zig</kbd>
1/1 test.error union... OK
All 1 tests passed.
</samp></pre></figure> <h4 id="Merging-Error-Sets">
<a href="#toc-Merging-Error-Sets">Merging Error Sets</a> </h4> <p> Use the <code><span class="line">||</span></code> operator to merge two error sets together. The resulting error set contains the errors of both error sets. Doc comments from the left-hand side override doc comments from the right-hand side. In this example, the doc comments for <code><span class="line">C.PathNotFound</span></code> is <code>A doc comment</code>. </p> <p> This is especially useful for functions which return different error sets depending on <a href="#comptime">comptime</a> branches. For example, the Zig standard library uses <code><span class="line">LinuxFileOpenError || WindowsFileOpenError</span></code> for the error set of opening files. </p> <figure><figcaption class="zig-cap"><cite class="file">test_merging_error_sets.zig</cite></figcaption><pre data-language="zig">const A = error{
    NotDir,

    /// A doc comment
    PathNotFound,
};
const B = error{
    OutOfMemory,

    /// B doc comment
    PathNotFound,
};

const C = A || B;

fn foo() C!void {
    return error.NotDir;
}

test "merge error sets" {
    if (foo()) {
        @panic("unexpected");
    } else |err| switch (err) {
        error.OutOfMemory =&gt; @panic("unexpected"),
        error.PathNotFound =&gt; @panic("unexpected"),
        error.NotDir =&gt; {},
    }
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_merging_error_sets.zig</kbd>
1/1 test.merge error sets... OK
All 1 tests passed.
</samp></pre></figure> <h4 id="Inferred-Error-Sets">
<a href="#toc-Inferred-Error-Sets">Inferred Error Sets</a> </h4> <p> Because many functions in Zig return a possible error, Zig supports inferring the error set. To infer the error set for a function, prepend the <code><span class="line">!</span></code> operator to the functionâ€™s return type, like <code><span class="line">!T</span></code>: </p> <figure><figcaption class="zig-cap"><cite class="file">test_inferred_error_sets.zig</cite></figcaption><pre data-language="zig">// With an inferred error set
pub fn add_inferred(comptime T: type, a: T, b: T) !T {
    const ov = @addWithOverflow(a, b);
    if (ov[1] != 0) return error.Overflow;
    return ov[0];
}

// With an explicit error set
pub fn add_explicit(comptime T: type, a: T, b: T) Error!T {
    const ov = @addWithOverflow(a, b);
    if (ov[1] != 0) return error.Overflow;
    return ov[0];
}

const Error = error {
    Overflow,
};

const std = @import("std");

test "inferred error set" {
    if (add_inferred(u8, 255, 1)) |_| unreachable else |err| switch (err) {
        error.Overflow =&gt; {}, // ok
    }
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_inferred_error_sets.zig</kbd>
1/1 test.inferred error set... OK
All 1 tests passed.
</samp></pre></figure> <p> When a function has an inferred error set, that function becomes generic and thus it becomes trickier to do certain things with it, such as obtain a function pointer, or have an error set that is consistent across different build targets. Additionally, inferred error sets are incompatible with recursion. </p> <p> In these situations, it is recommended to use an explicit error set. You can generally start with an empty error set and let compile errors guide you toward completing the set. </p> <p> These limitations may be overcome in a future version of Zig. </p> <h3 id="Error-Return-Traces">Error Return Traces </h3> <p> Error Return Traces show all the points in the code that an error was returned to the calling function. This makes it practical to use <a href="#try">try</a> everywhere and then still be able to know what happened if an error ends up bubbling all the way out of your application. </p> <figure><figcaption class="zig-cap"><cite class="file">error_return_trace.zig</cite></figcaption><pre data-language="zig">pub fn main() !void {
    try foo(12);
}

fn foo(x: i32) !void {
    if (x &gt;= 5) {
        try bar();
    } else {
        try bang2();
    }
}

fn bar() !void {
    if (baz()) {
        try quux();
    } else |err| switch (err) {
        error.FileNotFound =&gt; try hello(),
    }
}

fn baz() !void {
    try bang1();
}

fn quux() !void {
    try bang2();
}

fn hello() !void {
    try bang2();
}

fn bang1() !void {
    return error.FileNotFound;
}

fn bang2() !void {
    return error.PermissionDenied;
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig build-exe error_return_trace.zig</kbd>
$ <kbd>./error_return_trace</kbd>
error: PermissionDenied
/home/ci/actions-runner/_work/zig-bootstrap/zig/docgen_tmp/error_return_trace.zig:34:5: 0x21e378 in bang1 (error_return_trace)
    return error.FileNotFound;
    ^
/home/ci/actions-runner/_work/zig-bootstrap/zig/docgen_tmp/error_return_trace.zig:22:5: 0x21e483 in baz (error_return_trace)
    try bang1();
    ^
/home/ci/actions-runner/_work/zig-bootstrap/zig/docgen_tmp/error_return_trace.zig:38:5: 0x21e4a8 in bang2 (error_return_trace)
    return error.PermissionDenied;
    ^
/home/ci/actions-runner/_work/zig-bootstrap/zig/docgen_tmp/error_return_trace.zig:30:5: 0x21e513 in hello (error_return_trace)
    try bang2();
    ^
/home/ci/actions-runner/_work/zig-bootstrap/zig/docgen_tmp/error_return_trace.zig:17:31: 0x21e5e0 in bar (error_return_trace)
        error.FileNotFound =&gt; try hello(),
                              ^
/home/ci/actions-runner/_work/zig-bootstrap/zig/docgen_tmp/error_return_trace.zig:7:9: 0x21e6b0 in foo (error_return_trace)
        try bar();
        ^
/home/ci/actions-runner/_work/zig-bootstrap/zig/docgen_tmp/error_return_trace.zig:2:5: 0x21e708 in main (error_return_trace)
    try foo(12);
    ^
</samp></pre></figure> <p> Look closely at this example. This is no stack trace. </p> <p> You can see that the final error bubbled up was <code><span class="line">PermissionDenied</span></code>, but the original error that started this whole thing was <code><span class="line">FileNotFound</span></code>. In the <code><span class="line">bar</span></code> function, the code handles the original error code, and then returns another one, from the switch statement. Error Return Traces make this clear, whereas a stack trace would look like this: </p> <figure><figcaption class="zig-cap"><cite class="file">stack_trace.zig</cite></figcaption><pre data-language="zig">pub fn main() void {
    foo(12);
}

fn foo(x: i32) void {
    if (x &gt;= 5) {
        bar();
    } else {
        bang2();
    }
}

fn bar() void {
    if (baz()) {
        quux();
    } else {
        hello();
    }
}

fn baz() bool {
    return bang1();
}

fn quux() void {
    bang2();
}

fn hello() void {
    bang2();
}

fn bang1() bool {
    return false;
}

fn bang2() void {
    @panic("PermissionDenied");
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig build-exe stack_trace.zig</kbd>
$ <kbd>./stack_trace</kbd>
thread 2453974 panic: PermissionDenied
/home/ci/actions-runner/_work/zig-bootstrap/zig/docgen_tmp/stack_trace.zig:38:5: 0x221c95 in bang2 (stack_trace)
    @panic("PermissionDenied");
    ^
/home/ci/actions-runner/_work/zig-bootstrap/zig/docgen_tmp/stack_trace.zig:30:10: 0x24efd8 in hello (stack_trace)
    bang2();
         ^
/home/ci/actions-runner/_work/zig-bootstrap/zig/docgen_tmp/stack_trace.zig:17:14: 0x221c6c in bar (stack_trace)
        hello();
             ^
/home/ci/actions-runner/_work/zig-bootstrap/zig/docgen_tmp/stack_trace.zig:7:12: 0x22036c in foo (stack_trace)
        bar();
           ^
/home/ci/actions-runner/_work/zig-bootstrap/zig/docgen_tmp/stack_trace.zig:2:8: 0x21e77d in main (stack_trace)
    foo(12);
       ^
/home/ci/actions-runner/_work/zig-bootstrap/out/host/lib/zig/std/start.zig:564:22: 0x21e022 in posixCallMainAndExit (stack_trace)
            root.main();
                     ^
/home/ci/actions-runner/_work/zig-bootstrap/out/host/lib/zig/std/start.zig:243:5: 0x21db71 in _start (stack_trace)
    asm volatile (switch (native_arch) {
    ^
???:?:?: 0x0 in ??? (???)
(process terminated by signal)
</samp></pre></figure> <p> Here, the stack trace does not explain how the control flow in <code><span class="line">bar</span></code> got to the <code><span class="line">hello()</span></code> call. One would have to open a debugger or further instrument the application in order to find out. The error return trace, on the other hand, shows exactly how the error bubbled up. </p> <p> This debugging feature makes it easier to iterate quickly on code that robustly handles all error conditions. This means that Zig developers will naturally find themselves writing correct, robust code in order to increase their development pace. </p> <p> Error Return Traces are enabled by default in <a href="#Debug">Debug</a> and <a href="#ReleaseSafe">ReleaseSafe</a> builds and disabled by default in <a href="#ReleaseFast">ReleaseFast</a> and <a href="#ReleaseSmall">ReleaseSmall</a> builds. </p> <p> There are a few ways to activate this error return tracing feature: </p> <ul> <li>Return an error from main</li> <li>An error makes its way to <code><span class="line"><span class="tok-kw">catch</span> <span class="tok-kw">unreachable</span></span></code> and you have not overridden the default panic handler</li> <li>Use <a href="#errorReturnTrace">errorReturnTrace</a> to access the current return trace. You can use <code><span class="line">std.debug.dumpStackTrace</span></code> to print it. This function returns comptime-known <a href="#null">null</a> when building without error return tracing support.</li> </ul> <h4 id="Implementation-Details">
<a href="#toc-Implementation-Details">Implementation Details</a> </h4> <p> To analyze performance cost, there are two cases: </p> <ul> <li>when no errors are returned</li> <li>when returning errors</li> </ul> <p> For the case when no errors are returned, the cost is a single memory write operation, only in the first non-failable function in the call graph that calls a failable function, i.e. when a function returning <code><span class="line"><span class="tok-type">void</span></span></code> calls a function returning <code><span class="line"><span class="tok-kw">error</span></span></code>. This is to initialize this struct in the stack memory: </p> <figure><figcaption class="zig-cap"><cite class="file">stack_trace_struct.zig</cite></figcaption><pre data-language="zig">pub const StackTrace = struct {
    index: usize,
    instruction_addresses: [N]usize,
};</pre></figure> <p> Here, N is the maximum function call depth as determined by call graph analysis. Recursion is ignored and counts for 2. </p> <p> A pointer to <code><span class="line">StackTrace</span></code> is passed as a secret parameter to every function that can return an error, but it's always the first parameter, so it can likely sit in a register and stay there. </p> <p> That's it for the path when no errors occur. It's practically free in terms of performance. </p> <p> When generating the code for a function that returns an error, just before the <code><span class="line"><span class="tok-kw">return</span></span></code> statement (only for the <code><span class="line"><span class="tok-kw">return</span></span></code> statements that return errors), Zig generates a call to this function: </p> <figure><figcaption class="zig-cap"><cite class="file">zig_return_error_fn.zig</cite></figcaption><pre data-language="zig">// marked as "no-inline" in LLVM IR
fn __zig_return_error(stack_trace: *StackTrace) void {
    stack_trace.instruction_addresses[stack_trace.index] = @returnAddress();
    stack_trace.index = (stack_trace.index + 1) % N;
}</pre></figure> <p> The cost is 2 math operations plus some memory reads and writes. The memory accessed is constrained and should remain cached for the duration of the error return bubbling. </p> <p> As for code size cost, 1 function call before a return statement is no big deal. Even so, I have <a href="https://github.com/ziglang/zig/issues/690">a plan</a> to make the call to <code><span class="line">__zig_return_error</span></code> a tail call, which brings the code size cost down to actually zero. What is a return statement in code without error return tracing can become a jump instruction in code with error return tracing. </p> <h2 id="Optionals">Optionals </h2> <p> One area that Zig provides safety without compromising efficiency or readability is with the optional type. </p> <p> The question mark symbolizes the optional type. You can convert a type to an optional type by putting a question mark in front of it, like this: </p> <figure><figcaption class="zig-cap"><cite class="file">optional_integer.zig</cite></figcaption><pre data-language="zig">// normal integer
const normal_int: i32 = 1234;

// optional integer
const optional_int: ?i32 = 5678;</pre></figure> <p> Now the variable <code><span class="line">optional_int</span></code> could be an <code><span class="line"><span class="tok-type">i32</span></span></code>, or <code><span class="line"><span class="tok-null">null</span></span></code>. </p> <p> Instead of integers, let's talk about pointers. Null references are the source of many runtime exceptions, and even stand accused of being <a href="https://www.lucidchart.com/techblog/2015/08/31/the-worst-mistake-of-computer-science/">the worst mistake of computer science</a>. </p> <p>Zig does not have them.</p> <p> Instead, you can use an optional pointer. This secretly compiles down to a normal pointer, since we know we can use 0 as the null value for the optional type. But the compiler can check your work and make sure you don't assign null to something that can't be null. </p> <p> Typically the downside of not having null is that it makes the code more verbose to write. But, let's compare some equivalent C code and Zig code. </p> <p> Task: call malloc, if the result is null, return null. </p> <p>C code</p> <figure><figcaption class="c-cap"><cite class="file">call_malloc_in_c.c</cite></figcaption><pre data-language="zig">// malloc prototype included for reference
void *malloc(size_t size);

struct Foo *do_a_thing(void) {
    char *ptr = malloc(1234);
    if (!ptr) return NULL;
    // ...
}</pre></figure> <p>Zig code</p> <figure><figcaption class="zig-cap"><cite class="file">call_malloc_from_zig.zig</cite></figcaption><pre data-language="zig">// malloc prototype included for reference
extern fn malloc(size: usize) ?*u8;

fn doAThing() ?*Foo {
    const ptr = malloc(1234) orelse return null;
    _ = ptr; // ...
}</pre></figure> <p> Here, Zig is at least as convenient, if not more, than C. And, the type of "ptr" is <code><span class="line">*<span class="tok-type">u8</span></span></code> <em>not</em> <code><span class="line">?*<span class="tok-type">u8</span></span></code>. The <code><span class="line"><span class="tok-kw">orelse</span></span></code> keyword unwrapped the optional type and therefore <code><span class="line">ptr</span></code> is guaranteed to be non-null everywhere it is used in the function. </p> <p> The other form of checking against NULL you might see looks like this: </p> <figure><figcaption class="c-cap"><cite class="file">checking_null_in_c.c</cite></figcaption><pre data-language="zig">void do_a_thing(struct Foo *foo) {
    // do some stuff

    if (foo) {
        do_something_with_foo(foo);
    }

    // do some stuff
}</pre></figure> <p> In Zig you can accomplish the same thing: </p> <figure><figcaption class="zig-cap"><cite class="file">checking_null_in_zig.zig</cite></figcaption><pre data-language="zig">const Foo = struct{};
fn doSomethingWithFoo(foo: *Foo) void { _ = foo; }

fn doAThing(optional_foo: ?*Foo) void {
    // do some stuff

    if (optional_foo) |foo| {
      doSomethingWithFoo(foo);
    }

    // do some stuff
}</pre></figure> <p> Once again, the notable thing here is that inside the if block, <code><span class="line">foo</span></code> is no longer an optional pointer, it is a pointer, which cannot be null. </p> <p> One benefit to this is that functions which take pointers as arguments can be annotated with the "nonnull" attribute - <code>__attribute__((nonnull))</code> in <a href="https://gcc.gnu.org/onlinedocs/gcc-4.0.0/gcc/Function-Attributes.html">GCC</a>. The optimizer can sometimes make better decisions knowing that pointer arguments cannot be null. </p> <h3 id="Optional-Type">Optional Type </h3> <p>An optional is created by putting <code><span class="line">?</span></code> in front of a type. You can use compile-time reflection to access the child type of an optional:</p> <figure><figcaption class="zig-cap"><cite class="file">test_optional_type.zig</cite></figcaption><pre data-language="zig">const expect = @import("std").testing.expect;

test "optional type" {
    // Declare an optional and coerce from null:
    var foo: ?i32 = null;

    // Coerce from child type of an optional
    foo = 1234;

    // Use compile-time reflection to access the child type of the optional:
    try comptime expect(@typeInfo(@TypeOf(foo)).Optional.child == i32);
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_optional_type.zig</kbd>
1/1 test.optional type... OK
All 1 tests passed.
</samp></pre></figure> <h3 id="null">null </h3> <p> Just like <a href="#undefined">undefined</a>, <code><span class="line"><span class="tok-null">null</span></span></code> has its own type, and the only way to use it is to cast it to a different type: </p> <figure><figcaption class="zig-cap"><cite class="file">null.zig</cite></figcaption><pre data-language="zig">const optional_value: ?i32 = null;</pre></figure> <h3 id="Optional-Pointers">Optional Pointers </h3> <p>An optional pointer is guaranteed to be the same size as a pointer. The <code><span class="line"><span class="tok-null">null</span></span></code> of the optional is guaranteed to be address 0.</p> <figure><figcaption class="zig-cap"><cite class="file">test_optional_pointer.zig</cite></figcaption><pre data-language="zig">const expect = @import("std").testing.expect;

test "optional pointers" {
    // Pointers cannot be null. If you want a null pointer, use the optional
    // prefix `?` to make the pointer type optional.
    var ptr: ?*i32 = null;

    var x: i32 = 1;
    ptr = &amp;x;

    try expect(ptr.?.* == 1);

    // Optional pointers are the same size as normal pointers, because pointer
    // value 0 is used as the null value.
    try expect(@sizeOf(?*i32) == @sizeOf(*i32));
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_optional_pointer.zig</kbd>
1/1 test.optional pointers... OK
All 1 tests passed.
</samp></pre></figure> <h2 id="Casting">Casting </h2> <p> A <strong>type cast</strong> converts a value of one type to another. Zig has <a href="#Type-Coercion">Type Coercion</a> for conversions that are known to be completely safe and unambiguous, and <a href="#Explicit-Casts">Explicit Casts</a> for conversions that one would not want to happen on accident. There is also a third kind of type conversion called <a href="#Peer-Type-Resolution">Peer Type Resolution</a> for the case when a result type must be decided given multiple operand types. </p> <h3 id="Type-Coercion">Type Coercion </h3> <p> Type coercion occurs when one type is expected, but different type is provided: </p> <figure><figcaption class="zig-cap"><cite class="file">test_type_coercion.zig</cite></figcaption><pre data-language="zig">test "type coercion - variable declaration" {
    var a: u8 = 1;
    var b: u16 = a;
    _ = b;
}

test "type coercion - function call" {
    var a: u8 = 1;
    foo(a);
}

fn foo(b: u16) void {
    _ = b;
}

test "type coercion - @as builtin" {
    var a: u8 = 1;
    var b = @as(u16, a);
    _ = b;
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_type_coercion.zig</kbd>
1/3 test.type coercion - variable declaration... OK
2/3 test.type coercion - function call... OK
3/3 test.type coercion - @as builtin... OK
All 3 tests passed.
</samp></pre></figure> <p> Type coercions are only allowed when it is completely unambiguous how to get from one type to another, and the transformation is guaranteed to be safe. There is one exception, which is <a href="#C-Pointers">C Pointers</a>. </p> <h4 id="Type-Coercion-Stricter-Qualification">
<a href="#toc-Type-Coercion-Stricter-Qualification">Type Coercion: Stricter Qualification</a> </h4> <p> Values which have the same representation at runtime can be cast to increase the strictness of the qualifiers, no matter how nested the qualifiers are: </p> <ul> <li>
<code><span class="line"><span class="tok-kw">const</span></span></code> - non-const to const is allowed</li> <li>
<code><span class="line"><span class="tok-kw">volatile</span></span></code> - non-volatile to volatile is allowed</li> <li>
<code><span class="line"><span class="tok-kw">align</span></span></code> - bigger to smaller alignment is allowed </li> <li>
<a href="#Error-Set-Type">error sets</a> to supersets is allowed</li> </ul> <p> These casts are no-ops at runtime since the value representation does not change. </p> <figure><figcaption class="zig-cap"><cite class="file">test_no_op_casts.zig</cite></figcaption><pre data-language="zig">test "type coercion - const qualification" {
    var a: i32 = 1;
    var b: *i32 = &amp;a;
    foo(b);
}

fn foo(_: *const i32) void {}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_no_op_casts.zig</kbd>
1/1 test.type coercion - const qualification... OK
All 1 tests passed.
</samp></pre></figure> <p> In addition, pointers coerce to const optional pointers: </p> <figure><figcaption class="zig-cap"><cite class="file">test_pointer_coerce_const_optional.zig</cite></figcaption><pre data-language="zig">const std = @import("std");
const expect = std.testing.expect;
const mem = std.mem;

test "cast *[1][*]const u8 to [*]const ?[*]const u8" {
    const window_name = [1][*]const u8{"window name"};
    const x: [*]const ?[*]const u8 = &amp;window_name;
    try expect(mem.eql(u8, std.mem.sliceTo(@as([*:0]const u8, @ptrCast(x[0].?)), 0), "window name"));
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_pointer_coerce_const_optional.zig</kbd>
1/1 test.cast *[1][*]const u8 to [*]const ?[*]const u8... OK
All 1 tests passed.
</samp></pre></figure> <h4 id="Type-Coercion-Integer-and-Float-Widening">
<a href="#toc-Type-Coercion-Integer-and-Float-Widening">Type Coercion: Integer and Float Widening</a> </h4> <p> <a href="#Integers">Integers</a> coerce to integer types which can represent every value of the old type, and likewise <a href="#Floats">Floats</a> coerce to float types which can represent every value of the old type. </p> <figure><figcaption class="zig-cap"><cite class="file">test_integer_widening.zig</cite></figcaption><pre data-language="zig">const std = @import("std");
const builtin = @import("builtin");
const expect = std.testing.expect;
const mem = std.mem;

test "integer widening" {
    var a: u8 = 250;
    var b: u16 = a;
    var c: u32 = b;
    var d: u64 = c;
    var e: u64 = d;
    var f: u128 = e;
    try expect(f == a);
}

test "implicit unsigned integer to signed integer" {
    var a: u8 = 250;
    var b: i16 = a;
    try expect(b == 250);
}

test "float widening" {
    var a: f16 = 12.34;
    var b: f32 = a;
    var c: f64 = b;
    var d: f128 = c;
    try expect(d == a);
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_integer_widening.zig</kbd>
1/3 test.integer widening... OK
2/3 test.implicit unsigned integer to signed integer... OK
3/3 test.float widening... OK
All 3 tests passed.
</samp></pre></figure> <h4 id="Type-Coercion-Float-to-Int">
<a href="#toc-Type-Coercion-Float-to-Int">Type Coercion: Float to Int</a> </h4> <p> A compiler error is appropriate because this ambiguous expression leaves the compiler two choices about the coercion. </p> <ul> <li>Cast <code><span class="line"><span class="tok-number">54.0</span></span></code> to <code><span class="line"><span class="tok-type">comptime_int</span></span></code> resulting in <code><span class="line"><span class="tok-builtin">@as</span>(<span class="tok-type">comptime_int</span>, <span class="tok-number">10</span>)</span></code>, which is casted to <code><span class="line"><span class="tok-builtin">@as</span>(<span class="tok-type">f32</span>, <span class="tok-number">10</span>)</span></code>
</li> <li>Cast <code><span class="line"><span class="tok-number">5</span></span></code> to <code><span class="line"><span class="tok-type">comptime_float</span></span></code> resulting in <code><span class="line"><span class="tok-builtin">@as</span>(<span class="tok-type">comptime_float</span>, <span class="tok-number">10.8</span>)</span></code>, which is casted to <code><span class="line"><span class="tok-builtin">@as</span>(<span class="tok-type">f32</span>, <span class="tok-number">10.8</span>)</span></code>
</li> </ul> <figure><figcaption class="zig-cap"><cite class="file">test_ambiguous_coercion.zig</cite></figcaption><pre data-language="zig">// Compile time coercion of float to int
test "implicit cast to comptime_int" {
    var f: f32 = 54.0 / 5;
    _ = f;
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_ambiguous_coercion.zig</kbd>
<span class="sgr-1m">docgen_tmp/test_ambiguous_coercion.zig:3:23: </span><span class="sgr-31m">error: </span><span class="sgr-1m">ambiguous coercion of division operands 'comptime_float' and 'comptime_int'; non-zero remainder '4'
</span>    var f: f32 = 54.0 / 5;
                 <span class="sgr-32m">~~~~~^~~
</span>
</samp></pre></figure> <h4 id="Type-Coercion-Slices-Arrays-and-Pointers">
<a href="#toc-Type-Coercion-Slices-Arrays-and-Pointers">Type Coercion: Slices, Arrays and Pointers</a> </h4> <figure><figcaption class="zig-cap"><cite class="file">test_coerce_slices_arrays_and_pointers.zig</cite></figcaption><pre data-language="zig">const std = @import("std");
const expect = std.testing.expect;

// You can assign constant pointers to arrays to a slice with
// const modifier on the element type. Useful in particular for
// String literals.
test "*const [N]T to []const T" {
    var x1: []const u8 = "hello";
    var x2: []const u8 = &amp;[5]u8{ 'h', 'e', 'l', 'l', 111 };
    try expect(std.mem.eql(u8, x1, x2));

    var y: []const f32 = &amp;[2]f32{ 1.2, 3.4 };
    try expect(y[0] == 1.2);
}

// Likewise, it works when the destination type is an error union.
test "*const [N]T to E![]const T" {
    var x1: anyerror![]const u8 = "hello";
    var x2: anyerror![]const u8 = &amp;[5]u8{ 'h', 'e', 'l', 'l', 111 };
    try expect(std.mem.eql(u8, try x1, try x2));

    var y: anyerror![]const f32 = &amp;[2]f32{ 1.2, 3.4 };
    try expect((try y)[0] == 1.2);
}

// Likewise, it works when the destination type is an optional.
test "*const [N]T to ?[]const T" {
    var x1: ?[]const u8 = "hello";
    var x2: ?[]const u8 = &amp;[5]u8{ 'h', 'e', 'l', 'l', 111 };
    try expect(std.mem.eql(u8, x1.?, x2.?));

    var y: ?[]const f32 = &amp;[2]f32{ 1.2, 3.4 };
    try expect(y.?[0] == 1.2);
}

// In this cast, the array length becomes the slice length.
test "*[N]T to []T" {
    var buf: [5]u8 = "hello".*;
    const x: []u8 = &amp;buf;
    try expect(std.mem.eql(u8, x, "hello"));

    const buf2 = [2]f32{ 1.2, 3.4 };
    const x2: []const f32 = &amp;buf2;
    try expect(std.mem.eql(f32, x2, &amp;[2]f32{ 1.2, 3.4 }));
}

// Single-item pointers to arrays can be coerced to many-item pointers.
test "*[N]T to [*]T" {
    var buf: [5]u8 = "hello".*;
    const x: [*]u8 = &amp;buf;
    try expect(x[4] == 'o');
    // x[5] would be an uncaught out of bounds pointer dereference!
}

// Likewise, it works when the destination type is an optional.
test "*[N]T to ?[*]T" {
    var buf: [5]u8 = "hello".*;
    const x: ?[*]u8 = &amp;buf;
    try expect(x.?[4] == 'o');
}

// Single-item pointers can be cast to len-1 single-item arrays.
test "*T to *[1]T" {
    var x: i32 = 1234;
    const y: *[1]i32 = &amp;x;
    const z: [*]i32 = y;
    try expect(z[0] == 1234);
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_coerce_slices_arrays_and_pointers.zig</kbd>
1/7 test.*const [N]T to []const T... OK
2/7 test.*const [N]T to E![]const T... OK
3/7 test.*const [N]T to ?[]const T... OK
4/7 test.*[N]T to []T... OK
5/7 test.*[N]T to [*]T... OK
6/7 test.*[N]T to ?[*]T... OK
7/7 test.*T to *[1]T... OK
All 7 tests passed.
</samp></pre></figure> <p>See also:</p>
<ul> <li><a href="#C-Pointers">C Pointers</a></li> </ul> <h4 id="Type-Coercion-Optionals">
<a href="#toc-Type-Coercion-Optionals">Type Coercion: Optionals</a> </h4> <p> The payload type of <a href="#Optionals">Optionals</a>, as well as <a href="#null">null</a>, coerce to the optional type. </p> <figure><figcaption class="zig-cap"><cite class="file">test_coerce_optionals.zig</cite></figcaption><pre data-language="zig">const std = @import("std");
const expect = std.testing.expect;

test "coerce to optionals" {
    const x: ?i32 = 1234;
    const y: ?i32 = null;

    try expect(x.? == 1234);
    try expect(y == null);
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_coerce_optionals.zig</kbd>
1/1 test.coerce to optionals... OK
All 1 tests passed.
</samp></pre></figure> <p>It works nested inside the <a href="#Error-Union-Type">Error Union Type</a>, too:</p> <figure><figcaption class="zig-cap"><cite class="file">test_coerce_optional_wrapped_error_union.zig</cite></figcaption><pre data-language="zig">const std = @import("std");
const expect = std.testing.expect;

test "coerce to optionals wrapped in error union" {
    const x: anyerror!?i32 = 1234;
    const y: anyerror!?i32 = null;

    try expect((try x).? == 1234);
    try expect((try y) == null);
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_coerce_optional_wrapped_error_union.zig</kbd>
1/1 test.coerce to optionals wrapped in error union... OK
All 1 tests passed.
</samp></pre></figure> <h4 id="Type-Coercion-Error-Unions">
<a href="#toc-Type-Coercion-Error-Unions">Type Coercion: Error Unions</a> </h4> <p>The payload type of an <a href="#Error-Union-Type">Error Union Type</a> as well as the <a href="#Error-Set-Type">Error Set Type</a> coerce to the error union type: </p> <figure><figcaption class="zig-cap"><cite class="file">test_coerce_to_error_union.zig</cite></figcaption><pre data-language="zig">const std = @import("std");
const expect = std.testing.expect;

test "coercion to error unions" {
    const x: anyerror!i32 = 1234;
    const y: anyerror!i32 = error.Failure;

    try expect((try x) == 1234);
    try std.testing.expectError(error.Failure, y);
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_coerce_to_error_union.zig</kbd>
1/1 test.coercion to error unions... OK
All 1 tests passed.
</samp></pre></figure> <h4 id="Type-Coercion-Compile-Time-Known-Numbers">
<a href="#toc-Type-Coercion-Compile-Time-Known-Numbers">Type Coercion: Compile-Time Known Numbers</a> </h4> <p>When a number is <a href="#comptime">comptime</a>-known to be representable in the destination type, it may be coerced: </p> <figure><figcaption class="zig-cap"><cite class="file">test_coerce_large_to_small.zig</cite></figcaption><pre data-language="zig">const std = @import("std");
const expect = std.testing.expect;

test "coercing large integer type to smaller one when value is comptime-known to fit" {
    const x: u64 = 255;
    const y: u8 = x;
    try expect(y == 255);
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_coerce_large_to_small.zig</kbd>
1/1 test.coercing large integer type to smaller one when value is comptime-known to fit... OK
All 1 tests passed.
</samp></pre></figure> <h4 id="Type-Coercion-Unions-and-Enums">
<a href="#toc-Type-Coercion-Unions-and-Enums">Type Coercion: Unions and Enums</a> </h4> <p>Tagged unions can be coerced to enums, and enums can be coerced to tagged unions when they are <a href="#comptime">comptime</a>-known to be a field of the union that has only one possible value, such as <a href="#void">void</a>: </p> <figure><figcaption class="zig-cap"><cite class="file">test_coerce_unions_enums.zig</cite></figcaption><pre data-language="zig">const std = @import("std");
const expect = std.testing.expect;

const E = enum {
    one,
    two,
    three,
};

const U = union(E) {
    one: i32,
    two: f32,
    three,
};

const U2 = union(enum) {
    a: void,
    b: f32,

    fn tag(self: U2) usize {
        switch (self) {
            .a =&gt; return 1,
            .b =&gt; return 2,
        }
    }
};

test "coercion between unions and enums" {
    var u = U{ .two = 12.34 };
    var e: E = u; // coerce union to enum
    try expect(e == E.two);

    const three = E.three;
    var u_2: U = three; // coerce enum to union
    try expect(u_2 == E.three);

    var u_3: U = .three; // coerce enum literal to union
    try expect(u_3 == E.three);

    var u_4: U2 = .a; // coerce enum literal to union with inferred enum tag type.
    try expect(u_4.tag() == 1);

    // The following example is invalid.
    // error: coercion from enum '@TypeOf(.enum_literal)' to union 'test_coerce_unions_enum.U2' must initialize 'f32' field 'b'
    //var u_5: U2 = .b;
    //try expect(u_5.tag() == 2);
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_coerce_unions_enums.zig</kbd>
1/1 test.coercion between unions and enums... OK
All 1 tests passed.
</samp></pre></figure> <p>See also:</p>
<ul> <li><a href="#union">union</a></li> <li><a href="#enum">enum</a></li> </ul> <h4 id="Type-Coercion-undefined">
<a href="#toc-Type-Coercion-undefined">Type Coercion: undefined</a> </h4> <p><a href="#undefined">undefined</a> can be coerced to any type.</p> <h4 id="Type-Coercion-Tuples-to-Arrays">
<a href="#toc-Type-Coercion-Tuples-to-Arrays">Type Coercion: Tuples to Arrays</a> </h4> <p><a href="#Tuples">Tuples</a> can be coerced to arrays, if all of the fields have the same type.</p> <figure><figcaption class="zig-cap"><cite class="file">test_coerce_tuples_arrays.zig</cite></figcaption><pre data-language="zig">const std = @import("std");
const expect = std.testing.expect;

const Tuple = struct{ u8, u8 };
test "coercion from homogenous tuple to array" {
	const tuple: Tuple = .{5, 6};
	const array: [2]u8 = tuple;
	_ = array;
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_coerce_tuples_arrays.zig</kbd>
1/1 test.coercion from homogenous tuple to array... OK
All 1 tests passed.
</samp></pre></figure> <h3 id="Explicit-Casts">Explicit Casts </h3> <p> Explicit casts are performed via <a href="#Builtin-Functions">Builtin Functions</a>. Some explicit casts are safe; some are not. Some explicit casts perform language-level assertions; some do not. Some explicit casts are no-ops at runtime; some are not. </p> <ul> <li>
<a href="#bitCast">@bitCast</a> - change type but maintain bit representation</li> <li>
<a href="#alignCast">@alignCast</a> - make a pointer have more alignment</li> <li>
<a href="#enumFromInt">@enumFromInt</a> - obtain an enum value based on its integer tag value</li> <li>
<a href="#errorFromInt">@errorFromInt</a> - obtain an error code based on its integer value</li> <li>
<a href="#errSetCast">@errSetCast</a> - convert to a smaller error set</li> <li>
<a href="#floatCast">@floatCast</a> - convert a larger float to a smaller float</li> <li>
<a href="#floatFromInt">@floatFromInt</a> - convert an integer to a float value</li> <li>
<a href="#intCast">@intCast</a> - convert between integer types</li> <li>
<a href="#intFromBool">@intFromBool</a> - convert true to 1 and false to 0</li> <li>
<a href="#intFromEnum">@intFromEnum</a> - obtain the integer tag value of an enum or tagged union</li> <li>
<a href="#intFromError">@intFromError</a> - obtain the integer value of an error code</li> <li>
<a href="#intFromFloat">@intFromFloat</a> - obtain the integer part of a float value</li> <li>
<a href="#intFromPtr">@intFromPtr</a> - obtain the address of a pointer</li> <li>
<a href="#ptrFromInt">@ptrFromInt</a> - convert an address to a pointer</li> <li>
<a href="#ptrCast">@ptrCast</a> - convert between pointer types</li> <li>
<a href="#truncate">@truncate</a> - convert between integer types, chopping off bits</li> </ul> <h3 id="Peer-Type-Resolution">Peer Type Resolution </h3> <p>Peer Type Resolution occurs in these places:</p> <ul> <li>
<a href="#switch">switch</a> expressions</li> <li>
<a href="#if">if</a> expressions</li> <li>
<a href="#while">while</a> expressions</li> <li>
<a href="#for">for</a> expressions</li> <li>Multiple break statements in a block</li> <li>Some <a href="#Table-of-Operators">binary operations</a>
</li> </ul> <p> This kind of type resolution chooses a type that all peer types can coerce into. Here are some examples: </p> <figure><figcaption class="zig-cap"><cite class="file">test_peer_type_resolution.zig</cite></figcaption><pre data-language="zig">const std = @import("std");
const expect = std.testing.expect;
const mem = std.mem;

test "peer resolve int widening" {
    var a: i8 = 12;
    var b: i16 = 34;
    var c = a + b;
    try expect(c == 46);
    try expect(@TypeOf(c) == i16);
}

test "peer resolve arrays of different size to const slice" {
    try expect(mem.eql(u8, boolToStr(true), "true"));
    try expect(mem.eql(u8, boolToStr(false), "false"));
    try comptime expect(mem.eql(u8, boolToStr(true), "true"));
    try comptime expect(mem.eql(u8, boolToStr(false), "false"));
}
fn boolToStr(b: bool) []const u8 {
    return if (b) "true" else "false";
}

test "peer resolve array and const slice" {
    try testPeerResolveArrayConstSlice(true);
    try comptime testPeerResolveArrayConstSlice(true);
}
fn testPeerResolveArrayConstSlice(b: bool) !void {
    const value1 = if (b) "aoeu" else @as([]const u8, "zz");
    const value2 = if (b) @as([]const u8, "zz") else "aoeu";
    try expect(mem.eql(u8, value1, "aoeu"));
    try expect(mem.eql(u8, value2, "zz"));
}

test "peer type resolution: ?T and T" {
    try expect(peerTypeTAndOptionalT(true, false).? == 0);
    try expect(peerTypeTAndOptionalT(false, false).? == 3);
    comptime {
        try expect(peerTypeTAndOptionalT(true, false).? == 0);
        try expect(peerTypeTAndOptionalT(false, false).? == 3);
    }
}
fn peerTypeTAndOptionalT(c: bool, b: bool) ?usize {
    if (c) {
        return if (b) null else @as(usize, 0);
    }

    return @as(usize, 3);
}

test "peer type resolution: *[0]u8 and []const u8" {
    try expect(peerTypeEmptyArrayAndSlice(true, "hi").len == 0);
    try expect(peerTypeEmptyArrayAndSlice(false, "hi").len == 1);
    comptime {
        try expect(peerTypeEmptyArrayAndSlice(true, "hi").len == 0);
        try expect(peerTypeEmptyArrayAndSlice(false, "hi").len == 1);
    }
}
fn peerTypeEmptyArrayAndSlice(a: bool, slice: []const u8) []const u8 {
    if (a) {
        return &amp;[_]u8{};
    }

    return slice[0..1];
}
test "peer type resolution: *[0]u8, []const u8, and anyerror![]u8" {
    {
        var data = "hi".*;
        const slice = data[0..];
        try expect((try peerTypeEmptyArrayAndSliceAndError(true, slice)).len == 0);
        try expect((try peerTypeEmptyArrayAndSliceAndError(false, slice)).len == 1);
    }
    comptime {
        var data = "hi".*;
        const slice = data[0..];
        try expect((try peerTypeEmptyArrayAndSliceAndError(true, slice)).len == 0);
        try expect((try peerTypeEmptyArrayAndSliceAndError(false, slice)).len == 1);
    }
}
fn peerTypeEmptyArrayAndSliceAndError(a: bool, slice: []u8) anyerror![]u8 {
    if (a) {
        return &amp;[_]u8{};
    }

    return slice[0..1];
}

test "peer type resolution: *const T and ?*T" {
    const a: *const usize = @ptrFromInt(0x123456780);
    const b: ?*usize = @ptrFromInt(0x123456780);
    try expect(a == b);
    try expect(b == a);
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_peer_type_resolution.zig</kbd>
1/7 test.peer resolve int widening... OK
2/7 test.peer resolve arrays of different size to const slice... OK
3/7 test.peer resolve array and const slice... OK
4/7 test.peer type resolution: ?T and T... OK
5/7 test.peer type resolution: *[0]u8 and []const u8... OK
6/7 test.peer type resolution: *[0]u8, []const u8, and anyerror![]u8... OK
7/7 test.peer type resolution: *const T and ?*T... OK
All 7 tests passed.
</samp></pre></figure> <h2 id="Zero-Bit-Types">Zero Bit Types </h2> <p>For some types, <a href="#sizeOf">@sizeOf</a> is 0:</p> <ul> <li><a href="#void">void</a></li> <li>The <a href="#Integers">Integers</a> <code><span class="line"><span class="tok-type">u0</span></span></code> and <code><span class="line"><span class="tok-type">i0</span></span></code>.</li> <li>
<a href="#Arrays">Arrays</a> and <a href="#Vectors">Vectors</a> with len 0, or with an element type that is a zero bit type.</li> <li>An <a href="#enum">enum</a> with only 1 tag.</li> <li>A <a href="#struct">struct</a> with all fields being zero bit types.</li> <li>A <a href="#union">union</a> with only 1 field which is a zero bit type.</li> </ul> <p> These types can only ever have one possible value, and thus require 0 bits to represent. Code that makes use of these types is not included in the final generated code: </p> <figure><figcaption class="zig-cap"><cite class="file">zero_bit_types.zig</cite></figcaption><pre data-language="zig">export fn entry() void {
    var x: void = {};
    var y: void = {};
    x = y;
}</pre></figure> <p>When this turns into machine code, there is no code generated in the body of <code><span class="line">entry</span></code>, even in <a href="#Debug">Debug</a> mode. For example, on x86_64:</p> <pre data-language="zig">0000000000000010 &lt;entry&gt;:
  10:	55                   	push   %rbp
  11:	48 89 e5             	mov    %rsp,%rbp
  14:	5d                   	pop    %rbp
  15:	c3                   	retq   </pre> <p>These assembly instructions do not have any code associated with the void values - they only perform the function call prologue and epilogue.</p> <h3 id="void">void </h3> <p> <code><span class="line"><span class="tok-type">void</span></span></code> can be useful for instantiating generic types. For example, given a <code><span class="line">Map(Key, Value)</span></code>, one can pass <code><span class="line"><span class="tok-type">void</span></span></code> for the <code><span class="line">Value</span></code> type to make it into a <code><span class="line">Set</span></code>: </p> <figure><figcaption class="zig-cap"><cite class="file">test_void_in_hashmap.zig</cite></figcaption><pre data-language="zig">const std = @import("std");
const expect = std.testing.expect;

test "turn HashMap into a set with void" {
    var map = std.AutoHashMap(i32, void).init(std.testing.allocator);
    defer map.deinit();

    try map.put(1, {});
    try map.put(2, {});

    try expect(map.contains(2));
    try expect(!map.contains(3));

    _ = map.remove(2);
    try expect(!map.contains(2));
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_void_in_hashmap.zig</kbd>
1/1 test.turn HashMap into a set with void... OK
All 1 tests passed.
</samp></pre></figure> <p>Note that this is different from using a dummy value for the hash map value. By using <code><span class="line"><span class="tok-type">void</span></span></code> as the type of the value, the hash map entry type has no value field, and thus the hash map takes up less space. Further, all the code that deals with storing and loading the value is deleted, as seen above. </p> <p> <code><span class="line"><span class="tok-type">void</span></span></code> is distinct from <code><span class="line"><span class="tok-type">anyopaque</span></span></code>. <code><span class="line"><span class="tok-type">void</span></span></code> has a known size of 0 bytes, and <code><span class="line"><span class="tok-type">anyopaque</span></span></code> has an unknown, but non-zero, size. </p> <p> Expressions of type <code><span class="line"><span class="tok-type">void</span></span></code> are the only ones whose value can be ignored. For example: </p> <figure><figcaption class="zig-cap"><cite class="file">test_expression_ignored.zig</cite></figcaption><pre data-language="zig">test "ignoring expression value" {
    foo();
}

fn foo() i32 {
    return 1234;
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_expression_ignored.zig</kbd>
<span class="sgr-1m">docgen_tmp/test_expression_ignored.zig:2:8: </span><span class="sgr-31m">error: </span><span class="sgr-1m">value of type 'i32' ignored
</span>    foo();
    <span class="sgr-32m">~~~^~
</span><span class="sgr-1m">docgen_tmp/test_expression_ignored.zig:2:8: </span><span class="sgr-36m">note: </span><span class="sgr-1m">all non-void values must be used
</span><span class="sgr-1m">docgen_tmp/test_expression_ignored.zig:2:8: </span><span class="sgr-36m">note: </span><span class="sgr-1m">this error can be suppressed by assigning the value to '_'
</span>
</samp></pre></figure> <p>However, if the expression has type <code><span class="line"><span class="tok-type">void</span></span></code>, there will be no error. Function return values can also be explicitly ignored by assigning them to <code><span class="line">_</span></code>. </p> <figure><figcaption class="zig-cap"><cite class="file">test_void_ignored.zig</cite></figcaption><pre data-language="zig">test "void is ignored" {
    returnsVoid();
}

test "explicitly ignoring expression value" {
    _ = foo();
}

fn returnsVoid() void {}

fn foo() i32 {
    return 1234;
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_void_ignored.zig</kbd>
1/2 test.void is ignored... OK
2/2 test.explicitly ignoring expression value... OK
All 2 tests passed.
</samp></pre></figure> <h2 id="Result-Location-Semantics">Result Location Semantics </h2> <p> <a href="https://github.com/ziglang/zig/issues/2809">TODO add documentation for this</a> </p> <h2 id="usingnamespace">usingnamespace </h2> <p> <code><span class="line"><span class="tok-kw">usingnamespace</span></span></code> is a declaration that mixes all the public declarations of the operand, which must be a <a href="#struct">struct</a>, <a href="#union">union</a>, <a href="#enum">enum</a>, or <a href="#opaque">opaque</a>, into the namespace: </p> <figure><figcaption class="zig-cap"><cite class="file">test_usingnamespace.zig</cite></figcaption><pre data-language="zig">test "using std namespace" {
    const S = struct {
        usingnamespace @import("std");
    };
    try S.testing.expect(true);
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_usingnamespace.zig</kbd>
1/1 test.using std namespace... OK
All 1 tests passed.
</samp></pre></figure> <p> <code><span class="line"><span class="tok-kw">usingnamespace</span></span></code> has an important use case when organizing the public API of a file or package. For example, one might have <code class="file">c.zig</code> with all of the <a href="#Import-from-C-Header-File">C imports</a>: </p> <figure><figcaption class="zig-cap"><cite class="file">c.zig</cite></figcaption><pre data-language="zig">pub usingnamespace @cImport({
    @cInclude("epoxy/gl.h");
    @cInclude("GLFW/glfw3.h");
    @cDefine("STBI_ONLY_PNG", "");
    @cDefine("STBI_NO_STDIO", "");
    @cInclude("stb_image.h");
});</pre></figure> <p> The above example demonstrates using <code><span class="line"><span class="tok-kw">pub</span></span></code> to qualify the <code><span class="line"><span class="tok-kw">usingnamespace</span></span></code> additionally makes the imported declarations <code><span class="line"><span class="tok-kw">pub</span></span></code>. This can be used to forward declarations, giving precise control over what declarations a given file exposes. </p> <h2 id="comptime">comptime </h2> <p> Zig places importance on the concept of whether an expression is known at compile-time. There are a few different places this concept is used, and these building blocks are used to keep the language small, readable, and powerful. </p> <h3 id="Introducing-the-Compile-Time-Concept">Introducing the Compile-Time Concept </h3> <h4 id="Compile-Time-Parameters">
<a href="#toc-Compile-Time-Parameters">Compile-Time Parameters</a> </h4> <p> Compile-time parameters is how Zig implements generics. It is compile-time duck typing. </p> <figure><figcaption class="zig-cap"><cite class="file">compile-time_duck_typing.zig</cite></figcaption><pre data-language="zig">fn max(comptime T: type, a: T, b: T) T {
    return if (a &gt; b) a else b;
}
fn gimmeTheBiggerFloat(a: f32, b: f32) f32 {
    return max(f32, a, b);
}
fn gimmeTheBiggerInteger(a: u64, b: u64) u64 {
    return max(u64, a, b);
}</pre></figure> <p> In Zig, types are first-class citizens. They can be assigned to variables, passed as parameters to functions, and returned from functions. However, they can only be used in expressions which are known at <em>compile-time</em>, which is why the parameter <code><span class="line">T</span></code> in the above snippet must be marked with <code><span class="line"><span class="tok-kw">comptime</span></span></code>. </p> <p> A <code><span class="line"><span class="tok-kw">comptime</span></span></code> parameter means that: </p> <ul> <li>At the callsite, the value must be known at compile-time, or it is a compile error.</li> <li>In the function definition, the value is known at compile-time.</li> </ul> <p> For example, if we were to introduce another function to the above snippet: </p> <figure><figcaption class="zig-cap"><cite class="file">test_unresolved_comptime_value.zig</cite></figcaption><pre data-language="zig">fn max(comptime T: type, a: T, b: T) T {
    return if (a &gt; b) a else b;
}
test "try to pass a runtime type" {
    foo(false);
}
fn foo(condition: bool) void {
    const result = max(
        if (condition) f32 else u64,
        1234,
        5678);
    _ = result;
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_unresolved_comptime_value.zig</kbd>
<span class="sgr-1m">docgen_tmp/test_unresolved_comptime_value.zig:9:13: </span><span class="sgr-31m">error: </span><span class="sgr-1m">unable to resolve comptime value
</span>        if (condition) f32 else u64,
            <span class="sgr-32m">^~~~~~~~~
</span><span class="sgr-1m">docgen_tmp/test_unresolved_comptime_value.zig:9:13: </span><span class="sgr-36m">note: </span><span class="sgr-1m">condition in comptime branch must be comptime-known
</span><span class="sgr-2m">referenced by:
    test.try to pass a runtime type: docgen_tmp/test_unresolved_comptime_value.zig:5:5
    remaining reference traces hidden; use '-freference-trace' to see all reference traces
</span>
</samp></pre></figure> <p> This is an error because the programmer attempted to pass a value only known at run-time to a function which expects a value known at compile-time. </p> <p> Another way to get an error is if we pass a type that violates the type checker when the function is analyzed. This is what it means to have <em>compile-time duck typing</em>. </p> <p> For example: </p> <figure><figcaption class="zig-cap"><cite class="file">test_comptime_mismatched_type.zig</cite></figcaption><pre data-language="zig">fn max(comptime T: type, a: T, b: T) T {
    return if (a &gt; b) a else b;
}
test "try to compare bools" {
    _ = max(bool, true, false);
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_comptime_mismatched_type.zig</kbd>
<span class="sgr-1m">docgen_tmp/test_comptime_mismatched_type.zig:2:18: </span><span class="sgr-31m">error: </span><span class="sgr-1m">operator &gt; not allowed for type 'bool'
</span>    return if (a &gt; b) a else b;
               <span class="sgr-32m">~~^~~
</span>
</samp></pre></figure> <p> On the flip side, inside the function definition with the <code><span class="line"><span class="tok-kw">comptime</span></span></code> parameter, the value is known at compile-time. This means that we actually could make this work for the bool type if we wanted to: </p> <figure><figcaption class="zig-cap"><cite class="file">test_comptime_max_with_bool.zig</cite></figcaption><pre data-language="zig">fn max(comptime T: type, a: T, b: T) T {
    if (T == bool) {
        return a or b;
    } else if (a &gt; b) {
        return a;
    } else {
        return b;
    }
}
test "try to compare bools" {
    try @import("std").testing.expect(max(bool, false, true) == true);
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_comptime_max_with_bool.zig</kbd>
1/1 test.try to compare bools... OK
All 1 tests passed.
</samp></pre></figure> <p> This works because Zig implicitly inlines <code><span class="line"><span class="tok-kw">if</span></span></code> expressions when the condition is known at compile-time, and the compiler guarantees that it will skip analysis of the branch not taken. </p> <p> This means that the actual function generated for <code><span class="line">max</span></code> in this situation looks like this: </p> <figure><figcaption class="zig-cap"><cite class="file">compiler_generated_function.zig</cite></figcaption><pre data-language="zig">fn max(a: bool, b: bool) bool {
    return a or b;
}</pre></figure> <p> All the code that dealt with compile-time known values is eliminated and we are left with only the necessary run-time code to accomplish the task. </p> <p> This works the same way for <code><span class="line"><span class="tok-kw">switch</span></span></code> expressions - they are implicitly inlined when the target expression is compile-time known. </p> <h4 id="Compile-Time-Variables">
<a href="#toc-Compile-Time-Variables">Compile-Time Variables</a> </h4> <p> In Zig, the programmer can label variables as <code><span class="line"><span class="tok-kw">comptime</span></span></code>. This guarantees to the compiler that every load and store of the variable is performed at compile-time. Any violation of this results in a compile error. </p> <p> This combined with the fact that we can <code><span class="line"><span class="tok-kw">inline</span></span></code> loops allows us to write a function which is partially evaluated at compile-time and partially at run-time. </p> <p> For example: </p> <figure><figcaption class="zig-cap"><cite class="file">test_comptime_evaluation.zig</cite></figcaption><pre data-language="zig">const expect = @import("std").testing.expect;

const CmdFn = struct {
    name: []const u8,
    func: fn(i32) i32,
};

const cmd_fns = [_]CmdFn{
    CmdFn {.name = "one", .func = one},
    CmdFn {.name = "two", .func = two},
    CmdFn {.name = "three", .func = three},
};
fn one(value: i32) i32 { return value + 1; }
fn two(value: i32) i32 { return value + 2; }
fn three(value: i32) i32 { return value + 3; }

fn performFn(comptime prefix_char: u8, start_value: i32) i32 {
    var result: i32 = start_value;
    comptime var i = 0;
    inline while (i &lt; cmd_fns.len) : (i += 1) {
        if (cmd_fns[i].name[0] == prefix_char) {
            result = cmd_fns[i].func(result);
        }
    }
    return result;
}

test "perform fn" {
    try expect(performFn('t', 1) == 6);
    try expect(performFn('o', 0) == 1);
    try expect(performFn('w', 99) == 99);
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_comptime_evaluation.zig</kbd>
1/1 test.perform fn... OK
All 1 tests passed.
</samp></pre></figure> <p> This example is a bit contrived, because the compile-time evaluation component is unnecessary; this code would work fine if it was all done at run-time. But it does end up generating different code. In this example, the function <code><span class="line">performFn</span></code> is generated three different times, for the different values of <code><span class="line">prefix_char</span></code> provided: </p> <figure><figcaption class="zig-cap"><cite class="file">performFn_1</cite></figcaption><pre data-language="zig">// From the line:
// expect(performFn('t', 1) == 6);
fn performFn(start_value: i32) i32 {
    var result: i32 = start_value;
    result = two(result);
    result = three(result);
    return result;
}</pre></figure> <figure><figcaption class="zig-cap"><cite class="file">performFn_2</cite></figcaption><pre data-language="zig">// From the line:
// expect(performFn('o', 0) == 1);
fn performFn(start_value: i32) i32 {
    var result: i32 = start_value;
    result = one(result);
    return result;
}</pre></figure> <figure><figcaption class="zig-cap"><cite class="file">performFn_3</cite></figcaption><pre data-language="zig">// From the line:
// expect(performFn('w', 99) == 99);
fn performFn(start_value: i32) i32 {
    var result: i32 = start_value;
    return result;
}</pre></figure> <p> Note that this happens even in a debug build; in a release build these generated functions still pass through rigorous LLVM optimizations. The important thing to note, however, is not that this is a way to write more optimized code, but that it is a way to make sure that what <em>should</em> happen at compile-time, <em>does</em> happen at compile-time. This catches more errors and as demonstrated later in this article, allows expressiveness that in other languages requires using macros, generated code, or a preprocessor to accomplish. </p> <h4 id="Compile-Time-Expressions">
<a href="#toc-Compile-Time-Expressions">Compile-Time Expressions</a> </h4> <p> In Zig, it matters whether a given expression is known at compile-time or run-time. A programmer can use a <code><span class="line"><span class="tok-kw">comptime</span></span></code> expression to guarantee that the expression will be evaluated at compile-time. If this cannot be accomplished, the compiler will emit an error. For example: </p> <figure><figcaption class="zig-cap"><cite class="file">test_comptime_call_extern_function.zig</cite></figcaption><pre data-language="zig">extern fn exit() noreturn;

test "foo" {
    comptime {
        exit();
    }
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_comptime_call_extern_function.zig</kbd>
<span class="sgr-1m">docgen_tmp/test_comptime_call_extern_function.zig:5:13: </span><span class="sgr-31m">error: </span><span class="sgr-1m">comptime call of extern function
</span>        exit();
        <span class="sgr-32m">~~~~^~
</span>
</samp></pre></figure> <p> It doesn't make sense that a program could call <code><span class="line">exit()</span></code> (or any other external function) at compile-time, so this is a compile error. However, a <code><span class="line"><span class="tok-kw">comptime</span></span></code> expression does much more than sometimes cause a compile error. </p> <p> Within a <code><span class="line"><span class="tok-kw">comptime</span></span></code> expression: </p> <ul> <li>All variables are <code><span class="line"><span class="tok-kw">comptime</span></span></code> variables.</li> <li>All <code><span class="line"><span class="tok-kw">if</span></span></code>, <code><span class="line"><span class="tok-kw">while</span></span></code>, <code><span class="line"><span class="tok-kw">for</span></span></code>, and <code><span class="line"><span class="tok-kw">switch</span></span></code> expressions are evaluated at compile-time, or emit a compile error if this is not possible.</li> <li>All <code><span class="line"><span class="tok-kw">return</span></span></code> and <code><span class="line"><span class="tok-kw">try</span></span></code> expressions are invalid (unless the function itself is called at compile-time).</li> <li>All code with runtime side effects or depending on runtime values emits a compile error.</li> <li>All function calls cause the compiler to interpret the function at compile-time, emitting a compile error if the function tries to do something that has global runtime side effects.</li> </ul> <p> This means that a programmer can create a function which is called both at compile-time and run-time, with no modification to the function required. </p> <p> Let's look at an example: </p> <figure><figcaption class="zig-cap"><cite class="file">test_fibonacci_recursion.zig</cite></figcaption><pre data-language="zig">const expect = @import("std").testing.expect;

fn fibonacci(index: u32) u32 {
    if (index &lt; 2) return index;
    return fibonacci(index - 1) + fibonacci(index - 2);
}

test "fibonacci" {
    // test fibonacci at run-time
    try expect(fibonacci(7) == 13);

    // test fibonacci at compile-time
    try comptime expect(fibonacci(7) == 13);
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_fibonacci_recursion.zig</kbd>
1/1 test.fibonacci... OK
All 1 tests passed.
</samp></pre></figure> <p> Imagine if we had forgotten the base case of the recursive function and tried to run the tests: </p> <figure><figcaption class="zig-cap"><cite class="file">test_fibonacci_comptime_overflow.zig</cite></figcaption><pre data-language="zig">const expect = @import("std").testing.expect;

fn fibonacci(index: u32) u32 {
    //if (index &lt; 2) return index;
    return fibonacci(index - 1) + fibonacci(index - 2);
}

test "fibonacci" {
    try comptime expect(fibonacci(7) == 13);
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_fibonacci_comptime_overflow.zig</kbd>
<span class="sgr-1m">docgen_tmp/test_fibonacci_comptime_overflow.zig:5:28: </span><span class="sgr-31m">error: </span><span class="sgr-1m">overflow of integer type 'u32' with value '-1'
</span>    return fibonacci(index - 1) + fibonacci(index - 2);
                     <span class="sgr-32m">~~~~~~^~~
</span><span class="sgr-1m">docgen_tmp/test_fibonacci_comptime_overflow.zig:5:21: </span><span class="sgr-36m">note: </span><span class="sgr-1m">called from here</span><span class="sgr-2m"> (7 times)
</span>    return fibonacci(index - 1) + fibonacci(index - 2);
           <span class="sgr-32m">~~~~~~~~~^~~~~~~~~~~
</span><span class="sgr-1m">docgen_tmp/test_fibonacci_comptime_overflow.zig:9:34: </span><span class="sgr-36m">note: </span><span class="sgr-1m">called from here
</span>    try comptime expect(fibonacci(7) == 13);
                        <span class="sgr-32m">~~~~~~~~~^~~
</span>
</samp></pre></figure> <p> The compiler produces an error which is a stack trace from trying to evaluate the function at compile-time. </p> <p> Luckily, we used an unsigned integer, and so when we tried to subtract 1 from 0, it triggered undefined behavior, which is always a compile error if the compiler knows it happened. But what would have happened if we used a signed integer? </p> <figure><figcaption class="zig-cap"><cite class="file">fibonacci_comptime_infinite_recursion.zig</cite></figcaption><pre data-language="zig">const assert = @import("std").debug.assert;

fn fibonacci(index: i32) i32 {
    //if (index &lt; 2) return index;
    return fibonacci(index - 1) + fibonacci(index - 2);
}

test "fibonacci" {
    try comptime assert(fibonacci(7) == 13);
}</pre></figure> <p> The compiler is supposed to notice that evaluating this function at compile-time took more than 1000 branches, and thus emits an error and gives up. If the programmer wants to increase the budget for compile-time computation, they can use a built-in function called <a href="#setEvalBranchQuota">@setEvalBranchQuota</a> to change the default number 1000 to something else. </p> <p> However, there is a <a href="https://github.com/ziglang/zig/issues/13724">design flaw in the compiler</a> causing it to stack overflow instead of having the proper behavior here. I'm terribly sorry about that. I hope to get this resolved before the next release. </p> <p> What if we fix the base case, but put the wrong value in the <code><span class="line">expect</span></code> line? </p> <figure><figcaption class="zig-cap"><cite class="file">test_fibonacci_comptime_unreachable.zig</cite></figcaption><pre data-language="zig">const assert = @import("std").debug.assert;

fn fibonacci(index: i32) i32 {
    if (index &lt; 2) return index;
    return fibonacci(index - 1) + fibonacci(index - 2);
}

test "fibonacci" {
    try comptime assert(fibonacci(7) == 99999);
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_fibonacci_comptime_unreachable.zig</kbd>
<span class="sgr-1m">/home/ci/actions-runner/_work/zig-bootstrap/out/host/lib/zig/std/debug.zig:343:14: </span><span class="sgr-31m">error: </span><span class="sgr-1m">reached unreachable code
</span>    if (!ok) unreachable; // assertion failure
             <span class="sgr-32m">^~~~~~~~~~~
</span><span class="sgr-1m">docgen_tmp/test_fibonacci_comptime_unreachable.zig:9:24: </span><span class="sgr-36m">note: </span><span class="sgr-1m">called from here
</span>    try comptime assert(fibonacci(7) == 99999);
                 <span class="sgr-32m">~~~~~~^~~~~~~~~~~~~~~~~~~~~~~
</span>
</samp></pre></figure> <p> At <a href="#Containers">container</a> level (outside of any function), all expressions are implicitly <code><span class="line"><span class="tok-kw">comptime</span></span></code> expressions. This means that we can use functions to initialize complex static data. For example: </p> <figure><figcaption class="zig-cap"><cite class="file">test_container-level_comptime_expressions.zig</cite></figcaption><pre data-language="zig">const first_25_primes = firstNPrimes(25);
const sum_of_first_25_primes = sum(&amp;first_25_primes);

fn firstNPrimes(comptime n: usize) [n]i32 {
    var prime_list: [n]i32 = undefined;
    var next_index: usize = 0;
    var test_number: i32 = 2;
    while (next_index &lt; prime_list.len) : (test_number += 1) {
        var test_prime_index: usize = 0;
        var is_prime = true;
        while (test_prime_index &lt; next_index) : (test_prime_index += 1) {
            if (test_number % prime_list[test_prime_index] == 0) {
                is_prime = false;
                break;
            }
        }
        if (is_prime) {
            prime_list[next_index] = test_number;
            next_index += 1;
        }
    }
    return prime_list;
}

fn sum(numbers: []const i32) i32 {
    var result: i32 = 0;
    for (numbers) |x| {
        result += x;
    }
    return result;
}

test "variable values" {
    try @import("std").testing.expect(sum_of_first_25_primes == 1060);
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_container-level_comptime_expressions.zig</kbd>
1/1 test.variable values... OK
All 1 tests passed.
</samp></pre></figure> <p> When we compile this program, Zig generates the constants with the answer pre-computed. Here are the lines from the generated LLVM IR: </p> <pre data-language="zig">@0 = internal unnamed_addr constant [25 x i32] [i32 2, i32 3, i32 5, i32 7, i32 11, i32 13, i32 17, i32 19, i32 23, i32 29, i32 31, i32 37, i32 41, i32 43, i32 47, i32 53, i32 59, i32 61, i32 67, i32 71, i32 73, i32 79, i32 83, i32 89, i32 97]
@1 = internal unnamed_addr constant i32 1060</pre> <p> Note that we did not have to do anything special with the syntax of these functions. For example, we could call the <code><span class="line">sum</span></code> function as is with a slice of numbers whose length and values were only known at run-time. </p> <h3 id="Generic-Data-Structures">Generic Data Structures </h3> <p> Zig uses these capabilities to implement generic data structures without introducing any special-case syntax. If you followed along so far, you may already know how to create a generic data structure. </p> <p> Here is an example of a generic <code><span class="line">List</span></code> data structure. </p> <figure><figcaption class="zig-cap"><cite class="file">generic_data_structure.zig</cite></figcaption><pre data-language="zig">fn List(comptime T: type) type {
    return struct {
        items: []T,
        len: usize,
    };
}

// The generic List data structure can be instantiated by passing in a type:
var buffer: [10]i32 = undefined;
var list = List(i32){
    .items = &amp;buffer,
    .len = 0,
};</pre></figure> <p> That's it. It's a function that returns an anonymous <code><span class="line"><span class="tok-kw">struct</span></span></code>. To keep the language small and uniform, all aggregate types in Zig are anonymous. For the purposes of error messages and debugging, Zig infers the name <code><span class="line"><span class="tok-str">"List(i32)"</span></span></code> from the function name and parameters invoked when creating the anonymous struct. </p> <p> To explicitly give a type a name, we assign it to a constant. </p> <figure><figcaption class="zig-cap"><cite class="file">anonymous_struct_name.zig</cite></figcaption><pre data-language="zig">const Node = struct {
    next: ?*Node,
    name: []const u8,
};

var node_a = Node{
    .next = null,
    .name = &amp;"Node A",
};

var node_b = Node{
    .next = &amp;node_a,
    .name = &amp;"Node B",
};</pre></figure> <p> In this example, the <code><span class="line">Node</span></code> struct refers to itself. This works because all top level declarations are order-independent. As long as the compiler can determine the size of the struct, it is free to refer to itself. In this case, <code><span class="line">Node</span></code> refers to itself as a pointer, which has a well-defined size at compile time, so it works fine. </p> <h3 id="Case-Study-print-in-Zig">Case Study: print in Zig </h3> <p> Putting all of this together, let's see how <code><span class="line">print</span></code> works in Zig. </p> <figure><figcaption class="zig-cap"><cite class="file">print.zig</cite></figcaption><pre data-language="zig">const print = @import("std").debug.print;

const a_number: i32 = 1234;
const a_string = "foobar";

pub fn main() void {
    print("here is a string: '{s}' here is a number: {}\n", .{a_string, a_number});
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig build-exe print.zig</kbd>
$ <kbd>./print</kbd>
here is a string: 'foobar' here is a number: 1234
</samp></pre></figure> <p> Let's crack open the implementation of this and see how it works: </p> <figure><figcaption class="zig-cap"><cite class="file">poc_print_fn.zig</cite></figcaption><pre data-language="zig">const Writer = struct {
    /// Calls print and then flushes the buffer.
    pub fn print(self: *Writer, comptime format: []const u8, args: anytype) anyerror!void {
        const State = enum {
            start,
            open_brace,
            close_brace,
        };

        comptime var start_index: usize = 0;
        comptime var state = State.start;
        comptime var next_arg: usize = 0;

        inline for (format, 0..) |c, i| {
            switch (state) {
                State.start =&gt; switch (c) {
                    '{' =&gt; {
                        if (start_index &lt; i) try self.write(format[start_index..i]);
                        state = State.open_brace;
                    },
                    '}' =&gt; {
                        if (start_index &lt; i) try self.write(format[start_index..i]);
                        state = State.close_brace;
                    },
                    else =&gt; {},
                },
                State.open_brace =&gt; switch (c) {
                    '{' =&gt; {
                        state = State.start;
                        start_index = i;
                    },
                    '}' =&gt; {
                        try self.printValue(args[next_arg]);
                        next_arg += 1;
                        state = State.start;
                        start_index = i + 1;
                    },
                    's' =&gt; {
                        continue;
                    },
                    else =&gt; @compileError("Unknown format character: " ++ [1]u8{c}),
                },
                State.close_brace =&gt; switch (c) {
                    '}' =&gt; {
                        state = State.start;
                        start_index = i;
                    },
                    else =&gt; @compileError("Single '}' encountered in format string"),
                },
            }
        }
        comptime {
            if (args.len != next_arg) {
                @compileError("Unused arguments");
            }
            if (state != State.start) {
                @compileError("Incomplete format string: " ++ format);
            }
        }
        if (start_index &lt; format.len) {
            try self.write(format[start_index..format.len]);
        }
        try self.flush();
    }

    fn write(self: *Writer, value: []const u8) !void {
        _ = self;
        _ = value;
    }
    pub fn printValue(self: *Writer, value: anytype) !void {
        _ = self;
        _ = value;
    }
    fn flush(self: *Writer) !void {
        _ = self;
    }
};</pre></figure> <p> This is a proof of concept implementation; the actual function in the standard library has more formatting capabilities. </p> <p> Note that this is not hard-coded into the Zig compiler; this is userland code in the standard library. </p> <p> When this function is analyzed from our example code above, Zig partially evaluates the function and emits a function that actually looks like this: </p> <figure><figcaption class="zig-cap"><cite class="file">Emitted print Function</cite></figcaption><pre data-language="zig">pub fn print(self: *Writer, arg0: []const u8, arg1: i32) !void {
    try self.write("here is a string: '");
    try self.printValue(arg0);
    try self.write("' here is a number: ");
    try self.printValue(arg1);
    try self.write("\n");
    try self.flush();
}</pre></figure> <p> <code><span class="line">printValue</span></code> is a function that takes a parameter of any type, and does different things depending on the type: </p> <figure><figcaption class="zig-cap"><cite class="file">poc_printValue_fn.zig</cite></figcaption><pre data-language="zig">const Writer = struct {
    pub fn printValue(self: *Writer, value: anytype) !void {
        switch (@typeInfo(@TypeOf(value))) {
            .Int =&gt; {
                return self.writeInt(value);
            },
            .Float =&gt; {
                return self.writeFloat(value);
            },
            .Pointer =&gt; {
                return self.write(value);
            },
            else =&gt; {
                @compileError("Unable to print type '" ++ @typeName(@TypeOf(value)) ++ "'");
            },
        }
    }

    fn write(self: *Writer, value: []const u8) !void {
        _ = self;
        _ = value;
    }
    fn writeInt(self: *Writer, value: anytype) !void {
        _ = self;
        _ = value;
    }
    fn writeFloat(self: *Writer, value: anytype) !void {
        _ = self;
        _ = value;
    }
};</pre></figure> <p> And now, what happens if we give too many arguments to <code><span class="line">print</span></code>? </p> <figure><figcaption class="zig-cap"><cite class="file">test_print_too_many_args.zig</cite></figcaption><pre data-language="zig">const print = @import("std").debug.print;

const a_number: i32 = 1234;
const a_string = "foobar";

test "print too many arguments" {
    print("here is a string: '{s}' here is a number: {}\n", .{
        a_string,
        a_number,
        a_number,
    });
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_print_too_many_args.zig</kbd>
<span class="sgr-1m">/home/ci/actions-runner/_work/zig-bootstrap/out/host/lib/zig/std/fmt.zig:202:18: </span><span class="sgr-31m">error: </span><span class="sgr-1m">unused argument in 'here is a string: '{s}' here is a number: {}
                                                                                        '
</span>            1 =&gt; @compileError("unused argument in '" ++ fmt ++ "'"),
                 <span class="sgr-32m">^~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
</span>
</samp></pre></figure> <p> Zig gives programmers the tools needed to protect themselves against their own mistakes. </p> <p> Zig doesn't care whether the format argument is a string literal, only that it is a compile-time known value that can be coerced to a <code><span class="line">[]<span class="tok-kw">const</span> <span class="tok-type">u8</span></span></code>: </p> <figure><figcaption class="zig-cap"><cite class="file">print_comptime-known_format.zig</cite></figcaption><pre data-language="zig">const print = @import("std").debug.print;

const a_number: i32 = 1234;
const a_string = "foobar";
const fmt = "here is a string: '{s}' here is a number: {}\n";

pub fn main() void {
    print(fmt, .{a_string, a_number});
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig build-exe print_comptime-known_format.zig</kbd>
$ <kbd>./print_comptime-known_format</kbd>
here is a string: 'foobar' here is a number: 1234
</samp></pre></figure> <p> This works fine. </p> <p> Zig does not special case string formatting in the compiler and instead exposes enough power to accomplish this task in userland. It does so without introducing another language on top of Zig, such as a macro language or a preprocessor language. It's Zig all the way down. </p> <p>See also:</p>
<ul> <li><a href="#inline-while">inline while</a></li> <li><a href="#inline-for">inline for</a></li> </ul> <h2 id="Assembly">Assembly </h2> <p> For some use cases, it may be necessary to directly control the machine code generated by Zig programs, rather than relying on Zig's code generation. For these cases, one can use inline assembly. Here is an example of implementing Hello, World on x86_64 Linux using inline assembly: </p> <figure><figcaption class="zig-cap"><cite class="file">inline_assembly.zig</cite></figcaption><pre data-language="zig">pub fn main() noreturn {
    const msg = "hello world\n";
    _ = syscall3(SYS_write, STDOUT_FILENO, @intFromPtr(msg), msg.len);
    _ = syscall1(SYS_exit, 0);
    unreachable;
}

pub const SYS_write = 1;
pub const SYS_exit = 60;

pub const STDOUT_FILENO = 1;

pub fn syscall1(number: usize, arg1: usize) usize {
    return asm volatile ("syscall"
        : [ret] "={rax}" (-&gt; usize),
        : [number] "{rax}" (number),
          [arg1] "{rdi}" (arg1),
        : "rcx", "r11"
    );
}

pub fn syscall3(number: usize, arg1: usize, arg2: usize, arg3: usize) usize {
    return asm volatile ("syscall"
        : [ret] "={rax}" (-&gt; usize),
        : [number] "{rax}" (number),
          [arg1] "{rdi}" (arg1),
          [arg2] "{rsi}" (arg2),
          [arg3] "{rdx}" (arg3),
        : "rcx", "r11"
    );
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig build-exe inline_assembly.zig -target x86_64-linux</kbd>
$ <kbd>./inline_assembly</kbd>
hello world
</samp></pre></figure> <p> Dissecting the syntax: </p> <figure><figcaption class="zig-cap"><cite class="file">Assembly Syntax Explained.zig</cite></figcaption><pre data-language="zig">pub fn syscall1(number: usize, arg1: usize) usize {
    // Inline assembly is an expression which returns a value.
    // the `asm` keyword begins the expression.
    return asm
    // `volatile` is an optional modifier that tells Zig this
    // inline assembly expression has side-effects. Without
    // `volatile`, Zig is allowed to delete the inline assembly
    // code if the result is unused.
    volatile (
    // Next is a comptime string which is the assembly code.
    // Inside this string one may use `%[ret]`, `%[number]`,
    // or `%[arg1]` where a register is expected, to specify
    // the register that Zig uses for the argument or return value,
    // if the register constraint strings are used. However in
    // the below code, this is not used. A literal `%` can be
    // obtained by escaping it with a double percent: `%%`.
    // Often multiline string syntax comes in handy here.
    \\syscall
    // Next is the output. It is possible in the future Zig will
    // support multiple outputs, depending on how
    // https://github.com/ziglang/zig/issues/215 is resolved.
    // It is allowed for there to be no outputs, in which case
    // this colon would be directly followed by the colon for the inputs.
        :
    // This specifies the name to be used in `%[ret]` syntax in
    // the above assembly string. This example does not use it,
    // but the syntax is mandatory.
        [ret]
    // Next is the output constraint string. This feature is still
    // considered unstable in Zig, and so LLVM/GCC documentation
    // must be used to understand the semantics.
    // http://releases.llvm.org/10.0.0/docs/LangRef.html#inline-asm-constraint-string
    // https://gcc.gnu.org/onlinedocs/gcc/Extended-Asm.html
    // In this example, the constraint string means "the result value of
    // this inline assembly instruction is whatever is in $rax".
        "={rax}"
    // Next is either a value binding, or `-&gt;` and then a type. The
    // type is the result type of the inline assembly expression.
    // If it is a value binding, then `%[ret]` syntax would be used
    // to refer to the register bound to the value.
        (-&gt; usize),
    // Next is the list of inputs.
    // The constraint for these inputs means, "when the assembly code is
    // executed, $rax shall have the value of `number` and $rdi shall have
    // the value of `arg1`". Any number of input parameters is allowed,
    // including none.
        : [number] "{rax}" (number),
            [arg1] "{rdi}" (arg1),
    // Next is the list of clobbers. These declare a set of registers whose
    // values will not be preserved by the execution of this assembly code.
    // These do not include output or input registers. The special clobber
    // value of "memory" means that the assembly writes to arbitrary undeclared
    // memory locations - not only the memory pointed to by a declared indirect
    // output. In this example we list $rcx and $r11 because it is known the
    // kernel syscall does not preserve these registers.
        : "rcx", "r11"
    );
}</pre></figure> <p> For x86 and x86_64 targets, the syntax is AT&amp;T syntax, rather than the more popular Intel syntax. This is due to technical constraints; assembly parsing is provided by LLVM and its support for Intel syntax is buggy and not well tested. </p> <p> Some day Zig may have its own assembler. This would allow it to integrate more seamlessly into the language, as well as be compatible with the popular NASM syntax. This documentation section will be updated before 1.0.0 is released, with a conclusive statement about the status of AT&amp;T vs Intel/NASM syntax. </p> <h3 id="Output-Constraints">Output Constraints </h3> <p> Output constraints are still considered to be unstable in Zig, and so <a href="http://releases.llvm.org/10.0.0/docs/LangRef.html#inline-asm-constraint-string">LLVM documentation</a> and <a href="https://gcc.gnu.org/onlinedocs/gcc/Extended-Asm.html">GCC documentation</a> must be used to understand the semantics. </p> <p> Note that some breaking changes to output constraints are planned with <a href="https://github.com/ziglang/zig/issues/215">issue #215</a>. </p> <h3 id="Input-Constraints">Input Constraints </h3> <p> Input constraints are still considered to be unstable in Zig, and so <a href="http://releases.llvm.org/10.0.0/docs/LangRef.html#inline-asm-constraint-string">LLVM documentation</a> and <a href="https://gcc.gnu.org/onlinedocs/gcc/Extended-Asm.html">GCC documentation</a> must be used to understand the semantics. </p> <p> Note that some breaking changes to input constraints are planned with <a href="https://github.com/ziglang/zig/issues/215">issue #215</a>. </p> <h3 id="Clobbers">Clobbers </h3> <p> Clobbers are the set of registers whose values will not be preserved by the execution of the assembly code. These do not include output or input registers. The special clobber value of <code><span class="line"><span class="tok-str">"memory"</span></span></code> means that the assembly causes writes to arbitrary undeclared memory locations - not only the memory pointed to by a declared indirect output. </p> <p> Failure to declare the full set of clobbers for a given inline assembly expression is unchecked <a href="#Undefined-Behavior">Undefined Behavior</a>. </p> <h3 id="Global-Assembly">Global Assembly </h3> <p> When an assembly expression occurs in a <a href="#Containers">container</a> level <a href="#comptime">comptime</a> block, this is <strong>global assembly</strong>. </p> <p> This kind of assembly has different rules than inline assembly. First, <code><span class="line"><span class="tok-kw">volatile</span></span></code> is not valid because all global assembly is unconditionally included. Second, there are no inputs, outputs, or clobbers. All global assembly is concatenated verbatim into one long string and assembled together. There are no template substitution rules regarding <code>%</code> as there are in inline assembly expressions. </p> <figure><figcaption class="zig-cap"><cite class="file">test_global_assembly.zig</cite></figcaption><pre data-language="zig">const std = @import("std");
const expect = std.testing.expect;

comptime {
    asm (
        \\.global my_func;
        \\.type my_func, @function;
        \\my_func:
        \\  lea (%rdi,%rsi,1),%eax
        \\  retq
    );
}

extern fn my_func(a: i32, b: i32) i32;

test "global assembly" {
    try expect(my_func(12, 34) == 46);
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_global_assembly.zig -target x86_64-linux</kbd>
1/1 test.global assembly... OK
All 1 tests passed.
</samp></pre></figure> <h2 id="Atomics">Atomics </h2> <p>TODO: @fence()</p> <p>TODO: @atomic rmw</p> <p>TODO: builtin atomic memory ordering enum</p> <h2 id="Async-Functions">Async Functions </h2> <p>Async functions are being temporarily regressed and will be <a href="https://github.com/ziglang/zig/issues/6025">restored before Zig 0.11.0 is tagged</a>. I apologize for the instability. Please use Zig 0.10.0 with the <code>-fstage1</code> flag for now if you need this feature.</p> <h2 id="Builtin-Functions">Builtin Functions </h2> <p> Builtin functions are provided by the compiler and are prefixed with <code>@</code>. The <code><span class="line"><span class="tok-kw">comptime</span></span></code> keyword on a parameter means that the parameter must be known at compile time. </p> <h3 id="addrSpaceCast">@addrSpaceCast </h3> <pre data-language="zig">@addrSpaceCast(ptr: anytype) anytype</pre> <p> Converts a pointer from one address space to another. The new address space is inferred based on the result type. Depending on the current target and address spaces, this cast may be a no-op, a complex operation, or illegal. If the cast is legal, then the resulting pointer points to the same memory location as the pointer operand. It is always valid to cast a pointer between the same address spaces. </p> <h3 id="addWithOverflow">@addWithOverflow </h3> <pre data-language="zig">@addWithOverflow(a: anytype, b: anytype) struct { @TypeOf(a, b), u1 }</pre> <p> Performs <code><span class="line">a + b</span></code> and returns a tuple with the result and a possible overflow bit. </p> <h3 id="alignCast">@alignCast </h3> <pre data-language="zig">@alignCast(ptr: anytype) anytype</pre> <p> <code><span class="line">ptr</span></code> can be <code><span class="line">*T</span></code>, <code><span class="line">?*T</span></code>, or <code><span class="line">[]T</span></code>. Changes the alignment of a pointer. The alignment to use is inferred based on the result type. </p> <p>A <a href="#Incorrect-Pointer-Alignment">pointer alignment safety check</a> is added to the generated code to make sure the pointer is aligned as promised.</p> <h3 id="alignOf">@alignOf </h3> <pre data-language="zig">@alignOf(comptime T: type) comptime_int</pre> <p> This function returns the number of bytes that this type should be aligned to for the current target to match the C ABI. When the child type of a pointer has this alignment, the alignment can be omitted from the type. </p> <pre data-language="zig">const assert = @import("std").debug.assert;
comptime {
    assert(*u32 == *align(@alignOf(u32)) u32);
}</pre> <p> The result is a target-specific compile time constant. It is guaranteed to be less than or equal to <a href="#sizeOf">@sizeOf(T)</a>. </p> <p>See also:</p>
<ul> <li><a href="#Alignment">Alignment</a></li> </ul> <h3 id="as">@as </h3> <pre data-language="zig">@as(comptime T: type, expression) T</pre> <p> Performs <a href="#Type-Coercion">Type Coercion</a>. This cast is allowed when the conversion is unambiguous and safe, and is the preferred way to convert between types, whenever possible. </p> <h3 id="atomicLoad">@atomicLoad </h3> <pre data-language="zig">@atomicLoad(comptime T: type, ptr: *const T, comptime ordering: builtin.AtomicOrder) T</pre> <p> This builtin function atomically dereferences a pointer and returns the value. </p> <p> <code><span class="line">T</span></code> must be a pointer, a <code><span class="line"><span class="tok-type">bool</span></span></code>, a float, an integer or an enum. </p> <p>See also:</p>
<ul> <li><a href="#atomicStore">@atomicStore</a></li> <li><a href="#atomicRmw">@atomicRmw</a></li> <li><a href="#fence">@fence</a></li> <li><a href="#cmpxchgWeak">@cmpxchgWeak</a></li> <li><a href="#cmpxchgStrong">@cmpxchgStrong</a></li> </ul> <h3 id="atomicRmw">@atomicRmw </h3> <pre data-language="zig">@atomicRmw(comptime T: type, ptr: *T, comptime op: builtin.AtomicRmwOp, operand: T, comptime ordering: builtin.AtomicOrder) T</pre> <p> This builtin function atomically modifies memory and then returns the previous value. </p> <p> <code><span class="line">T</span></code> must be a pointer, a <code><span class="line"><span class="tok-type">bool</span></span></code>, a float, an integer or an enum. </p> <p> Supported operations: </p> <ul> <li>
<code><span class="line">.Xchg</span></code> - stores the operand unmodified. Supports enums, integers and floats.</li> <li>
<code><span class="line">.Add</span></code> - for integers, twos complement wraparound addition. Also supports <a href="#Floats">Floats</a>.</li> <li>
<code><span class="line">.Sub</span></code> - for integers, twos complement wraparound subtraction. Also supports <a href="#Floats">Floats</a>.</li> <li>
<code><span class="line">.And</span></code> - bitwise and</li> <li>
<code><span class="line">.Nand</span></code> - bitwise nand</li> <li>
<code><span class="line">.Or</span></code> - bitwise or</li> <li>
<code><span class="line">.Xor</span></code> - bitwise xor</li> <li>
<code><span class="line">.Max</span></code> - stores the operand if it is larger. Supports integers and floats.</li> <li>
<code><span class="line">.Min</span></code> - stores the operand if it is smaller. Supports integers and floats.</li> </ul> <p>See also:</p>
<ul> <li><a href="#atomicStore">@atomicStore</a></li> <li><a href="#atomicLoad">@atomicLoad</a></li> <li><a href="#fence">@fence</a></li> <li><a href="#cmpxchgWeak">@cmpxchgWeak</a></li> <li><a href="#cmpxchgStrong">@cmpxchgStrong</a></li> </ul> <h3 id="atomicStore">@atomicStore </h3> <pre data-language="zig">@atomicStore(comptime T: type, ptr: *T, value: T, comptime ordering: builtin.AtomicOrder) void</pre> <p> This builtin function atomically stores a value. </p> <p> <code><span class="line">T</span></code> must be a pointer, a <code><span class="line"><span class="tok-type">bool</span></span></code>, a float, an integer or an enum. </p> <p>See also:</p>
<ul> <li><a href="#atomicLoad">@atomicLoad</a></li> <li><a href="#atomicRmw">@atomicRmw</a></li> <li><a href="#fence">@fence</a></li> <li><a href="#cmpxchgWeak">@cmpxchgWeak</a></li> <li><a href="#cmpxchgStrong">@cmpxchgStrong</a></li> </ul> <h3 id="bitCast">@bitCast </h3> <pre data-language="zig">@bitCast(value: anytype) anytype</pre> <p> Converts a value of one type to another type. The return type is the inferred result type. </p> <p> Asserts that <code><span class="line"><span class="tok-builtin">@sizeOf</span>(<span class="tok-builtin">@TypeOf</span>(value)) == <span class="tok-builtin">@sizeOf</span>(DestType)</span></code>. </p> <p> Asserts that <code><span class="line"><span class="tok-builtin">@typeInfo</span>(DestType) != .Pointer</span></code>. Use <code><span class="line"><span class="tok-builtin">@ptrCast</span></span></code> or <code><span class="line"><span class="tok-builtin">@ptrFromInt</span></span></code> if you need this. </p> <p> Can be used for these things for example: </p> <ul> <li>Convert <code><span class="line"><span class="tok-type">f32</span></span></code> to <code><span class="line"><span class="tok-type">u32</span></span></code> bits</li> <li>Convert <code><span class="line"><span class="tok-type">i32</span></span></code> to <code><span class="line"><span class="tok-type">u32</span></span></code> preserving twos complement</li> </ul> <p> Works at compile-time if <code><span class="line">value</span></code> is known at compile time. It's a compile error to bitcast a value of undefined layout; this means that, besides the restriction from types which possess dedicated casting builtins (enums, pointers, error sets), bare structs, error unions, slices, optionals, and any other type without a well-defined memory layout, also cannot be used in this operation. </p> <h3 id="bitOffsetOf">@bitOffsetOf </h3> <pre data-language="zig">@bitOffsetOf(comptime T: type, comptime field_name: []const u8) comptime_int</pre> <p> Returns the bit offset of a field relative to its containing struct. </p> <p> For non <a href="#packed-struct">packed structs</a>, this will always be divisible by <code><span class="line"><span class="tok-number">8</span></span></code>. For packed structs, non-byte-aligned fields will share a byte offset, but they will have different bit offsets. </p> <p>See also:</p>
<ul> <li><a href="#offsetOf">@offsetOf</a></li> </ul> <h3 id="bitSizeOf">@bitSizeOf </h3> <pre data-language="zig">@bitSizeOf(comptime T: type) comptime_int</pre> <p> This function returns the number of bits it takes to store <code><span class="line">T</span></code> in memory if the type were a field in a packed struct/union. The result is a target-specific compile time constant. </p> <p> This function measures the size at runtime. For types that are disallowed at runtime, such as <code><span class="line"><span class="tok-type">comptime_int</span></span></code> and <code><span class="line"><span class="tok-type">type</span></span></code>, the result is <code><span class="line"><span class="tok-number">0</span></span></code>. </p> <p>See also:</p>
<ul> <li><a href="#sizeOf">@sizeOf</a></li> <li><a href="#typeInfo">@typeInfo</a></li> </ul> <h3 id="breakpoint">@breakpoint </h3> <pre data-language="zig">@breakpoint() void</pre> <p> This function inserts a platform-specific debug trap instruction which causes debuggers to break there. Unlike for <code><span class="line"><span class="tok-builtin">@trap</span>()</span></code>, execution may continue after this point if the program is resumed. </p> <p> This function is only valid within function scope. </p> <p>See also:</p>
<ul> <li><a href="#trap">@trap</a></li> </ul> <h3 id="mulAdd">@mulAdd </h3> <pre data-language="zig">@mulAdd(comptime T: type, a: T, b: T, c: T) T</pre> <p> Fused multiply-add, similar to <code><span class="line">(a * b) + c</span></code>, except only rounds once, and is thus more accurate. </p> <p> Supports <a href="#Floats">Floats</a> and <a href="#Vectors">Vectors</a> of floats. </p> <h3 id="byteSwap">@byteSwap </h3> <pre data-language="zig">@byteSwap(operand: anytype) T</pre> <p><code><span class="line"><span class="tok-builtin">@TypeOf</span>(operand)</span></code> must be an integer type or an integer vector type with bit count evenly divisible by 8.</p> <p><code><span class="line">operand</span></code> may be an <a href="#Integers">integer</a> or <a href="#Vectors">vector</a>.</p> <p> Swaps the byte order of the integer. This converts a big endian integer to a little endian integer, and converts a little endian integer to a big endian integer. </p> <p> Note that for the purposes of memory layout with respect to endianness, the integer type should be related to the number of bytes reported by <a href="#sizeOf">@sizeOf</a> bytes. This is demonstrated with <code><span class="line"><span class="tok-type">u24</span></span></code>. <code><span class="line"><span class="tok-builtin">@sizeOf</span>(<span class="tok-type">u24</span>) == <span class="tok-number">4</span></span></code>, which means that a <code><span class="line"><span class="tok-type">u24</span></span></code> stored in memory takes 4 bytes, and those 4 bytes are what are swapped on a little vs big endian system. On the other hand, if <code><span class="line">T</span></code> is specified to be <code><span class="line"><span class="tok-type">u24</span></span></code>, then only 3 bytes are reversed. </p> <h3 id="bitReverse">@bitReverse </h3> <pre data-language="zig">@bitReverse(integer: anytype) T</pre> <p><code><span class="line"><span class="tok-builtin">@TypeOf</span>(<span class="tok-kw">anytype</span>)</span></code> accepts any integer type or integer vector type.</p> <p> Reverses the bitpattern of an integer value, including the sign bit if applicable. </p> <p> For example 0b10110110 (<code><span class="line"><span class="tok-type">u8</span> = <span class="tok-number">182</span></span></code>, <code><span class="line"><span class="tok-type">i8</span> = -<span class="tok-number">74</span></span></code>) becomes 0b01101101 (<code><span class="line"><span class="tok-type">u8</span> = <span class="tok-number">109</span></span></code>, <code><span class="line"><span class="tok-type">i8</span> = <span class="tok-number">109</span></span></code>). </p> <h3 id="offsetOf">@offsetOf </h3> <pre data-language="zig">@offsetOf(comptime T: type, comptime field_name: []const u8) comptime_int</pre> <p> Returns the byte offset of a field relative to its containing struct. </p> <p>See also:</p>
<ul> <li><a href="#bitOffsetOf">@bitOffsetOf</a></li> </ul> <h3 id="call">@call </h3> <pre data-language="zig">@call(modifier: std.builtin.CallModifier, function: anytype, args: anytype) anytype</pre> <p> Calls a function, in the same way that invoking an expression with parentheses does: </p> <figure><figcaption class="zig-cap"><cite class="file">test_call_builtin.zig</cite></figcaption><pre data-language="zig">const expect = @import("std").testing.expect;

test "noinline function call" {
    try expect(@call(.auto, add, .{3, 9}) == 12);
}

fn add(a: i32, b: i32) i32 {
    return a + b;
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_call_builtin.zig</kbd>
1/1 test.noinline function call... OK
All 1 tests passed.
</samp></pre></figure> <p> <code><span class="line"><span class="tok-builtin">@call</span></span></code> allows more flexibility than normal function call syntax does. The <code><span class="line">CallModifier</span></code> enum is reproduced here: </p> <figure><figcaption class="zig-cap"><cite class="file">builtin.CallModifier struct.zig</cite></figcaption><pre data-language="zig">pub const CallModifier = enum {
    /// Equivalent to function call syntax.
    auto,

    /// Equivalent to async keyword used with function call syntax.
    async_kw,

    /// Prevents tail call optimization. This guarantees that the return
    /// address will point to the callsite, as opposed to the callsite's
    /// callsite. If the call is otherwise required to be tail-called
    /// or inlined, a compile error is emitted instead.
    never_tail,

    /// Guarantees that the call will not be inlined. If the call is
    /// otherwise required to be inlined, a compile error is emitted instead.
    never_inline,

    /// Asserts that the function call will not suspend. This allows a
    /// non-async function to call an async function.
    no_async,

    /// Guarantees that the call will be generated with tail call optimization.
    /// If this is not possible, a compile error is emitted instead.
    always_tail,

    /// Guarantees that the call will inlined at the callsite.
    /// If this is not possible, a compile error is emitted instead.
    always_inline,

    /// Evaluates the call at compile-time. If the call cannot be completed at
    /// compile-time, a compile error is emitted instead.
    compile_time,
};</pre></figure> <h3 id="cDefine">@cDefine </h3> <pre data-language="zig">@cDefine(comptime name: []const u8, value) void</pre> <p> This function can only occur inside <code><span class="line"><span class="tok-builtin">@cImport</span></span></code>. </p> <p> This appends <code>#define $name $value</code> to the <code><span class="line"><span class="tok-builtin">@cImport</span></span></code> temporary buffer. </p> <p> To define without a value, like this: </p> <pre data-language="zig">#define _GNU_SOURCE</pre> <p> Use the void value, like this: </p> <pre data-language="zig">@cDefine("_GNU_SOURCE", {})</pre> <p>See also:</p>
<ul> <li><a href="#Import-from-C-Header-File">Import from C Header File</a></li> <li><a href="#cInclude">@cInclude</a></li> <li><a href="#cImport">@cImport</a></li> <li><a href="#cUndef">@cUndef</a></li> <li><a href="#void">void</a></li> </ul> <h3 id="cImport">@cImport </h3> <pre data-language="zig">@cImport(expression) type</pre> <p> This function parses C code and imports the functions, types, variables, and compatible macro definitions into a new empty struct type, and then returns that type. </p> <p> <code><span class="line">expression</span></code> is interpreted at compile time. The builtin functions <code><span class="line"><span class="tok-builtin">@cInclude</span></span></code>, <code><span class="line"><span class="tok-builtin">@cDefine</span></span></code>, and <code><span class="line"><span class="tok-builtin">@cUndef</span></span></code> work within this expression, appending to a temporary buffer which is then parsed as C code. </p> <p> Usually you should only have one <code><span class="line"><span class="tok-builtin">@cImport</span></span></code> in your entire application, because it saves the compiler from invoking clang multiple times, and prevents inline functions from being duplicated. </p> <p> Reasons for having multiple <code><span class="line"><span class="tok-builtin">@cImport</span></span></code> expressions would be: </p> <ul> <li>To avoid a symbol collision, for example if foo.h and bar.h both <code>#define CONNECTION_COUNT</code>
</li> <li>To analyze the C code with different preprocessor defines</li> </ul> <p>See also:</p>
<ul> <li><a href="#Import-from-C-Header-File">Import from C Header File</a></li> <li><a href="#cInclude">@cInclude</a></li> <li><a href="#cDefine">@cDefine</a></li> <li><a href="#cUndef">@cUndef</a></li> </ul> <h3 id="cInclude">@cInclude </h3> <pre data-language="zig">@cInclude(comptime path: []const u8) void</pre> <p> This function can only occur inside <code><span class="line"><span class="tok-builtin">@cImport</span></span></code>. </p> <p> This appends <code>#include &lt;$path&gt;\n</code> to the <code><span class="line">c_import</span></code> temporary buffer. </p> <p>See also:</p>
<ul> <li><a href="#Import-from-C-Header-File">Import from C Header File</a></li> <li><a href="#cImport">@cImport</a></li> <li><a href="#cDefine">@cDefine</a></li> <li><a href="#cUndef">@cUndef</a></li> </ul> <h3 id="clz">@clz </h3> <pre data-language="zig">@clz(operand: anytype) anytype</pre> <p><code><span class="line"><span class="tok-builtin">@TypeOf</span>(operand)</span></code> must be an integer type or an integer vector type.</p> <p><code><span class="line">operand</span></code> may be an <a href="#Integers">integer</a> or <a href="#Vectors">vector</a>.</p> <p> Counts the number of most-significant (leading in a big-endian sense) zeroes in an integer - "count leading zeroes". </p> <p> If <code><span class="line">operand</span></code> is a <a href="#comptime">comptime</a>-known integer, the return type is <code><span class="line"><span class="tok-type">comptime_int</span></span></code>. Otherwise, the return type is an unsigned integer or vector of unsigned integers with the minimum number of bits that can represent the bit count of the integer type. </p> <p> If <code><span class="line">operand</span></code> is zero, <code><span class="line"><span class="tok-builtin">@clz</span></span></code> returns the bit width of integer type <code><span class="line">T</span></code>. </p> <p>See also:</p>
<ul> <li><a href="#ctz">@ctz</a></li> <li><a href="#popCount">@popCount</a></li> </ul> <h3 id="cmpxchgStrong">@cmpxchgStrong </h3> <pre data-language="zig">@cmpxchgStrong(comptime T: type, ptr: *T, expected_value: T, new_value: T, success_order: AtomicOrder, fail_order: AtomicOrder) ?T</pre> <p> This function performs a strong atomic compare exchange operation. It's the equivalent of this code, except atomic: </p> <figure><figcaption class="zig-cap"><cite class="file">not_atomic_cmpxchgStrong.zig</cite></figcaption><pre data-language="zig">fn cmpxchgStrongButNotAtomic(comptime T: type, ptr: *T, expected_value: T, new_value: T) ?T {
    const old_value = ptr.*;
    if (old_value == expected_value) {
        ptr.* = new_value;
        return null;
    } else {
        return old_value;
    }
}</pre></figure> <p> If you are using cmpxchg in a loop, <a href="#cmpxchgWeak">@cmpxchgWeak</a> is the better choice, because it can be implemented more efficiently in machine instructions. </p> <p> <code><span class="line">T</span></code> must be a pointer, a <code><span class="line"><span class="tok-type">bool</span></span></code>, a float, an integer or an enum. </p> <p><code><span class="line"><span class="tok-builtin">@typeInfo</span>(<span class="tok-builtin">@TypeOf</span>(ptr)).Pointer.alignment</span></code> must be <code><span class="line">&gt;= <span class="tok-builtin">@sizeOf</span>(T).</span></code></p> <p>See also:</p>
<ul> <li><a href="#atomicStore">@atomicStore</a></li> <li><a href="#atomicLoad">@atomicLoad</a></li> <li><a href="#atomicRmw">@atomicRmw</a></li> <li><a href="#fence">@fence</a></li> <li><a href="#cmpxchgWeak">@cmpxchgWeak</a></li> </ul> <h3 id="cmpxchgWeak">@cmpxchgWeak </h3> <pre data-language="zig">@cmpxchgWeak(comptime T: type, ptr: *T, expected_value: T, new_value: T, success_order: AtomicOrder, fail_order: AtomicOrder) ?T</pre> <p> This function performs a weak atomic compare exchange operation. It's the equivalent of this code, except atomic: </p> <figure><figcaption class="zig-cap"><cite class="file">cmpxchgWeakButNotAtomic</cite></figcaption><pre data-language="zig">fn cmpxchgWeakButNotAtomic(comptime T: type, ptr: *T, expected_value: T, new_value: T) ?T {
    const old_value = ptr.*;
    if (old_value == expected_value and usuallyTrueButSometimesFalse()) {
        ptr.* = new_value;
        return null;
    } else {
        return old_value;
    }
}</pre></figure> <p> If you are using cmpxchg in a loop, the sporadic failure will be no problem, and <code><span class="line">cmpxchgWeak</span></code> is the better choice, because it can be implemented more efficiently in machine instructions. However if you need a stronger guarantee, use <a href="#cmpxchgStrong">@cmpxchgStrong</a>. </p> <p> <code><span class="line">T</span></code> must be a pointer, a <code><span class="line"><span class="tok-type">bool</span></span></code>, a float, an integer or an enum. </p> <p><code><span class="line"><span class="tok-builtin">@typeInfo</span>(<span class="tok-builtin">@TypeOf</span>(ptr)).Pointer.alignment</span></code> must be <code><span class="line">&gt;= <span class="tok-builtin">@sizeOf</span>(T).</span></code></p> <p>See also:</p>
<ul> <li><a href="#atomicStore">@atomicStore</a></li> <li><a href="#atomicLoad">@atomicLoad</a></li> <li><a href="#atomicRmw">@atomicRmw</a></li> <li><a href="#fence">@fence</a></li> <li><a href="#cmpxchgStrong">@cmpxchgStrong</a></li> </ul> <h3 id="compileError">@compileError </h3> <pre data-language="zig">@compileError(comptime msg: []const u8) noreturn</pre> <p> This function, when semantically analyzed, causes a compile error with the message <code><span class="line">msg</span></code>. </p> <p> There are several ways that code avoids being semantically checked, such as using <code><span class="line"><span class="tok-kw">if</span></span></code> or <code><span class="line"><span class="tok-kw">switch</span></span></code> with compile time constants, and <code><span class="line"><span class="tok-kw">comptime</span></span></code> functions. </p> <h3 id="compileLog">@compileLog </h3> <pre data-language="zig">@compileLog(args: ...) void</pre> <p> This function prints the arguments passed to it at compile-time. </p> <p> To prevent accidentally leaving compile log statements in a codebase, a compilation error is added to the build, pointing to the compile log statement. This error prevents code from being generated, but does not otherwise interfere with analysis. </p> <p> This function can be used to do "printf debugging" on compile-time executing code. </p> <figure><figcaption class="zig-cap"><cite class="file">test_compileLog_builtin.zig</cite></figcaption><pre data-language="zig">const print = @import("std").debug.print;

const num1 = blk: {
    var val1: i32 = 99;
    @compileLog("comptime val1 = ", val1);
    val1 = val1 + 1;
    break :blk val1;
};

test "main" {
    @compileLog("comptime in main");

    print("Runtime in main, num1 = {}.\n", .{num1});
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_compileLog_builtin.zig</kbd>
<span class="sgr-1m">docgen_tmp/test_compileLog_builtin.zig:11:5: </span><span class="sgr-31m">error: </span><span class="sgr-1m">found compile log statement
</span>    @compileLog("comptime in main");
    <span class="sgr-32m">^~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
</span><span class="sgr-1m">docgen_tmp/test_compileLog_builtin.zig:5:5: </span><span class="sgr-36m">note: </span><span class="sgr-1m">also here
</span>    @compileLog("comptime val1 = ", val1);
    <span class="sgr-32m">^~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
</span>
Compile Log Output:
@as(*const [16:0]u8, "comptime in main")
@as(*const [16:0]u8, "comptime val1 = "), @as(i32, 99)
</samp></pre></figure> <p> If all <code><span class="line"><span class="tok-builtin">@compileLog</span></span></code> calls are removed or not encountered by analysis, the program compiles successfully and the generated executable prints: </p> <figure><figcaption class="zig-cap"><cite class="file">test_without_compileLog_builtin.zig</cite></figcaption><pre data-language="zig">const print = @import("std").debug.print;

const num1 = blk: {
    var val1: i32 = 99;
    val1 = val1 + 1;
    break :blk val1;
};

test "main" {
    print("Runtime in main, num1 = {}.\n", .{num1});
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_without_compileLog_builtin.zig</kbd>
1/1 test.main... Runtime in main, num1 = 100.
OK
All 1 tests passed.
</samp></pre></figure> <h3 id="constCast">@constCast </h3> <pre data-language="zig">@constCast(value: anytype) DestType</pre> <p> Remove <code><span class="line"><span class="tok-kw">const</span></span></code> qualifier from a pointer. </p> <h3 id="ctz">@ctz </h3> <pre data-language="zig">@ctz(operand: anytype) anytype</pre> <p><code><span class="line"><span class="tok-builtin">@TypeOf</span>(operand)</span></code> must be an integer type or an integer vector type.</p> <p><code><span class="line">operand</span></code> may be an <a href="#Integers">integer</a> or <a href="#Vectors">vector</a>.</p> <p> Counts the number of least-significant (trailing in a big-endian sense) zeroes in an integer - "count trailing zeroes". </p> <p> If <code><span class="line">operand</span></code> is a <a href="#comptime">comptime</a>-known integer, the return type is <code><span class="line"><span class="tok-type">comptime_int</span></span></code>. Otherwise, the return type is an unsigned integer or vector of unsigned integers with the minimum number of bits that can represent the bit count of the integer type. </p> <p> If <code><span class="line">operand</span></code> is zero, <code><span class="line"><span class="tok-builtin">@ctz</span></span></code> returns the bit width of integer type <code><span class="line">T</span></code>. </p> <p>See also:</p>
<ul> <li><a href="#clz">@clz</a></li> <li><a href="#popCount">@popCount</a></li> </ul> <h3 id="cUndef">@cUndef </h3> <pre data-language="zig">@cUndef(comptime name: []const u8) void</pre> <p> This function can only occur inside <code><span class="line"><span class="tok-builtin">@cImport</span></span></code>. </p> <p> This appends <code>#undef $name</code> to the <code><span class="line"><span class="tok-builtin">@cImport</span></span></code> temporary buffer. </p> <p>See also:</p>
<ul> <li><a href="#Import-from-C-Header-File">Import from C Header File</a></li> <li><a href="#cImport">@cImport</a></li> <li><a href="#cDefine">@cDefine</a></li> <li><a href="#cInclude">@cInclude</a></li> </ul> <h3 id="cVaArg">@cVaArg </h3> <pre data-language="zig">@cVaArg(operand: *std.builtin.VaList, comptime T: type) T</pre> <p> Implements the C macro <code><span class="line">va_arg</span></code>. </p> <p>See also:</p>
<ul> <li><a href="#cVaCopy">@cVaCopy</a></li> <li><a href="#cVaEnd">@cVaEnd</a></li> <li><a href="#cVaStart">@cVaStart</a></li> </ul> <h3 id="cVaCopy">@cVaCopy </h3> <pre data-language="zig">@cVaCopy(src: *std.builtin.VaList) std.builtin.VaList</pre> <p> Implements the C macro <code><span class="line">va_copy</span></code>. </p> <p>See also:</p>
<ul> <li><a href="#cVaArg">@cVaArg</a></li> <li><a href="#cVaEnd">@cVaEnd</a></li> <li><a href="#cVaStart">@cVaStart</a></li> </ul> <h3 id="cVaEnd">@cVaEnd </h3> <pre data-language="zig">@cVaEnd(src: *std.builtin.VaList) void</pre> <p> Implements the C macro <code><span class="line">va_end</span></code>. </p> <p>See also:</p>
<ul> <li><a href="#cVaArg">@cVaArg</a></li> <li><a href="#cVaCopy">@cVaCopy</a></li> <li><a href="#cVaStart">@cVaStart</a></li> </ul> <h3 id="cVaStart">@cVaStart </h3> <pre data-language="zig">@cVaStart() std.builtin.VaList</pre> <p> Implements the C macro <code><span class="line">va_start</span></code>. Only valid inside a variadic function. </p> <p>See also:</p>
<ul> <li><a href="#cVaArg">@cVaArg</a></li> <li><a href="#cVaCopy">@cVaCopy</a></li> <li><a href="#cVaEnd">@cVaEnd</a></li> </ul> <h3 id="divExact">@divExact </h3> <pre data-language="zig">@divExact(numerator: T, denominator: T) T</pre> <p> Exact division. Caller guarantees <code><span class="line">denominator != <span class="tok-number">0</span></span></code> and <code><span class="line"><span class="tok-builtin">@divTrunc</span>(numerator, denominator) * denominator == numerator</span></code>. </p> <ul> <li><code><span class="line"><span class="tok-builtin">@divExact</span>(<span class="tok-number">6</span>, <span class="tok-number">3</span>) == <span class="tok-number">2</span></span></code></li> <li><code><span class="line"><span class="tok-builtin">@divExact</span>(a, b) * b == a</span></code></li> </ul> <p>For a function that returns a possible error code, use <code><span class="line"><span class="tok-builtin">@import</span>(<span class="tok-str">"std"</span>).math.divExact</span></code>.</p> <p>See also:</p>
<ul> <li><a href="#divTrunc">@divTrunc</a></li> <li><a href="#divFloor">@divFloor</a></li> </ul> <h3 id="divFloor">@divFloor </h3> <pre data-language="zig">@divFloor(numerator: T, denominator: T) T</pre> <p> Floored division. Rounds toward negative infinity. For unsigned integers it is the same as <code><span class="line">numerator / denominator</span></code>. Caller guarantees <code><span class="line">denominator != <span class="tok-number">0</span></span></code> and <code><span class="line">!(<span class="tok-builtin">@typeInfo</span>(T) == .Int <span class="tok-kw">and</span> T.is_signed <span class="tok-kw">and</span> numerator == std.math.minInt(T) <span class="tok-kw">and</span> denominator == -<span class="tok-number">1</span>)</span></code>. </p> <ul> <li><code><span class="line"><span class="tok-builtin">@divFloor</span>(-<span class="tok-number">5</span>, <span class="tok-number">3</span>) == -<span class="tok-number">2</span></span></code></li> <li><code><span class="line">(<span class="tok-builtin">@divFloor</span>(a, b) * b) + <span class="tok-builtin">@mod</span>(a, b) == a</span></code></li> </ul> <p>For a function that returns a possible error code, use <code><span class="line"><span class="tok-builtin">@import</span>(<span class="tok-str">"std"</span>).math.divFloor</span></code>.</p> <p>See also:</p>
<ul> <li><a href="#divTrunc">@divTrunc</a></li> <li><a href="#divExact">@divExact</a></li> </ul> <h3 id="divTrunc">@divTrunc </h3> <pre data-language="zig">@divTrunc(numerator: T, denominator: T) T</pre> <p> Truncated division. Rounds toward zero. For unsigned integers it is the same as <code><span class="line">numerator / denominator</span></code>. Caller guarantees <code><span class="line">denominator != <span class="tok-number">0</span></span></code> and <code><span class="line">!(<span class="tok-builtin">@typeInfo</span>(T) == .Int <span class="tok-kw">and</span> T.is_signed <span class="tok-kw">and</span> numerator == std.math.minInt(T) <span class="tok-kw">and</span> denominator == -<span class="tok-number">1</span>)</span></code>. </p> <ul> <li><code><span class="line"><span class="tok-builtin">@divTrunc</span>(-<span class="tok-number">5</span>, <span class="tok-number">3</span>) == -<span class="tok-number">1</span></span></code></li> <li><code><span class="line">(<span class="tok-builtin">@divTrunc</span>(a, b) * b) + <span class="tok-builtin">@rem</span>(a, b) == a</span></code></li> </ul> <p>For a function that returns a possible error code, use <code><span class="line"><span class="tok-builtin">@import</span>(<span class="tok-str">"std"</span>).math.divTrunc</span></code>.</p> <p>See also:</p>
<ul> <li><a href="#divFloor">@divFloor</a></li> <li><a href="#divExact">@divExact</a></li> </ul> <h3 id="embedFile">@embedFile </h3> <pre data-language="zig">@embedFile(comptime path: []const u8) *const [N:0]u8</pre> <p> This function returns a compile time constant pointer to null-terminated, fixed-size array with length equal to the byte count of the file given by <code><span class="line">path</span></code>. The contents of the array are the contents of the file. This is equivalent to a <a href="#String-Literals-and-Unicode-Code-Point-Literals">string literal</a> with the file contents. </p> <p> <code><span class="line">path</span></code> is absolute or relative to the current file, just like <code><span class="line"><span class="tok-builtin">@import</span></span></code>. </p> <p>See also:</p>
<ul> <li><a href="#import">@import</a></li> </ul> <h3 id="enumFromInt">@enumFromInt </h3> <pre data-language="zig">@enumFromInt(integer: anytype) anytype</pre> <p> Converts an integer into an <a href="#enum">enum</a> value. The return type is the inferred result type. </p> <p> Attempting to convert an integer which represents no value in the chosen enum type invokes safety-checked <a href="#Undefined-Behavior">Undefined Behavior</a>. </p> <p>See also:</p>
<ul> <li><a href="#intFromEnum">@intFromEnum</a></li> </ul> <h3 id="errorFromInt">@errorFromInt </h3> <pre data-language="zig">@errorFromInt(value: std.meta.Int(.unsigned, @sizeOf(anyerror) * 8)) anyerror</pre> <p> Converts from the integer representation of an error into <a href="#The-Global-Error-Set">The Global Error Set</a> type. </p> <p> It is generally recommended to avoid this cast, as the integer representation of an error is not stable across source code changes. </p> <p> Attempting to convert an integer that does not correspond to any error results in safety-protected <a href="#Undefined-Behavior">Undefined Behavior</a>. </p> <p>See also:</p>
<ul> <li><a href="#intFromError">@intFromError</a></li> </ul> <h3 id="errorName">@errorName </h3> <pre data-language="zig">@errorName(err: anyerror) [:0]const u8</pre> <p> This function returns the string representation of an error. The string representation of <code><span class="line"><span class="tok-kw">error</span>.OutOfMem</span></code> is <code><span class="line"><span class="tok-str">"OutOfMem"</span></span></code>. </p> <p> If there are no calls to <code><span class="line"><span class="tok-builtin">@errorName</span></span></code> in an entire application, or all calls have a compile-time known value for <code><span class="line">err</span></code>, then no error name table will be generated. </p> <h3 id="errorReturnTrace">@errorReturnTrace </h3> <pre data-language="zig">@errorReturnTrace() ?*builtin.StackTrace</pre> <p> If the binary is built with error return tracing, and this function is invoked in a function that calls a function with an error or error union return type, returns a stack trace object. Otherwise returns <a href="#null">null</a>. </p> <h3 id="errSetCast">@errSetCast </h3> <pre data-language="zig">@errSetCast(value: anytype) anytype</pre> <p> Converts an error value from one error set to another error set. The return type is the inferred result type. Attempting to convert an error which is not in the destination error set results in safety-protected <a href="#Undefined-Behavior">Undefined Behavior</a>. </p> <h3 id="export">@export </h3> <pre data-language="zig">@export(declaration, comptime options: std.builtin.ExportOptions) void</pre> <p> Creates a symbol in the output object file. </p> <p> <code>declaration</code> must be one of two things: </p> <ul> <li>An identifier (<code><span class="line">x</span></code>) identifying a <a href="#Functions">function</a> or a <a href="#Container-Level-Variables">variable</a>.</li> <li>Field access (<code><span class="line">x.y</span></code>) looking up a <a href="#Functions">function</a> or a <a href="#Container-Level-Variables">variable</a>.</li> </ul> <p> This builtin can be called from a <a href="#comptime">comptime</a> block to conditionally export symbols. When <code>declaration</code> is a function with the C calling convention and <code><span class="line">options.linkage</span></code> is <code><span class="line">Strong</span></code>, this is equivalent to the <code><span class="line"><span class="tok-kw">export</span></span></code> keyword used on a function: </p> <figure><figcaption class="zig-cap"><cite class="file">export_builtin.zig</cite></figcaption><pre data-language="zig">comptime {
    @export(internalName, .{ .name = "foo", .linkage = .Strong });
}

fn internalName() callconv(.C) void {}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig build-obj export_builtin.zig</kbd>
</samp></pre></figure> <p>This is equivalent to:</p> <figure><figcaption class="zig-cap"><cite class="file">export_builtin_equivalent_code.zig</cite></figcaption><pre data-language="zig">export fn foo() void {}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig build-obj export_builtin_equivalent_code.zig</kbd>
</samp></pre></figure> <p>Note that even when using <code><span class="line"><span class="tok-kw">export</span></span></code>, the <code><span class="line">@"foo"</span></code> syntax for <a href="#Identifiers">identifiers</a> can be used to choose any string for the symbol name:</p> <figure><figcaption class="zig-cap"><cite class="file">export_any_symbol_name.zig</cite></figcaption><pre data-language="zig">export fn @"A function name that is a complete sentence."() void {}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig build-obj export_any_symbol_name.zig</kbd>
</samp></pre></figure> <p> When looking at the resulting object, you can see the symbol is used verbatim: </p> <pre data-language="zig">00000000000001f0 T A function name that is a complete sentence.</pre> <p>See also:</p>
<ul> <li><a href="#Exporting-a-C-Library">Exporting a C Library</a></li> </ul> <h3 id="extern">@extern </h3> <pre data-language="zig">@extern(T: type, comptime options: std.builtin.ExternOptions) T</pre> <p> Creates a reference to an external symbol in the output object file. T must be a pointer type. </p> <p>See also:</p>
<ul> <li><a href="#export">@export</a></li> </ul> <h3 id="fence">@fence </h3> <pre data-language="zig">@fence(order: AtomicOrder) void</pre> <p> The <code><span class="line">fence</span></code> function is used to introduce happens-before edges between operations. </p> <p> <code><span class="line">AtomicOrder</span></code> can be found with <code><span class="line"><span class="tok-builtin">@import</span>(<span class="tok-str">"std"</span>).builtin.AtomicOrder</span></code>. </p> <p>See also:</p>
<ul> <li><a href="#atomicStore">@atomicStore</a></li> <li><a href="#atomicLoad">@atomicLoad</a></li> <li><a href="#atomicRmw">@atomicRmw</a></li> <li><a href="#cmpxchgWeak">@cmpxchgWeak</a></li> <li><a href="#cmpxchgStrong">@cmpxchgStrong</a></li> </ul> <h3 id="field">@field </h3> <pre data-language="zig">@field(lhs: anytype, comptime field_name: []const u8) (field)</pre> <p>Performs field access by a compile-time string. Works on both fields and declarations. </p> <figure><figcaption class="zig-cap"><cite class="file">test_field_builtin.zig</cite></figcaption><pre data-language="zig">const std = @import("std");

const Point = struct {
    x: u32,
    y: u32,

    pub var z: u32 = 1;
};

test "field access by string" {
    const expect = std.testing.expect;
    var p = Point{ .x = 0, .y = 0 };

    @field(p, "x") = 4;
    @field(p, "y") = @field(p, "x") + 1;

    try expect(@field(p, "x") == 4);
    try expect(@field(p, "y") == 5);
}

test "decl access by string" {
    const expect = std.testing.expect;

    try expect(@field(Point, "z") == 1);

    @field(Point, "z") = 2;
    try expect(@field(Point, "z") == 2);
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_field_builtin.zig</kbd>
1/2 test.field access by string... OK
2/2 test.decl access by string... OK
All 2 tests passed.
</samp></pre></figure> <h3 id="fieldParentPtr">@fieldParentPtr </h3> <pre data-language="zig">@fieldParentPtr(comptime ParentType: type, comptime field_name: []const u8,
    field_ptr: *T) *ParentType</pre> <p> Given a pointer to a field, returns the base pointer of a struct. </p> <h3 id="floatCast">@floatCast </h3> <pre data-language="zig">@floatCast(value: anytype) anytype</pre> <p> Convert from one float type to another. This cast is safe, but may cause the numeric value to lose precision. The return type is the inferred result type. </p> <h3 id="floatFromInt">@floatFromInt </h3> <pre data-language="zig">@floatFromInt(int: anytype) anytype</pre> <p> Converts an integer to the closest floating point representation. The return type is the inferred result type. To convert the other way, use <a href="#intFromFloat">@intFromFloat</a>. This cast is always safe. </p> <h3 id="frameAddress">@frameAddress </h3> <pre data-language="zig">@frameAddress() usize</pre> <p> This function returns the base pointer of the current stack frame. </p> <p> The implications of this are target-specific and not consistent across all platforms. The frame address may not be available in release mode due to aggressive optimizations. </p> <p> This function is only valid within function scope. </p> <h3 id="hasDecl">@hasDecl </h3> <pre data-language="zig">@hasDecl(comptime Container: type, comptime name: []const u8) bool</pre> <p> Returns whether or not a <a href="#Containers">container</a> has a declaration matching <code><span class="line">name</span></code>. </p> <figure><figcaption class="zig-cap"><cite class="file">test_hasDecl_builtin.zig</cite></figcaption><pre data-language="zig">const std = @import("std");
const expect = std.testing.expect;

const Foo = struct {
    nope: i32,

    pub var blah = "xxx";
    const hi = 1;
};

test "@hasDecl" {
    try expect(@hasDecl(Foo, "blah"));

    // Even though `hi` is private, @hasDecl returns true because this test is
    // in the same file scope as Foo. It would return false if Foo was declared
    // in a different file.
    try expect(@hasDecl(Foo, "hi"));

    // @hasDecl is for declarations; not fields.
    try expect(!@hasDecl(Foo, "nope"));
    try expect(!@hasDecl(Foo, "nope1234"));
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_hasDecl_builtin.zig</kbd>
1/1 test.@hasDecl... OK
All 1 tests passed.
</samp></pre></figure> <p>See also:</p>
<ul> <li><a href="#hasField">@hasField</a></li> </ul> <h3 id="hasField">@hasField </h3> <pre data-language="zig">@hasField(comptime Container: type, comptime name: []const u8) bool</pre> <p>Returns whether the field name of a struct, union, or enum exists.</p> <p> The result is a compile time constant. </p> <p> It does not include functions, variables, or constants. </p> <p>See also:</p>
<ul> <li><a href="#hasDecl">@hasDecl</a></li> </ul> <h3 id="import">@import </h3> <pre data-language="zig">@import(comptime path: []const u8) type</pre> <p> This function finds a zig file corresponding to <code><span class="line">path</span></code> and adds it to the build, if it is not already added. </p> <p> Zig source files are implicitly structs, with a name equal to the file's basename with the extension truncated. <code><span class="line"><span class="tok-builtin">@import</span></span></code> returns the struct type corresponding to the file. </p> <p> Declarations which have the <code><span class="line"><span class="tok-kw">pub</span></span></code> keyword may be referenced from a different source file than the one they are declared in. </p> <p> <code><span class="line">path</span></code> can be a relative path or it can be the name of a package. If it is a relative path, it is relative to the file that contains the <code><span class="line"><span class="tok-builtin">@import</span></span></code> function call. </p> <p> The following packages are always available: </p> <ul> <li>
<code><span class="line"><span class="tok-builtin">@import</span>(<span class="tok-str">"std"</span>)</span></code> - Zig Standard Library</li> <li>
<code><span class="line"><span class="tok-builtin">@import</span>(<span class="tok-str">"builtin"</span>)</span></code> - Target-specific information The command <code>zig build-exe --show-builtin</code> outputs the source to stdout for reference. </li> <li>
<code><span class="line"><span class="tok-builtin">@import</span>(<span class="tok-str">"root"</span>)</span></code> - Root source file This is usually <code>src/main.zig</code> but depends on what file is built. </li> </ul> <p>See also:</p>
<ul> <li><a href="#Compile-Variables">Compile Variables</a></li> <li><a href="#embedFile">@embedFile</a></li> </ul> <h3 id="inComptime">@inComptime </h3> <pre data-language="zig">@inComptime() bool</pre> <p> Returns whether the builtin was run in a <code><span class="line"><span class="tok-kw">comptime</span></span></code> context. The result is a compile-time constant. </p> <p> This can be used to provide alternative, comptime-friendly implementations of functions. It should not be used, for instance, to exclude certain functions from being evaluated at comptime. </p> <p>See also:</p>
<ul> <li><a href="#comptime">comptime</a></li> </ul> <h3 id="intCast">@intCast </h3> <pre data-language="zig">@intCast(int: anytype) anytype</pre> <p> Converts an integer to another integer while keeping the same numerical value. The return type is the inferred result type. Attempting to convert a number which is out of range of the destination type results in safety-protected <a href="#Undefined-Behavior">Undefined Behavior</a>. </p> <figure><figcaption class="zig-cap"><cite class="file">test_intCast_builtin.zig</cite></figcaption><pre data-language="zig">test "integer cast panic" {
    var a: u16 = 0xabcd;
    var b: u8 = @intCast(a);
    _ = b;
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_intCast_builtin.zig</kbd>
1/1 test.integer cast panic... thread 2455340 panic: integer cast truncated bits
/home/ci/actions-runner/_work/zig-bootstrap/zig/docgen_tmp/test_intCast_builtin.zig:3:17: 0x2241c7 in test.integer cast panic (test)
    var b: u8 = @intCast(a);
                ^
/home/ci/actions-runner/_work/zig-bootstrap/out/host/lib/zig/test_runner.zig:176:28: 0x22d3d9 in mainTerminal (test)
        } else test_fn.func();
                           ^
/home/ci/actions-runner/_work/zig-bootstrap/out/host/lib/zig/test_runner.zig:36:28: 0x22522a in main (test)
        return mainTerminal();
                           ^
/home/ci/actions-runner/_work/zig-bootstrap/out/host/lib/zig/std/start.zig:564:22: 0x224702 in posixCallMainAndExit (test)
            root.main();
                     ^
/home/ci/actions-runner/_work/zig-bootstrap/out/host/lib/zig/std/start.zig:243:5: 0x224251 in _start (test)
    asm volatile (switch (native_arch) {
    ^
???:?:?: 0x0 in ??? (???)
error: the following test command crashed:
/home/ci/actions-runner/_work/zig-bootstrap/out/zig-local-cache/o/34bca970f2215feafead7412bacb3a2c/test
</samp></pre></figure> <p> To truncate the significant bits of a number out of range of the destination type, use <a href="#truncate">@truncate</a>. </p> <p> If <code><span class="line">T</span></code> is <code><span class="line"><span class="tok-type">comptime_int</span></span></code>, then this is semantically equivalent to <a href="#Type-Coercion">Type Coercion</a>. </p> <h3 id="intFromBool">@intFromBool </h3> <pre data-language="zig">@intFromBool(value: bool) u1</pre> <p> Converts <code><span class="line"><span class="tok-null">true</span></span></code> to <code><span class="line"><span class="tok-builtin">@as</span>(<span class="tok-type">u1</span>, <span class="tok-number">1</span>)</span></code> and <code><span class="line"><span class="tok-null">false</span></span></code> to <code><span class="line"><span class="tok-builtin">@as</span>(<span class="tok-type">u1</span>, <span class="tok-number">0</span>)</span></code>. </p> <h3 id="intFromEnum">@intFromEnum </h3> <pre data-language="zig">@intFromEnum(enum_or_tagged_union: anytype) anytype</pre> <p> Converts an enumeration value into its integer tag type. When a tagged union is passed, the tag value is used as the enumeration value. </p> <p> If there is only one possible enum value, the result is a <code><span class="line"><span class="tok-type">comptime_int</span></span></code> known at <a href="#comptime">comptime</a>. </p> <p>See also:</p>
<ul> <li><a href="#enumFromInt">@enumFromInt</a></li> </ul> <h3 id="intFromError">@intFromError </h3> <pre data-language="zig">@intFromError(err: anytype) std.meta.Int(.unsigned, @sizeOf(anyerror) * 8)</pre> <p> Supports the following types: </p> <ul> <li><a href="#The-Global-Error-Set">The Global Error Set</a></li> <li><a href="#Error-Set-Type">Error Set Type</a></li> <li><a href="#Error-Union-Type">Error Union Type</a></li> </ul> <p> Converts an error to the integer representation of an error. </p> <p> It is generally recommended to avoid this cast, as the integer representation of an error is not stable across source code changes. </p> <p>See also:</p>
<ul> <li><a href="#errorFromInt">@errorFromInt</a></li> </ul> <h3 id="intFromFloat">@intFromFloat </h3> <pre data-language="zig">@intFromFloat(float: anytype) anytype</pre> <p> Converts the integer part of a floating point number to the inferred result type. </p> <p> If the integer part of the floating point number cannot fit in the destination type, it invokes safety-checked <a href="#Undefined-Behavior">Undefined Behavior</a>. </p> <p>See also:</p>
<ul> <li><a href="#floatFromInt">@floatFromInt</a></li> </ul> <h3 id="intFromPtr">@intFromPtr </h3> <pre data-language="zig">@intFromPtr(value: anytype) usize</pre> <p> Converts <code><span class="line">value</span></code> to a <code><span class="line"><span class="tok-type">usize</span></span></code> which is the address of the pointer. <code><span class="line">value</span></code> can be <code><span class="line">*T</span></code> or <code><span class="line">?*T</span></code>. </p> <p>To convert the other way, use <a href="#ptrFromInt">@ptrFromInt</a></p> <h3 id="max">@max </h3> <pre data-language="zig">@max(a: T, b: T) T</pre> <p> Returns the maximum value of <code><span class="line">a</span></code> and <code><span class="line">b</span></code>. This builtin accepts integers, floats, and vectors of either. In the latter case, the operation is performed element wise. </p> <p> NaNs are handled as follows: if one of the operands of a (pairwise) operation is NaN, the other operand is returned. If both operands are NaN, NaN is returned. </p> <p>See also:</p>
<ul> <li><a href="#min">@min</a></li> <li><a href="#Vectors">Vectors</a></li> </ul> <h3 id="memcpy">@memcpy </h3> <pre data-language="zig">@memcpy(noalias dest, noalias source) void</pre> <p>This function copies bytes from one region of memory to another.</p> <p><code><span class="line">dest</span></code> must be a mutable slice, a mutable pointer to an array, or a mutable many-item <a href="#Pointers">pointer</a>. It may have any alignment, and it may have any element type.</p> <p><code><span class="line">source</span></code> must be a slice, a pointer to an array, or a many-item <a href="#Pointers">pointer</a>. It may have any alignment, and it may have any element type.</p> <p>The <code><span class="line">source</span></code> element type must support <a href="#Type-Coercion">Type Coercion</a> into the <code><span class="line">dest</span></code> element type. The element types may have different ABI size, however, that may incur a performance penalty.</p> <p>Similar to <a href="#for">for</a> loops, at least one of <code><span class="line">source</span></code> and <code><span class="line">dest</span></code> must provide a length, and if two lengths are provided, they must be equal.</p> <p>Finally, the two memory regions must not overlap.</p> <h3 id="memset">@memset </h3> <pre data-language="zig">@memset(dest, elem) void</pre> <p>This function sets all the elements of a memory region to <code><span class="line">elem</span></code>.</p> <p><code><span class="line">dest</span></code> must be a mutable slice or a mutable pointer to an array. It may have any alignment, and it may have any element type.</p> <p><code><span class="line">elem</span></code> is coerced to the element type of <code><span class="line">dest</span></code>.</p> <p>For securely zeroing out sensitive contents from memory, you should use <code><span class="line">std.crypto.utils.secureZero</span></code></p> <h3 id="min">@min </h3> <pre data-language="zig">@min(a: T, b: T) T</pre> <p> Returns the minimum value of <code><span class="line">a</span></code> and <code><span class="line">b</span></code>. This builtin accepts integers, floats, and vectors of either. In the latter case, the operation is performed element wise. </p> <p> NaNs are handled as follows: if one of the operands of a (pairwise) operation is NaN, the other operand is returned. If both operands are NaN, NaN is returned. </p> <p>See also:</p>
<ul> <li><a href="#max">@max</a></li> <li><a href="#Vectors">Vectors</a></li> </ul> <h3 id="wasmMemorySize">@wasmMemorySize </h3> <pre data-language="zig">@wasmMemorySize(index: u32) u32</pre> <p> This function returns the size of the Wasm memory identified by <code><span class="line">index</span></code> as an unsigned value in units of Wasm pages. Note that each Wasm page is 64KB in size. </p> <p> This function is a low level intrinsic with no safety mechanisms usually useful for allocator designers targeting Wasm. So unless you are writing a new allocator from scratch, you should use something like <code><span class="line"><span class="tok-builtin">@import</span>(<span class="tok-str">"std"</span>).heap.WasmPageAllocator</span></code>. </p> <p>See also:</p>
<ul> <li><a href="#wasmMemoryGrow">@wasmMemoryGrow</a></li> </ul> <h3 id="wasmMemoryGrow">@wasmMemoryGrow </h3> <pre data-language="zig">@wasmMemoryGrow(index: u32, delta: u32) i32</pre> <p> This function increases the size of the Wasm memory identified by <code><span class="line">index</span></code> by <code><span class="line">delta</span></code> in units of unsigned number of Wasm pages. Note that each Wasm page is 64KB in size. On success, returns previous memory size; on failure, if the allocation fails, returns -1. </p> <p> This function is a low level intrinsic with no safety mechanisms usually useful for allocator designers targeting Wasm. So unless you are writing a new allocator from scratch, you should use something like <code><span class="line"><span class="tok-builtin">@import</span>(<span class="tok-str">"std"</span>).heap.WasmPageAllocator</span></code>. </p> <figure><figcaption class="zig-cap"><cite class="file">test_wasmMemoryGrow_builtin.zig</cite></figcaption><pre data-language="zig">const std = @import("std");
const native_arch = @import("builtin").target.cpu.arch;
const expect = std.testing.expect;

test "@wasmMemoryGrow" {
    if (native_arch != .wasm32) return error.SkipZigTest;

    var prev = @wasmMemorySize(0);
    try expect(prev == @wasmMemoryGrow(0, 1));
    try expect(prev + 1 == @wasmMemorySize(0));
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_wasmMemoryGrow_builtin.zig</kbd>
1/1 test.@wasmMemoryGrow... SKIP
0 passed; 1 skipped; 0 failed.
</samp></pre></figure> <p>See also:</p>
<ul> <li><a href="#wasmMemorySize">@wasmMemorySize</a></li> </ul> <h3 id="mod">@mod </h3> <pre data-language="zig">@mod(numerator: T, denominator: T) T</pre> <p> Modulus division. For unsigned integers this is the same as <code><span class="line">numerator % denominator</span></code>. Caller guarantees <code><span class="line">denominator &gt; <span class="tok-number">0</span></span></code>, otherwise the operation will result in a <a href="#Remainder-Division-by-Zero">Remainder Division by Zero</a> when runtime safety checks are enabled. </p> <ul> <li><code><span class="line"><span class="tok-builtin">@mod</span>(-<span class="tok-number">5</span>, <span class="tok-number">3</span>) == <span class="tok-number">1</span></span></code></li> <li><code><span class="line">(<span class="tok-builtin">@divFloor</span>(a, b) * b) + <span class="tok-builtin">@mod</span>(a, b) == a</span></code></li> </ul> <p>For a function that returns an error code, see <code><span class="line"><span class="tok-builtin">@import</span>(<span class="tok-str">"std"</span>).math.mod</span></code>.</p> <p>See also:</p>
<ul> <li><a href="#rem">@rem</a></li> </ul> <h3 id="mulWithOverflow">@mulWithOverflow </h3> <pre data-language="zig">@mulWithOverflow(a: anytype, b: anytype) struct { @TypeOf(a, b), u1 }</pre> <p> Performs <code><span class="line">a * b</span></code> and returns a tuple with the result and a possible overflow bit. </p> <h3 id="panic">@panic </h3> <pre data-language="zig">@panic(message: []const u8) noreturn</pre> <p> Invokes the panic handler function. By default the panic handler function calls the public <code><span class="line">panic</span></code> function exposed in the root source file, or if there is not one specified, the <code><span class="line">std.builtin.default_panic</span></code> function from <code><span class="line">std/builtin.zig</span></code>. </p> <p>Generally it is better to use <code><span class="line"><span class="tok-builtin">@import</span>(<span class="tok-str">"std"</span>).debug.panic</span></code>. However, <code><span class="line"><span class="tok-builtin">@panic</span></span></code> can be useful for 2 scenarios: </p> <ul> <li>From library code, calling the programmer's panic function if they exposed one in the root source file.</li> <li>When mixing C and Zig code, calling the canonical panic implementation across multiple .o files.</li> </ul> <p>See also:</p>
<ul> <li><a href="#Root-Source-File">Root Source File</a></li> </ul> <h3 id="popCount">@popCount </h3> <pre data-language="zig">@popCount(operand: anytype) anytype</pre> <p><code><span class="line"><span class="tok-builtin">@TypeOf</span>(operand)</span></code> must be an integer type.</p> <p><code><span class="line">operand</span></code> may be an <a href="#Integers">integer</a> or <a href="#Vectors">vector</a>.</p> <p> Counts the number of bits set in an integer - "population count". </p> <p> If <code><span class="line">operand</span></code> is a <a href="#comptime">comptime</a>-known integer, the return type is <code><span class="line"><span class="tok-type">comptime_int</span></span></code>. Otherwise, the return type is an unsigned integer or vector of unsigned integers with the minimum number of bits that can represent the bit count of the integer type. </p> <p>See also:</p>
<ul> <li><a href="#ctz">@ctz</a></li> <li><a href="#clz">@clz</a></li> </ul> <h3 id="prefetch">@prefetch </h3> <pre data-language="zig">@prefetch(ptr: anytype, comptime options: std.builtin.PrefetchOptions) void</pre> <p> This builtin tells the compiler to emit a prefetch instruction if supported by the target CPU. If the target CPU does not support the requested prefetch instruction, this builtin is a no-op. This function has no effect on the behavior of the program, only on the performance characteristics. </p> <p> The <code><span class="line">ptr</span></code> argument may be any pointer type and determines the memory address to prefetch. This function does not dereference the pointer, it is perfectly legal to pass a pointer to invalid memory to this function and no illegal behavior will result. </p> <p> The <code><span class="line">options</span></code> argument is the following struct: </p> <figure><figcaption class="zig-cap"><cite class="file">builtin.zig</cite></figcaption><pre data-language="zig">/// This data structure is used by the Zig language code generation and
/// therefore must be kept in sync with the compiler implementation.
pub const PrefetchOptions = struct {
    /// Whether the prefetch should prepare for a read or a write.
    rw: Rw = .read,
    /// The data's locality in an inclusive range from 0 to 3.
    ///
    /// 0 means no temporal locality. That is, the data can be immediately
    /// dropped from the cache after it is accessed.
    ///
    /// 3 means high temporal locality. That is, the data should be kept in
    /// the cache as it is likely to be accessed again soon.
    locality: u2 = 3,
    /// The cache that the prefetch should be preformed on.
    cache: Cache = .data,

    pub const Rw = enum(u1) {
        read,
        write,
    };

    pub const Cache = enum(u1) {
        instruction,
        data,
    };
};</pre></figure> <h3 id="ptrCast">@ptrCast </h3> <pre data-language="zig">@ptrCast(value: anytype) anytype</pre> <p> Converts a pointer of one type to a pointer of another type. The return type is the inferred result type. </p> <p> <a href="#Optional-Pointers">Optional Pointers</a> are allowed. Casting an optional pointer which is <a href="#null">null</a> to a non-optional pointer invokes safety-checked <a href="#Undefined-Behavior">Undefined Behavior</a>. </p> <p> <code><span class="line"><span class="tok-builtin">@ptrCast</span></span></code> cannot be used for: </p> <ul> <li>Removing <code><span class="line"><span class="tok-kw">const</span></span></code> qualifier, use <a href="#constCast">@constCast</a>.</li> <li>Removing <code><span class="line"><span class="tok-kw">volatile</span></span></code> qualifier, use <a href="#volatileCast">@volatileCast</a>.</li> <li>Changing pointer address space, use <a href="#addrSpaceCast">@addrSpaceCast</a>.</li> <li>Increasing pointer alignment, use <a href="#alignCast">@alignCast</a>.</li> <li>Casting a non-slice pointer to a slice, use slicing syntax <code><span class="line">ptr[start..end]</span></code>.</li> </ul> <h3 id="ptrFromInt">@ptrFromInt </h3> <pre data-language="zig">@ptrFromInt(address: usize) anytype</pre> <p> Converts an integer to a <a href="#Pointers">pointer</a>. The return type is the inferred result type. To convert the other way, use <a href="#intFromPtr">@intFromPtr</a>. Casting an address of 0 to a destination type which in not <a href="#Optional-Pointers">optional</a> and does not have the <code><span class="line"><span class="tok-kw">allowzero</span></span></code> attribute will result in a <a href="#Pointer-Cast-Invalid-Null">Pointer Cast Invalid Null</a> panic when runtime safety checks are enabled. </p> <p> If the destination pointer type does not allow address zero and <code><span class="line">address</span></code> is zero, this invokes safety-checked <a href="#Undefined-Behavior">Undefined Behavior</a>. </p> <h3 id="rem">@rem </h3> <pre data-language="zig">@rem(numerator: T, denominator: T) T</pre> <p> Remainder division. For unsigned integers this is the same as <code><span class="line">numerator % denominator</span></code>. Caller guarantees <code><span class="line">denominator &gt; <span class="tok-number">0</span></span></code>, otherwise the operation will result in a <a href="#Remainder-Division-by-Zero">Remainder Division by Zero</a> when runtime safety checks are enabled. </p> <ul> <li><code><span class="line"><span class="tok-builtin">@rem</span>(-<span class="tok-number">5</span>, <span class="tok-number">3</span>) == -<span class="tok-number">2</span></span></code></li> <li><code><span class="line">(<span class="tok-builtin">@divTrunc</span>(a, b) * b) + <span class="tok-builtin">@rem</span>(a, b) == a</span></code></li> </ul> <p>For a function that returns an error code, see <code><span class="line"><span class="tok-builtin">@import</span>(<span class="tok-str">"std"</span>).math.rem</span></code>.</p> <p>See also:</p>
<ul> <li><a href="#mod">@mod</a></li> </ul> <h3 id="returnAddress">@returnAddress </h3> <pre data-language="zig">@returnAddress() usize</pre> <p> This function returns the address of the next machine code instruction that will be executed when the current function returns. </p> <p> The implications of this are target-specific and not consistent across all platforms. </p> <p> This function is only valid within function scope. If the function gets inlined into a calling function, the returned address will apply to the calling function. </p> <h3 id="select">@select </h3> <pre data-language="zig">@select(comptime T: type, pred: @Vector(len, bool), a: @Vector(len, T), b: @Vector(len, T)) @Vector(len, T)</pre> <p> Selects values element-wise from <code><span class="line">a</span></code> or <code><span class="line">b</span></code> based on <code><span class="line">pred</span></code>. If <code><span class="line">pred[i]</span></code> is <code><span class="line"><span class="tok-null">true</span></span></code>, the corresponding element in the result will be <code><span class="line">a[i]</span></code> and otherwise <code><span class="line">b[i]</span></code>. </p> <p>See also:</p>
<ul> <li><a href="#Vectors">Vectors</a></li> </ul> <h3 id="setAlignStack">@setAlignStack </h3> <pre data-language="zig">@setAlignStack(comptime alignment: u29) void</pre> <p> Ensures that a function will have a stack alignment of at least <code><span class="line">alignment</span></code> bytes. </p> <h3 id="setCold">@setCold </h3> <pre data-language="zig">@setCold(comptime is_cold: bool) void</pre> <p> Tells the optimizer that a function is rarely called. </p> <h3 id="setEvalBranchQuota">@setEvalBranchQuota </h3> <pre data-language="zig">@setEvalBranchQuota(comptime new_quota: u32) void</pre> <p> Changes the maximum number of backwards branches that compile-time code execution can use before giving up and making a compile error. </p> <p> If the <code><span class="line">new_quota</span></code> is smaller than the default quota (<code><span class="line"><span class="tok-number">1000</span></span></code>) or a previously explicitly set quota, it is ignored. </p> <p> Example: </p> <figure><figcaption class="zig-cap"><cite class="file">test_without_setEvalBranchQuota_builtin.zig</cite></figcaption><pre data-language="zig">test "foo" {
    comptime {
        var i = 0;
        while (i &lt; 1001) : (i += 1) {}
    }
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_without_setEvalBranchQuota_builtin.zig</kbd>
<span class="sgr-1m">docgen_tmp/test_without_setEvalBranchQuota_builtin.zig:4:9: </span><span class="sgr-31m">error: </span><span class="sgr-1m">evaluation exceeded 1000 backwards branches
</span>        while (i &lt; 1001) : (i += 1) {}
        <span class="sgr-32m">^~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
</span><span class="sgr-1m">docgen_tmp/test_without_setEvalBranchQuota_builtin.zig:4:9: </span><span class="sgr-36m">note: </span><span class="sgr-1m">use @setEvalBranchQuota() to raise the branch limit from 1000
</span>
</samp></pre></figure> <p>Now we use <code><span class="line"><span class="tok-builtin">@setEvalBranchQuota</span></span></code>:</p> <figure><figcaption class="zig-cap"><cite class="file">test_setEvalBranchQuota_builtin.zig</cite></figcaption><pre data-language="zig">test "foo" {
    comptime {
        @setEvalBranchQuota(1001);
        var i = 0;
        while (i &lt; 1001) : (i += 1) {}
    }
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_setEvalBranchQuota_builtin.zig</kbd>
1/1 test.foo... OK
All 1 tests passed.
</samp></pre></figure> <p>See also:</p>
<ul> <li><a href="#comptime">comptime</a></li> </ul> <h3 id="setFloatMode">@setFloatMode </h3> <pre data-language="zig">@setFloatMode(comptime mode: @import("std").builtin.FloatMode) void</pre> <p> Sets the floating point mode of the current scope. Possible values are: </p> <figure><figcaption class="zig-cap"><cite class="file">FloatMode.zig</cite></figcaption><pre data-language="zig">pub const FloatMode = enum {
    Strict,
    Optimized,
};</pre></figure> <ul> <li> <code><span class="line">Strict</span></code> (default) - Floating point operations follow strict IEEE compliance. </li> <li> <code><span class="line">Optimized</span></code> - Floating point operations may do all of the following: <ul> <li>Assume the arguments and result are not NaN. Optimizations are required to retain defined behavior over NaNs, but the value of the result is undefined.</li> <li>Assume the arguments and result are not +/-Inf. Optimizations are required to retain defined behavior over +/-Inf, but the value of the result is undefined.</li> <li>Treat the sign of a zero argument or result as insignificant.</li> <li>Use the reciprocal of an argument rather than perform division.</li> <li>Perform floating-point contraction (e.g. fusing a multiply followed by an addition into a fused multiply-add).</li> <li>Perform algebraically equivalent transformations that may change results in floating point (e.g. reassociate).</li> </ul> This is equivalent to <code>-ffast-math</code> in GCC. </li> </ul> <p> The floating point mode is inherited by child scopes, and can be overridden in any scope. You can set the floating point mode in a struct or module scope by using a comptime block. </p> <p>See also:</p>
<ul> <li><a href="#Floating-Point-Operations">Floating Point Operations</a></li> </ul> <h3 id="setRuntimeSafety">@setRuntimeSafety </h3> <pre data-language="zig">@setRuntimeSafety(comptime safety_on: bool) void</pre> <p> Sets whether runtime safety checks are enabled for the scope that contains the function call. </p> <figure><figcaption class="zig-cap"><cite class="file">test_setRuntimeSafety_builtin.zig</cite></figcaption><pre data-language="zig">test "@setRuntimeSafety" {
    // The builtin applies to the scope that it is called in. So here, integer overflow
    // will not be caught in ReleaseFast and ReleaseSmall modes:
    // var x: u8 = 255;
    // x += 1; // undefined behavior in ReleaseFast/ReleaseSmall modes.
    {
        // However this block has safety enabled, so safety checks happen here,
        // even in ReleaseFast and ReleaseSmall modes.
        @setRuntimeSafety(true);
        var x: u8 = 255;
        x += 1;

        {
            // The value can be overridden at any scope. So here integer overflow
            // would not be caught in any build mode.
            @setRuntimeSafety(false);
            // var x: u8 = 255;
            // x += 1; // undefined behavior in all build modes.
        }
    }
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_setRuntimeSafety_builtin.zig -OReleaseFast</kbd>
1/1 test.@setRuntimeSafety... thread 2455486 panic: integer overflow
/home/ci/actions-runner/_work/zig-bootstrap/zig/docgen_tmp/test_setRuntimeSafety_builtin.zig:11:11: 0x20bd61 in test.@setRuntimeSafety (test)
        x += 1;
          ^
/home/ci/actions-runner/_work/zig-bootstrap/out/host/lib/zig/test_runner.zig:176:28: 0x20cbfb in main (test)
        } else test_fn.func();
                           ^
/home/ci/actions-runner/_work/zig-bootstrap/out/host/lib/zig/std/start.zig:564:22: 0x20be54 in posixCallMainAndExit (test)
            root.main();
                     ^
/home/ci/actions-runner/_work/zig-bootstrap/out/host/lib/zig/std/start.zig:243:5: 0x20bd81 in _start (test)
    asm volatile (switch (native_arch) {
    ^
???:?:?: 0x0 in ??? (???)
error: the following test command crashed:
/home/ci/actions-runner/_work/zig-bootstrap/out/zig-local-cache/o/03b1a10fe075355702464d1e69bf1fa9/test
</samp></pre></figure> <p>Note: it is <a href="https://github.com/ziglang/zig/issues/978">planned</a> to replace <code><span class="line"><span class="tok-builtin">@setRuntimeSafety</span></span></code> with <code>@optimizeFor</code></p> <h3 id="shlExact">@shlExact </h3> <pre data-language="zig">@shlExact(value: T, shift_amt: Log2T) T</pre> <p> Performs the left shift operation (<code><span class="line">&lt;&lt;</span></code>). For unsigned integers, the result is <a href="#undefined">undefined</a> if any 1 bits are shifted out. For signed integers, the result is <a href="#undefined">undefined</a> if any bits that disagree with the resultant sign bit are shifted out. </p> <p> The type of <code><span class="line">shift_amt</span></code> is an unsigned integer with <code><span class="line">log2(<span class="tok-builtin">@typeInfo</span>(T).Int.bits)</span></code> bits. This is because <code><span class="line">shift_amt &gt;= <span class="tok-builtin">@typeInfo</span>(T).Int.bits</span></code> is undefined behavior. </p> <p>See also:</p>
<ul> <li><a href="#shrExact">@shrExact</a></li> <li><a href="#shlWithOverflow">@shlWithOverflow</a></li> </ul> <h3 id="shlWithOverflow">@shlWithOverflow </h3> <pre data-language="zig">@shlWithOverflow(a: anytype, shift_amt: Log2T) struct { @TypeOf(a), u1 }</pre> <p> Performs <code><span class="line">a &lt;&lt; b</span></code> and returns a tuple with the result and a possible overflow bit. </p> <p> The type of <code><span class="line">shift_amt</span></code> is an unsigned integer with <code><span class="line">log2(<span class="tok-builtin">@typeInfo</span>(<span class="tok-builtin">@TypeOf</span>(a)).Int.bits)</span></code> bits. This is because <code><span class="line">shift_amt &gt;= <span class="tok-builtin">@typeInfo</span>(<span class="tok-builtin">@TypeOf</span>(a)).Int.bits</span></code> is undefined behavior. </p> <p>See also:</p>
<ul> <li><a href="#shlExact">@shlExact</a></li> <li><a href="#shrExact">@shrExact</a></li> </ul> <h3 id="shrExact">@shrExact </h3> <pre data-language="zig">@shrExact(value: T, shift_amt: Log2T) T</pre> <p> Performs the right shift operation (<code><span class="line">&gt;&gt;</span></code>). Caller guarantees that the shift will not shift any 1 bits out. </p> <p> The type of <code><span class="line">shift_amt</span></code> is an unsigned integer with <code><span class="line">log2(<span class="tok-builtin">@typeInfo</span>(T).Int.bits)</span></code> bits. This is because <code><span class="line">shift_amt &gt;= <span class="tok-builtin">@typeInfo</span>(T).Int.bits</span></code> is undefined behavior. </p> <p>See also:</p>
<ul> <li><a href="#shlExact">@shlExact</a></li> <li><a href="#shlWithOverflow">@shlWithOverflow</a></li> </ul> <h3 id="shuffle">@shuffle </h3> <pre data-language="zig">@shuffle(comptime E: type, a: @Vector(a_len, E), b: @Vector(b_len, E), comptime mask: @Vector(mask_len, i32)) @Vector(mask_len, E)</pre> <p> Constructs a new <a href="#Vectors">vector</a> by selecting elements from <code><span class="line">a</span></code> and <code><span class="line">b</span></code> based on <code><span class="line">mask</span></code>. </p> <p> Each element in <code><span class="line">mask</span></code> selects an element from either <code><span class="line">a</span></code> or <code><span class="line">b</span></code>. Positive numbers select from <code><span class="line">a</span></code> starting at 0. Negative values select from <code><span class="line">b</span></code>, starting at <code><span class="line">-<span class="tok-number">1</span></span></code> and going down. It is recommended to use the <code><span class="line">~</span></code> operator for indexes from <code><span class="line">b</span></code> so that both indexes can start from <code><span class="line"><span class="tok-number">0</span></span></code> (i.e. <code><span class="line">~<span class="tok-builtin">@as</span>(<span class="tok-type">i32</span>, <span class="tok-number">0</span>)</span></code> is <code><span class="line">-<span class="tok-number">1</span></span></code>). </p> <p> For each element of <code><span class="line">mask</span></code>, if it or the selected value from <code><span class="line">a</span></code> or <code><span class="line">b</span></code> is <code><span class="line"><span class="tok-null">undefined</span></span></code>, then the resulting element is <code><span class="line"><span class="tok-null">undefined</span></span></code>. </p> <p> <code><span class="line">a_len</span></code> and <code><span class="line">b_len</span></code> may differ in length. Out-of-bounds element indexes in <code><span class="line">mask</span></code> result in compile errors. </p> <p> If <code><span class="line">a</span></code> or <code><span class="line">b</span></code> is <code><span class="line"><span class="tok-null">undefined</span></span></code>, it is equivalent to a vector of all <code><span class="line"><span class="tok-null">undefined</span></span></code> with the same length as the other vector. If both vectors are <code><span class="line"><span class="tok-null">undefined</span></span></code>, <code><span class="line"><span class="tok-builtin">@shuffle</span></span></code> returns a vector with all elements <code><span class="line"><span class="tok-null">undefined</span></span></code>. </p> <p> <code><span class="line">E</span></code> must be an <a href="#Integers">integer</a>, <a href="#Floats">float</a>, <a href="#Pointers">pointer</a>, or <code><span class="line"><span class="tok-type">bool</span></span></code>. The mask may be any vector length, and its length determines the result length. </p> <figure><figcaption class="zig-cap"><cite class="file">test_shuffle_builtin.zig</cite></figcaption><pre data-language="zig">const std = @import("std");
const expect = std.testing.expect;

test "vector @shuffle" {
    const a = @Vector(7, u8){ 'o', 'l', 'h', 'e', 'r', 'z', 'w' };
    const b = @Vector(4, u8){ 'w', 'd', '!', 'x' };

    // To shuffle within a single vector, pass undefined as the second argument.
    // Notice that we can re-order, duplicate, or omit elements of the input vector
    const mask1 = @Vector(5, i32){ 2, 3, 1, 1, 0 };
    const res1: @Vector(5, u8) = @shuffle(u8, a, undefined, mask1);
    try expect(std.mem.eql(u8, &amp;@as([5]u8, res1), "hello"));

    // Combining two vectors
    const mask2 = @Vector(6, i32){ -1, 0, 4, 1, -2, -3 };
    const res2: @Vector(6, u8) = @shuffle(u8, a, b, mask2);
    try expect(std.mem.eql(u8, &amp;@as([6]u8, res2), "world!"));
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_shuffle_builtin.zig</kbd>
1/1 test.vector @shuffle... OK
All 1 tests passed.
</samp></pre></figure> <p>See also:</p>
<ul> <li><a href="#Vectors">Vectors</a></li> </ul> <h3 id="sizeOf">@sizeOf </h3> <pre data-language="zig">@sizeOf(comptime T: type) comptime_int</pre> <p> This function returns the number of bytes it takes to store <code><span class="line">T</span></code> in memory. The result is a target-specific compile time constant. </p> <p> This size may contain padding bytes. If there were two consecutive T in memory, this would be the offset in bytes between element at index 0 and the element at index 1. For <a href="#Integers">integer</a>, consider whether you want to use <code><span class="line"><span class="tok-builtin">@sizeOf</span>(T)</span></code> or <code><span class="line"><span class="tok-builtin">@typeInfo</span>(T).Int.bits</span></code>. </p> <p> This function measures the size at runtime. For types that are disallowed at runtime, such as <code><span class="line"><span class="tok-type">comptime_int</span></span></code> and <code><span class="line"><span class="tok-type">type</span></span></code>, the result is <code><span class="line"><span class="tok-number">0</span></span></code>. </p> <p>See also:</p>
<ul> <li><a href="#bitSizeOf">@bitSizeOf</a></li> <li><a href="#typeInfo">@typeInfo</a></li> </ul> <h3 id="splat">@splat </h3> <pre data-language="zig">@splat(scalar: anytype) anytype</pre> <p> Produces a vector where each element is the value <code><span class="line">scalar</span></code>. The return type and thus the length of the vector is inferred. </p> <figure><figcaption class="zig-cap"><cite class="file">test_splat_builtin.zig</cite></figcaption><pre data-language="zig">const std = @import("std");
const expect = std.testing.expect;

test "vector @splat" {
    const scalar: u32 = 5;
    const result: @Vector(4, u32) = @splat(scalar);
    try expect(std.mem.eql(u32, &amp;@as([4]u32, result), &amp;[_]u32{ 5, 5, 5, 5 }));
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_splat_builtin.zig</kbd>
1/1 test.vector @splat... OK
All 1 tests passed.
</samp></pre></figure> <p> <code><span class="line">scalar</span></code> must be an <a href="#Integers">integer</a>, <a href="#Primitive-Types">bool</a>, <a href="#Floats">float</a>, or <a href="#Pointers">pointer</a>. </p> <p>See also:</p>
<ul> <li><a href="#Vectors">Vectors</a></li> <li><a href="#shuffle">@shuffle</a></li> </ul> <h3 id="reduce">@reduce </h3> <pre data-language="zig">@reduce(comptime op: std.builtin.ReduceOp, value: anytype) E</pre> <p> Transforms a <a href="#Vectors">vector</a> into a scalar value (of type <code>E</code>) by performing a sequential horizontal reduction of its elements using the specified operator <code><span class="line">op</span></code>. </p> <p> Not every operator is available for every vector element type: </p> <ul> <li>Every operator is available for <a href="#Integers">integer</a> vectors.</li> <li>
<code><span class="line">.And</span></code>, <code><span class="line">.Or</span></code>, <code><span class="line">.Xor</span></code> are additionally available for <code><span class="line"><span class="tok-type">bool</span></span></code> vectors,</li> <li>
<code><span class="line">.Min</span></code>, <code><span class="line">.Max</span></code>, <code><span class="line">.Add</span></code>, <code><span class="line">.Mul</span></code> are additionally available for <a href="#Floats">floating point</a> vectors,</li> </ul> <p> Note that <code><span class="line">.Add</span></code> and <code><span class="line">.Mul</span></code> reductions on integral types are wrapping; when applied on floating point types the operation associativity is preserved, unless the float mode is set to <code><span class="line">Optimized</span></code>. </p> <figure><figcaption class="zig-cap"><cite class="file">test_reduce_builtin.zig</cite></figcaption><pre data-language="zig">const std = @import("std");
const expect = std.testing.expect;

test "vector @reduce" {
    const V = @Vector(4, i32);
    const value = V{ 1, -1, 1, -1 };
    const result = value &gt; @as(V, @splat(0));
    // result is { true, false, true, false };
    try comptime expect(@TypeOf(result) == @Vector(4, bool));
    const is_all_true = @reduce(.And, result);
    try comptime expect(@TypeOf(is_all_true) == bool);
    try expect(is_all_true == false);
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_reduce_builtin.zig</kbd>
1/1 test.vector @reduce... OK
All 1 tests passed.
</samp></pre></figure> <p>See also:</p>
<ul> <li><a href="#Vectors">Vectors</a></li> <li><a href="#setFloatMode">@setFloatMode</a></li> </ul> <h3 id="src">@src </h3> <pre data-language="zig">@src() std.builtin.SourceLocation</pre> <p> Returns a <code><span class="line">SourceLocation</span></code> struct representing the function's name and location in the source code. This must be called in a function. </p> <figure><figcaption class="zig-cap"><cite class="file">test_src_builtin.zig</cite></figcaption><pre data-language="zig">const std = @import("std");
const expect = std.testing.expect;

test "@src" {
    try doTheTest();
}

fn doTheTest() !void {
    const src = @src();

    try expect(src.line == 9);
    try expect(src.column == 17);
    try expect(std.mem.endsWith(u8, src.fn_name, "doTheTest"));
    try expect(std.mem.endsWith(u8, src.file, "test_src_builtin.zig"));
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_src_builtin.zig</kbd>
1/1 test.@src... OK
All 1 tests passed.
</samp></pre></figure> <h3 id="sqrt">@sqrt </h3> <pre data-language="zig">@sqrt(value: anytype) @TypeOf(value)</pre> <p> Performs the square root of a floating point number. Uses a dedicated hardware instruction when available. </p> <p> Supports <a href="#Floats">Floats</a> and <a href="#Vectors">Vectors</a> of floats. </p> <h3 id="sin">@sin </h3> <pre data-language="zig">@sin(value: anytype) @TypeOf(value)</pre> <p> Sine trigonometric function on a floating point number in radians. Uses a dedicated hardware instruction when available. </p> <p> Supports <a href="#Floats">Floats</a> and <a href="#Vectors">Vectors</a> of floats. </p> <h3 id="cos">@cos </h3> <pre data-language="zig">@cos(value: anytype) @TypeOf(value)</pre> <p> Cosine trigonometric function on a floating point number in radians. Uses a dedicated hardware instruction when available. </p> <p> Supports <a href="#Floats">Floats</a> and <a href="#Vectors">Vectors</a> of floats. </p> <h3 id="tan">@tan </h3> <pre data-language="zig">@tan(value: anytype) @TypeOf(value)</pre> <p> Tangent trigonometric function on a floating point number in radians. Uses a dedicated hardware instruction when available. </p> <p> Supports <a href="#Floats">Floats</a> and <a href="#Vectors">Vectors</a> of floats. </p> <h3 id="exp">@exp </h3> <pre data-language="zig">@exp(value: anytype) @TypeOf(value)</pre> <p> Base-e exponential function on a floating point number. Uses a dedicated hardware instruction when available. </p> <p> Supports <a href="#Floats">Floats</a> and <a href="#Vectors">Vectors</a> of floats. </p> <h3 id="exp2">@exp2 </h3> <pre data-language="zig">@exp2(value: anytype) @TypeOf(value)</pre> <p> Base-2 exponential function on a floating point number. Uses a dedicated hardware instruction when available. </p> <p> Supports <a href="#Floats">Floats</a> and <a href="#Vectors">Vectors</a> of floats. </p> <h3 id="log">@log </h3> <pre data-language="zig">@log(value: anytype) @TypeOf(value)</pre> <p> Returns the natural logarithm of a floating point number. Uses a dedicated hardware instruction when available. </p> <p> Supports <a href="#Floats">Floats</a> and <a href="#Vectors">Vectors</a> of floats. </p> <h3 id="log2">@log2 </h3> <pre data-language="zig">@log2(value: anytype) @TypeOf(value)</pre> <p> Returns the logarithm to the base 2 of a floating point number. Uses a dedicated hardware instruction when available. </p> <p> Supports <a href="#Floats">Floats</a> and <a href="#Vectors">Vectors</a> of floats. </p> <h3 id="log10">@log10 </h3> <pre data-language="zig">@log10(value: anytype) @TypeOf(value)</pre> <p> Returns the logarithm to the base 10 of a floating point number. Uses a dedicated hardware instruction when available. </p> <p> Supports <a href="#Floats">Floats</a> and <a href="#Vectors">Vectors</a> of floats. </p> <h3 id="fabs">@fabs </h3> <pre data-language="zig">@fabs(value: anytype) @TypeOf(value)</pre> <p> Returns the absolute value of a floating point number. Uses a dedicated hardware instruction when available. </p> <p> Supports <a href="#Floats">Floats</a> and <a href="#Vectors">Vectors</a> of floats. </p> <h3 id="floor">@floor </h3> <pre data-language="zig">@floor(value: anytype) @TypeOf(value)</pre> <p> Returns the largest integral value not greater than the given floating point number. Uses a dedicated hardware instruction when available. </p> <p> Supports <a href="#Floats">Floats</a> and <a href="#Vectors">Vectors</a> of floats. </p> <h3 id="ceil">@ceil </h3> <pre data-language="zig">@ceil(value: anytype) @TypeOf(value)</pre> <p> Returns the smallest integral value not less than the given floating point number. Uses a dedicated hardware instruction when available. </p> <p> Supports <a href="#Floats">Floats</a> and <a href="#Vectors">Vectors</a> of floats. </p> <h3 id="trunc">@trunc </h3> <pre data-language="zig">@trunc(value: anytype) @TypeOf(value)</pre> <p> Rounds the given floating point number to an integer, towards zero. Uses a dedicated hardware instruction when available. </p> <p> Supports <a href="#Floats">Floats</a> and <a href="#Vectors">Vectors</a> of floats. </p> <h3 id="round">@round </h3> <pre data-language="zig">@round(value: anytype) @TypeOf(value)</pre> <p> Rounds the given floating point number to an integer, away from zero. Uses a dedicated hardware instruction when available. </p> <p> Supports <a href="#Floats">Floats</a> and <a href="#Vectors">Vectors</a> of floats. </p> <h3 id="subWithOverflow">@subWithOverflow </h3> <pre data-language="zig">@subWithOverflow(a: anytype, b: anytype) struct { @TypeOf(a, b), u1 }</pre> <p> Performs <code><span class="line">a - b</span></code> and returns a tuple with the result and a possible overflow bit. </p> <h3 id="tagName">@tagName </h3> <pre data-language="zig">@tagName(value: anytype) [:0]const u8</pre> <p> Converts an enum value or union value to a string literal representing the name.</p>
<p>If the enum is non-exhaustive and the tag value does not map to a name, it invokes safety-checked <a href="#Undefined-Behavior">Undefined Behavior</a>. </p> <h3 id="This">@This </h3> <pre data-language="zig">@This() type</pre> <p> Returns the innermost struct, enum, or union that this function call is inside. This can be useful for an anonymous struct that needs to refer to itself: </p> <figure><figcaption class="zig-cap"><cite class="file">test_this_builtin.zig</cite></figcaption><pre data-language="zig">const std = @import("std");
const expect = std.testing.expect;

test "@This()" {
    var items = [_]i32{ 1, 2, 3, 4 };
    const list = List(i32){ .items = items[0..] };
    try expect(list.length() == 4);
}

fn List(comptime T: type) type {
    return struct {
        const Self = @This();

        items: []T,

        fn length(self: Self) usize {
            return self.items.len;
        }
    };
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_this_builtin.zig</kbd>
1/1 test.@This()... OK
All 1 tests passed.
</samp></pre></figure> <p> When <code><span class="line"><span class="tok-builtin">@This</span>()</span></code> is used at file scope, it returns a reference to the struct that corresponds to the current file. </p> <h3 id="trap">@trap </h3> <pre data-language="zig">@trap() noreturn</pre> <p> This function inserts a platform-specific trap/jam instruction which can be used to exit the program abnormally. This may be implemented by explicitly emitting an invalid instruction which may cause an illegal instruction exception of some sort. Unlike for <code><span class="line"><span class="tok-builtin">@breakpoint</span>()</span></code>, execution does not continue after this point. </p> <p> Outside function scope, this builtin causes a compile error. </p> <p>See also:</p>
<ul> <li><a href="#breakpoint">@breakpoint</a></li> </ul> <h3 id="truncate">@truncate </h3> <pre data-language="zig">@truncate(integer: anytype) anytype</pre> <p> This function truncates bits from an integer type, resulting in a smaller or same-sized integer type. The return type is the inferred result type. </p> <p> This function always truncates the significant bits of the integer, regardless of endianness on the target platform. </p> <p> Calling <code><span class="line"><span class="tok-builtin">@truncate</span></span></code> on a number out of range of the destination type is well defined and working code: </p> <figure><figcaption class="zig-cap"><cite class="file">test_truncate_builtin.zig</cite></figcaption><pre data-language="zig">const std = @import("std");
const expect = std.testing.expect;

test "integer truncation" {
    var a: u16 = 0xabcd;
    var b: u8 = @truncate(a);
    try expect(b == 0xcd);
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_truncate_builtin.zig</kbd>
1/1 test.integer truncation... OK
All 1 tests passed.
</samp></pre></figure> <p> Use <a href="#intCast">@intCast</a> to convert numbers guaranteed to fit the destination type. </p> <h3 id="Type">@Type </h3> <pre data-language="zig">@Type(comptime info: std.builtin.Type) type</pre> <p> This function is the inverse of <a href="#typeInfo">@typeInfo</a>. It reifies type information into a <code><span class="line"><span class="tok-type">type</span></span></code>. </p> <p> It is available for the following types: </p> <ul> <li><code><span class="line"><span class="tok-type">type</span></span></code></li> <li><code><span class="line"><span class="tok-type">noreturn</span></span></code></li> <li><code><span class="line"><span class="tok-type">void</span></span></code></li> <li><code><span class="line"><span class="tok-type">bool</span></span></code></li> <li>
<a href="#Integers">Integers</a> - The maximum bit count for an integer type is <code><span class="line"><span class="tok-number">65535</span></span></code>.</li> <li><a href="#Floats">Floats</a></li> <li><a href="#Pointers">Pointers</a></li> <li><code><span class="line"><span class="tok-type">comptime_int</span></span></code></li> <li><code><span class="line"><span class="tok-type">comptime_float</span></span></code></li> <li><code><span class="line"><span class="tok-builtin">@TypeOf</span>(<span class="tok-null">undefined</span>)</span></code></li> <li><code><span class="line"><span class="tok-builtin">@TypeOf</span>(<span class="tok-null">null</span>)</span></code></li> <li><a href="#Arrays">Arrays</a></li> <li><a href="#Optionals">Optionals</a></li> <li><a href="#Error-Set-Type">Error Set Type</a></li> <li><a href="#Error-Union-Type">Error Union Type</a></li> <li><a href="#Vectors">Vectors</a></li> <li><a href="#opaque">opaque</a></li> <li><code><span class="line"><span class="tok-kw">anyframe</span></span></code></li> <li><a href="#struct">struct</a></li> <li><a href="#enum">enum</a></li> <li><a href="#Enum-Literals">Enum Literals</a></li> <li><a href="#union">union</a></li> <li><a href="#Functions">Functions</a></li> </ul> <h3 id="typeInfo">@typeInfo </h3> <pre data-language="zig">@typeInfo(comptime T: type) std.builtin.Type</pre> <p> Provides type reflection. </p> <p> Type information of <a href="#struct">structs</a>, <a href="#union">unions</a>, <a href="#enum">enums</a>, and <a href="#Error-Set-Type">error sets</a> has fields which are guaranteed to be in the same order as appearance in the source file. </p> <p> Type information of <a href="#struct">structs</a>, <a href="#union">unions</a>, <a href="#enum">enums</a>, and <a href="#opaque">opaques</a> has declarations, which are also guaranteed to be in the same order as appearance in the source file. </p> <h3 id="typeName">@typeName </h3> <pre data-language="zig">@typeName(T: type) *const [N:0]u8</pre> <p> This function returns the string representation of a type, as an array. It is equivalent to a string literal of the type name. The returned type name is fully qualified with the parent namespace included as part of the type name with a series of dots. </p> <h3 id="TypeOf">@TypeOf </h3> <pre data-language="zig">@TypeOf(...) type</pre> <p> <code><span class="line"><span class="tok-builtin">@TypeOf</span></span></code> is a special builtin function that takes any (nonzero) number of expressions as parameters and returns the type of the result, using <a href="#Peer-Type-Resolution">Peer Type Resolution</a>. </p> <p> The expressions are evaluated, however they are guaranteed to have no <em>runtime</em> side-effects: </p> <figure><figcaption class="zig-cap"><cite class="file">test_TypeOf_builtin.zig</cite></figcaption><pre data-language="zig">const std = @import("std");
const expect = std.testing.expect;

test "no runtime side effects" {
    var data: i32 = 0;
    const T = @TypeOf(foo(i32, &amp;data));
    try comptime expect(T == i32);
    try expect(data == 0);
}

fn foo(comptime T: type, ptr: *T) T {
    ptr.* += 1;
    return ptr.*;
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_TypeOf_builtin.zig</kbd>
1/1 test.no runtime side effects... OK
All 1 tests passed.
</samp></pre></figure> <h3 id="unionInit">@unionInit </h3> <pre data-language="zig">@unionInit(comptime Union: type, comptime active_field_name: []const u8, init_expr) Union</pre> <p> This is the same thing as <a href="#union">union</a> initialization syntax, except that the field name is a <a href="#comptime">comptime</a>-known value rather than an identifier token. </p> <p> <code><span class="line"><span class="tok-builtin">@unionInit</span></span></code> forwards its <a href="#Result-Location-Semantics">result location</a> to <code><span class="line">init_expr</span></code>. </p> <h3 id="Vector">@Vector </h3> <pre data-language="zig">@Vector(len: comptime_int, Element: type) type</pre> <p>Creates <a href="#Vectors">Vectors</a>.</p> <h3 id="volatileCast">@volatileCast </h3> <pre data-language="zig">@volatileCast(value: anytype) DestType</pre> <p> Remove <code><span class="line"><span class="tok-kw">volatile</span></span></code> qualifier from a pointer. </p> <h3 id="workGroupId">@workGroupId </h3> <pre data-language="zig">@workGroupId(comptime dimension: u32) u32</pre> <p> Returns the index of the work group in the current kernel invocation in dimension <code><span class="line">dimension</span></code>. </p> <h3 id="workGroupSize">@workGroupSize </h3> <pre data-language="zig">@workGroupSize(comptime dimension: u32) u32</pre> <p> Returns the number of work items that a work group has in dimension <code><span class="line">dimension</span></code>. </p> <h3 id="workItemId">@workItemId </h3> <pre data-language="zig">@workItemId(comptime dimension: u32) u32</pre> <p> Returns the index of the work item in the work group in dimension <code><span class="line">dimension</span></code>. This function returns values between <code><span class="line"><span class="tok-number">0</span></span></code> (inclusive) and <code><span class="line"><span class="tok-builtin">@workGroupSize</span>(dimension)</span></code> (exclusive). </p> <h2 id="Build-Mode">Build Mode </h2> <p> Zig has four build modes: </p> <ul> <li>
<a href="#Debug">Debug</a> (default)</li> <li><a href="#ReleaseFast">ReleaseFast</a></li> <li><a href="#ReleaseSafe">ReleaseSafe</a></li> <li><a href="#ReleaseSmall">ReleaseSmall</a></li> </ul> <p> To add standard build options to a <code class="file">build.zig</code> file: </p> <figure><figcaption class="zig-cap"><cite class="file">build.zig</cite></figcaption><pre data-language="zig">const std = @import("std");

pub fn build(b: *std.Build) void {
    const optimize = b.standardOptimizeOption(.{});
    const exe = b.addExecutable(.{
        .name = "example",
        .root_source_file = .{ .path = "example.zig" },
        .optimize = optimize,
    });
    b.default_step.dependOn(&amp;exe.step);
}</pre></figure> <p> This causes these options to be available: </p> <dl> <dt><kbd>-Doptimize=Debug</kbd></dt>
<dd>Optimizations off and safety on (default)</dd> <dt><kbd>-Doptimize=ReleaseSafe</kbd></dt>
<dd>Optimizations on and safety on</dd> <dt><kbd>-Doptimize=ReleaseFast</kbd></dt>
<dd>Optimizations on and safety off</dd> <dt><kbd>-Doptimize=ReleaseSmall</kbd></dt>
<dd>Size optimizations on and safety off</dd> </dl> <h3 id="Debug">Debug </h3> <figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig build-exe example.zig</kbd>
</samp></pre></figure> <ul> <li>Fast compilation speed</li> <li>Safety checks enabled</li> <li>Slow runtime performance</li> <li>Large binary size</li> <li>No reproducible build requirement</li> </ul> <h3 id="ReleaseFast">ReleaseFast </h3> <figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig build-exe example.zig -O ReleaseFast</kbd>
</samp></pre></figure> <ul> <li>Fast runtime performance</li> <li>Safety checks disabled</li> <li>Slow compilation speed</li> <li>Large binary size</li> <li>Reproducible build</li> </ul> <h3 id="ReleaseSafe">ReleaseSafe </h3> <figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig build-exe example.zig -O ReleaseSafe</kbd>
</samp></pre></figure> <ul> <li>Medium runtime performance</li> <li>Safety checks enabled</li> <li>Slow compilation speed</li> <li>Large binary size</li> <li>Reproducible build</li> </ul> <h3 id="ReleaseSmall">ReleaseSmall </h3> <figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig build-exe example.zig -O ReleaseSmall</kbd>
</samp></pre></figure> <ul> <li>Medium runtime performance</li> <li>Safety checks disabled</li> <li>Slow compilation speed</li> <li>Small binary size</li> <li>Reproducible build</li> </ul> <p>See also:</p>
<ul> <li><a href="#Compile-Variables">Compile Variables</a></li> <li><a href="#Zig-Build-System">Zig Build System</a></li> <li><a href="#Undefined-Behavior">Undefined Behavior</a></li> </ul> <h2 id="Single-Threaded-Builds">Single Threaded Builds </h2> <p>Zig has a compile option <kbd>-fsingle-threaded</kbd> which has the following effects:</p> <ul> <li>All <a href="#Thread-Local-Variables">Thread Local Variables</a> are treated as regular <a href="#Container-Level-Variables">Container Level Variables</a>.</li> <li>The overhead of <a href="#Async-Functions">Async Functions</a> becomes equivalent to function call overhead.</li> <li>The <code><span class="line"><span class="tok-builtin">@import</span>(<span class="tok-str">"builtin"</span>).single_threaded</span></code> becomes <code><span class="line"><span class="tok-null">true</span></span></code> and therefore various userland APIs which read this variable become more efficient. For example <code><span class="line">std.Mutex</span></code> becomes an empty data structure and all of its functions become no-ops.</li> </ul> <h2 id="Undefined-Behavior">Undefined Behavior </h2> <p> Zig has many instances of undefined behavior. If undefined behavior is detected at compile-time, Zig emits a compile error and refuses to continue. Most undefined behavior that cannot be detected at compile-time can be detected at runtime. In these cases, Zig has safety checks. Safety checks can be disabled on a per-block basis with <a href="#setRuntimeSafety">@setRuntimeSafety</a>. The <a href="#ReleaseFast">ReleaseFast</a> and <a href="#ReleaseSmall">ReleaseSmall</a> build modes disable all safety checks (except where overridden by <a href="#setRuntimeSafety">@setRuntimeSafety</a>) in order to facilitate optimizations. </p> <p> When a safety check fails, Zig crashes with a stack trace, like this: </p> <figure><figcaption class="zig-cap"><cite class="file">test_undefined_behavior.zig</cite></figcaption><pre data-language="zig">test "safety check" {
    unreachable;
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_undefined_behavior.zig</kbd>
1/1 test.safety check... thread 2455728 panic: reached unreachable code
/home/ci/actions-runner/_work/zig-bootstrap/zig/docgen_tmp/test_undefined_behavior.zig:2:5: 0x224165 in test.safety check (test)
    unreachable;
    ^
/home/ci/actions-runner/_work/zig-bootstrap/out/host/lib/zig/test_runner.zig:176:28: 0x22d379 in mainTerminal (test)
        } else test_fn.func();
                           ^
/home/ci/actions-runner/_work/zig-bootstrap/out/host/lib/zig/test_runner.zig:36:28: 0x2251ca in main (test)
        return mainTerminal();
                           ^
/home/ci/actions-runner/_work/zig-bootstrap/out/host/lib/zig/std/start.zig:564:22: 0x2246a2 in posixCallMainAndExit (test)
            root.main();
                     ^
/home/ci/actions-runner/_work/zig-bootstrap/out/host/lib/zig/std/start.zig:243:5: 0x2241f1 in _start (test)
    asm volatile (switch (native_arch) {
    ^
???:?:?: 0x0 in ??? (???)
error: the following test command crashed:
/home/ci/actions-runner/_work/zig-bootstrap/out/zig-local-cache/o/7d7f3a85be0a1deb3cb4c93f04df3dc5/test
</samp></pre></figure> <h3 id="Reaching-Unreachable-Code">Reaching Unreachable Code </h3> <p>At compile-time:</p> <figure><figcaption class="zig-cap"><cite class="file">test_comptime_reaching_unreachable.zig</cite></figcaption><pre data-language="zig">comptime {
    assert(false);
}
fn assert(ok: bool) void {
    if (!ok) unreachable; // assertion failure
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_comptime_reaching_unreachable.zig</kbd>
<span class="sgr-1m">docgen_tmp/test_comptime_reaching_unreachable.zig:5:14: </span><span class="sgr-31m">error: </span><span class="sgr-1m">reached unreachable code
</span>    if (!ok) unreachable; // assertion failure
             <span class="sgr-32m">^~~~~~~~~~~
</span><span class="sgr-1m">docgen_tmp/test_comptime_reaching_unreachable.zig:2:11: </span><span class="sgr-36m">note: </span><span class="sgr-1m">called from here
</span>    assert(false);
    <span class="sgr-32m">~~~~~~^~~~~~~
</span>
</samp></pre></figure> <p>At runtime:</p> <figure><figcaption class="zig-cap"><cite class="file">runtime_reaching_unreachable.zig</cite></figcaption><pre data-language="zig">const std = @import("std");

pub fn main() void {
    std.debug.assert(false);
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig build-exe runtime_reaching_unreachable.zig</kbd>
$ <kbd>./runtime_reaching_unreachable</kbd>
thread 2455768 panic: reached unreachable code
/home/ci/actions-runner/_work/zig-bootstrap/out/host/lib/zig/std/debug.zig:343:14: 0x21fe12 in assert (runtime_reaching_unreachable)
    if (!ok) unreachable; // assertion failure
             ^
/home/ci/actions-runner/_work/zig-bootstrap/zig/docgen_tmp/runtime_reaching_unreachable.zig:4:21: 0x21e64a in main (runtime_reaching_unreachable)
    std.debug.assert(false);
                    ^
/home/ci/actions-runner/_work/zig-bootstrap/out/host/lib/zig/std/start.zig:564:22: 0x21def2 in posixCallMainAndExit (runtime_reaching_unreachable)
            root.main();
                     ^
/home/ci/actions-runner/_work/zig-bootstrap/out/host/lib/zig/std/start.zig:243:5: 0x21da41 in _start (runtime_reaching_unreachable)
    asm volatile (switch (native_arch) {
    ^
???:?:?: 0x0 in ??? (???)
(process terminated by signal)
</samp></pre></figure> <h3 id="Index-out-of-Bounds">Index out of Bounds </h3> <p>At compile-time:</p> <figure><figcaption class="zig-cap"><cite class="file">test_comptime_index_out_of_bounds.zig</cite></figcaption><pre data-language="zig">comptime {
    const array: [5]u8 = "hello".*;
    const garbage = array[5];
    _ = garbage;
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_comptime_index_out_of_bounds.zig</kbd>
<span class="sgr-1m">docgen_tmp/test_comptime_index_out_of_bounds.zig:3:27: </span><span class="sgr-31m">error: </span><span class="sgr-1m">index 5 outside array of length 5
</span>    const garbage = array[5];
                          <span class="sgr-32m">^
</span>
</samp></pre></figure> <p>At runtime:</p> <figure><figcaption class="zig-cap"><cite class="file">runtime_index_out_of_bounds.zig</cite></figcaption><pre data-language="zig">pub fn main() void {
    var x = foo("hello");
    _ = x;
}

fn foo(x: []const u8) u8 {
    return x[5];
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig build-exe runtime_index_out_of_bounds.zig</kbd>
$ <kbd>./runtime_index_out_of_bounds</kbd>
thread 2455808 panic: index out of bounds: index 5, len 5
/home/ci/actions-runner/_work/zig-bootstrap/zig/docgen_tmp/runtime_index_out_of_bounds.zig:7:13: 0x2202b9 in foo (runtime_index_out_of_bounds)
    return x[5];
            ^
/home/ci/actions-runner/_work/zig-bootstrap/zig/docgen_tmp/runtime_index_out_of_bounds.zig:2:16: 0x21e68b in main (runtime_index_out_of_bounds)
    var x = foo("hello");
               ^
/home/ci/actions-runner/_work/zig-bootstrap/out/host/lib/zig/std/start.zig:564:22: 0x21df22 in posixCallMainAndExit (runtime_index_out_of_bounds)
            root.main();
                     ^
/home/ci/actions-runner/_work/zig-bootstrap/out/host/lib/zig/std/start.zig:243:5: 0x21da71 in _start (runtime_index_out_of_bounds)
    asm volatile (switch (native_arch) {
    ^
???:?:?: 0x0 in ??? (???)
(process terminated by signal)
</samp></pre></figure> <h3 id="Cast-Negative-Number-to-Unsigned-Integer">Cast Negative Number to Unsigned Integer </h3> <p>At compile-time:</p> <figure><figcaption class="zig-cap"><cite class="file">test_comptime_invalid_cast.zig</cite></figcaption><pre data-language="zig">comptime {
    var value: i32 = -1;
    const unsigned: u32 = @intCast(value);
    _ = unsigned;
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_comptime_invalid_cast.zig</kbd>
<span class="sgr-1m">docgen_tmp/test_comptime_invalid_cast.zig:3:36: </span><span class="sgr-31m">error: </span><span class="sgr-1m">type 'u32' cannot represent integer value '-1'
</span>    const unsigned: u32 = @intCast(value);
                                   <span class="sgr-32m">^~~~~
</span>
</samp></pre></figure> <p>At runtime:</p> <figure><figcaption class="zig-cap"><cite class="file">runtime_invalid_cast.zig</cite></figcaption><pre data-language="zig">const std = @import("std");

pub fn main() void {
    var value: i32 = -1;
    var unsigned: u32 = @intCast(value);
    std.debug.print("value: {}\n", .{unsigned});
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig build-exe runtime_invalid_cast.zig</kbd>
$ <kbd>./runtime_invalid_cast</kbd>
thread 2455848 panic: attempt to cast negative value to unsigned integer
/home/ci/actions-runner/_work/zig-bootstrap/zig/docgen_tmp/runtime_invalid_cast.zig:5:25: 0x21e82a in main (runtime_invalid_cast)
    var unsigned: u32 = @intCast(value);
                        ^
/home/ci/actions-runner/_work/zig-bootstrap/out/host/lib/zig/std/start.zig:564:22: 0x21e082 in posixCallMainAndExit (runtime_invalid_cast)
            root.main();
                     ^
/home/ci/actions-runner/_work/zig-bootstrap/out/host/lib/zig/std/start.zig:243:5: 0x21dbd1 in _start (runtime_invalid_cast)
    asm volatile (switch (native_arch) {
    ^
???:?:?: 0x0 in ??? (???)
(process terminated by signal)
</samp></pre></figure> <p> To obtain the maximum value of an unsigned integer, use <code><span class="line">std.math.maxInt</span></code>. </p> <h3 id="Cast-Truncates-Data">Cast Truncates Data </h3> <p>At compile-time:</p> <figure><figcaption class="zig-cap"><cite class="file">test_comptime_invalid_cast_truncate.zig</cite></figcaption><pre data-language="zig">comptime {
    const spartan_count: u16 = 300;
    const byte: u8 = @intCast(spartan_count);
    _ = byte;
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_comptime_invalid_cast_truncate.zig</kbd>
<span class="sgr-1m">docgen_tmp/test_comptime_invalid_cast_truncate.zig:3:31: </span><span class="sgr-31m">error: </span><span class="sgr-1m">type 'u8' cannot represent integer value '300'
</span>    const byte: u8 = @intCast(spartan_count);
                              <span class="sgr-32m">^~~~~~~~~~~~~
</span>
</samp></pre></figure> <p>At runtime:</p> <figure><figcaption class="zig-cap"><cite class="file">runtime_invalid_cast_truncate.zig</cite></figcaption><pre data-language="zig">const std = @import("std");

pub fn main() void {
    var spartan_count: u16 = 300;
    const byte: u8 = @intCast(spartan_count);
    std.debug.print("value: {}\n", .{byte});
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig build-exe runtime_invalid_cast_truncate.zig</kbd>
$ <kbd>./runtime_invalid_cast_truncate</kbd>
thread 2455888 panic: integer cast truncated bits
/home/ci/actions-runner/_work/zig-bootstrap/zig/docgen_tmp/runtime_invalid_cast_truncate.zig:5:22: 0x21e8f1 in main (runtime_invalid_cast_truncate)
    const byte: u8 = @intCast(spartan_count);
                     ^
/home/ci/actions-runner/_work/zig-bootstrap/out/host/lib/zig/std/start.zig:564:22: 0x21e142 in posixCallMainAndExit (runtime_invalid_cast_truncate)
            root.main();
                     ^
/home/ci/actions-runner/_work/zig-bootstrap/out/host/lib/zig/std/start.zig:243:5: 0x21dc91 in _start (runtime_invalid_cast_truncate)
    asm volatile (switch (native_arch) {
    ^
???:?:?: 0x0 in ??? (???)
(process terminated by signal)
</samp></pre></figure> <p> To truncate bits, use <a href="#truncate">@truncate</a>. </p> <h3 id="Integer-Overflow">Integer Overflow </h3> <h4 id="Default-Operations">
<a href="#toc-Default-Operations">Default Operations</a> </h4> <p>The following operators can cause integer overflow:</p> <ul> <li>
<code><span class="line">+</span></code> (addition)</li> <li>
<code><span class="line">-</span></code> (subtraction)</li> <li>
<code><span class="line">-</span></code> (negation)</li> <li>
<code><span class="line">*</span></code> (multiplication)</li> <li>
<code><span class="line">/</span></code> (division)</li> <li>
<a href="#divTrunc">@divTrunc</a> (division)</li> <li>
<a href="#divFloor">@divFloor</a> (division)</li> <li>
<a href="#divExact">@divExact</a> (division)</li> </ul> <p>Example with addition at compile-time:</p> <figure><figcaption class="zig-cap"><cite class="file">test_comptime_overflow.zig</cite></figcaption><pre data-language="zig">comptime {
    var byte: u8 = 255;
    byte += 1;
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_comptime_overflow.zig</kbd>
<span class="sgr-1m">docgen_tmp/test_comptime_overflow.zig:3:10: </span><span class="sgr-31m">error: </span><span class="sgr-1m">overflow of integer type 'u8' with value '256'
</span>    byte += 1;
    <span class="sgr-32m">~~~~~^~~~
</span>
</samp></pre></figure> <p>At runtime:</p> <figure><figcaption class="zig-cap"><cite class="file">runtime_overflow.zig</cite></figcaption><pre data-language="zig">const std = @import("std");

pub fn main() void {
    var byte: u8 = 255;
    byte += 1;
    std.debug.print("value: {}\n", .{byte});
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig build-exe runtime_overflow.zig</kbd>
$ <kbd>./runtime_overflow</kbd>
thread 2455928 panic: integer overflow
/home/ci/actions-runner/_work/zig-bootstrap/zig/docgen_tmp/runtime_overflow.zig:5:10: 0x21e8ce in main (runtime_overflow)
    byte += 1;
         ^
/home/ci/actions-runner/_work/zig-bootstrap/out/host/lib/zig/std/start.zig:564:22: 0x21e142 in posixCallMainAndExit (runtime_overflow)
            root.main();
                     ^
/home/ci/actions-runner/_work/zig-bootstrap/out/host/lib/zig/std/start.zig:243:5: 0x21dc91 in _start (runtime_overflow)
    asm volatile (switch (native_arch) {
    ^
???:?:?: 0x0 in ??? (???)
(process terminated by signal)
</samp></pre></figure> <h4 id="Standard-Library-Math-Functions">
<a href="#toc-Standard-Library-Math-Functions">Standard Library Math Functions</a> </h4> <p>These functions provided by the standard library return possible errors.</p> <ul> <li><code><span class="line"><span class="tok-builtin">@import</span>(<span class="tok-str">"std"</span>).math.add</span></code></li> <li><code><span class="line"><span class="tok-builtin">@import</span>(<span class="tok-str">"std"</span>).math.sub</span></code></li> <li><code><span class="line"><span class="tok-builtin">@import</span>(<span class="tok-str">"std"</span>).math.mul</span></code></li> <li><code><span class="line"><span class="tok-builtin">@import</span>(<span class="tok-str">"std"</span>).math.divTrunc</span></code></li> <li><code><span class="line"><span class="tok-builtin">@import</span>(<span class="tok-str">"std"</span>).math.divFloor</span></code></li> <li><code><span class="line"><span class="tok-builtin">@import</span>(<span class="tok-str">"std"</span>).math.divExact</span></code></li> <li><code><span class="line"><span class="tok-builtin">@import</span>(<span class="tok-str">"std"</span>).math.shl</span></code></li> </ul> <p>Example of catching an overflow for addition:</p> <figure><figcaption class="zig-cap"><cite class="file">math_add.zig</cite></figcaption><pre data-language="zig">const math = @import("std").math;
const print = @import("std").debug.print;
pub fn main() !void {
    var byte: u8 = 255;

    byte = if (math.add(u8, byte, 1)) |result| result else |err| {
        print("unable to add one: {s}\n", .{@errorName(err)});
        return err;
    };

    print("result: {}\n", .{byte});
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig build-exe math_add.zig</kbd>
$ <kbd>./math_add</kbd>
unable to add one: Overflow
error: Overflow
/home/ci/actions-runner/_work/zig-bootstrap/out/host/lib/zig/std/math.zig:475:21: 0x21e6f5 in add__anon_3060 (math_add)
    if (ov[1] != 0) return error.Overflow;
                    ^
/home/ci/actions-runner/_work/zig-bootstrap/zig/docgen_tmp/math_add.zig:8:9: 0x21e68d in main (math_add)
        return err;
        ^
</samp></pre></figure> <h4 id="Builtin-Overflow-Functions">
<a href="#toc-Builtin-Overflow-Functions">Builtin Overflow Functions</a> </h4> <p> These builtins return a tuple containing whether there was an overflow (as a <code><span class="line"><span class="tok-type">u1</span></span></code>) and the possibly overflowed bits of the operation: </p> <ul> <li><a href="#addWithOverflow">@addWithOverflow</a></li> <li><a href="#subWithOverflow">@subWithOverflow</a></li> <li><a href="#mulWithOverflow">@mulWithOverflow</a></li> <li><a href="#shlWithOverflow">@shlWithOverflow</a></li> </ul> <p> Example of <a href="#addWithOverflow">@addWithOverflow</a>: </p> <figure><figcaption class="zig-cap"><cite class="file">addWithOverflow_builtin.zig</cite></figcaption><pre data-language="zig">const print = @import("std").debug.print;
pub fn main() void {
    var byte: u8 = 255;

    const ov = @addWithOverflow(byte, 10);
    if (ov[1] != 0) {
        print("overflowed result: {}\n", .{ov[0]});
    } else {
        print("result: {}\n", .{ov[0]});
    }
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig build-exe addWithOverflow_builtin.zig</kbd>
$ <kbd>./addWithOverflow_builtin</kbd>
overflowed result: 9
</samp></pre></figure> <h4 id="Wrapping-Operations">
<a href="#toc-Wrapping-Operations">Wrapping Operations</a> </h4> <p> These operations have guaranteed wraparound semantics. </p> <ul> <li>
<code><span class="line">+%</span></code> (wraparound addition)</li> <li>
<code><span class="line">-%</span></code> (wraparound subtraction)</li> <li>
<code><span class="line">-%</span></code> (wraparound negation)</li> <li>
<code><span class="line">*%</span></code> (wraparound multiplication)</li> </ul> <figure><figcaption class="zig-cap"><cite class="file">test_wraparound_semantics.zig</cite></figcaption><pre data-language="zig">const std = @import("std");
const expect = std.testing.expect;
const minInt = std.math.minInt;
const maxInt = std.math.maxInt;

test "wraparound addition and subtraction" {
    const x: i32 = maxInt(i32);
    const min_val = x +% 1;
    try expect(min_val == minInt(i32));
    const max_val = min_val -% 1;
    try expect(max_val == maxInt(i32));
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_wraparound_semantics.zig</kbd>
1/1 test.wraparound addition and subtraction... OK
All 1 tests passed.
</samp></pre></figure> <h3 id="Exact-Left-Shift-Overflow">Exact Left Shift Overflow </h3> <p>At compile-time:</p> <figure><figcaption class="zig-cap"><cite class="file">test_comptime_shlExact_overwlow.zig</cite></figcaption><pre data-language="zig">comptime {
    const x = @shlExact(@as(u8, 0b01010101), 2);
    _ = x;
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_comptime_shlExact_overwlow.zig</kbd>
<span class="sgr-1m">docgen_tmp/test_comptime_shlExact_overwlow.zig:2:15: </span><span class="sgr-31m">error: </span><span class="sgr-1m">operation caused overflow
</span>    const x = @shlExact(@as(u8, 0b01010101), 2);
              <span class="sgr-32m">^~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
</span>
</samp></pre></figure> <p>At runtime:</p> <figure><figcaption class="zig-cap"><cite class="file">runtime_shlExact_overflow.zig</cite></figcaption><pre data-language="zig">const std = @import("std");

pub fn main() void {
    var x: u8 = 0b01010101;
    var y = @shlExact(x, 2);
    std.debug.print("value: {}\n", .{y});
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig build-exe runtime_shlExact_overflow.zig</kbd>
$ <kbd>./runtime_shlExact_overflow</kbd>
thread 2456051 panic: left shift overflowed bits
/home/ci/actions-runner/_work/zig-bootstrap/zig/docgen_tmp/runtime_shlExact_overflow.zig:5:5: 0x21e915 in main (runtime_shlExact_overflow)
    var y = @shlExact(x, 2);
    ^
/home/ci/actions-runner/_work/zig-bootstrap/out/host/lib/zig/std/start.zig:564:22: 0x21e162 in posixCallMainAndExit (runtime_shlExact_overflow)
            root.main();
                     ^
/home/ci/actions-runner/_work/zig-bootstrap/out/host/lib/zig/std/start.zig:243:5: 0x21dcb1 in _start (runtime_shlExact_overflow)
    asm volatile (switch (native_arch) {
    ^
???:?:?: 0x0 in ??? (???)
(process terminated by signal)
</samp></pre></figure> <h3 id="Exact-Right-Shift-Overflow">Exact Right Shift Overflow </h3> <p>At compile-time:</p> <figure><figcaption class="zig-cap"><cite class="file">test_comptime_shrExact_overflow.zig</cite></figcaption><pre data-language="zig">comptime {
    const x = @shrExact(@as(u8, 0b10101010), 2);
    _ = x;
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_comptime_shrExact_overflow.zig</kbd>
<span class="sgr-1m">docgen_tmp/test_comptime_shrExact_overflow.zig:2:15: </span><span class="sgr-31m">error: </span><span class="sgr-1m">exact shift shifted out 1 bits
</span>    const x = @shrExact(@as(u8, 0b10101010), 2);
              <span class="sgr-32m">^~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
</span>
</samp></pre></figure> <p>At runtime:</p> <figure><figcaption class="zig-cap"><cite class="file">runtime_shrExact_overflow.zig</cite></figcaption><pre data-language="zig">const std = @import("std");

pub fn main() void {
    var x: u8 = 0b10101010;
    var y = @shrExact(x, 2);
    std.debug.print("value: {}\n", .{y});
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig build-exe runtime_shrExact_overflow.zig</kbd>
$ <kbd>./runtime_shrExact_overflow</kbd>
thread 2456091 panic: right shift overflowed bits
/home/ci/actions-runner/_work/zig-bootstrap/zig/docgen_tmp/runtime_shrExact_overflow.zig:5:5: 0x21e90e in main (runtime_shrExact_overflow)
    var y = @shrExact(x, 2);
    ^
/home/ci/actions-runner/_work/zig-bootstrap/out/host/lib/zig/std/start.zig:564:22: 0x21e162 in posixCallMainAndExit (runtime_shrExact_overflow)
            root.main();
                     ^
/home/ci/actions-runner/_work/zig-bootstrap/out/host/lib/zig/std/start.zig:243:5: 0x21dcb1 in _start (runtime_shrExact_overflow)
    asm volatile (switch (native_arch) {
    ^
???:?:?: 0x0 in ??? (???)
(process terminated by signal)
</samp></pre></figure> <h3 id="Division-by-Zero">Division by Zero </h3> <p>At compile-time:</p> <figure><figcaption class="zig-cap"><cite class="file">test_comptime_division_by_zero.zig</cite></figcaption><pre data-language="zig">comptime {
    const a: i32 = 1;
    const b: i32 = 0;
    const c = a / b;
    _ = c;
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_comptime_division_by_zero.zig</kbd>
<span class="sgr-1m">docgen_tmp/test_comptime_division_by_zero.zig:4:19: </span><span class="sgr-31m">error: </span><span class="sgr-1m">division by zero here causes undefined behavior
</span>    const c = a / b;
                  <span class="sgr-32m">^
</span>
</samp></pre></figure> <p>At runtime:</p> <figure><figcaption class="zig-cap"><cite class="file">runtime_division_by_zero.zig</cite></figcaption><pre data-language="zig">const std = @import("std");

pub fn main() void {
    var a: u32 = 1;
    var b: u32 = 0;
    var c = a / b;
    std.debug.print("value: {}\n", .{c});
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig build-exe runtime_division_by_zero.zig</kbd>
$ <kbd>./runtime_division_by_zero</kbd>
thread 2456131 panic: division by zero
/home/ci/actions-runner/_work/zig-bootstrap/zig/docgen_tmp/runtime_division_by_zero.zig:6:15: 0x21e83e in main (runtime_division_by_zero)
    var c = a / b;
              ^
/home/ci/actions-runner/_work/zig-bootstrap/out/host/lib/zig/std/start.zig:564:22: 0x21e082 in posixCallMainAndExit (runtime_division_by_zero)
            root.main();
                     ^
/home/ci/actions-runner/_work/zig-bootstrap/out/host/lib/zig/std/start.zig:243:5: 0x21dbd1 in _start (runtime_division_by_zero)
    asm volatile (switch (native_arch) {
    ^
???:?:?: 0x0 in ??? (???)
(process terminated by signal)
</samp></pre></figure> <h3 id="Remainder-Division-by-Zero">Remainder Division by Zero </h3> <p>At compile-time:</p> <figure><figcaption class="zig-cap"><cite class="file">test_comptime_remainder_division_by_zero.zig</cite></figcaption><pre data-language="zig">comptime {
    const a: i32 = 10;
    const b: i32 = 0;
    const c = a % b;
    _ = c;
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_comptime_remainder_division_by_zero.zig</kbd>
<span class="sgr-1m">docgen_tmp/test_comptime_remainder_division_by_zero.zig:4:19: </span><span class="sgr-31m">error: </span><span class="sgr-1m">division by zero here causes undefined behavior
</span>    const c = a % b;
                  <span class="sgr-32m">^
</span>
</samp></pre></figure> <p>At runtime:</p> <figure><figcaption class="zig-cap"><cite class="file">runtime_remainder_division_by_zero.zig</cite></figcaption><pre data-language="zig">const std = @import("std");

pub fn main() void {
    var a: u32 = 10;
    var b: u32 = 0;
    var c = a % b;
    std.debug.print("value: {}\n", .{c});
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig build-exe runtime_remainder_division_by_zero.zig</kbd>
$ <kbd>./runtime_remainder_division_by_zero</kbd>
thread 2456173 panic: division by zero
/home/ci/actions-runner/_work/zig-bootstrap/zig/docgen_tmp/runtime_remainder_division_by_zero.zig:6:15: 0x21e83e in main (runtime_remainder_division_by_zero)
    var c = a % b;
              ^
/home/ci/actions-runner/_work/zig-bootstrap/out/host/lib/zig/std/start.zig:564:22: 0x21e082 in posixCallMainAndExit (runtime_remainder_division_by_zero)
            root.main();
                     ^
/home/ci/actions-runner/_work/zig-bootstrap/out/host/lib/zig/std/start.zig:243:5: 0x21dbd1 in _start (runtime_remainder_division_by_zero)
    asm volatile (switch (native_arch) {
    ^
???:?:?: 0x0 in ??? (???)
(process terminated by signal)
</samp></pre></figure> <h3 id="Exact-Division-Remainder">Exact Division Remainder </h3> <p>At compile-time:</p> <figure><figcaption class="zig-cap"><cite class="file">test_comptime_divExact_remainder.zig</cite></figcaption><pre data-language="zig">comptime {
    const a: u32 = 10;
    const b: u32 = 3;
    const c = @divExact(a, b);
    _ = c;
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_comptime_divExact_remainder.zig</kbd>
<span class="sgr-1m">docgen_tmp/test_comptime_divExact_remainder.zig:4:15: </span><span class="sgr-31m">error: </span><span class="sgr-1m">exact division produced remainder
</span>    const c = @divExact(a, b);
              <span class="sgr-32m">^~~~~~~~~~~~~~~
</span>
</samp></pre></figure> <p>At runtime:</p> <figure><figcaption class="zig-cap"><cite class="file">runtime_divExact_remainder.zig</cite></figcaption><pre data-language="zig">const std = @import("std");

pub fn main() void {
    var a: u32 = 10;
    var b: u32 = 3;
    var c = @divExact(a, b);
    std.debug.print("value: {}\n", .{c});
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig build-exe runtime_divExact_remainder.zig</kbd>
$ <kbd>./runtime_divExact_remainder</kbd>
thread 2456213 panic: exact division produced remainder
/home/ci/actions-runner/_work/zig-bootstrap/zig/docgen_tmp/runtime_divExact_remainder.zig:6:13: 0x21e898 in main (runtime_divExact_remainder)
    var c = @divExact(a, b);
            ^
/home/ci/actions-runner/_work/zig-bootstrap/out/host/lib/zig/std/start.zig:564:22: 0x21e0a2 in posixCallMainAndExit (runtime_divExact_remainder)
            root.main();
                     ^
/home/ci/actions-runner/_work/zig-bootstrap/out/host/lib/zig/std/start.zig:243:5: 0x21dbf1 in _start (runtime_divExact_remainder)
    asm volatile (switch (native_arch) {
    ^
???:?:?: 0x0 in ??? (???)
(process terminated by signal)
</samp></pre></figure> <h3 id="Attempt-to-Unwrap-Null">Attempt to Unwrap Null </h3> <p>At compile-time:</p> <figure><figcaption class="zig-cap"><cite class="file">test_comptime_unwrap_null.zig</cite></figcaption><pre data-language="zig">comptime {
    const optional_number: ?i32 = null;
    const number = optional_number.?;
    _ = number;
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_comptime_unwrap_null.zig</kbd>
<span class="sgr-1m">docgen_tmp/test_comptime_unwrap_null.zig:3:35: </span><span class="sgr-31m">error: </span><span class="sgr-1m">unable to unwrap null
</span>    const number = optional_number.?;
                   <span class="sgr-32m">~~~~~~~~~~~~~~~^~
</span>
</samp></pre></figure> <p>At runtime:</p> <figure><figcaption class="zig-cap"><cite class="file">runtime_unwrap_null.zig</cite></figcaption><pre data-language="zig">const std = @import("std");

pub fn main() void {
    var optional_number: ?i32 = null;
    var number = optional_number.?;
    std.debug.print("value: {}\n", .{number});
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig build-exe runtime_unwrap_null.zig</kbd>
$ <kbd>./runtime_unwrap_null</kbd>
thread 2456253 panic: attempt to use null value
/home/ci/actions-runner/_work/zig-bootstrap/zig/docgen_tmp/runtime_unwrap_null.zig:5:33: 0x21e8fa in main (runtime_unwrap_null)
    var number = optional_number.?;
                                ^
/home/ci/actions-runner/_work/zig-bootstrap/out/host/lib/zig/std/start.zig:564:22: 0x21e152 in posixCallMainAndExit (runtime_unwrap_null)
            root.main();
                     ^
/home/ci/actions-runner/_work/zig-bootstrap/out/host/lib/zig/std/start.zig:243:5: 0x21dca1 in _start (runtime_unwrap_null)
    asm volatile (switch (native_arch) {
    ^
???:?:?: 0x0 in ??? (???)
(process terminated by signal)
</samp></pre></figure> <p>One way to avoid this crash is to test for null instead of assuming non-null, with the <code><span class="line"><span class="tok-kw">if</span></span></code> expression:</p> <figure><figcaption class="zig-cap"><cite class="file">testing_null_with_if.zig</cite></figcaption><pre data-language="zig">const print = @import("std").debug.print;
pub fn main() void {
    const optional_number: ?i32 = null;

    if (optional_number) |number| {
        print("got number: {}\n", .{number});
    } else {
        print("it's null\n", .{});
    }
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig build-exe testing_null_with_if.zig</kbd>
$ <kbd>./testing_null_with_if</kbd>
it's null
</samp></pre></figure> <p>See also:</p>
<ul> <li><a href="#Optionals">Optionals</a></li> </ul> <h3 id="Attempt-to-Unwrap-Error">Attempt to Unwrap Error </h3> <p>At compile-time:</p> <figure><figcaption class="zig-cap"><cite class="file">test_comptime_unwrap_error.zig</cite></figcaption><pre data-language="zig">comptime {
    const number = getNumberOrFail() catch unreachable;
    _ = number;
}

fn getNumberOrFail() !i32 {
    return error.UnableToReturnNumber;
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_comptime_unwrap_error.zig</kbd>
<span class="sgr-1m">docgen_tmp/test_comptime_unwrap_error.zig:2:44: </span><span class="sgr-31m">error: </span><span class="sgr-1m">caught unexpected error 'UnableToReturnNumber'
</span>    const number = getNumberOrFail() catch unreachable;
                                           <span class="sgr-32m">^~~~~~~~~~~
</span>
</samp></pre></figure> <p>At runtime:</p> <figure><figcaption class="zig-cap"><cite class="file">runtime_unwrap_error.zig</cite></figcaption><pre data-language="zig">const std = @import("std");

pub fn main() void {
    const number = getNumberOrFail() catch unreachable;
    std.debug.print("value: {}\n", .{number});
}

fn getNumberOrFail() !i32 {
    return error.UnableToReturnNumber;
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig build-exe runtime_unwrap_error.zig</kbd>
$ <kbd>./runtime_unwrap_error</kbd>
thread 2456320 panic: attempt to unwrap error: UnableToReturnNumber
/home/ci/actions-runner/_work/zig-bootstrap/zig/docgen_tmp/runtime_unwrap_error.zig:9:5: 0x22053f in getNumberOrFail (runtime_unwrap_error)
    return error.UnableToReturnNumber;
    ^
/home/ci/actions-runner/_work/zig-bootstrap/zig/docgen_tmp/runtime_unwrap_error.zig:4:44: 0x21e941 in main (runtime_unwrap_error)
    const number = getNumberOrFail() catch unreachable;
                                           ^
/home/ci/actions-runner/_work/zig-bootstrap/out/host/lib/zig/std/start.zig:564:22: 0x21e182 in posixCallMainAndExit (runtime_unwrap_error)
            root.main();
                     ^
/home/ci/actions-runner/_work/zig-bootstrap/out/host/lib/zig/std/start.zig:243:5: 0x21dcd1 in _start (runtime_unwrap_error)
    asm volatile (switch (native_arch) {
    ^
???:?:?: 0x0 in ??? (???)
(process terminated by signal)
</samp></pre></figure> <p>One way to avoid this crash is to test for an error instead of assuming a successful result, with the <code><span class="line"><span class="tok-kw">if</span></span></code> expression:</p> <figure><figcaption class="zig-cap"><cite class="file">testing_error_with_if.zig</cite></figcaption><pre data-language="zig">const print = @import("std").debug.print;

pub fn main() void {
    const result = getNumberOrFail();

    if (result) |number| {
        print("got number: {}\n", .{number});
    } else |err| {
        print("got error: {s}\n", .{@errorName(err)});
    }
}

fn getNumberOrFail() !i32 {
    return error.UnableToReturnNumber;
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig build-exe testing_error_with_if.zig</kbd>
$ <kbd>./testing_error_with_if</kbd>
got error: UnableToReturnNumber
</samp></pre></figure> <p>See also:</p>
<ul> <li><a href="#Errors">Errors</a></li> </ul> <h3 id="Invalid-Error-Code">Invalid Error Code </h3> <p>At compile-time:</p> <figure><figcaption class="zig-cap"><cite class="file">test_comptime_invalid_error_code.zig</cite></figcaption><pre data-language="zig">comptime {
    const err = error.AnError;
    const number = @intFromError(err) + 10;
    const invalid_err = @errorFromInt(number);
    _ = invalid_err;
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_comptime_invalid_error_code.zig</kbd>
<span class="sgr-1m">docgen_tmp/test_comptime_invalid_error_code.zig:4:39: </span><span class="sgr-31m">error: </span><span class="sgr-1m">integer value '11' represents no error
</span>    const invalid_err = @errorFromInt(number);
                                      <span class="sgr-32m">^~~~~~
</span>
</samp></pre></figure> <p>At runtime:</p> <figure><figcaption class="zig-cap"><cite class="file">runtime_invalid_error_code.zig</cite></figcaption><pre data-language="zig">const std = @import("std");

pub fn main() void {
    var err = error.AnError;
    var number = @intFromError(err) + 500;
    var invalid_err = @errorFromInt(number);
    std.debug.print("value: {}\n", .{invalid_err});
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig build-exe runtime_invalid_error_code.zig</kbd>
$ <kbd>./runtime_invalid_error_code</kbd>
thread 2456387 panic: invalid error code
/home/ci/actions-runner/_work/zig-bootstrap/zig/docgen_tmp/runtime_invalid_error_code.zig:6:5: 0x21e8c9 in main (runtime_invalid_error_code)
    var invalid_err = @errorFromInt(number);
    ^
/home/ci/actions-runner/_work/zig-bootstrap/out/host/lib/zig/std/start.zig:564:22: 0x21e102 in posixCallMainAndExit (runtime_invalid_error_code)
            root.main();
                     ^
/home/ci/actions-runner/_work/zig-bootstrap/out/host/lib/zig/std/start.zig:243:5: 0x21dc51 in _start (runtime_invalid_error_code)
    asm volatile (switch (native_arch) {
    ^
???:?:?: 0x0 in ??? (???)
(process terminated by signal)
</samp></pre></figure> <h3 id="Invalid-Enum-Cast">Invalid Enum Cast </h3> <p>At compile-time:</p> <figure><figcaption class="zig-cap"><cite class="file">test_comptime_invalid_enum_cast.zig</cite></figcaption><pre data-language="zig">const Foo = enum {
    a,
    b,
    c,
};
comptime {
    const a: u2 = 3;
    const b: Foo = @enumFromInt(a);
    _ = b;
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_comptime_invalid_enum_cast.zig</kbd>
<span class="sgr-1m">docgen_tmp/test_comptime_invalid_enum_cast.zig:8:20: </span><span class="sgr-31m">error: </span><span class="sgr-1m">enum 'test_comptime_invalid_enum_cast.Foo' has no tag with value '3'
</span>    const b: Foo = @enumFromInt(a);
                   <span class="sgr-32m">^~~~~~~~~~~~~~~
</span><span class="sgr-1m">docgen_tmp/test_comptime_invalid_enum_cast.zig:1:13: </span><span class="sgr-36m">note: </span><span class="sgr-1m">enum declared here
</span>const Foo = enum {
            <span class="sgr-32m">^~~~
</span>
</samp></pre></figure> <p>At runtime:</p> <figure><figcaption class="zig-cap"><cite class="file">runtime_invalid_enum_cast.zig</cite></figcaption><pre data-language="zig">const std = @import("std");

const Foo = enum {
    a,
    b,
    c,
};

pub fn main() void {
    var a: u2 = 3;
    var b: Foo = @enumFromInt(a);
    std.debug.print("value: {s}\n", .{@tagName(b)});
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig build-exe runtime_invalid_enum_cast.zig</kbd>
$ <kbd>./runtime_invalid_enum_cast</kbd>
thread 2456427 panic: invalid enum value
/home/ci/actions-runner/_work/zig-bootstrap/zig/docgen_tmp/runtime_invalid_enum_cast.zig:11:18: 0x21e87f in main (runtime_invalid_enum_cast)
    var b: Foo = @enumFromInt(a);
                 ^
/home/ci/actions-runner/_work/zig-bootstrap/out/host/lib/zig/std/start.zig:564:22: 0x21e0d2 in posixCallMainAndExit (runtime_invalid_enum_cast)
            root.main();
                     ^
/home/ci/actions-runner/_work/zig-bootstrap/out/host/lib/zig/std/start.zig:243:5: 0x21dc21 in _start (runtime_invalid_enum_cast)
    asm volatile (switch (native_arch) {
    ^
???:?:?: 0x0 in ??? (???)
(process terminated by signal)
</samp></pre></figure> <h3 id="Invalid-Error-Set-Cast">Invalid Error Set Cast </h3> <p>At compile-time:</p> <figure><figcaption class="zig-cap"><cite class="file">test_comptime_invalid_error_set_cast.zig</cite></figcaption><pre data-language="zig">const Set1 = error{
    A,
    B,
};
const Set2 = error{
    A,
    C,
};
comptime {
    _ = @as(Set2, @errSetCast(Set1.B));
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_comptime_invalid_error_set_cast.zig</kbd>
<span class="sgr-1m">docgen_tmp/test_comptime_invalid_error_set_cast.zig:10:19: </span><span class="sgr-31m">error: </span><span class="sgr-1m">'error.B' not a member of error set 'error{C,A}'
</span>    _ = @as(Set2, @errSetCast(Set1.B));
                  <span class="sgr-32m">^~~~~~~~~~~~~~~~~~~
</span>
</samp></pre></figure> <p>At runtime:</p> <figure><figcaption class="zig-cap"><cite class="file">runtime_invalid_error_set_cast.zig</cite></figcaption><pre data-language="zig">const std = @import("std");

const Set1 = error{
    A,
    B,
};
const Set2 = error{
    A,
    C,
};
pub fn main() void {
    foo(Set1.B);
}
fn foo(set1: Set1) void {
    const x: Set2 = @errSetCast(set1);
    std.debug.print("value: {}\n", .{x});
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig build-exe runtime_invalid_error_set_cast.zig</kbd>
$ <kbd>./runtime_invalid_error_set_cast</kbd>
thread 2456467 panic: invalid error code
/home/ci/actions-runner/_work/zig-bootstrap/zig/docgen_tmp/runtime_invalid_error_set_cast.zig:15:21: 0x2204dc in foo (runtime_invalid_error_set_cast)
    const x: Set2 = @errSetCast(set1);
                    ^
/home/ci/actions-runner/_work/zig-bootstrap/zig/docgen_tmp/runtime_invalid_error_set_cast.zig:12:8: 0x21e88d in main (runtime_invalid_error_set_cast)
    foo(Set1.B);
       ^
/home/ci/actions-runner/_work/zig-bootstrap/out/host/lib/zig/std/start.zig:564:22: 0x21e132 in posixCallMainAndExit (runtime_invalid_error_set_cast)
            root.main();
                     ^
/home/ci/actions-runner/_work/zig-bootstrap/out/host/lib/zig/std/start.zig:243:5: 0x21dc81 in _start (runtime_invalid_error_set_cast)
    asm volatile (switch (native_arch) {
    ^
???:?:?: 0x0 in ??? (???)
(process terminated by signal)
</samp></pre></figure> <h3 id="Incorrect-Pointer-Alignment">Incorrect Pointer Alignment </h3> <p>At compile-time:</p> <figure><figcaption class="zig-cap"><cite class="file">test_comptime_incorrect_pointer_alignment.zig</cite></figcaption><pre data-language="zig">comptime {
    const ptr: *align(1) i32 = @ptrFromInt(0x1);
    const aligned: *align(4) i32 = @alignCast(ptr);
    _ = aligned;
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_comptime_incorrect_pointer_alignment.zig</kbd>
<span class="sgr-1m">docgen_tmp/test_comptime_incorrect_pointer_alignment.zig:3:47: </span><span class="sgr-31m">error: </span><span class="sgr-1m">pointer address 0x1 is not aligned to 4 bytes
</span>    const aligned: *align(4) i32 = @alignCast(ptr);
                                              <span class="sgr-32m">^~~
</span>
</samp></pre></figure> <p>At runtime:</p> <figure><figcaption class="zig-cap"><cite class="file">runtime_incorrect_pointer_alignment.zig</cite></figcaption><pre data-language="zig">const mem = @import("std").mem;
pub fn main() !void {
    var array align(4) = [_]u32{ 0x11111111, 0x11111111 };
    const bytes = mem.sliceAsBytes(array[0..]);
    if (foo(bytes) != 0x11111111) return error.Wrong;
}
fn foo(bytes: []u8) u32 {
    const slice4 = bytes[1..5];
    const int_slice = mem.bytesAsSlice(u32, @as([]align(4) u8, @alignCast(slice4)));
    return int_slice[0];
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig build-exe runtime_incorrect_pointer_alignment.zig</kbd>
$ <kbd>./runtime_incorrect_pointer_alignment</kbd>
thread 2456507 panic: incorrect alignment
/home/ci/actions-runner/_work/zig-bootstrap/zig/docgen_tmp/runtime_incorrect_pointer_alignment.zig:9:64: 0x21e43f in foo (runtime_incorrect_pointer_alignment)
    const int_slice = mem.bytesAsSlice(u32, @as([]align(4) u8, @alignCast(slice4)));
                                                               ^
/home/ci/actions-runner/_work/zig-bootstrap/zig/docgen_tmp/runtime_incorrect_pointer_alignment.zig:5:12: 0x21e33f in main (runtime_incorrect_pointer_alignment)
    if (foo(bytes) != 0x11111111) return error.Wrong;
           ^
/home/ci/actions-runner/_work/zig-bootstrap/out/host/lib/zig/std/start.zig:574:37: 0x21e24e in posixCallMainAndExit (runtime_incorrect_pointer_alignment)
            const result = root.main() catch |err| {
                                    ^
/home/ci/actions-runner/_work/zig-bootstrap/out/host/lib/zig/std/start.zig:243:5: 0x21dd31 in _start (runtime_incorrect_pointer_alignment)
    asm volatile (switch (native_arch) {
    ^
???:?:?: 0x0 in ??? (???)
(process terminated by signal)
</samp></pre></figure> <h3 id="Wrong-Union-Field-Access">Wrong Union Field Access </h3> <p>At compile-time:</p> <figure><figcaption class="zig-cap"><cite class="file">test_comptime_wrong_union_field_access.zig</cite></figcaption><pre data-language="zig">comptime {
    var f = Foo{ .int = 42 };
    f.float = 12.34;
}

const Foo = union {
    float: f32,
    int: u32,
};</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_comptime_wrong_union_field_access.zig</kbd>
<span class="sgr-1m">docgen_tmp/test_comptime_wrong_union_field_access.zig:3:6: </span><span class="sgr-31m">error: </span><span class="sgr-1m">access of union field 'float' while field 'int' is active
</span>    f.float = 12.34;
    <span class="sgr-32m">~^~~~~~
</span><span class="sgr-1m">docgen_tmp/test_comptime_wrong_union_field_access.zig:6:13: </span><span class="sgr-36m">note: </span><span class="sgr-1m">union declared here
</span>const Foo = union {
            <span class="sgr-32m">^~~~~
</span>
</samp></pre></figure> <p>At runtime:</p> <figure><figcaption class="zig-cap"><cite class="file">runtime_wrong_union_field_access.zig</cite></figcaption><pre data-language="zig">const std = @import("std");

const Foo = union {
    float: f32,
    int: u32,
};

pub fn main() void {
    var f = Foo{ .int = 42 };
    bar(&amp;f);
}

fn bar(f: *Foo) void {
    f.float = 12.34;
    std.debug.print("value: {}\n", .{f.float});
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig build-exe runtime_wrong_union_field_access.zig</kbd>
$ <kbd>./runtime_wrong_union_field_access</kbd>
thread 2456547 panic: access of union field 'float' while field 'int' is active
/home/ci/actions-runner/_work/zig-bootstrap/zig/docgen_tmp/runtime_wrong_union_field_access.zig:14:6: 0x234cc0 in bar (runtime_wrong_union_field_access)
    f.float = 12.34;
     ^
/home/ci/actions-runner/_work/zig-bootstrap/zig/docgen_tmp/runtime_wrong_union_field_access.zig:10:8: 0x23308c in main (runtime_wrong_union_field_access)
    bar(&amp;f);
       ^
/home/ci/actions-runner/_work/zig-bootstrap/out/host/lib/zig/std/start.zig:564:22: 0x232922 in posixCallMainAndExit (runtime_wrong_union_field_access)
            root.main();
                     ^
/home/ci/actions-runner/_work/zig-bootstrap/out/host/lib/zig/std/start.zig:243:5: 0x232471 in _start (runtime_wrong_union_field_access)
    asm volatile (switch (native_arch) {
    ^
???:?:?: 0x0 in ??? (???)
(process terminated by signal)
</samp></pre></figure> <p> This safety is not available for <code><span class="line"><span class="tok-kw">extern</span></span></code> or <code><span class="line"><span class="tok-kw">packed</span></span></code> unions. </p> <p> To change the active field of a union, assign the entire union, like this: </p> <figure><figcaption class="zig-cap"><cite class="file">change_active_union_field.zig</cite></figcaption><pre data-language="zig">const std = @import("std");

const Foo = union {
    float: f32,
    int: u32,
};

pub fn main() void {
    var f = Foo{ .int = 42 };
    bar(&amp;f);
}

fn bar(f: *Foo) void {
    f.* = Foo{ .float = 12.34 };
    std.debug.print("value: {}\n", .{f.float});
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig build-exe change_active_union_field.zig</kbd>
$ <kbd>./change_active_union_field</kbd>
value: 1.23400001e+01
</samp></pre></figure> <p> To change the active field of a union when a meaningful value for the field is not known, use <a href="#undefined">undefined</a>, like this: </p> <figure><figcaption class="zig-cap"><cite class="file">undefined_active_union_field.zig</cite></figcaption><pre data-language="zig">const std = @import("std");

const Foo = union {
    float: f32,
    int: u32,
};

pub fn main() void {
    var f = Foo{ .int = 42 };
    f = Foo{ .float = undefined };
    bar(&amp;f);
    std.debug.print("value: {}\n", .{f.float});
}

fn bar(f: *Foo) void {
    f.float = 12.34;
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig build-exe undefined_active_union_field.zig</kbd>
$ <kbd>./undefined_active_union_field</kbd>
value: 1.23400001e+01
</samp></pre></figure> <p>See also:</p>
<ul> <li><a href="#union">union</a></li> <li><a href="#extern-union">extern union</a></li> </ul> <h3 id="Out-of-Bounds-Float-to-Integer-Cast">Out of Bounds Float to Integer Cast </h3> <p> This happens when casting a float to an integer where the float has a value outside the integer type's range. </p> <p>At compile-time:</p> <figure><figcaption class="zig-cap"><cite class="file">test_comptime_out_of_bounds_float_to_integer_cast.zig</cite></figcaption><pre data-language="zig">comptime {
	const float: f32 = 4294967296;
	const int: i32 = @intFromFloat(float);
	_ = int;
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_comptime_out_of_bounds_float_to_integer_cast.zig</kbd>
<span class="sgr-1m">docgen_tmp/test_comptime_out_of_bounds_float_to_integer_cast.zig:3:33: </span><span class="sgr-31m">error: </span><span class="sgr-1m">float value '4294967296' cannot be stored in integer type 'i32'
</span> const int: i32 = @intFromFloat(float);
                                <span class="sgr-32m">^~~~~
</span>
</samp></pre></figure> <p>At runtime:</p> <figure><figcaption class="zig-cap"><cite class="file">runtime_out_of_bounds_float_to_integer_cast.zig</cite></figcaption><pre data-language="zig">pub fn main() void {
	var float: f32 = 4294967296;
	var int: i32 = @intFromFloat(float);
	_ = int;
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig build-exe runtime_out_of_bounds_float_to_integer_cast.zig</kbd>
$ <kbd>./runtime_out_of_bounds_float_to_integer_cast</kbd>
thread 2456641 panic: integer part of floating point value out of bounds
/home/ci/actions-runner/_work/zig-bootstrap/zig/docgen_tmp/runtime_out_of_bounds_float_to_integer_cast.zig:3:17: 0x21e70e in main (runtime_out_of_bounds_float_to_integer_cast)
 var int: i32 = @intFromFloat(float);
                ^
/home/ci/actions-runner/_work/zig-bootstrap/out/host/lib/zig/std/start.zig:564:22: 0x21df42 in posixCallMainAndExit (runtime_out_of_bounds_float_to_integer_cast)
            root.main();
                     ^
/home/ci/actions-runner/_work/zig-bootstrap/out/host/lib/zig/std/start.zig:243:5: 0x21da91 in _start (runtime_out_of_bounds_float_to_integer_cast)
    asm volatile (switch (native_arch) {
    ^
???:?:?: 0x0 in ??? (???)
(process terminated by signal)
</samp></pre></figure> <h3 id="Pointer-Cast-Invalid-Null">Pointer Cast Invalid Null </h3> <p> This happens when casting a pointer with the address 0 to a pointer which may not have the address 0. For example, <a href="#C-Pointers">C Pointers</a>, <a href="#Optional-Pointers">Optional Pointers</a>, and <a href="#allowzero">allowzero</a> pointers allow address zero, but normal <a href="#Pointers">Pointers</a> do not. </p> <p>At compile-time:</p> <figure><figcaption class="zig-cap"><cite class="file">test_comptime_invalid_null_pointer_cast.zig</cite></figcaption><pre data-language="zig">comptime {
    const opt_ptr: ?*i32 = null;
    const ptr: *i32 = @ptrCast(opt_ptr);
    _ = ptr;
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_comptime_invalid_null_pointer_cast.zig</kbd>
<span class="sgr-1m">docgen_tmp/test_comptime_invalid_null_pointer_cast.zig:3:32: </span><span class="sgr-31m">error: </span><span class="sgr-1m">null pointer casted to type '*i32'
</span>    const ptr: *i32 = @ptrCast(opt_ptr);
                               <span class="sgr-32m">^~~~~~~
</span>
</samp></pre></figure> <p>At runtime:</p> <figure><figcaption class="zig-cap"><cite class="file">runtime_invalid_null_pointer_cast.zig</cite></figcaption><pre data-language="zig">pub fn main() void {
    var opt_ptr: ?*i32 = null;
    var ptr: *i32 = @ptrCast(opt_ptr);
    _ = ptr;
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig build-exe runtime_invalid_null_pointer_cast.zig</kbd>
$ <kbd>./runtime_invalid_null_pointer_cast</kbd>
thread 2456681 panic: cast causes pointer to be null
/home/ci/actions-runner/_work/zig-bootstrap/zig/docgen_tmp/runtime_invalid_null_pointer_cast.zig:3:21: 0x21e691 in main (runtime_invalid_null_pointer_cast)
    var ptr: *i32 = @ptrCast(opt_ptr);
                    ^
/home/ci/actions-runner/_work/zig-bootstrap/out/host/lib/zig/std/start.zig:564:22: 0x21def2 in posixCallMainAndExit (runtime_invalid_null_pointer_cast)
            root.main();
                     ^
/home/ci/actions-runner/_work/zig-bootstrap/out/host/lib/zig/std/start.zig:243:5: 0x21da41 in _start (runtime_invalid_null_pointer_cast)
    asm volatile (switch (native_arch) {
    ^
???:?:?: 0x0 in ??? (???)
(process terminated by signal)
</samp></pre></figure> <h2 id="Memory">Memory </h2> <p> The Zig language performs no memory management on behalf of the programmer. This is why Zig has no runtime, and why Zig code works seamlessly in so many environments, including real-time software, operating system kernels, embedded devices, and low latency servers. As a consequence, Zig programmers must always be able to answer the question: </p> <p><a href="#Where-are-the-bytes">Where are the bytes?</a></p> <p> Like Zig, the C programming language has manual memory management. However, unlike Zig, C has a default allocator - <code>malloc</code>, <code>realloc</code>, and <code>free</code>. When linking against libc, Zig exposes this allocator with <code><span class="line">std.heap.c_allocator</span></code>. However, by convention, there is no default allocator in Zig. Instead, functions which need to allocate accept an <code><span class="line">Allocator</span></code> parameter. Likewise, data structures such as <code><span class="line">std.ArrayList</span></code> accept an <code><span class="line">Allocator</span></code> parameter in their initialization functions: </p> <figure><figcaption class="zig-cap"><cite class="file">test_allocator.zig</cite></figcaption><pre data-language="zig">const std = @import("std");
const Allocator = std.mem.Allocator;
const expect = std.testing.expect;

test "using an allocator" {
    var buffer: [100]u8 = undefined;
    var fba = std.heap.FixedBufferAllocator.init(&amp;buffer);
    const allocator = fba.allocator();
    const result = try concat(allocator, "foo", "bar");
    try expect(std.mem.eql(u8, "foobar", result));
}

fn concat(allocator: Allocator, a: []const u8, b: []const u8) ![]u8 {
    const result = try allocator.alloc(u8, a.len + b.len);
    std.mem.copy(u8, result, a);
    std.mem.copy(u8, result[a.len..], b);
    return result;
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_allocator.zig</kbd>
1/1 test.using an allocator... OK
All 1 tests passed.
</samp></pre></figure> <p> In the above example, 100 bytes of stack memory are used to initialize a <code><span class="line">FixedBufferAllocator</span></code>, which is then passed to a function. As a convenience there is a global <code><span class="line">FixedBufferAllocator</span></code> available for quick tests at <code><span class="line">std.testing.allocator</span></code>, which will also perform basic leak detection. </p> <p> Zig has a general purpose allocator available to be imported with <code><span class="line">std.heap.GeneralPurposeAllocator</span></code>. However, it is still recommended to follow the <a href="#Choosing-an-Allocator">Choosing an Allocator</a> guide. </p> <h3 id="Choosing-an-Allocator">Choosing an Allocator </h3> <p>What allocator to use depends on a number of factors. Here is a flow chart to help you decide: </p> <ol> <li> Are you making a library? In this case, best to accept an <code><span class="line">Allocator</span></code> as a parameter and allow your library's users to decide what allocator to use. </li> <li>Are you linking libc? In this case, <code><span class="line">std.heap.c_allocator</span></code> is likely the right choice, at least for your main allocator.</li> <li> Is the maximum number of bytes that you will need bounded by a number known at <a href="#comptime">comptime</a>? In this case, use <code><span class="line">std.heap.FixedBufferAllocator</span></code> or <code><span class="line">std.heap.ThreadSafeFixedBufferAllocator</span></code> depending on whether you need thread-safety or not. </li> <li> Is your program a command line application which runs from start to end without any fundamental cyclical pattern (such as a video game main loop, or a web server request handler), such that it would make sense to free everything at once at the end? In this case, it is recommended to follow this pattern: <figure><figcaption class="zig-cap"><cite class="file">cli_allocation.zig</cite></figcaption><pre data-language="zig">const std = @import("std");

pub fn main() !void {
    var arena = std.heap.ArenaAllocator.init(std.heap.page_allocator);
    defer arena.deinit();

    const allocator = arena.allocator();

    const ptr = try allocator.create(i32);
    std.debug.print("ptr={*}\n", .{ptr});
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig build-exe cli_allocation.zig</kbd>
$ <kbd>./cli_allocation</kbd>
ptr=i32@7f1b2eaae010
</samp></pre></figure> When using this kind of allocator, there is no need to free anything manually. Everything gets freed at once with the call to <code><span class="line">arena.deinit()</span></code>. </li> <li> Are the allocations part of a cyclical pattern such as a video game main loop, or a web server request handler? If the allocations can all be freed at once, at the end of the cycle, for example once the video game frame has been fully rendered, or the web server request has been served, then <code><span class="line">std.heap.ArenaAllocator</span></code> is a great candidate. As demonstrated in the previous bullet point, this allows you to free entire arenas at once. Note also that if an upper bound of memory can be established, then <code><span class="line">std.heap.FixedBufferAllocator</span></code> can be used as a further optimization. </li> <li> Are you writing a test, and you want to make sure <code><span class="line"><span class="tok-kw">error</span>.OutOfMemory</span></code> is handled correctly? In this case, use <code><span class="line">std.testing.FailingAllocator</span></code>. </li> <li> Are you writing a test? In this case, use <code><span class="line">std.testing.allocator</span></code>. </li> <li> Finally, if none of the above apply, you need a general purpose allocator. Zig's general purpose allocator is available as a function that takes a <a href="#comptime">comptime</a> <a href="#struct">struct</a> of configuration options and returns a type. Generally, you will set up one <code><span class="line">std.heap.GeneralPurposeAllocator</span></code> in your main function, and then pass it or sub-allocators around to various parts of your application. </li> <li> You can also consider <a href="#Implementing-an-Allocator">Implementing an Allocator</a>. </li> </ol> <h3 id="Where-are-the-bytes">Where are the bytes? </h3> <p>String literals such as <code><span class="line"><span class="tok-str">"foo"</span></span></code> are in the global constant data section. This is why it is an error to pass a string literal to a mutable slice, like this: </p> <figure><figcaption class="zig-cap"><cite class="file">test_string_literal_to_slice.zig</cite></figcaption><pre data-language="zig">fn foo(s: []u8) void {
    _ = s;
}

test "string literal to mutable slice" {
    foo("hello");
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_string_literal_to_slice.zig</kbd>
<span class="sgr-1m">docgen_tmp/test_string_literal_to_slice.zig:6:9: </span><span class="sgr-31m">error: </span><span class="sgr-1m">expected type '[]u8', found '*const [5:0]u8'
</span>    foo("hello");
        <span class="sgr-32m">^~~~~~~
</span><span class="sgr-1m">docgen_tmp/test_string_literal_to_slice.zig:6:9: </span><span class="sgr-36m">note: </span><span class="sgr-1m">cast discards const qualifier
</span><span class="sgr-1m">docgen_tmp/test_string_literal_to_slice.zig:1:11: </span><span class="sgr-36m">note: </span><span class="sgr-1m">parameter type declared here
</span>fn foo(s: []u8) void {
          <span class="sgr-32m">^~~~
</span>
</samp></pre></figure> <p>However if you make the slice constant, then it works:</p> <figure><figcaption class="zig-cap"><cite class="file">test_string_literal_to_const_slice.zig</cite></figcaption><pre data-language="zig">fn foo(s: []const u8) void {
    _ = s;
}

test "string literal to constant slice" {
    foo("hello");
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_string_literal_to_const_slice.zig</kbd>
1/1 test.string literal to constant slice... OK
All 1 tests passed.
</samp></pre></figure> <p> Just like string literals, <code><span class="line"><span class="tok-kw">const</span></span></code> declarations, when the value is known at <a href="#comptime">comptime</a>, are stored in the global constant data section. Also <a href="#Compile-Time-Variables">Compile Time Variables</a> are stored in the global constant data section. </p> <p> <code><span class="line"><span class="tok-kw">var</span></span></code> declarations inside functions are stored in the function's stack frame. Once a function returns, any <a href="#Pointers">Pointers</a> to variables in the function's stack frame become invalid references, and dereferencing them becomes unchecked <a href="#Undefined-Behavior">Undefined Behavior</a>. </p> <p> <code><span class="line"><span class="tok-kw">var</span></span></code> declarations at the top level or in <a href="#struct">struct</a> declarations are stored in the global data section. </p> <p> The location of memory allocated with <code><span class="line">allocator.alloc</span></code> or <code><span class="line">allocator.create</span></code> is determined by the allocator's implementation. </p> <p>TODO: thread local variables</p> <h3 id="Implementing-an-Allocator">Implementing an Allocator </h3> <p>Zig programmers can implement their own allocators by fulfilling the Allocator interface. In order to do this one must read carefully the documentation comments in std/mem.zig and then supply a <code><span class="line">allocFn</span></code> and a <code><span class="line">resizeFn</span></code>. </p> <p> There are many example allocators to look at for inspiration. Look at std/heap.zig and <code><span class="line">std.heap.GeneralPurposeAllocator</span></code>. </p> <h3 id="Heap-Allocation-Failure">Heap Allocation Failure </h3> <p> Many programming languages choose to handle the possibility of heap allocation failure by unconditionally crashing. By convention, Zig programmers do not consider this to be a satisfactory solution. Instead, <code><span class="line"><span class="tok-kw">error</span>.OutOfMemory</span></code> represents heap allocation failure, and Zig libraries return this error code whenever heap allocation failure prevented an operation from completing successfully. </p> <p> Some have argued that because some operating systems such as Linux have memory overcommit enabled by default, it is pointless to handle heap allocation failure. There are many problems with this reasoning: </p> <ul> <li>Only some operating systems have an overcommit feature. <ul> <li>Linux has it enabled by default, but it is configurable.</li> <li>Windows does not overcommit.</li> <li>Embedded systems do not have overcommit.</li> <li>Hobby operating systems may or may not have overcommit.</li> </ul> </li> <li> For real-time systems, not only is there no overcommit, but typically the maximum amount of memory per application is determined ahead of time. </li> <li> When writing a library, one of the main goals is code reuse. By making code handle allocation failure correctly, a library becomes eligible to be reused in more contexts. </li> <li> Although some software has grown to depend on overcommit being enabled, its existence is the source of countless user experience disasters. When a system with overcommit enabled, such as Linux on default settings, comes close to memory exhaustion, the system locks up and becomes unusable. At this point, the OOM Killer selects an application to kill based on heuristics. This non-deterministic decision often results in an important process being killed, and often fails to return the system back to working order. </li> </ul> <h3 id="Recursion">Recursion </h3> <p> Recursion is a fundamental tool in modeling software. However it has an often-overlooked problem: unbounded memory allocation. </p> <p> Recursion is an area of active experimentation in Zig and so the documentation here is not final. You can read a <a href="https://ziglang.org/download/0.3.0/release-notes.html#recursion">summary of recursion status in the 0.3.0 release notes</a>. </p> <p> The short summary is that currently recursion works normally as you would expect. Although Zig code is not yet protected from stack overflow, it is planned that a future version of Zig will provide such protection, with some degree of cooperation from Zig code required. </p> <h3 id="Lifetime-and-Ownership">Lifetime and Ownership </h3> <p> It is the Zig programmer's responsibility to ensure that a <a href="#Pointers">pointer</a> is not accessed when the memory pointed to is no longer available. Note that a <a href="#Slices">slice</a> is a form of pointer, in that it references other memory. </p> <p> In order to prevent bugs, there are some helpful conventions to follow when dealing with pointers. In general, when a function returns a pointer, the documentation for the function should explain who "owns" the pointer. This concept helps the programmer decide when it is appropriate, if ever, to free the pointer. </p> <p> For example, the function's documentation may say "caller owns the returned memory", in which case the code that calls the function must have a plan for when to free that memory. Probably in this situation, the function will accept an <code><span class="line">Allocator</span></code> parameter. </p> <p> Sometimes the lifetime of a pointer may be more complicated. For example, the <code><span class="line">std.ArrayList(T).items</span></code> slice has a lifetime that remains valid until the next time the list is resized, such as by appending new elements. </p> <p> The API documentation for functions and data structures should take great care to explain the ownership and lifetime semantics of pointers. Ownership determines whose responsibility it is to free the memory referenced by the pointer, and lifetime determines the point at which the memory becomes inaccessible (lest <a href="#Undefined-Behavior">Undefined Behavior</a> occur). </p> <h2 id="Compile-Variables">Compile Variables </h2> <p> Compile variables are accessible by importing the <code><span class="line"><span class="tok-str">"builtin"</span></span></code> package, which the compiler makes available to every Zig source file. It contains compile-time constants such as the current target, endianness, and release mode. </p> <figure><figcaption class="zig-cap"><cite class="file">compile_variables.zig</cite></figcaption><pre data-language="zig">const builtin = @import("builtin");
const separator = if (builtin.os.tag == .windows) '\\' else '/';</pre></figure> <p> Example of what is imported with <code><span class="line"><span class="tok-builtin">@import</span>(<span class="tok-str">"builtin"</span>)</span></code>: </p> <figure><figcaption class="zig-cap"><cite>@import("builtin")</cite></figcaption><pre data-language="zig">const std = @import("std");
/// Zig version. When writing code that supports multiple versions of Zig, prefer
/// feature detection (i.e. with `@hasDecl` or `@hasField`) over version checks.
pub const zig_version = std.SemanticVersion.parse(zig_version_string) catch unreachable;
pub const zig_version_string = "0.11.0";
pub const zig_backend = std.builtin.CompilerBackend.stage2_x86_64;

pub const output_mode = std.builtin.OutputMode.Obj;
pub const link_mode = std.builtin.LinkMode.Static;
pub const is_test = false;
pub const single_threaded = false;
pub const abi = std.Target.Abi.gnu;
pub const cpu: std.Target.Cpu = .{
    .arch = .x86_64,
    .model = &amp;std.Target.x86.cpu.znver2,
    .features = std.Target.x86.featureSet(&amp;[_]std.Target.x86.Feature{
        .@"64bit",
        .adx,
        .aes,
        .allow_light_256_bit,
        .avx,
        .avx2,
        .bmi,
        .bmi2,
        .branchfusion,
        .clflushopt,
        .clwb,
        .clzero,
        .cmov,
        .crc32,
        .cx16,
        .cx8,
        .f16c,
        .fast_15bytenop,
        .fast_bextr,
        .fast_lzcnt,
        .fast_movbe,
        .fast_scalar_fsqrt,
        .fast_scalar_shift_masks,
        .fast_variable_perlane_shuffle,
        .fast_vector_fsqrt,
        .fma,
        .fsgsbase,
        .fxsr,
        .lzcnt,
        .mmx,
        .movbe,
        .mwaitx,
        .nopl,
        .pclmul,
        .popcnt,
        .prfchw,
        .rdpid,
        .rdpru,
        .rdrnd,
        .rdseed,
        .sahf,
        .sbb_dep_breaking,
        .sha,
        .slow_shld,
        .sse,
        .sse2,
        .sse3,
        .sse4_1,
        .sse4_2,
        .sse4a,
        .ssse3,
        .vzeroupper,
        .wbnoinvd,
        .x87,
        .xsave,
        .xsavec,
        .xsaveopt,
        .xsaves,
    }),
};
pub const os = std.Target.Os{
    .tag = .linux,
    .version_range = .{ .linux = .{
        .range = .{
            .min = .{
                .major = 5,
                .minor = 10,
                .patch = 0,
            },
            .max = .{
                .major = 5,
                .minor = 10,
                .patch = 0,
            },
        },
        .glibc = .{
            .major = 2,
            .minor = 19,
            .patch = 0,
        },
    }},
};
pub const target = std.Target{
    .cpu = cpu,
    .os = os,
    .abi = abi,
    .ofmt = object_format,
};
pub const object_format = std.Target.ObjectFormat.elf;
pub const mode = std.builtin.Mode.Debug;
pub const link_libc = false;
pub const link_libcpp = false;
pub const have_error_return_tracing = true;
pub const valgrind_support = true;
pub const sanitize_thread = false;
pub const position_independent_code = false;
pub const position_independent_executable = false;
pub const strip_debug_info = false;
pub const code_model = std.builtin.CodeModel.default;
pub const omit_frame_pointer = false;</pre></figure> <p>See also:</p>
<ul> <li><a href="#Build-Mode">Build Mode</a></li> </ul> <h2 id="Root-Source-File">Root Source File </h2> <p>TODO: explain how root source file finds other files</p> <p>TODO: pub fn main</p> <p>TODO: pub fn panic</p> <p>TODO: if linking with libc you can use export fn main</p> <p>TODO: order independent top level declarations</p> <p>TODO: lazy analysis</p> <p>TODO: using comptime { _ = @import() }</p> <h2 id="Zig-Build-System">Zig Build System </h2> <p> The Zig Build System provides a cross-platform, dependency-free way to declare the logic required to build a project. With this system, the logic to build a project is written in a build.zig file, using the Zig Build System API to declare and configure build artifacts and other tasks. </p> <p> Some examples of tasks the build system can help with: </p> <ul> <li>Creating build artifacts by executing the Zig compiler. This includes building Zig source code as well as C and C++ source code.</li> <li>Capturing user-configured options and using those options to configure the build.</li> <li>Surfacing build configuration as <a href="#comptime">comptime</a> values by providing a file that can be <a href="#import">imported</a> by Zig code.</li> <li>Caching build artifacts to avoid unnecessarily repeating steps.</li> <li>Executing build artifacts or system-installed tools.</li> <li>Running tests and verifying the output of executing a build artifact matches the expected value.</li> <li>Running <code>zig fmt</code> on a codebase or a subset of it.</li> <li>Custom tasks.</li> </ul> <p> To use the build system, run <kbd>zig build --help</kbd> to see a command-line usage help menu. This will include project-specific options that were declared in the build.zig script. </p> <h3 id="Building-an-Executable">Building an Executable </h3> <p>This <code class="file">build.zig</code> file is automatically generated by <kbd>zig init-exe</kbd>.</p> <figure><figcaption class="zig-cap"><cite class="file">build_executable.zig</cite></figcaption><pre data-language="zig">const std = @import("std");

pub fn build(b: *std.Build) void {
    // Standard target options allows the person running `zig build` to choose
    // what target to build for. Here we do not override the defaults, which
    // means any target is allowed, and the default is native. Other options
    // for restricting supported target set are available.
    const target = b.standardTargetOptions(.{});

    // Standard optimization options allow the person running `zig build` to select
    // between Debug, ReleaseSafe, ReleaseFast, and ReleaseSmall. Here we do not
    // set a preferred release mode, allowing the user to decide how to optimize.
    const optimize = b.standardOptimizeOption(.{});

    const exe = b.addExecutable(.{
        .name = "example",
        .root_source_file = .{ .path = "src/main.zig" },
        .target = target,
        .optimize = optimize,
    });
    exe.install();

    const run_cmd = exe.run();
    run_cmd.step.dependOn(b.getInstallStep());
    if (b.args) |args| {
        run_cmd.addArgs(args);
    }

    const run_step = b.step("run", "Run the app");
    run_step.dependOn(&amp;run_cmd.step);
}</pre></figure> <h3 id="Building-a-Library">Building a Library </h3> <p>This <code class="file">build.zig</code> file is automatically generated by <kbd>zig init-lib</kbd>.</p> <figure><figcaption class="zig-cap"><cite class="file">build_library.zig</cite></figcaption><pre data-language="zig">const std = @import("std");

pub fn build(b: *std.Build) void {
    const optimize = b.standardOptimizeOption(.{});
    const lib = b.addStaticLibrary(.{
        .name = "example",
        .root_source_file = .{ .path = "src/main.zig" },
        .optimize = optimize,
    });
    lib.install();

    const main_tests = b.addTest(.{
        .root_source_file = .{ .path = "src/main.zig" },
        .optimize = optimize,
    });

    const test_step = b.step("test", "Run library tests");
    test_step.dependOn(&amp;main_tests.step);
}</pre></figure> <h3 id="Compiling-C-Source-Code">Compiling C Source Code </h3> <pre data-language="zig">lib.addCSourceFile("src/lib.c", &amp;[_][]const u8{
    "-Wall",
    "-Wextra",
    "-Werror",
});</pre> <h2 id="C">C </h2> <p> Although Zig is independent of C, and, unlike most other languages, does not depend on libc, Zig acknowledges the importance of interacting with existing C code. </p> <p> There are a few ways that Zig facilitates C interop. </p> <h3 id="C-Type-Primitives">C Type Primitives </h3> <p> These have guaranteed C ABI compatibility and can be used like any other type. </p> <ul> <li><code><span class="line"><span class="tok-type">c_char</span></span></code></li> <li><code><span class="line"><span class="tok-type">c_short</span></span></code></li> <li><code><span class="line"><span class="tok-type">c_ushort</span></span></code></li> <li><code><span class="line"><span class="tok-type">c_int</span></span></code></li> <li><code><span class="line"><span class="tok-type">c_uint</span></span></code></li> <li><code><span class="line"><span class="tok-type">c_long</span></span></code></li> <li><code><span class="line"><span class="tok-type">c_ulong</span></span></code></li> <li><code><span class="line"><span class="tok-type">c_longlong</span></span></code></li> <li><code><span class="line"><span class="tok-type">c_ulonglong</span></span></code></li> <li><code><span class="line"><span class="tok-type">c_longdouble</span></span></code></li> </ul> <p> To interop with the C <code><span class="line"><span class="tok-type">void</span></span></code> type, use <code><span class="line"><span class="tok-type">anyopaque</span></span></code>. </p> <p>See also:</p>
<ul> <li><a href="#Primitive-Types">Primitive Types</a></li> </ul> <h3 id="Import-from-C-Header-File">Import from C Header File </h3> <p> The <code><span class="line"><span class="tok-builtin">@cImport</span></span></code> builtin function can be used to directly import symbols from <code class="file">.h</code> files: </p> <figure><figcaption class="zig-cap"><cite class="file">cImport_builtin.zig</cite></figcaption><pre data-language="zig">const c = @cImport({
    // See https://github.com/ziglang/zig/issues/515
    @cDefine("_NO_CRT_STDIO_INLINE", "1");
    @cInclude("stdio.h");
});
pub fn main() void {
    _ = c.printf("hello\n");
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig build-exe cImport_builtin.zig -lc</kbd>
$ <kbd>./cImport_builtin</kbd>
hello
</samp></pre></figure> <p> The <code><span class="line"><span class="tok-builtin">@cImport</span></span></code> function takes an expression as a parameter. This expression is evaluated at compile-time and is used to control preprocessor directives and include multiple <code class="file">.h</code> files: </p> <figure><figcaption class="zig-cap"><cite class="file">@cImport Expression</cite></figcaption><pre data-language="zig">const builtin = @import("builtin");

const c = @cImport({
    @cDefine("NDEBUG", builtin.mode == .ReleaseFast);
    if (something) {
        @cDefine("_GNU_SOURCE", {});
    }
    @cInclude("stdlib.h");
    if (something) {
        @cUndef("_GNU_SOURCE");
    }
    @cInclude("soundio.h");
});</pre></figure> <p>See also:</p>
<ul> <li><a href="#cImport">@cImport</a></li> <li><a href="#cInclude">@cInclude</a></li> <li><a href="#cDefine">@cDefine</a></li> <li><a href="#cUndef">@cUndef</a></li> <li><a href="#import">@import</a></li> </ul> <h3 id="C-Translation-CLI">C Translation CLI </h3> <p> Zig's C translation capability is available as a CLI tool via <kbd>zig translate-c</kbd>. It requires a single filename as an argument. It may also take a set of optional flags that are forwarded to clang. It writes the translated file to stdout. </p> <h4 id="Command-line-flags">
<a href="#toc-Command-line-flags">Command line flags</a> </h4> <ul> <li> <kbd>-I</kbd>: Specify a search directory for include files. May be used multiple times. Equivalent to <a href="https://releases.llvm.org/12.0.0/tools/clang/docs/ClangCommandLineReference.html#cmdoption-clang-i-dir"> clang's <kbd>-I</kbd> flag</a>. The current directory is <em>not</em> included by default; use <kbd>-I.</kbd> to include it. </li> <li> <kbd>-D</kbd>: Define a preprocessor macro. Equivalent to <a href="https://releases.llvm.org/12.0.0/tools/clang/docs/ClangCommandLineReference.html#cmdoption-clang-d-macro"> clang's <kbd>-D</kbd> flag</a>. </li> <li> <kbd>-cflags [flags] --</kbd>: Pass arbitrary additional <a href="https://releases.llvm.org/12.0.0/tools/clang/docs/ClangCommandLineReference.html">command line flags</a> to clang. Note: the list of flags must end with <kbd>--</kbd> </li> <li> <kbd>-target</kbd>: The <a href="#Targets">target triple</a> for the translated Zig code. If no target is specified, the current host target will be used. </li> </ul> <h4 id="Using--target-and--cflags">
<a href="#toc-Using--target-and--cflags">Using -target and -cflags</a> </h4> <p> <strong>Important!</strong> When translating C code with <kbd>zig translate-c</kbd>, you <strong>must</strong> use the same <kbd>-target</kbd> triple that you will use when compiling the translated code. In addition, you <strong>must</strong> ensure that the <kbd>-cflags</kbd> used, if any, match the cflags used by code on the target system. Using the incorrect <kbd>-target</kbd> or <kbd>-cflags</kbd> could result in clang or Zig parse failures, or subtle ABI incompatibilities when linking with C code. </p> <figure><figcaption class="c-cap"><cite class="file">varytarget.h</cite></figcaption><pre data-language="zig">long FOO = __LONG_MAX__;</pre></figure> <figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig translate-c -target thumb-freestanding-gnueabihf varytarget.h|grep FOO</kbd>
pub export var FOO: c_long = 2147483647;
$ <kbd>zig translate-c -target x86_64-macos-gnu varytarget.h|grep FOO</kbd>
pub export var FOO: c_long = 9223372036854775807;
</samp></pre></figure> <figure><figcaption class="c-cap"><cite class="file">varycflags.h</cite></figcaption><pre data-language="zig">enum FOO { BAR };
int do_something(enum FOO foo);</pre></figure> <figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig translate-c varycflags.h|grep -B1 do_something</kbd>
pub const enum_FOO = c_uint;
pub extern fn do_something(foo: enum_FOO) c_int;
$ <kbd>zig translate-c -cflags -fshort-enums -- varycflags.h|grep -B1 do_something</kbd>
pub const enum_FOO = u8;
pub extern fn do_something(foo: enum_FOO) c_int;
</samp></pre></figure> <h4 id="cImport-vs-translate-c">
<a href="#toc-cImport-vs-translate-c">@cImport vs translate-c</a> </h4> <p><code><span class="line"><span class="tok-builtin">@cImport</span></span></code> and <kbd>zig translate-c</kbd> use the same underlying C translation functionality, so on a technical level they are equivalent. In practice, <code><span class="line"><span class="tok-builtin">@cImport</span></span></code> is useful as a way to quickly and easily access numeric constants, typedefs, and record types without needing any extra setup. If you need to pass <a href="#Using--target-and--cflags">cflags</a> to clang, or if you would like to edit the translated code, it is recommended to use <kbd>zig translate-c</kbd> and save the results to a file. Common reasons for editing the generated code include: changing <code><span class="line"><span class="tok-kw">anytype</span></span></code> parameters in function-like macros to more specific types; changing <code><span class="line">[*c]T</span></code> pointers to <code><span class="line">[*]T</span></code> or <code><span class="line">*T</span></code> pointers for improved type safety; and <a href="#setRuntimeSafety">enabling or disabling runtime safety</a> within specific functions. </p> <p>See also:</p>
<ul> <li><a href="#Targets">Targets</a></li> <li><a href="#C-Type-Primitives">C Type Primitives</a></li> <li><a href="#Pointers">Pointers</a></li> <li><a href="#C-Pointers">C Pointers</a></li> <li><a href="#Import-from-C-Header-File">Import from C Header File</a></li> <li><a href="#cInclude">@cInclude</a></li> <li><a href="#cImport">@cImport</a></li> <li><a href="#setRuntimeSafety">@setRuntimeSafety</a></li> </ul> <h3 id="C-Translation-Caching">C Translation Caching </h3> <p> The C translation feature (whether used via <kbd>zig translate-c</kbd> or <code><span class="line"><span class="tok-builtin">@cImport</span></span></code>) integrates with the Zig caching system. Subsequent runs with the same source file, target, and cflags will use the cache instead of repeatedly translating the same code. </p> <p> To see where the cached files are stored when compiling code that uses <code><span class="line"><span class="tok-builtin">@cImport</span></span></code>, use the <kbd>--verbose-cimport</kbd> flag: </p> <figure><figcaption class="zig-cap"><cite class="file">verbose_cimport_flag.zig</cite></figcaption><pre data-language="zig">const c = @cImport({
    @cDefine("_NO_CRT_STDIO_INLINE", "1");
    @cInclude("stdio.h");
});
pub fn main() void {
    _ = c;
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig build-exe verbose_cimport_flag.zig -lc --verbose-cimport</kbd>
info(compilation): C import source: /home/ci/actions-runner/_work/zig-bootstrap/out/zig-local-cache/o/cb3726b36a399e0d94e4fdd382d000fe/cimport.h
info(compilation): C import .d file: /home/ci/actions-runner/_work/zig-bootstrap/out/zig-local-cache/o/cb3726b36a399e0d94e4fdd382d000fe/cimport.h.d
info(compilation): C import output: /home/ci/actions-runner/_work/zig-bootstrap/out/zig-local-cache/o/247ae563161ab677314a658db45e91da/cimport.zig
LLVM Emit Object... [20D[0KLLVM Emit Object... [20D[0KLLD Link... [12D[0K$ ./verbose_cimport_flag
</samp></pre></figure> <p> <code class="file">cimport.h</code> contains the file to translate (constructed from calls to <code><span class="line"><span class="tok-builtin">@cInclude</span></span></code>, <code><span class="line"><span class="tok-builtin">@cDefine</span></span></code>, and <code><span class="line"><span class="tok-builtin">@cUndef</span></span></code>), <code class="file">cimport.h.d</code> is the list of file dependencies, and <code class="file">cimport.zig</code> contains the translated output. </p> <p>See also:</p>
<ul> <li><a href="#Import-from-C-Header-File">Import from C Header File</a></li> <li><a href="#C-Translation-CLI">C Translation CLI</a></li> <li><a href="#cInclude">@cInclude</a></li> <li><a href="#cImport">@cImport</a></li> </ul> <h3 id="Translation-failures">Translation failures </h3> <p> Some C constructs cannot be translated to Zig - for example, <em>goto</em>, structs with bitfields, and token-pasting macros. Zig employs <em>demotion</em> to allow translation to continue in the face of non-translatable entities. </p> <p> Demotion comes in three varieties - <a href="#opaque">opaque</a>, <em>extern</em>, and <code><span class="line"><span class="tok-builtin">@compileError</span></span></code>. C structs and unions that cannot be translated correctly will be translated as <code><span class="line"><span class="tok-kw">opaque</span>{}</span></code>. Functions that contain opaque types or code constructs that cannot be translated will be demoted to <code><span class="line"><span class="tok-kw">extern</span></span></code> declarations. Thus, non-translatable types can still be used as pointers, and non-translatable functions can be called so long as the linker is aware of the compiled function. </p> <p> <code><span class="line"><span class="tok-builtin">@compileError</span></span></code> is used when top-level definitions (global variables, function prototypes, macros) cannot be translated or demoted. Since Zig uses lazy analysis for top-level declarations, untranslatable entities will not cause a compile error in your code unless you actually use them. </p> <p>See also:</p>
<ul> <li><a href="#opaque">opaque</a></li> <li><a href="#extern">extern</a></li> <li><a href="#compileError">@compileError</a></li> </ul> <h3 id="C-Macros">C Macros </h3> <p> C Translation makes a best-effort attempt to translate function-like macros into equivalent Zig functions. Since C macros operate at the level of lexical tokens, not all C macros can be translated to Zig. Macros that cannot be translated will be demoted to <code><span class="line"><span class="tok-builtin">@compileError</span></span></code>. Note that C code which <em>uses</em> macros will be translated without any additional issues (since Zig operates on the pre-processed source with macros expanded). It is merely the macros themselves which may not be translatable to Zig. </p> <p>Consider the following example:</p> <figure><figcaption class="c-cap"><cite class="file">macro.c</cite></figcaption><pre data-language="zig">#define MAKELOCAL(NAME, INIT) int NAME = INIT
int foo(void) {
   MAKELOCAL(a, 1);
   MAKELOCAL(b, 2);
   return a + b;
}</pre></figure> <figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig translate-c macro.c &gt; macro.zig</kbd>
</samp></pre></figure> <figure><figcaption class="zig-cap"><cite class="file">macro.zig</cite></figcaption><pre data-language="zig">pub export fn foo() c_int {
    var a: c_int = 1;
    var b: c_int = 2;
    return a + b;
}
pub const MAKELOCAL = @compileError("unable to translate C expr: unexpected token .Equal"); // macro.c:1:9</pre></figure> <p>Note that <code><span class="line">foo</span></code> was translated correctly despite using a non-translatable macro. <code><span class="line">MAKELOCAL</span></code> was demoted to <code><span class="line"><span class="tok-builtin">@compileError</span></span></code> since it cannot be expressed as a Zig function; this simply means that you cannot directly use <code><span class="line">MAKELOCAL</span></code> from Zig. </p> <p>See also:</p>
<ul> <li><a href="#compileError">@compileError</a></li> </ul> <h3 id="C-Pointers">C Pointers </h3> <p> This type is to be avoided whenever possible. The only valid reason for using a C pointer is in auto-generated code from translating C code. </p> <p> When importing C header files, it is ambiguous whether pointers should be translated as single-item pointers (<code><span class="line">*T</span></code>) or many-item pointers (<code><span class="line">[*]T</span></code>). C pointers are a compromise so that Zig code can utilize translated header files directly. </p> <p><code><span class="line">[*c]T</span></code> - C pointer.</p> <ul> <li>Supports all the syntax of the other two pointer types (<code><span class="line">*T</span></code>) and (<code><span class="line">[*]T</span></code>).</li> <li>Coerces to other pointer types, as well as <a href="#Optional-Pointers">Optional Pointers</a>. When a C pointer is coerced to a non-optional pointer, safety-checked <a href="#Undefined-Behavior">Undefined Behavior</a> occurs if the address is 0. </li> <li>Allows address 0. On non-freestanding targets, dereferencing address 0 is safety-checked <a href="#Undefined-Behavior">Undefined Behavior</a>. Optional C pointers introduce another bit to keep track of null, just like <code><span class="line">?<span class="tok-type">usize</span></span></code>. Note that creating an optional C pointer is unnecessary as one can use normal <a href="#Optional-Pointers">Optional Pointers</a>. </li> <li>Supports <a href="#Type-Coercion">Type Coercion</a> to and from integers.</li> <li>Supports comparison with integers.</li> <li>Does not support Zig-only pointer attributes such as alignment. Use normal <a href="#Pointers">Pointers</a> please!</li> </ul> <p>When a C pointer is pointing to a single struct (not an array), dereference the C pointer to access the struct's fields or member data. That syntax looks like this: </p> <p><code><span class="line">ptr_to_struct.*.struct_member</span></code></p> <p>This is comparable to doing <code><span class="line">-&gt;</span></code> in C.</p> <p> When a C pointer is pointing to an array of structs, the syntax reverts to this:</p> <p><code><span class="line">ptr_to_struct_array[index].struct_member</span></code></p> <h3 id="C-Variadic-Functions">C Variadic Functions </h3> <p>Zig supports extern variadic functions.</p> <figure><figcaption class="zig-cap"><cite class="file">test_variadic_function.zig</cite></figcaption><pre data-language="zig">const std = @import("std");
const testing = std.testing;

pub extern "c" fn printf(format: [*:0]const u8, ...) c_int;

test "variadic function" {
    try testing.expect(printf("Hello, world!\n") == 14);
    try testing.expect(@typeInfo(@TypeOf(printf)).Fn.is_var_args);
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_variadic_function.zig -lc</kbd>
1/1 test.variadic function... OK
All 1 tests passed.
Hello, world!
</samp></pre></figure> <p> Variadic functions can be implemented using <a href="#cVaStart">@cVaStart</a>, <a href="#cVaEnd">@cVaEnd</a>, <a href="#cVaArg">@cVaArg</a> and <a href="#cVaCopy">@cVaCopy</a>. </p> <figure><figcaption class="zig-cap"><cite class="file">test_defining_variadic_function.zig</cite></figcaption><pre data-language="zig">const std = @import("std");
const testing = std.testing;
const builtin = @import("builtin");

fn add(count: c_int, ...) callconv(.C) c_int {
    var ap = @cVaStart();
    defer @cVaEnd(&amp;ap);
    var i: usize = 0;
    var sum: c_int = 0;
    while (i &lt; count) : (i += 1) {
        sum += @cVaArg(&amp;ap, c_int);
    }
    return sum;
}

test "defining a variadic function" {
    // Variadic functions are currently disabled on some targets due to miscompilations.
    if (builtin.cpu.arch == .aarch64 and builtin.os.tag != .windows and builtin.os.tag != .macos) return error.SkipZigTest;
    if (builtin.cpu.arch == .x86_64 and builtin.os.tag == .windows) return error.SkipZigTest;

    try std.testing.expectEqual(@as(c_int, 0), add(0));
    try std.testing.expectEqual(@as(c_int, 1), add(1, @as(c_int, 1)));
    try std.testing.expectEqual(@as(c_int, 3), add(2, @as(c_int, 1), @as(c_int, 2)));
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig test test_defining_variadic_function.zig</kbd>
1/1 test.defining a variadic function... OK
All 1 tests passed.
</samp></pre></figure> <h3 id="Exporting-a-C-Library">Exporting a C Library </h3> <p> One of the primary use cases for Zig is exporting a library with the C ABI for other programming languages to call into. The <code><span class="line"><span class="tok-kw">export</span></span></code> keyword in front of functions, variables, and types causes them to be part of the library API: </p> <figure><figcaption class="zig-cap"><cite class="file">mathtest.zig</cite></figcaption><pre data-language="zig">export fn add(a: i32, b: i32) i32 {
    return a + b;
}</pre></figure> <p>To make a static library:</p> <figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig build-lib mathtest.zig</kbd>
</samp></pre></figure> <p>To make a shared library:</p> <figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig build-lib mathtest.zig -dynamic</kbd>
</samp></pre></figure> <p>Here is an example with the <a href="#Zig-Build-System">Zig Build System</a>:</p> <figure><figcaption class="c-cap"><cite class="file">test.c</cite></figcaption><pre data-language="zig">// This header is generated by zig from mathtest.zig
#include "mathtest.h"
#include &lt;stdio.h&gt;

int main(int argc, char **argv) {
    int32_t result = add(42, 1337);
    printf("%d\n", result);
    return 0;
}</pre></figure> <figure><figcaption class="zig-cap"><cite class="file">build_c.zig</cite></figcaption><pre data-language="zig">const std = @import("std");

pub fn build(b: *std.Build) void {
    const lib = b.addSharedLibrary(.{
        .name = "mathtest",
        .root_source_file = .{ .path = "mathtest.zig" },
        .version = .{ .major = 1, .minor = 0, .patch = 0 },
    });
    const exe = b.addExecutable(.{
        .name = "test",
    });
    exe.addCSourceFile("test.c", &amp;[_][]const u8{"-std=c99"});
    exe.linkLibrary(lib);
    exe.linkSystemLibrary("c");

    b.default_step.dependOn(&amp;exe.step);

    const run_cmd = exe.run();

    const test_step = b.step("test", "Test the program");
    test_step.dependOn(&amp;run_cmd.step);
}</pre></figure> <figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig build test</kbd>
1379
</samp></pre></figure> <p>See also:</p>
<ul> <li><a href="#export">export</a></li> </ul> <h3 id="Mixing-Object-Files">Mixing Object Files </h3> <p> You can mix Zig object files with any other object files that respect the C ABI. Example: </p> <figure><figcaption class="zig-cap"><cite class="file">base64.zig</cite></figcaption><pre data-language="zig">const base64 = @import("std").base64;

export fn decode_base_64(
    dest_ptr: [*]u8,
    dest_len: usize,
    source_ptr: [*]const u8,
    source_len: usize,
) usize {
    const src = source_ptr[0..source_len];
    const dest = dest_ptr[0..dest_len];
    const base64_decoder = base64.standard.Decoder;
    const decoded_size = base64_decoder.calcSizeForSlice(src) catch unreachable;
    base64_decoder.decode(dest[0..decoded_size], src) catch unreachable;
    return decoded_size;
}</pre></figure> <figure><figcaption class="c-cap"><cite class="file">test.c</cite></figcaption><pre data-language="zig">// This header is generated by zig from base64.zig
#include "base64.h"

#include &lt;string.h&gt;
#include &lt;stdio.h&gt;

int main(int argc, char **argv) {
    const char *encoded = "YWxsIHlvdXIgYmFzZSBhcmUgYmVsb25nIHRvIHVz";
    char buf[200];

    size_t len = decode_base_64(buf, 200, encoded, strlen(encoded));
    buf[len] = 0;
    puts(buf);

    return 0;
}</pre></figure> <figure><figcaption class="zig-cap"><cite class="file">build_object.zig</cite></figcaption><pre data-language="zig">const std = @import("std");

pub fn build(b: *std.Build) void {
    const obj = b.addObject(.{
        .name = "base64",
        .root_source_file = .{ .path = "base64.zig" },
    });

    const exe = b.addExecutable(.{
        .name = "test",
    });
    exe.addCSourceFile("test.c", &amp;[_][]const u8{"-std=c99"});
    exe.addObject(obj);
    exe.linkSystemLibrary("c");
    exe.install();
}</pre></figure> <figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig build</kbd>
$ <kbd>./zig-out/bin/test</kbd>
all your base are belong to us
</samp></pre></figure> <p>See also:</p>
<ul> <li><a href="#Targets">Targets</a></li> <li><a href="#Zig-Build-System">Zig Build System</a></li> </ul> <h2 id="WebAssembly">WebAssembly </h2> <p>Zig supports building for WebAssembly out of the box.</p> <h3 id="Freestanding">Freestanding </h3> <p>For host environments like the web browser and nodejs, build as a dynamic library using the freestanding OS target. Here's an example of running Zig code compiled to WebAssembly with nodejs.</p> <figure><figcaption class="zig-cap"><cite class="file">math.zig</cite></figcaption><pre data-language="zig">extern fn print(i32) void;

export fn add(a: i32, b: i32) void {
    print(a + b);
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig build-lib math.zig -target wasm32-freestanding -dynamic -rdynamic</kbd>
</samp></pre></figure> <figure><figcaption class="javascript-cap"><cite class="file">test.js</cite></figcaption><pre data-language="zig">const fs = require('fs');
const source = fs.readFileSync("./math.wasm");
const typedArray = new Uint8Array(source);

WebAssembly.instantiate(typedArray, {
  env: {
    print: (result) =&gt; { console.log(`The result is ${result}`); }
  }}).then(result =&gt; {
  const add = result.instance.exports.add;
  add(1, 2);
});</pre></figure> <figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>node test.js</kbd>
The result is 3
</samp></pre></figure> <h3 id="WASI">WASI </h3> <p>Zig's support for WebAssembly System Interface (WASI) is under active development. Example of using the standard library and reading command line arguments:</p> <figure><figcaption class="zig-cap"><cite class="file">wasi_args.zig</cite></figcaption><pre data-language="zig">const std = @import("std");

pub fn main() !void {
    var general_purpose_allocator = std.heap.GeneralPurposeAllocator(.{}){};
    const gpa = general_purpose_allocator.allocator();
    const args = try std.process.argsAlloc(gpa);
    defer std.process.argsFree(gpa, args);

    for (args, 0..) |arg, i| {
        std.debug.print("{}: {s}\n", .{ i, arg });
    }
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig build-exe wasi_args.zig -target wasm32-wasi</kbd>
</samp></pre></figure> <figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>wasmtime wasi_args.wasm 123 hello</kbd>
0: wasi_args.wasm
1: 123
2: hello
</samp></pre></figure> <p>A more interesting example would be extracting the list of preopens from the runtime. This is now supported in the standard library via <code><span class="line">std.fs.wasi.PreopenList</span></code>:</p> <figure><figcaption class="zig-cap"><cite class="file">wasi_preopens.zig</cite></figcaption><pre data-language="zig">const std = @import("std");
const fs = std.fs;

pub fn main() !void {
    var general_purpose_allocator = std.heap.GeneralPurposeAllocator(.{}){};
    const gpa = general_purpose_allocator.allocator();

    var arena_instance = std.heap.ArenaAllocator.init(gpa);
    defer arena_instance.deinit();
    const arena = arena_instance.allocator();

    const preopens = try fs.wasi.preopensAlloc(arena);

    for (preopens.names, 0..) |preopen, i| {
        std.debug.print("{}: {s}\n", .{ i, preopen });
    }
}</pre></figure><figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig build-exe wasi_preopens.zig -target wasm32-wasi</kbd>
</samp></pre></figure> <figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>wasmtime --dir=. wasi_preopens.wasm</kbd>
0: stdin
1: stdout
2: stderr
3: .
</samp></pre></figure> <h2 id="Targets">Targets </h2> <p> Zig supports generating code for all targets that LLVM supports. Here is what it looks like to execute <code>zig targets</code> on a Linux x86_64 computer: </p> <figure><figcaption class="shell-cap">Shell</figcaption><pre><samp>$ <kbd>zig targets</kbd>
Architectures:
  arm
    v8_4a
    v8_3a
    v8_2a
    v8_1a
    v8
    v8r
    v8m_baseline
    v8m_mainline
    v7
    v7em
    v7m
    v7s
    v7k
    v7ve
    v6
    v6m
    v6k
    v6t2
    v5
    v5te
    v4t
  armeb
    v8_4a
    v8_3a
    v8_2a
    v8_1a
    v8
    v8r
    v8m_baseline
    v8m_mainline
    v7
    v7em
    v7m
    v7s
    v7k
    v7ve
    v6
    v6m
    v6k
    v6t2
    v5
    v5te
    v4t
  aarch64
    v8_4a
    v8_3a
    v8_2a
    v8_1a
    v8
    v8r
    v8m_baseline
    v8m_mainline
  aarch64_be
    v8_4a
    v8_3a
    v8_2a
    v8_1a
    v8
    v8r
    v8m_baseline
    v8m_mainline
  avr
  bpfel
  bpfeb
  hexagon
  mips
  mipsel
  mips64
  mips64el
  msp430
  powerpc
  powerpc64
  powerpc64le
  r600
  amdgcn
  riscv32
  riscv64
  sparc
  sparc64
  sparcel
  s390x
  thumb
    v8_4a
    v8_3a
    v8_2a
    v8_1a
    v8
    v8r
    v8m_baseline
    v8m_mainline
    v7
    v7em
    v7m
    v7s
    v7k
    v7ve
    v6
    v6m
    v6k
    v6t2
    v5
    v5te
    v4t
  thumbeb
    v8_4a
    v8_3a
    v8_2a
    v8_1a
    v8
    v8r
    v8m_baseline
    v8m_mainline
    v7
    v7em
    v7m
    v7s
    v7k
    v7ve
    v6
    v6m
    v6k
    v6t2
    v5
    v5te
    v4t
  x86
  x86_64 (native)
  xcore
  nvptx
  nvptx64
  lanai
  wasm32
  wasm64

Operating Systems:
  freestanding
  ananas
  cloudabi
  dragonfly
  freebsd
  fuchsia
  ios
  kfreebsd
  linux (native)
  lv2
  macos
  netbsd
  openbsd
  solaris
  windows
  haiku
  minix
  rtems
  nacl
  cnk
  aix
  cuda
  nvcl
  amdhsa
  ps4
  elfiamcu
  tvos
  wasi
  watchos
  mesa3d
  contiki
  amdpal
  zen
  uefi

C ABIs:
  none
  gnu (native)
  gnuabin32
  gnuabi64
  gnueabi
  gnueabihf
  gnux32
  code16
  eabi
  eabihf
  android
  musl
  musleabi
  musleabihf
  msvc
  itanium
  cygnus
  coreclr
  simulator

Available libcs:
  aarch64_be-linux-gnu
  aarch64_be-linux-musl
  aarch64-linux-gnu
  aarch64-linux-musleabi
  armeb-linux-gnueabi
  armeb-linux-gnueabihf
  armeb-linux-musleabi
  armeb-linux-musleabihf
  arm-linux-gnueabi
  arm-linux-gnueabihf
  arm-linux-musleabi
  arm-linux-musleabihf
  x86-linux-gnu
  x86-linux-musl
  mips64el-linux-gnuabi64
  mips64el-linux-gnuabin32
  mips64el-linux-musl
  mips64-linux-gnuabi64
  mips64-linux-gnuabin32
  mips64-linux-musl
  mipsel-linux-gnu
  mipsel-linux-musl
  mips-linux-gnu
  mips-linux-musl
  nios2-linux-gnu
  powerpc64le-linux-gnu
  powerpc64le-linux-musl
  powerpc64-linux-gnu
  powerpc64-linux-musl
  powerpc-linux-gnu
  powerpc-linux-musl
  riscv32-linux-musl
  riscv64-linux-gnu
  riscv64-linux-musl
  s390x-linux-gnu
  s390x-linux-musl
  sparc-linux-gnu
  sparc64-linux-gnu
  wasm32-freestanding-musl
  wasm32-wasi-musl
  x86_64-linux-gnu
  x86_64-linux-gnux32
  x86_64-linux-musl
</samp></pre></figure> <p> The Zig Standard Library (<code><span class="line"><span class="tok-builtin">@import</span>(<span class="tok-str">"std"</span>)</span></code>) has architecture, environment, and operating system abstractions, and thus takes additional work to support more platforms. Not all standard library code requires operating system abstractions, however, so things such as generic data structures work on all above platforms. </p> <p>The current list of targets supported by the Zig Standard Library is:</p> <ul> <li>Linux x86_64</li> <li>Windows x86_64</li> <li>macOS x86_64</li> </ul> <h2 id="Style-Guide">Style Guide </h2> <p> These coding conventions are not enforced by the compiler, but they are shipped in this documentation along with the compiler in order to provide a point of reference, should anyone wish to point to an authority on agreed upon Zig coding style. </p> <h3 id="Whitespace">Whitespace </h3> <ul> <li> 4 space indentation </li> <li> Open braces on same line, unless you need to wrap. </li> <li>If a list of things is longer than 2, put each item on its own line and exercise the ability to put an extra comma at the end. </li> <li> Line length: aim for 100; use common sense. </li> </ul> <h3 id="Names">Names </h3> <p> Roughly speaking: <code><span class="line">camelCaseFunctionName</span></code>, <code><span class="line">TitleCaseTypeName</span></code>, <code><span class="line">snake_case_variable_name</span></code>. More precisely: </p> <ul> <li> If <code><span class="line">x</span></code> is a <code><span class="line"><span class="tok-type">type</span></span></code> then <code><span class="line">x</span></code> should be <code><span class="line">TitleCase</span></code>, unless it is a <code><span class="line"><span class="tok-kw">struct</span></span></code> with 0 fields and is never meant to be instantiated, in which case it is considered to be a "namespace" and uses <code><span class="line">snake_case</span></code>. </li> <li> If <code><span class="line">x</span></code> is callable, and <code><span class="line">x</span></code>'s return type is <code><span class="line"><span class="tok-type">type</span></span></code>, then <code><span class="line">x</span></code> should be <code><span class="line">TitleCase</span></code>. </li> <li> If <code><span class="line">x</span></code> is otherwise callable, then <code><span class="line">x</span></code> should be <code><span class="line">camelCase</span></code>. </li> <li> Otherwise, <code><span class="line">x</span></code> should be <code><span class="line">snake_case</span></code>. </li> </ul> <p> Acronyms, initialisms, proper nouns, or any other word that has capitalization rules in written English are subject to naming conventions just like any other word. Even acronyms that are only 2 letters long are subject to these conventions. </p> <p> File names fall into two categories: types and namespaces. If the file (implicitly a struct) has top level fields, it should be named like any other struct with fields using <code class="file">TitleCase</code>. Otherwise, it should use <code class="file">snake_case</code>. Directory names should be <code class="file">snake_case</code>. </p> <p> These are general rules of thumb; if it makes sense to do something different, do what makes sense. For example, if there is an established convention such as <code><span class="line">ENOENT</span></code>, follow the established convention. </p> <h3 id="Examples">Examples </h3> <figure><figcaption class="zig-cap"><cite class="file">style_example.zig</cite></figcaption><pre data-language="zig">const namespace_name = @import("dir_name/file_name.zig");
const TypeName = @import("dir_name/TypeName.zig");
var global_var: i32 = undefined;
const const_name = 42;
const primitive_type_alias = f32;
const string_alias = []u8;

const StructName = struct {
    field: i32,
};
const StructAlias = StructName;

fn functionName(param_name: TypeName) void {
    var functionPointer = functionName;
    functionPointer();
    functionPointer = otherFunction;
    functionPointer();
}
const functionAlias = functionName;

fn ListTemplateFunction(comptime ChildType: type, comptime fixed_size: usize) type {
    return List(ChildType, fixed_size);
}

fn ShortList(comptime T: type, comptime n: usize) type {
    return struct {
        field_name: [n]T,
        fn methodName() void {}
    };
}

// The word XML loses its casing when used in Zig identifiers.
const xml_document =
    \\&lt;?xml version="1.0" encoding="UTF-8"?&gt;
    \\&lt;document&gt;
    \\&lt;/document&gt;
;
const XmlParser = struct {
    field: i32,
};

// The initials BE (Big Endian) are just another word in Zig identifier names.
fn readU32Be() u32 {}</pre></figure> <p> See the <a href="#Zig-Standard-Library">Zig Standard Library</a> for more examples. </p> <h3 id="Doc-Comment-Guidance">Doc Comment Guidance </h3> <ul> <li>Omit any information that is redundant based on the name of the thing being documented.</li> <li>Duplicating information onto multiple similar functions is encouraged because it helps IDEs and other tools provide better help text.</li> <li>Use the word <strong>assume</strong> to indicate invariants that cause <a href="#Undefined-Behavior">Undefined Behavior</a> when violated.</li> <li>Use the word <strong>assert</strong> to indicate invariants that cause <em>safety-checked</em> <a href="#Undefined-Behavior">Undefined Behavior</a> when violated.</li> </ul> <h2 id="Source-Encoding">Source Encoding </h2> <p>Zig source code is encoded in UTF-8. An invalid UTF-8 byte sequence results in a compile error.</p> <p>Throughout all zig source code (including in comments), some code points are never allowed:</p> <ul> <li>Ascii control characters, except for U+000a (LF), U+000d (CR), and U+0009 (HT): U+0000 - U+0008, U+000b - U+000c, U+000e - U+0001f, U+007f.</li> <li>Non-Ascii Unicode line endings: U+0085 (NEL), U+2028 (LS), U+2029 (PS).</li> </ul> <p> LF (byte value 0x0a, code point U+000a, <code><span class="line"><span class="tok-str">'\n'</span></span></code>) is the line terminator in Zig source code. This byte value terminates every line of zig source code except the last line of the file. It is recommended that non-empty source files end with an empty line, which means the last byte would be 0x0a (LF). </p> <p> Each LF may be immediately preceded by a single CR (byte value 0x0d, code point U+000d, <code><span class="line"><span class="tok-str">'\r'</span></span></code>) to form a Windows style line ending, but this is discouraged. Note that in mulitline strings, CRLF sequences will be encoded as LF when compiled into a zig program. A CR in any other context is not allowed. </p> <p> HT hard tabs (byte value 0x09, code point U+0009, <code><span class="line"><span class="tok-str">'\t'</span></span></code>) are interchangeable with SP spaces (byte value 0x20, code point U+0020, <code><span class="line"><span class="tok-str">' '</span></span></code>) as a token separator, but use of hard tabs is discouraged. See <a href="#Grammar">Grammar</a>. </p> <p> For compatibility with other tools, the compiler ignores a UTF-8-encoded byte order mark (U+FEFF) if it is the first Unicode code point in the source text. A byte order mark is not allowed anywhere else in the source. </p> <p> Note that running <kbd>zig fmt</kbd> on a source file will implement all recommendations mentioned here. </p> <p> Note that a tool reading Zig source code can make assumptions if the source code is assumed to be correct Zig code. For example, when identifying the ends of lines, a tool can use a naive search such as <code>/\n/</code>, or an <a href="https://msdn.microsoft.com/en-us/library/dd409797.aspx">advanced</a> search such as <code>/\r\n?|[\n\u0085\u2028\u2029]/</code>, and in either case line endings will be correctly identified. For another example, when identifying the whitespace before the first token on a line, a tool can either use a naive search such as <code>/[ \t]/</code>, or an <a href="https://tc39.es/ecma262/#sec-characterclassescape">advanced</a> search such as <code>/\s/</code>, and in either case whitespace will be correctly identified. </p> <h2 id="Keyword-Reference">Keyword Reference </h2> <div class="table-wrapper"> <table> <caption>Keywords</caption> <thead> <tr> <th scope="col">Keyword</th> <th scope="col">Description</th> </tr> </thead> <tbody> <tr> <th scope="row"> <pre data-language="zig">addrspace</pre> </th> <td> The <code><span class="line"><span class="tok-kw">addrspace</span></span></code> keyword. <ul> <li>TODO add documentation for addrspace</li> </ul> </td> </tr> <tr> <th scope="row"> <pre data-language="zig">align</pre> </th> <td> <code><span class="line"><span class="tok-kw">align</span></span></code> can be used to specify the alignment of a pointer. It can also be used after a variable or function declaration to specify the alignment of pointers to that variable or function. <ul> <li>See also <a href="#Alignment">Alignment</a>
</li> </ul> </td> </tr> <tr> <th scope="row"> <pre data-language="zig">allowzero</pre> </th> <td> The pointer attribute <code><span class="line"><span class="tok-kw">allowzero</span></span></code> allows a pointer to have address zero. <ul> <li>See also <a href="#allowzero">allowzero</a>
</li> </ul> </td> </tr> <tr> <th scope="row"> <pre data-language="zig">and</pre> </th> <td> The boolean operator <code><span class="line"><span class="tok-kw">and</span></span></code>. <ul> <li>See also <a href="#Operators">Operators</a>
</li> </ul> </td> </tr> <tr> <th scope="row"> <pre data-language="zig">anyframe</pre> </th> <td> <code><span class="line"><span class="tok-kw">anyframe</span></span></code> can be used as a type for variables which hold pointers to function frames. <ul> <li>See also <a href="#Async-Functions">Async Functions</a>
</li> </ul> </td> </tr> <tr> <th scope="row"> <pre data-language="zig">anytype</pre> </th> <td> Function parameters can be declared with <code><span class="line"><span class="tok-kw">anytype</span></span></code> in place of the type. The type will be inferred where the function is called. <ul> <li>See also <a href="#Function-Parameter-Type-Inference">Function Parameter Type Inference</a>
</li> </ul> </td> </tr> <tr> <th scope="row"> <pre data-language="zig">asm</pre> </th> <td> <code><span class="line"><span class="tok-kw">asm</span></span></code> begins an inline assembly expression. This allows for directly controlling the machine code generated on compilation. <ul> <li>See also <a href="#Assembly">Assembly</a>
</li> </ul> </td> </tr> <tr> <th scope="row"> <pre data-language="zig">async</pre> </th> <td> <code><span class="line"><span class="tok-kw">async</span></span></code> can be used before a function call to get a pointer to the function's frame when it suspends. <ul> <li>See also <a href="#Async-Functions">Async Functions</a>
</li> </ul> </td> </tr> <tr> <th scope="row"> <pre data-language="zig">await</pre> </th> <td> <code><span class="line"><span class="tok-kw">await</span></span></code> can be used to suspend the current function until the frame provided after the <code><span class="line"><span class="tok-kw">await</span></span></code> completes. <code><span class="line"><span class="tok-kw">await</span></span></code> copies the value returned from the target function's frame to the caller. <ul> <li>See also <a href="#Async-Functions">Async Functions</a>
</li> </ul> </td> </tr> <tr> <th scope="row"> <pre data-language="zig">break</pre> </th> <td> <code><span class="line"><span class="tok-kw">break</span></span></code> can be used with a block label to return a value from the block. It can also be used to exit a loop before iteration completes naturally. <ul> <li>See also <a href="#Blocks">Blocks</a>, <a href="#while">while</a>, <a href="#for">for</a>
</li> </ul> </td> </tr> <tr> <th scope="row"> <pre data-language="zig">catch</pre> </th> <td> <code><span class="line"><span class="tok-kw">catch</span></span></code> can be used to evaluate an expression if the expression before it evaluates to an error. The expression after the <code><span class="line"><span class="tok-kw">catch</span></span></code> can optionally capture the error value. <ul> <li>See also <a href="#catch">catch</a>, <a href="#Operators">Operators</a>
</li> </ul> </td> </tr> <tr> <th scope="row"> <pre data-language="zig">comptime</pre> </th> <td> <code><span class="line"><span class="tok-kw">comptime</span></span></code> before a declaration can be used to label variables or function parameters as known at compile time. It can also be used to guarantee an expression is run at compile time. <ul> <li>See also <a href="#comptime">comptime</a>
</li> </ul> </td> </tr> <tr> <th scope="row"> <pre data-language="zig">const</pre> </th> <td> <code><span class="line"><span class="tok-kw">const</span></span></code> declares a variable that can not be modified. Used as a pointer attribute, it denotes the value referenced by the pointer cannot be modified. <ul> <li>See also <a href="#Variables">Variables</a>
</li> </ul> </td> </tr> <tr> <th scope="row"> <pre data-language="zig">continue</pre> </th> <td> <code><span class="line"><span class="tok-kw">continue</span></span></code> can be used in a loop to jump back to the beginning of the loop. <ul> <li>See also <a href="#while">while</a>, <a href="#for">for</a>
</li> </ul> </td> </tr> <tr> <th scope="row"> <pre data-language="zig">defer</pre> </th> <td> <code><span class="line"><span class="tok-kw">defer</span></span></code> will execute an expression when control flow leaves the current block. <ul> <li>See also <a href="#defer">defer</a>
</li> </ul> </td> </tr> <tr> <th scope="row"> <pre data-language="zig">else</pre> </th> <td> <code><span class="line"><span class="tok-kw">else</span></span></code> can be used to provide an alternate branch for <code><span class="line"><span class="tok-kw">if</span></span></code>, <code><span class="line"><span class="tok-kw">switch</span></span></code>, <code><span class="line"><span class="tok-kw">while</span></span></code>, and <code><span class="line"><span class="tok-kw">for</span></span></code> expressions. <ul> <li>If used after an if expression, the else branch will be executed if the test value returns false, null, or an error.</li> <li>If used within a switch expression, the else branch will be executed if the test value matches no other cases.</li> <li>If used after a loop expression, the else branch will be executed if the loop finishes without breaking.</li> <li>See also <a href="#if">if</a>, <a href="#switch">switch</a>, <a href="#while">while</a>, <a href="#for">for</a>
</li> </ul> </td> </tr> <tr> <th scope="row"> <pre data-language="zig">enum</pre> </th> <td> <code><span class="line"><span class="tok-kw">enum</span></span></code> defines an enum type. <ul> <li>See also <a href="#enum">enum</a>
</li> </ul> </td> </tr> <tr> <th scope="row"> <pre data-language="zig">errdefer</pre> </th> <td> <code><span class="line"><span class="tok-kw">errdefer</span></span></code> will execute an expression when control flow leaves the current block if the function returns an error, the errdefer expression can capture the unwrapped value. <ul> <li>See also <a href="#errdefer">errdefer</a>
</li> </ul> </td> </tr> <tr> <th scope="row"> <pre data-language="zig">error</pre> </th> <td> <code><span class="line"><span class="tok-kw">error</span></span></code> defines an error type. <ul> <li>See also <a href="#Errors">Errors</a>
</li> </ul> </td> </tr> <tr> <th scope="row"> <pre data-language="zig">export</pre> </th> <td> <code><span class="line"><span class="tok-kw">export</span></span></code> makes a function or variable externally visible in the generated object file. Exported functions default to the C calling convention. <ul> <li>See also <a href="#Functions">Functions</a>
</li> </ul> </td> </tr> <tr> <th scope="row"> <pre data-language="zig">extern</pre> </th> <td> <code><span class="line"><span class="tok-kw">extern</span></span></code> can be used to declare a function or variable that will be resolved at link time, when linking statically or at runtime, when linking dynamically. <ul> <li>See also <a href="#Functions">Functions</a>
</li> </ul> </td> </tr> <tr> <th scope="row"> <pre data-language="zig">fn</pre> </th> <td> <code><span class="line"><span class="tok-kw">fn</span></span></code> declares a function. <ul> <li>See also <a href="#Functions">Functions</a>
</li> </ul> </td> </tr> <tr> <th scope="row"> <pre data-language="zig">for</pre> </th> <td> A <code><span class="line"><span class="tok-kw">for</span></span></code> expression can be used to iterate over the elements of a slice, array, or tuple. <ul> <li>See also <a href="#for">for</a>
</li> </ul> </td> </tr> <tr> <th scope="row"> <pre data-language="zig">if</pre> </th> <td> An <code><span class="line"><span class="tok-kw">if</span></span></code> expression can test boolean expressions, optional values, or error unions. For optional values or error unions, the if expression can capture the unwrapped value. <ul> <li>See also <a href="#if">if</a>
</li> </ul> </td> </tr> <tr> <th scope="row"> <pre data-language="zig">inline</pre> </th> <td> <code><span class="line"><span class="tok-kw">inline</span></span></code> can be used to label a loop expression such that it will be unrolled at compile time. It can also be used to force a function to be inlined at all call sites. <ul> <li>See also <a href="#inline-while">inline while</a>, <a href="#inline-for">inline for</a>, <a href="#Functions">Functions</a>
</li> </ul> </td> </tr> <tr> <th scope="row"> <pre data-language="zig">linksection</pre> </th> <td> The <code><span class="line"><span class="tok-kw">linksection</span></span></code> keyword. <ul> <li>TODO add documentation for linksection</li> </ul> </td> </tr> <tr> <th scope="row"> <pre data-language="zig">noalias</pre> </th> <td> The <code><span class="line"><span class="tok-kw">noalias</span></span></code> keyword. <ul> <li>TODO add documentation for noalias</li> </ul> </td> </tr> <tr> <th scope="row"> <pre data-language="zig">noinline</pre> </th> <td> <code><span class="line"><span class="tok-kw">noinline</span></span></code> disallows function to be inlined in all call sites. <ul> <li>See also <a href="#Functions">Functions</a>
</li> </ul> </td> </tr> <tr> <th scope="row"> <pre data-language="zig">nosuspend</pre> </th> <td> The <code><span class="line"><span class="tok-kw">nosuspend</span></span></code> keyword can be used in front of a block, statement or expression, to mark a scope where no suspension points are reached. In particular, inside a <code><span class="line"><span class="tok-kw">nosuspend</span></span></code> scope: <ul> <li>Using the <code><span class="line"><span class="tok-kw">suspend</span></span></code> keyword results in a compile error.</li> <li>Using <code><span class="line"><span class="tok-kw">await</span></span></code> on a function frame which hasn't completed yet results in safety-checked <a href="#Undefined-Behavior">Undefined Behavior</a>.</li> <li>Calling an async function may result in safety-checked <a href="#Undefined-Behavior">Undefined Behavior</a>, because it's equivalent to <code>await async some_async_fn()</code>, which contains an <code><span class="line"><span class="tok-kw">await</span></span></code>.</li> </ul> Code inside a <code><span class="line"><span class="tok-kw">nosuspend</span></span></code> scope does not cause the enclosing function to become an <a href="#Async-Functions">async function</a>. <ul> <li>See also <a href="#Async-Functions">Async Functions</a>
</li> </ul> </td> </tr> <tr> <th scope="row"> <pre data-language="zig">or</pre> </th> <td> The boolean operator <code><span class="line"><span class="tok-kw">or</span></span></code>. <ul> <li>See also <a href="#Operators">Operators</a>
</li> </ul> </td> </tr> <tr> <th scope="row"> <pre data-language="zig">orelse</pre> </th> <td> <code><span class="line"><span class="tok-kw">orelse</span></span></code> can be used to evaluate an expression if the expression before it evaluates to null. <ul> <li>See also <a href="#Optionals">Optionals</a>, <a href="#Operators">Operators</a>
</li> </ul> </td> </tr> <tr> <th scope="row"> <pre data-language="zig">packed</pre> </th> <td> The <code><span class="line"><span class="tok-kw">packed</span></span></code> keyword before a struct definition changes the struct's in-memory layout to the guaranteed <code><span class="line"><span class="tok-kw">packed</span></span></code> layout. <ul> <li>See also <a href="#packed-struct">packed struct</a>
</li> </ul> </td> </tr> <tr> <th scope="row"> <pre data-language="zig">pub</pre> </th> <td> The <code><span class="line"><span class="tok-kw">pub</span></span></code> in front of a top level declaration makes the declaration available to reference from a different file than the one it is declared in. <ul> <li>See also <a href="#import">import</a>
</li> </ul> </td> </tr> <tr> <th scope="row"> <pre data-language="zig">resume</pre> </th> <td> <code><span class="line"><span class="tok-kw">resume</span></span></code> will continue execution of a function frame after the point the function was suspended. </td> </tr> <tr> <th scope="row"> <pre data-language="zig">return</pre> </th> <td> <code><span class="line"><span class="tok-kw">return</span></span></code> exits a function with a value. <ul> <li>See also <a href="#Functions">Functions</a>
</li> </ul> </td> </tr> <tr> <th scope="row"> <pre data-language="zig">struct</pre> </th> <td> <code><span class="line"><span class="tok-kw">struct</span></span></code> defines a struct. <ul> <li>See also <a href="#struct">struct</a>
</li> </ul> </td> </tr> <tr> <th scope="row"> <pre data-language="zig">suspend</pre> </th> <td> <code><span class="line"><span class="tok-kw">suspend</span></span></code> will cause control flow to return to the call site or resumer of the function. <code><span class="line"><span class="tok-kw">suspend</span></span></code> can also be used before a block within a function, to allow the function access to its frame before control flow returns to the call site. </td> </tr> <tr> <th scope="row"> <pre data-language="zig">switch</pre> </th> <td> A <code><span class="line"><span class="tok-kw">switch</span></span></code> expression can be used to test values of a common type. <code><span class="line"><span class="tok-kw">switch</span></span></code> cases can capture field values of a <a href="#Tagged-union">Tagged union</a>. <ul> <li>See also <a href="#switch">switch</a>
</li> </ul> </td> </tr> <tr> <th scope="row"> <pre data-language="zig">test</pre> </th> <td> The <code><span class="line"><span class="tok-kw">test</span></span></code> keyword can be used to denote a top-level block of code used to make sure behavior meets expectations. <ul> <li>See also <a href="#Zig-Test">Zig Test</a>
</li> </ul> </td> </tr> <tr> <th scope="row"> <pre data-language="zig">threadlocal</pre> </th> <td> <code><span class="line"><span class="tok-kw">threadlocal</span></span></code> can be used to specify a variable as thread-local. <ul> <li>See also <a href="#Thread-Local-Variables">Thread Local Variables</a>
</li> </ul> </td> </tr> <tr> <th scope="row"> <pre data-language="zig">try</pre> </th> <td> <code><span class="line"><span class="tok-kw">try</span></span></code> evaluates an error union expression. If it is an error, it returns from the current function with the same error. Otherwise, the expression results in the unwrapped value. <ul> <li>See also <a href="#try">try</a>
</li> </ul> </td> </tr> <tr> <th scope="row"> <pre data-language="zig">union</pre> </th> <td> <code><span class="line"><span class="tok-kw">union</span></span></code> defines a union. <ul> <li>See also <a href="#union">union</a>
</li> </ul> </td> </tr> <tr> <th scope="row"> <pre data-language="zig">unreachable</pre> </th> <td> <code><span class="line"><span class="tok-kw">unreachable</span></span></code> can be used to assert that control flow will never happen upon a particular location. Depending on the build mode, <code><span class="line"><span class="tok-kw">unreachable</span></span></code> may emit a panic. <ul> <li>Emits a panic in <code><span class="line">Debug</span></code> and <code><span class="line">ReleaseSafe</span></code> mode, or when using <kbd>zig test</kbd>.</li> <li>Does not emit a panic in <code><span class="line">ReleaseFast</span></code> and <code><span class="line">ReleaseSmall</span></code> mode.</li> <li>See also <a href="#unreachable">unreachable</a>
</li> </ul> </td> </tr> <tr> <th scope="row"> <pre data-language="zig">usingnamespace</pre> </th> <td> <code><span class="line"><span class="tok-kw">usingnamespace</span></span></code> is a top-level declaration that imports all the public declarations of the operand, which must be a struct, union, or enum, into the current scope. <ul> <li>See also <a href="#usingnamespace">usingnamespace</a>
</li> </ul> </td> </tr> <tr> <th scope="row"> <pre data-language="zig">var</pre> </th> <td> <code><span class="line"><span class="tok-kw">var</span></span></code> declares a variable that may be modified. <ul> <li>See also <a href="#Variables">Variables</a>
</li> </ul> </td> </tr> <tr> <th scope="row"> <pre data-language="zig">volatile</pre> </th> <td> <code><span class="line"><span class="tok-kw">volatile</span></span></code> can be used to denote loads or stores of a pointer have side effects. It can also modify an inline assembly expression to denote it has side effects. <ul> <li>See also <a href="#volatile">volatile</a>, <a href="#Assembly">Assembly</a>
</li> </ul> </td> </tr> <tr> <th scope="row"> <pre data-language="zig">while</pre> </th> <td> A <code><span class="line"><span class="tok-kw">while</span></span></code> expression can be used to repeatedly test a boolean, optional, or error union expression, and cease looping when that expression evaluates to false, null, or an error, respectively. <ul> <li>See also <a href="#while">while</a>
</li> </ul> </td> </tr> </tbody> </table> </div> <h2 id="Appendix">Appendix </h2> <h3 id="Containers">Containers </h3> <p> A <em>container</em> in Zig is any syntactical construct that acts as a namespace to hold <a href="#Container-Level-Variables">variable</a> and <a href="#Functions">function</a> declarations. Containers are also type definitions which can be instantiated. <a href="#struct">Structs</a>, <a href="#enum">enums</a>, <a href="#union">unions</a>, <a href="#opaque">opaques</a>, and even Zig source files themselves are containers. </p> <p> Although containers (except Zig source files) use curly braces to surround their definition, they should not be confused with <a href="#Blocks">blocks</a> or functions. Containers do not contain statements. </p> <h3 id="Grammar">Grammar </h3> <figure><figcaption class="peg-cap"><cite class="file">grammar.y</cite></figcaption><pre data-language="zig">Root &lt;- skip container_doc_comment? ContainerMembers eof

# *** Top level ***
ContainerMembers &lt;- ContainerDeclarations (ContainerField COMMA)* (ContainerField / ContainerDeclarations)

ContainerDeclarations
    &lt;- TestDecl ContainerDeclarations
     / ComptimeDecl ContainerDeclarations
     / doc_comment? KEYWORD_pub? Decl ContainerDeclarations
     /

TestDecl &lt;- KEYWORD_test (STRINGLITERALSINGLE / IDENTIFIER)? Block

ComptimeDecl &lt;- KEYWORD_comptime Block

Decl
    &lt;- (KEYWORD_export / KEYWORD_extern STRINGLITERALSINGLE? / (KEYWORD_inline / KEYWORD_noinline))? FnProto (SEMICOLON / Block)
     / (KEYWORD_export / KEYWORD_extern STRINGLITERALSINGLE?)? KEYWORD_threadlocal? VarDecl
     / KEYWORD_usingnamespace Expr SEMICOLON

FnProto &lt;- KEYWORD_fn IDENTIFIER? LPAREN ParamDeclList RPAREN ByteAlign? AddrSpace? LinkSection? CallConv? EXCLAMATIONMARK? TypeExpr

VarDecl &lt;- (KEYWORD_const / KEYWORD_var) IDENTIFIER (COLON TypeExpr)? ByteAlign? AddrSpace? LinkSection? (EQUAL Expr)? SEMICOLON

ContainerField
     &lt;- doc_comment? KEYWORD_comptime? IDENTIFIER (COLON TypeExpr)? ByteAlign? (EQUAL Expr)?
      / doc_comment? KEYWORD_comptime? (IDENTIFIER COLON)? !KEYWORD_fn TypeExpr ByteAlign? (EQUAL Expr)?

# *** Block Level ***
Statement
    &lt;- KEYWORD_comptime? VarDecl
     / KEYWORD_comptime BlockExprStatement
     / KEYWORD_nosuspend BlockExprStatement
     / KEYWORD_suspend BlockExprStatement
     / KEYWORD_defer BlockExprStatement
     / KEYWORD_errdefer Payload? BlockExprStatement
     / IfStatement
     / LabeledStatement
     / SwitchExpr
     / AssignExpr SEMICOLON

IfStatement
    &lt;- IfPrefix BlockExpr ( KEYWORD_else Payload? Statement )?
     / IfPrefix AssignExpr ( SEMICOLON / KEYWORD_else Payload? Statement )

LabeledStatement &lt;- BlockLabel? (Block / LoopStatement)

LoopStatement &lt;- KEYWORD_inline? (ForStatement / WhileStatement)

ForStatement
    &lt;- ForPrefix BlockExpr ( KEYWORD_else Statement )?
     / ForPrefix AssignExpr ( SEMICOLON / KEYWORD_else Statement )

WhileStatement
    &lt;- WhilePrefix BlockExpr ( KEYWORD_else Payload? Statement )?
     / WhilePrefix AssignExpr ( SEMICOLON / KEYWORD_else Payload? Statement )

BlockExprStatement
    &lt;- BlockExpr
     / AssignExpr SEMICOLON

BlockExpr &lt;- BlockLabel? Block

# *** Expression Level ***
AssignExpr &lt;- Expr (AssignOp Expr)?

Expr &lt;- BoolOrExpr

BoolOrExpr &lt;- BoolAndExpr (KEYWORD_or BoolAndExpr)*

BoolAndExpr &lt;- CompareExpr (KEYWORD_and CompareExpr)*

CompareExpr &lt;- BitwiseExpr (CompareOp BitwiseExpr)?

BitwiseExpr &lt;- BitShiftExpr (BitwiseOp BitShiftExpr)*

BitShiftExpr &lt;- AdditionExpr (BitShiftOp AdditionExpr)*

AdditionExpr &lt;- MultiplyExpr (AdditionOp MultiplyExpr)*

MultiplyExpr &lt;- PrefixExpr (MultiplyOp PrefixExpr)*

PrefixExpr &lt;- PrefixOp* PrimaryExpr

PrimaryExpr
    &lt;- AsmExpr
     / IfExpr
     / KEYWORD_break BreakLabel? Expr?
     / KEYWORD_comptime Expr
     / KEYWORD_nosuspend Expr
     / KEYWORD_continue BreakLabel?
     / KEYWORD_resume Expr
     / KEYWORD_return Expr?
     / BlockLabel? LoopExpr
     / Block
     / CurlySuffixExpr

IfExpr &lt;- IfPrefix Expr (KEYWORD_else Payload? Expr)?

Block &lt;- LBRACE Statement* RBRACE

LoopExpr &lt;- KEYWORD_inline? (ForExpr / WhileExpr)

ForExpr &lt;- ForPrefix Expr (KEYWORD_else Expr)?

WhileExpr &lt;- WhilePrefix Expr (KEYWORD_else Payload? Expr)?

CurlySuffixExpr &lt;- TypeExpr InitList?

InitList
    &lt;- LBRACE FieldInit (COMMA FieldInit)* COMMA? RBRACE
     / LBRACE Expr (COMMA Expr)* COMMA? RBRACE
     / LBRACE RBRACE

TypeExpr &lt;- PrefixTypeOp* ErrorUnionExpr

ErrorUnionExpr &lt;- SuffixExpr (EXCLAMATIONMARK TypeExpr)?

SuffixExpr
    &lt;- KEYWORD_async PrimaryTypeExpr SuffixOp* FnCallArguments
     / PrimaryTypeExpr (SuffixOp / FnCallArguments)*

PrimaryTypeExpr
    &lt;- BUILTINIDENTIFIER FnCallArguments
     / CHAR_LITERAL
     / ContainerDecl
     / DOT IDENTIFIER
     / DOT InitList
     / ErrorSetDecl
     / FLOAT
     / FnProto
     / GroupedExpr
     / LabeledTypeExpr
     / IDENTIFIER
     / IfTypeExpr
     / INTEGER
     / KEYWORD_comptime TypeExpr
     / KEYWORD_error DOT IDENTIFIER
     / KEYWORD_anyframe
     / KEYWORD_unreachable
     / STRINGLITERAL
     / SwitchExpr

ContainerDecl &lt;- (KEYWORD_extern / KEYWORD_packed)? ContainerDeclAuto

ErrorSetDecl &lt;- KEYWORD_error LBRACE IdentifierList RBRACE

GroupedExpr &lt;- LPAREN Expr RPAREN

IfTypeExpr &lt;- IfPrefix TypeExpr (KEYWORD_else Payload? TypeExpr)?

LabeledTypeExpr
    &lt;- BlockLabel Block
     / BlockLabel? LoopTypeExpr

LoopTypeExpr &lt;- KEYWORD_inline? (ForTypeExpr / WhileTypeExpr)

ForTypeExpr &lt;- ForPrefix TypeExpr (KEYWORD_else TypeExpr)?

WhileTypeExpr &lt;- WhilePrefix TypeExpr (KEYWORD_else Payload? TypeExpr)?

SwitchExpr &lt;- KEYWORD_switch LPAREN Expr RPAREN LBRACE SwitchProngList RBRACE

# *** Assembly ***
AsmExpr &lt;- KEYWORD_asm KEYWORD_volatile? LPAREN Expr AsmOutput? RPAREN

AsmOutput &lt;- COLON AsmOutputList AsmInput?

AsmOutputItem &lt;- LBRACKET IDENTIFIER RBRACKET STRINGLITERAL LPAREN (MINUSRARROW TypeExpr / IDENTIFIER) RPAREN

AsmInput &lt;- COLON AsmInputList AsmClobbers?

AsmInputItem &lt;- LBRACKET IDENTIFIER RBRACKET STRINGLITERAL LPAREN Expr RPAREN

AsmClobbers &lt;- COLON StringList

# *** Helper grammar ***
BreakLabel &lt;- COLON IDENTIFIER

BlockLabel &lt;- IDENTIFIER COLON

FieldInit &lt;- DOT IDENTIFIER EQUAL Expr

WhileContinueExpr &lt;- COLON LPAREN AssignExpr RPAREN

LinkSection &lt;- KEYWORD_linksection LPAREN Expr RPAREN

AddrSpace &lt;- KEYWORD_addrspace LPAREN Expr RPAREN

# Fn specific
CallConv &lt;- KEYWORD_callconv LPAREN Expr RPAREN

ParamDecl
    &lt;- doc_comment? (KEYWORD_noalias / KEYWORD_comptime)? (IDENTIFIER COLON)? ParamType
     / DOT3

ParamType
    &lt;- KEYWORD_anytype
     / TypeExpr

# Control flow prefixes
IfPrefix &lt;- KEYWORD_if LPAREN Expr RPAREN PtrPayload?

WhilePrefix &lt;- KEYWORD_while LPAREN Expr RPAREN PtrPayload? WhileContinueExpr?

ForPrefix &lt;- KEYWORD_for LPAREN Expr RPAREN PtrIndexPayload

# Payloads
Payload &lt;- PIPE IDENTIFIER PIPE

PtrPayload &lt;- PIPE ASTERISK? IDENTIFIER PIPE

PtrIndexPayload &lt;- PIPE ASTERISK? IDENTIFIER (COMMA IDENTIFIER)? PIPE


# Switch specific
SwitchProng &lt;- KEYWORD_inline? SwitchCase EQUALRARROW PtrIndexPayload? AssignExpr

SwitchCase
    &lt;- SwitchItem (COMMA SwitchItem)* COMMA?
     / KEYWORD_else

SwitchItem &lt;- Expr (DOT3 Expr)?

# Operators
AssignOp
    &lt;- ASTERISKEQUAL
     / ASTERISKPIPEEQUAL
     / SLASHEQUAL
     / PERCENTEQUAL
     / PLUSEQUAL
     / PLUSPIPEEQUAL
     / MINUSEQUAL
     / MINUSPIPEEQUAL
     / LARROW2EQUAL
     / LARROW2PIPEEQUAL
     / RARROW2EQUAL
     / AMPERSANDEQUAL
     / CARETEQUAL
     / PIPEEQUAL
     / ASTERISKPERCENTEQUAL
     / PLUSPERCENTEQUAL
     / MINUSPERCENTEQUAL
     / EQUAL

CompareOp
    &lt;- EQUALEQUAL
     / EXCLAMATIONMARKEQUAL
     / LARROW
     / RARROW
     / LARROWEQUAL
     / RARROWEQUAL

BitwiseOp
    &lt;- AMPERSAND
     / CARET
     / PIPE
     / KEYWORD_orelse
     / KEYWORD_catch Payload?

BitShiftOp
    &lt;- LARROW2
     / RARROW2
     / LARROW2PIPE

AdditionOp
    &lt;- PLUS
     / MINUS
     / PLUS2
     / PLUSPERCENT
     / MINUSPERCENT
     / PLUSPIPE
     / MINUSPIPE

MultiplyOp
    &lt;- PIPE2
     / ASTERISK
     / SLASH
     / PERCENT
     / ASTERISK2
     / ASTERISKPERCENT
     / ASTERISKPIPE

PrefixOp
    &lt;- EXCLAMATIONMARK
     / MINUS
     / TILDE
     / MINUSPERCENT
     / AMPERSAND
     / KEYWORD_try
     / KEYWORD_await

PrefixTypeOp
    &lt;- QUESTIONMARK
     / KEYWORD_anyframe MINUSRARROW
     / SliceTypeStart (ByteAlign / AddrSpace / KEYWORD_const / KEYWORD_volatile / KEYWORD_allowzero)*
     / PtrTypeStart (AddrSpace / KEYWORD_align LPAREN Expr (COLON Expr COLON Expr)? RPAREN / KEYWORD_const / KEYWORD_volatile / KEYWORD_allowzero)*
     / ArrayTypeStart

SuffixOp
    &lt;- LBRACKET Expr (DOT2 (Expr? (COLON Expr)?)?)? RBRACKET
     / DOT IDENTIFIER
     / DOTASTERISK
     / DOTQUESTIONMARK

FnCallArguments &lt;- LPAREN ExprList RPAREN

# Ptr specific
SliceTypeStart &lt;- LBRACKET (COLON Expr)? RBRACKET

PtrTypeStart
    &lt;- ASTERISK
     / ASTERISK2
     / LBRACKET ASTERISK (LETTERC / COLON Expr)? RBRACKET

ArrayTypeStart &lt;- LBRACKET Expr (COLON Expr)? RBRACKET

# ContainerDecl specific
ContainerDeclAuto &lt;- ContainerDeclType LBRACE container_doc_comment? ContainerMembers RBRACE

ContainerDeclType
    &lt;- KEYWORD_struct (LPAREN Expr RPAREN)?
     / KEYWORD_opaque
     / KEYWORD_enum (LPAREN Expr RPAREN)?
     / KEYWORD_union (LPAREN (KEYWORD_enum (LPAREN Expr RPAREN)? / Expr) RPAREN)?

# Alignment
ByteAlign &lt;- KEYWORD_align LPAREN Expr RPAREN

# Lists
IdentifierList &lt;- (doc_comment? IDENTIFIER COMMA)* (doc_comment? IDENTIFIER)?

SwitchProngList &lt;- (SwitchProng COMMA)* SwitchProng?

AsmOutputList &lt;- (AsmOutputItem COMMA)* AsmOutputItem?

AsmInputList &lt;- (AsmInputItem COMMA)* AsmInputItem?

StringList &lt;- (STRINGLITERAL COMMA)* STRINGLITERAL?

ParamDeclList &lt;- (ParamDecl COMMA)* ParamDecl?

ExprList &lt;- (Expr COMMA)* Expr?

# *** Tokens ***
eof &lt;- !.
bin &lt;- [01]
bin_ &lt;- '_'? bin
oct &lt;- [0-7]
oct_ &lt;- '_'? oct
hex &lt;- [0-9a-fA-F]
hex_ &lt;- '_'? hex
dec &lt;- [0-9]
dec_ &lt;- '_'? dec

bin_int &lt;- bin bin_*
oct_int &lt;- oct oct_*
dec_int &lt;- dec dec_*
hex_int &lt;- hex hex_*

ox80_oxBF &lt;- [\200-\277]
oxF4 &lt;- '\364'
ox80_ox8F &lt;- [\200-\217]
oxF1_oxF3 &lt;- [\361-\363]
oxF0 &lt;- '\360'
ox90_0xBF &lt;- [\220-\277]
oxEE_oxEF &lt;- [\356-\357]
oxED &lt;- '\355'
ox80_ox9F &lt;- [\200-\237]
oxE1_oxEC &lt;- [\341-\354]
oxE0 &lt;- '\340'
oxA0_oxBF &lt;- [\240-\277]
oxC2_oxDF &lt;- [\302-\337]

# From https://lemire.me/blog/2018/05/09/how-quickly-can-you-check-that-a-string-is-valid-unicode-utf-8/
# First Byte      Second Byte     Third Byte      Fourth Byte
# [0x00,0x7F]
# [0xC2,0xDF]     [0x80,0xBF]
#    0xE0         [0xA0,0xBF]     [0x80,0xBF]
# [0xE1,0xEC]     [0x80,0xBF]     [0x80,0xBF]
#    0xED         [0x80,0x9F]     [0x80,0xBF]
# [0xEE,0xEF]     [0x80,0xBF]     [0x80,0xBF]
#    0xF0         [0x90,0xBF]     [0x80,0xBF]     [0x80,0xBF]
# [0xF1,0xF3]     [0x80,0xBF]     [0x80,0xBF]     [0x80,0xBF]
#    0xF4         [0x80,0x8F]     [0x80,0xBF]     [0x80,0xBF]

mb_utf8_literal &lt;-
       oxF4      ox80_ox8F ox80_oxBF ox80_oxBF
     / oxF1_oxF3 ox80_oxBF ox80_oxBF ox80_oxBF
     / oxF0      ox90_0xBF ox80_oxBF ox80_oxBF
     / oxEE_oxEF ox80_oxBF ox80_oxBF
     / oxED      ox80_ox9F ox80_oxBF
     / oxE1_oxEC ox80_oxBF ox80_oxBF
     / oxE0      oxA0_oxBF ox80_oxBF
     / oxC2_oxDF ox80_oxBF

ascii_char_not_nl_slash_squote &lt;- [\000-\011\013-\046-\050-\133\135-\177]

char_escape
    &lt;- "\\x" hex hex
     / "\\u{" hex+ "}"
     / "\\" [nr\\t'"]
char_char
    &lt;- mb_utf8_literal
     / char_escape
     / ascii_char_not_nl_slash_squote

string_char
    &lt;- char_escape
     / [^\\"\n]

container_doc_comment &lt;- ('//!' [^\n]* [ \n]* skip)+
doc_comment &lt;- ('///' [^\n]* [ \n]* skip)+
line_comment &lt;- '//' ![!/][^\n]* / '////' [^\n]*
line_string &lt;- ("\\\\" [^\n]* [ \n]*)+
skip &lt;- ([ \n] / line_comment)*

CHAR_LITERAL &lt;- "'" char_char "'" skip
FLOAT
    &lt;- "0x" hex_int "." hex_int ([pP] [-+]? dec_int)? skip
     /      dec_int "." dec_int ([eE] [-+]? dec_int)? skip
     / "0x" hex_int [pP] [-+]? dec_int skip
     /      dec_int [eE] [-+]? dec_int skip
INTEGER
    &lt;- "0b" bin_int skip
     / "0o" oct_int skip
     / "0x" hex_int skip
     /      dec_int   skip
STRINGLITERALSINGLE &lt;- "\"" string_char* "\"" skip
STRINGLITERAL
    &lt;- STRINGLITERALSINGLE
     / (line_string                 skip)+
IDENTIFIER
    &lt;- !keyword [A-Za-z_] [A-Za-z0-9_]* skip
     / "@\"" string_char* "\""                            skip
BUILTINIDENTIFIER &lt;- "@"[A-Za-z_][A-Za-z0-9_]* skip


AMPERSAND            &lt;- '&amp;'      ![=]      skip
AMPERSANDEQUAL       &lt;- '&amp;='               skip
ASTERISK             &lt;- '*'      ![*%=|]   skip
ASTERISK2            &lt;- '**'               skip
ASTERISKEQUAL        &lt;- '*='               skip
ASTERISKPERCENT      &lt;- '*%'     ![=]      skip
ASTERISKPERCENTEQUAL &lt;- '*%='              skip
ASTERISKPIPE         &lt;- '*|'     ![=]      skip
ASTERISKPIPEEQUAL    &lt;- '*|='              skip
CARET                &lt;- '^'      ![=]      skip
CARETEQUAL           &lt;- '^='               skip
COLON                &lt;- ':'                skip
COMMA                &lt;- ','                skip
DOT                  &lt;- '.'      ![*.?]    skip
DOT2                 &lt;- '..'     ![.]      skip
DOT3                 &lt;- '...'              skip
DOTASTERISK          &lt;- '.*'               skip
DOTQUESTIONMARK      &lt;- '.?'               skip
EQUAL                &lt;- '='      ![&gt;=]     skip
EQUALEQUAL           &lt;- '=='               skip
EQUALRARROW          &lt;- '=&gt;'               skip
EXCLAMATIONMARK      &lt;- '!'      ![=]      skip
EXCLAMATIONMARKEQUAL &lt;- '!='               skip
LARROW               &lt;- '&lt;'      ![&lt;=]     skip
LARROW2              &lt;- '&lt;&lt;'     ![=|]     skip
LARROW2EQUAL         &lt;- '&lt;&lt;='              skip
LARROW2PIPE          &lt;- '&lt;&lt;|'    ![=]      skip
LARROW2PIPEEQUAL     &lt;- '&lt;&lt;|='             skip
LARROWEQUAL          &lt;- '&lt;='               skip
LBRACE               &lt;- '{'                skip
LBRACKET             &lt;- '['                skip
LPAREN               &lt;- '('                skip
MINUS                &lt;- '-'      ![%=&gt;|]   skip
MINUSEQUAL           &lt;- '-='               skip
MINUSPERCENT         &lt;- '-%'     ![=]      skip
MINUSPERCENTEQUAL    &lt;- '-%='              skip
MINUSPIPE            &lt;- '-|'     ![=]      skip
MINUSPIPEEQUAL       &lt;- '-|='              skip
MINUSRARROW          &lt;- '-&gt;'               skip
PERCENT              &lt;- '%'      ![=]      skip
PERCENTEQUAL         &lt;- '%='               skip
PIPE                 &lt;- '|'      ![|=]     skip
PIPE2                &lt;- '||'               skip
PIPEEQUAL            &lt;- '|='               skip
PLUS                 &lt;- '+'      ![%+=|]   skip
PLUS2                &lt;- '++'               skip
PLUSEQUAL            &lt;- '+='               skip
PLUSPERCENT          &lt;- '+%'     ![=]      skip
PLUSPERCENTEQUAL     &lt;- '+%='              skip
PLUSPIPE             &lt;- '+|'     ![=]      skip
PLUSPIPEEQUAL        &lt;- '+|='              skip
LETTERC              &lt;- 'c'                skip
QUESTIONMARK         &lt;- '?'                skip
RARROW               &lt;- '&gt;'      ![&gt;=]     skip
RARROW2              &lt;- '&gt;&gt;'     ![=]      skip
RARROW2EQUAL         &lt;- '&gt;&gt;='              skip
RARROWEQUAL          &lt;- '&gt;='               skip
RBRACE               &lt;- '}'                skip
RBRACKET             &lt;- ']'                skip
RPAREN               &lt;- ')'                skip
SEMICOLON            &lt;- ';'                skip
SLASH                &lt;- '/'      ![=]      skip
SLASHEQUAL           &lt;- '/='               skip
TILDE                &lt;- '~'                skip

end_of_word &lt;- ![a-zA-Z0-9_] skip
KEYWORD_addrspace   &lt;- 'addrspace'   end_of_word
KEYWORD_align       &lt;- 'align'       end_of_word
KEYWORD_allowzero   &lt;- 'allowzero'   end_of_word
KEYWORD_and         &lt;- 'and'         end_of_word
KEYWORD_anyframe    &lt;- 'anyframe'    end_of_word
KEYWORD_anytype     &lt;- 'anytype'     end_of_word
KEYWORD_asm         &lt;- 'asm'         end_of_word
KEYWORD_async       &lt;- 'async'       end_of_word
KEYWORD_await       &lt;- 'await'       end_of_word
KEYWORD_break       &lt;- 'break'       end_of_word
KEYWORD_callconv    &lt;- 'callconv'    end_of_word
KEYWORD_catch       &lt;- 'catch'       end_of_word
KEYWORD_comptime    &lt;- 'comptime'    end_of_word
KEYWORD_const       &lt;- 'const'       end_of_word
KEYWORD_continue    &lt;- 'continue'    end_of_word
KEYWORD_defer       &lt;- 'defer'       end_of_word
KEYWORD_else        &lt;- 'else'        end_of_word
KEYWORD_enum        &lt;- 'enum'        end_of_word
KEYWORD_errdefer    &lt;- 'errdefer'    end_of_word
KEYWORD_error       &lt;- 'error'       end_of_word
KEYWORD_export      &lt;- 'export'      end_of_word
KEYWORD_extern      &lt;- 'extern'      end_of_word
KEYWORD_fn          &lt;- 'fn'          end_of_word
KEYWORD_for         &lt;- 'for'         end_of_word
KEYWORD_if          &lt;- 'if'          end_of_word
KEYWORD_inline      &lt;- 'inline'      end_of_word
KEYWORD_noalias     &lt;- 'noalias'     end_of_word
KEYWORD_nosuspend   &lt;- 'nosuspend'   end_of_word
KEYWORD_noinline    &lt;- 'noinline'    end_of_word
KEYWORD_opaque      &lt;- 'opaque'      end_of_word
KEYWORD_or          &lt;- 'or'          end_of_word
KEYWORD_orelse      &lt;- 'orelse'      end_of_word
KEYWORD_packed      &lt;- 'packed'      end_of_word
KEYWORD_pub         &lt;- 'pub'         end_of_word
KEYWORD_resume      &lt;- 'resume'      end_of_word
KEYWORD_return      &lt;- 'return'      end_of_word
KEYWORD_linksection &lt;- 'linksection' end_of_word
KEYWORD_struct      &lt;- 'struct'      end_of_word
KEYWORD_suspend     &lt;- 'suspend'     end_of_word
KEYWORD_switch      &lt;- 'switch'      end_of_word
KEYWORD_test        &lt;- 'test'        end_of_word
KEYWORD_threadlocal &lt;- 'threadlocal' end_of_word
KEYWORD_try         &lt;- 'try'         end_of_word
KEYWORD_union       &lt;- 'union'       end_of_word
KEYWORD_unreachable &lt;- 'unreachable' end_of_word
KEYWORD_usingnamespace &lt;- 'usingnamespace' end_of_word
KEYWORD_var         &lt;- 'var'         end_of_word
KEYWORD_volatile    &lt;- 'volatile'    end_of_word
KEYWORD_while       &lt;- 'while'       end_of_word

keyword &lt;- KEYWORD_addrspace / KEYWORD_align / KEYWORD_allowzero / KEYWORD_and
         / KEYWORD_anyframe / KEYWORD_anytype / KEYWORD_asm / KEYWORD_async
         / KEYWORD_await / KEYWORD_break / KEYWORD_callconv / KEYWORD_catch
         / KEYWORD_comptime / KEYWORD_const / KEYWORD_continue / KEYWORD_defer
         / KEYWORD_else / KEYWORD_enum / KEYWORD_errdefer / KEYWORD_error / KEYWORD_export
         / KEYWORD_extern / KEYWORD_fn / KEYWORD_for / KEYWORD_if
         / KEYWORD_inline / KEYWORD_noalias / KEYWORD_nosuspend / KEYWORD_noinline
         / KEYWORD_opaque / KEYWORD_or / KEYWORD_orelse / KEYWORD_packed
         / KEYWORD_pub / KEYWORD_resume / KEYWORD_return / KEYWORD_linksection
         / KEYWORD_struct / KEYWORD_suspend / KEYWORD_switch / KEYWORD_test
         / KEYWORD_threadlocal / KEYWORD_try / KEYWORD_union / KEYWORD_unreachable
         / KEYWORD_usingnamespace / KEYWORD_var / KEYWORD_volatile / KEYWORD_while</pre></figure> <h3 id="Zen">Zen </h3> <ul> <li>Communicate intent precisely.</li> <li>Edge cases matter.</li> <li>Favor reading code over writing code.</li> <li>Only one obvious way to do things.</li> <li>Runtime crashes are better than bugs.</li> <li>Compile errors are better than runtime crashes.</li> <li>Incremental improvements.</li> <li>Avoid local maximums.</li> <li>Reduce the amount one must remember.</li> <li>Focus on code rather than style.</li> <li>Resource allocation may fail; resource deallocation must succeed.</li> <li>Memory is a resource.</li> <li>Together we serve the users.</li> </ul><div class="_attribution">
  <p class="_attribution-p">
    &copy; 2015â€“2023, Zig contributors<br>Licensed under the MIT License.<br>
    <a href="https://ziglang.org/documentation/0.11.0/" class="_attribution-link">https://ziglang.org/documentation/0.11.0/</a>
  </p>
</div>
