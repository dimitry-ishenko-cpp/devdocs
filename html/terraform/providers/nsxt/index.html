<h1 id="the-nsx-terraform-provider">  The NSX Terraform Provider </h1> <p>The NSX Terraform provider gives the NSX administrator a way to automate NSX to provide virtualized networking and security services using both ESXi and KVM based hypervisor hosts as well as container networking and security.</p> <p>More information on NSX can be found on the <a href="https://www.vmware.com/products/nsx.html">NSX Product Page</a></p> <p>Documentation on the NSX platform can be found on the <a href="https://docs.vmware.com/en/VMware-NSX-T/index.html">NSX Documentation Page</a></p> <p>Please use the navigation to the left to read about available data sources and resources.</p> <h2 id="basic-configuration-of-the-nsx-terraform-provider">  Basic Configuration of the NSX Terraform Provider </h2> <p>In order to use the NSX Terraform provider you must first configure the provider to communicate with the VMware NSX manager. The NSX manager is the system which serves the NSX REST API and provides a way to configure the desired state of the NSX system. The configuration of the NSX provider requires the IP address, hostname, or FQDN of the NSX manager.</p> <p>The NSX provider offers several ways to authenticate to the NSX manager. Credentials can be provided statically or provided as environment variables. In addition, client certificates can be used for authentication. For authentication with certificates Terraform will require a certificate file and private key file in PEM format. To use client certificates the client certificate needs to be registered with NSX-T manager prior to invoking Terraform.</p> <p>The provider also can accept both signed and self-signed server certificates. It is recommended that in production environments you only use certificates signed by a certificate authority. NSX ships by default with a self-signed server certificates as the hostname of the NSX manager is not known until the NSX administrator determines what name or IP to use.</p> <p>Setting the <code>allow_unverified_ssl</code> parameter to <code>true</code> will direct the Terraform client to skip server certificate verification. This is not recommended in production deployments as it is recommended that you use trusted connection using certificates signed by a certificate authority.</p> <p>With the <code>ca_file</code> parameter you can also specify a file that contains your certificate authority certificate in PEM format to verify certificates with a certificate authority.</p> <p>There are also a number of other parameters that can be set to tune how the provider connects to the NSX REST API. It is recommended you leave these to the defaults unless you experience issues in which case they can be tuned to optimize the system in your environment.</p> <p>Note that in all of the examples you will need to update the <code>host</code>, <code>username</code>, and <code>password</code> settings to match those configured in your NSX deployment.</p> <h3 id="example-of-configuration-with-credentials">  Example of Configuration with Credentials </h3> <pre data-language="ruby">provider "nsxt" {
  host                     = "192.168.110.41"
  username                 = "admin"
  password                 = "default"
  allow_unverified_ssl     = true
  max_retries              = 10
  retry_min_delay          = 500
  retry_max_delay          = 5000
  retry_on_status_codes    = [429]
}

</pre>
<h3 id="example-of-setting-environment-variables">  Example of Setting Environment Variables </h3> <pre data-language="ruby">export NSXT_MANAGER_HOST="192.168.110.41"
export NSXT_USERNAME="admin"
export NSXT_PASSWORD="default"
</pre>
<h3 id="example-using-a-client-certificate">  Example using a Client Certificate </h3> <pre data-language="ruby">provider "nsxt" {
  host                  = "192.168.110.41"
  client_auth_cert_file = "mycert.pem"
  client_auth_key_file  = "mykey.pem"
  allow_unverified_ssl  = true
}

</pre>
<h3 id="example-with-certificate-authority-certificate">  Example with Certificate Authority Certificate </h3> <pre data-language="ruby">provider "nsxt" {
  host     = "10.160.94.11"
  username = "admin"
  password = "qwerty"
  ca_file  = "myca.pem"
}

</pre>
<h2 id="argument-reference">  Argument Reference </h2> <p>The following arguments are used to configure the VMware NSX-T Provider:</p> <ul> <li>
<a href="#host"><code>host</code></a> - (Required) The host name or IP address of the NSX-T manager. Can also be specified with the <code>NSXT_MANAGER_HOST</code> environment variable. </li> <li>
<a href="#username"><code>username</code></a> - (Required) The user name to connect to the NSX-T manager as. Can also be specified with the <code>NSXT_USERNAME</code> environment variable. </li> <li>
<a href="#password"><code>password</code></a> - (Required) The password for the NSX-T manager user. Can also be specified with the <code>NSXT_PASSWORD</code> environment variable. </li> <li>
<a href="#client_auth_cert_file"><code>client_auth_cert_file</code></a> - (Optional) The path to a certificate file for certificate authorization. Can also be specified with the <code>NSXT_CLIENT_AUTH_CERT_FILE</code> environment variable. </li> <li>
<a href="#client_auth_key_file"><code>client_auth_key_file</code></a> - (Optional) The path to a private key file for the certificate supplied to <code>client_auth_cert_file</code>. Can also be specified with the <code>NSXT_CLIENT_AUTH_KEY_FILE</code> environment variable. </li> <li>
<a href="#allow_unverified_ssl"><code>allow_unverified_ssl</code></a> - (Optional) Boolean that can be set to true to disable SSL certificate verification. This should be used with care as it could allow an attacker to intercept your auth token. If omitted, default value is <code>false</code>. Can also be specified with the <code>NSXT_ALLOW_UNVERIFIED_SSL</code> environment variable. </li> <li>
<a href="#ca_file"><code>ca_file</code></a> - (Optional) The path to an optional CA certificate file for SSL validation. Can also be specified with the <code>NSXT_CA_FILE</code> environment variable. </li> <li>
<a href="#max_retries"><code>max_retries</code></a> - (Optional) The maximum number of retires before failing an API request. Default: <code>10</code> Can also be specified with the <code>NSXT_MAX_RETRIES</code> environment variable. </li> <li>
<a href="#retry_min_delay"><code>retry_min_delay</code></a> - (Optional) The minimum delay, in milliseconds, between retires made to the API. Default:<code>500</code>. Can also be specified with the <code>NSXT_RETRY_MIN_DELAY</code> environment variable. </li> <li>
<a href="#retry_max_delay"><code>retry_max_delay</code></a> - (Optional) The maximum delay, in milliseconds, between retires made to the API. Default:<code>5000</code>. Can also be specified with the <code>NSXT_RETRY_MAX_DELAY</code> environment variable. </li> <li>
<a href="#retry_on_status_codes"><code>retry_on_status_codes</code></a> - (Optional) A list of HTTP status codes to retry on. By default, the provider will retry on HTTP error 429 (too many requests), essentially retrying on throttled connections. Can also be specified with the <code>NSXT_RETRY_ON_STATUS_CODES</code> environment variable. </li> </ul> <h2 id="nsx-logical-networking">  NSX Logical Networking </h2> <p>The NSX Terraform provider can be used to manage logical networking and security constructs in NSX. This includes logical switching, routing and firewall.</p> <h3 id="logical-networking-and-security-example-usage">  Logical Networking and Security Example Usage </h3> <p>The following example demonstrates using the NSX Terraform provider to create a logical switch and tier1 logical router. It then connects the logical switch to the tier1 logical router and uplinks the T1 router to a pre-created T0 router.</p> <h4 id="example-variables-tf-file">  Example variables.tf File </h4> <p>This file allows you to define some variables that can be reused in multiple .tf files.</p> <pre data-language="ruby">variable "nsx_manager" {}
variable "nsx_username" {}
variable "nsx_password" {}
</pre>
<h4 id="example-terraform-tfvars-file">  Example terraform.tfvars File </h4> <p>This file allows you to set some variables that can be reused in multiple .tf files.</p> <pre data-language="ruby">nsx_manager = "192.168.110.41"

nsx_username = "admin"

nsx_password = "default"
</pre>
<h4 id="example-nsx-tf-file">  Example nsx.tf file </h4> <p>This file will define the logical networking topology that Terraform will create in NSX.</p> <pre data-language="ruby">#
# The first step is to configure the VMware NSX provider to connect to the NSX
# REST API running on the NSX manager.
#
provider "nsxt" {
  host                  = "${var.nsx_manager}"
  username              = "${var.nsx_username}"
  password              = "${var.nsx_password}"
  allow_unverified_ssl  = true
  max_retries           = 10
  retry_min_delay       = 500
  retry_max_delay       = 5000
  retry_on_status_codes = [429]
}

#
# Here we show that you define a NSX tag which can be used later to easily to
# search for the created objects in NSX.
#
variable "nsx_tag_scope" {
  default = "project"
}

variable "nsx_tag" {
  default = "terraform-demo"
}

#
# This part of the example shows some data sources we will need to refer to
# later in the .tf file. They include the transport zone, tier 0 router and
# edge cluster.
#
data "nsxt_transport_zone" "overlay_tz" {
  display_name = "tz1"
}

#
# The tier 0 router (T0) is considered a "provider" router that is pre-created
# by the NSX admin. A T0 router is used for north/south connectivity between
# the logical networking space and the physical networking space. Many tier 1
# routers will be connected to a tier 0 router.
#
data "nsxt_logical_tier0_router" "tier0_router" {
  display_name = "DefaultT0Router"
}

data "nsxt_edge_cluster" "edge_cluster1" {
  display_name = "EdgeCluster1"
}

#
# This shows the settings required to create a NSX logical switch to which you
# can attach virtual machines.
#
resource "nsxt_logical_switch" "switch1" {
  admin_state       = "UP"
  description       = "LS created by Terraform"
  display_name      = "TfLogicalSwitch"
  transport_zone_id = "${data.nsxt_transport_zone.overlay_tz.id}"
  replication_mode  = "MTEP"

  tag {
    scope = "${var.nsx_tag_scope}"
    tag   = "${var.nsx_tag}"
  }

  tag {
    scope = "tenant"
    tag   = "second_example_tag"
  }
}

#
# In this part of the example the settings are defined that are required to
# create a T1 router. In NSX a T1 router is often used on a per user, tenant,
# or application basis. Each application may have it's own T1 router. The T1
# router provides the default gateway for machines on logical switches
# connected to the T1 router.
#
resource "nsxt_logical_tier1_router" "tier1_router" {
  description                 = "Tier1 router provisioned by Terraform"
  display_name                = "TfTier1"
  failover_mode               = "PREEMPTIVE"
  high_availability_mode      = "ACTIVE_STANDBY"
  edge_cluster_id             = "${data.nsxt_edge_cluster.edge_cluster1.id}"
  enable_router_advertisement = true
  advertise_connected_routes  = true
  advertise_static_routes     = false
  advertise_nat_routes        = true

  tag {
    scope = "${var.nsx_tag_scope}"
    tag   = "${var.nsx_tag}"
  }
}

#
# This resource creates a logical port on the T0 router. We will connect the T1
# router to this port to enable connectivity from the tenant / application
# networks to the networks to the cloud.
#
resource "nsxt_logical_router_link_port_on_tier0" "link_port_tier0" {
  description       = "TIER0_PORT1 provisioned by Terraform"
  display_name      = "TIER0_PORT1"
  logical_router_id = "${data.nsxt_logical_tier0_router.tier0_router.id}"

  tag {
    scope = "${var.nsx_tag_scope}"
    tag   = "${var.nsx_tag}"
  }
}

#
# Here we create a tier 1 router uplink port and connect it to T0 router port
# created in the previous example.
#
resource "nsxt_logical_router_link_port_on_tier1" "link_port_tier1" {
  description                   = "TIER1_PORT1 provisioned by Terraform"
  display_name                  = "TIER1_PORT1"
  logical_router_id             = "${nsxt_logical_tier1_router.tier1_router.id}"
  linked_logical_router_port_id = "${nsxt_logical_router_link_port_on_tier0.link_port_tier0.id}"

  tag {
    scope = "${var.nsx_tag_scope}"
    tag   = "${var.nsx_tag}"
  }
}

#
# Like their physical counterpart a logical switch can have switch ports. In
# this example Terraform will create a logical switch port on a logical switch.
#
resource "nsxt_logical_port" "logical_port1" {
  admin_state       = "UP"
  description       = "LP1 provisioned by Terraform"
  display_name      = "LP1"
  logical_switch_id = "${nsxt_logical_switch.switch1.id}"

  tag {
    scope = "${var.nsx_tag_scope}"
    tag   = "${var.nsx_tag}"
  }
}

#
# In order to connect a logical switch to a tier 1 logical router we will need
# a downlink port on the tier 1 router and will need to  connect it to the
# switch port we created above.
#
# The IP address provided in the `ip_address` property will be default gateway
# for virtual machines connected to this logical switch.
#
resource "nsxt_logical_router_downlink_port" "downlink_port" {
  description                   = "DP1 provisioned by Terraform"
  display_name                  = "DP1"
  logical_router_id             = "${nsxt_logical_tier1_router.tier1_router.id}"
  linked_logical_switch_port_id = "${nsxt_logical_port.logical_port1.id}"
  ip_address                    = "192.168.245.1/24"

  tag {
    scope = "${var.nsx_tag_scope}"
    tag   = "${var.nsx_tag}"
  }
}
</pre>
<p>In order to be able to connect VMs to the newly created logical switch a new <code>vpshere_network</code> datasource need to be defined.</p> <pre data-language="ruby">data "vsphere_network" "terraform_switch1" {
    name = "${nsxt_logical_switch.switch1.display_name}"
    datacenter_id = "${data.vsphere_datacenter.dc.id}"
    depends_on = ["nsxt_logical_switch.switch1"]
}

</pre>
<p>The datasource in the above example should be referred in <code>network_id</code> inside <code>network_interface</code> section for <code>vsphere_virtual_machine</code> resource.</p> <h2 id="feature-requests-bug-reports-and-contributing">  Feature Requests, Bug Reports, and Contributing </h2> <p>For more information how how to submit feature requests, bug reports, or details on how to make your own contributions to the provider, see the <a href="https://github.com/terraform-providers/terraform-provider-nsxt">NSX-T provider project page</a>.</p><div class="_attribution">
  <p class="_attribution-p">
    &copy; 2018 HashiCorp</br>Licensed under the MPL 2.0 License.<br>
    <a href="https://www.terraform.io/docs/providers/nsxt/index.html" class="_attribution-link">https://www.terraform.io/docs/providers/nsxt/index.html</a>
  </p>
</div>
