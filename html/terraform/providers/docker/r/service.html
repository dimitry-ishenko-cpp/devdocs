<h1 id="docker_service">  docker_service </h1> <p>This resource manages the lifecycle of a Docker service. By default, the creation, update and delete of services are detached.</p> <p>With the <a href="#convergeconfig">Converge Config</a> the behavior of the <code>docker cli</code> is imitated to guarantee that for example, all tasks of a service are running or successfully updated or to inform <code>terraform</code> that a service could not be updated and was successfully rolled back.</p> <h2 id="example-usage">  Example Usage </h2> <p>The following examples show the basic and advanced usage of the Docker Service resource assuming the host machine is already part of a Swarm.</p> <h3 id="basic">  Basic </h3> <p>The following configuration starts a Docker Service with - the given image, - 1 replica - exposes the port <code>8080</code> in <code>vip</code> mode to the host machine - moreover, uses the <code>container</code> runtime</p> <pre data-language="ruby">resource "docker_service" "foo" {
    name     = "foo-service"
    task_spec {
        container_spec {
            image = "repo.mycompany.com:8080/foo-service:v1"
        }
    }

    endpoint_spec {
      ports {
        target_port = "8080"
      }
    }
}
</pre>
<p>The following command is the equivalent:</p> <pre data-language="shell">$ docker service create -d -p 8080 --name foo-service repo.mycompany.com:8080/foo-service:v1
</pre>
<h3 id="advanced">  Advanced </h3> <p>The following configuration shows the full capabilities of a Docker Service. Currently, the <a href="https://docs.docker.com/engine/api/v1.32">Docker API 1.32</a> is implemented.</p> <pre data-language="ruby">resource "docker_volume" "test_volume" {
    name = "tftest-volume"
}

resource "docker_config" "service_config" {
    name = "tftest-full-myconfig"
    data = "ewogICJwcmVmaXgiOiAiMTIzIgp9"
}

resource "docker_secret" "service_secret" {
    name = "tftest-mysecret"
    data = "ewogICJrZXkiOiAiUVdFUlRZIgp9"
}

resource "docker_network" "test_network" {
    name   = "tftest-network"
    driver = "overlay"
}

resource "docker_service" "foo" {
    name = "tftest-service-basic"

    task_spec {
        container_spec {
            image = "repo.mycompany.com:8080/foo-service:v1"

            labels {
                foo = "bar"
            }

            command  = ["ls"]
            args     = ["-las"]
            hostname = "my-fancy-service"

            env {
                MYFOO = "BAR"
            }

            dir    = "/root"
            user   = "root"
            groups = ["docker", "foogroup"]

            privileges {
                se_linux_context {
                    disable = true
                    user    = "user-label"
                    role    = "role-label"
                    type    = "type-label"
                    level   = "level-label"
                }
            }

            read_only = true

            mounts = [
                {
                    target      = "/mount/test"
                    source      = "${docker_volume.test_volume.name}"
                    type        = "volume"
                    read_only   = true

                    bind_options {
                        propagation = "private"
                    }
                },
            ]

            stop_signal       = "SIGTERM"
            stop_grace_period = "10s"

            healthcheck {
                test     = ["CMD", "curl", "-f", "http://localhost:8080/health"]
                interval = "5s"
                timeout  = "2s"
                retries  = 4
            }

            hosts {
                host = "testhost"
                ip   = "10.0.1.0"
            }

            dns_config {
                nameservers = ["8.8.8.8"]
                search      = ["example.org"]
                options     = ["timeout:3"]
            }

            secrets = [
                {
                    secret_id   = "${docker_secret.service_secret.id}"
                    secret_name = "${docker_secret.service_secret.name}"
                    file_name = "/secrets.json"
                },
            ]

            configs = [
                {
                    config_id   = "${docker_config.service_config.id}"
                    config_name = "${docker_config.service_config.name}"
                    file_name = "/configs.json"
                },
            ]
        }

        resources {
            limits {
                nano_cpus    = 1000000
                memory_bytes = 536870912

                generic_resources {
                    named_resources_spec = [
                        "GPU=UUID1"
                    ]

                    discrete_resources_spec = [
                        "SSD=3"
                    ]
                }
            }

            reservation {
                nano_cpus    = 1000000
                memory_bytes = 536870912

                generic_resources {
                    named_resources_spec = [
                        "GPU=UUID1"
                    }

                    discrete_resources_spec = [
                        "SSD=3"
                    ]
                }
            }
        }

        restart_policy {
            condition    = "on-failure"
            delay        = "3s"
            max_attempts = 4
            window       = "10s"
        }

        placement {
            constraints = [
                "node.role==manager",
            ]

            prefs = [
                "spread=node.role.manager",
            ]
        }

        force_update = 0
        runtime      = "container"
        networks     = ["${docker_network.test_network.id}"]

        log_driver {
            name = "json-file"

            options {
                max-size = "10m"
                max-file = "3"
            }
        }
    }

    mode {
        replicated {
            replicas = 2
        }
    }

    update_config {
        parallelism       = 2
        delay             = "10s"
        failure_action    = "pause"
        monitor           = "5s"
        max_failure_ratio = "0.1"
        order             = "start-first"
    }

    rollback_config {
        parallelism       = 2
        delay             = "5ms"
        failure_action    = "pause"
        monitor           = "10h"
        max_failure_ratio = "0.9"
        order             = "stop-first"
    }

    endpoint_spec {
        mode = "vip"

        ports {
            name           = "random"
            protocol       = "tcp"
            target_port    = "8080"
            published_port = "8080"
            publish_mode   = "ingress"
        }
    }
}
</pre>
<p>See also the <code>TestAccDockerService_full</code> test or all the other tests for a complete overview.</p> <h2 id="argument-reference">  Argument Reference </h2> <p>The following arguments are supported:</p> <ul> <li>
<a href="#auth"><code>auth</code></a> - (Optional, block) See <a href="#auth">Auth</a> below for details. </li> <li>
<a href="#name"><code>name</code></a> - (Required, string) The name of the Docker service. </li> <li>
<a href="#task_spec"><code>task_spec</code></a> - (Required, block) See <a href="#task-spec">TaskSpec</a> below for details. </li> <li>
<a href="#mode"><code>mode</code></a> - (Optional, block) See <a href="#mode">Mode</a> below for details. </li> <li>
<a href="#update_config"><code>update_config</code></a> - (Optional, block) See <a href="#update-rollback-config">UpdateConfig</a> below for details. </li> <li>
<a href="#rollback_config"><code>rollback_config</code></a> - (Optional, block) See <a href="#update-rollback-config">RollbackConfig</a> below for details. </li> <li>
<a href="#endpoint_spec"><code>endpoint_spec</code></a> - (Optional, block) See <a href="#endpoint-spec">EndpointSpec</a> below for details. </li> <li>
<a href="#converge_config"><code>converge_config</code></a> - (Optional, block) See <a href="#converge-config">Converge Config</a> below for details. </li> </ul>  <h3 id="auth-1">  Auth </h3> <p><code>auth</code> can be used additionally to the <code>registry_auth</code>. If both properties are given the <code>auth</code> wins and overwrites the auth of the provider.</p> <ul> <li>
<a href="#server_address"><code>server_address</code></a> - (Required, string) The address of the registry server </li> <li>
<a href="#username"><code>username</code></a> - (Optional, string) The username to use for authenticating to the registry. If this is blank, the <code>DOCKER_REGISTRY_USER</code> is also be checked. </li> <li>
<a href="#password"><code>password</code></a> - (Optional, string) The password to use for authenticating to the registry. If this is blank, the <code>DOCKER_REGISTRY_PASS</code> is also be checked. </li> </ul>   <h3 id="taskspec">  TaskSpec </h3> <p><code>task_spec</code> is a block within the configuration that can be repeated only <strong>once</strong> to specify the mode configuration for the service. The <code>task_spec</code> block is the user modifiable task configuration and supports the following:</p> <ul> <li>
<a href="#container_spec"><code>container_spec</code></a> (Required, block) See <a href="#container-spec">ContainerSpec</a> below for details. </li> <li>
<a href="#resources"><code>resources</code></a> (Optional, block) See <a href="#resources">Resources</a> below for details. </li> <li>
<a href="#restart_policy"><code>restart_policy</code></a> (Optional, block) See <a href="#restart-policy">Restart Policy</a> below for details. </li> <li>
<a href="#placement"><code>placement</code></a> (Optional, block) See <a href="#placement">Placement</a> below for details. </li> <li>
<a href="#force_update"><code>force_update</code></a> (Optional, int) A counter that triggers an update even if no relevant parameters have been changed. See <a href="https://github.com/docker/swarmkit/blob/master/api/specs.proto#L126">Docker Spec</a>. </li> <li>
<a href="#runtime"><code>runtime</code></a> (Optional, string) Runtime is the type of runtime specified for the task executor. See <a href="https://github.com/moby/moby/blob/master/api/types/swarm/runtime.go">Docker Runtime</a>. </li> <li>
<a href="#networks"><code>networks</code></a> - (Optional, set of strings) Ids of the networks in which the container will be put in. </li> <li>
<a href="#log_driver"><code>log_driver</code></a> - (Optional, block) See <a href="#log-driver">Log Driver</a> below for details. </li> </ul>   <h4 id="containerspec">  ContainerSpec </h4> <p><code>container_spec</code> is a block within the configuration that can be repeated only <strong>once</strong> to specify the mode configuration for the service. The <code>container_spec</code> block is the spec for each container and supports the following:</p> <ul> <li>
<a href="#image"><code>image</code></a> - (Required, string) The image used to create the Docker service. </li> <li>
<a href="#labels"><code>labels</code></a> - (Optional, map of string/string key/value pairs) User-defined key/value metadata. </li> <li>
<a href="#command"><code>command</code></a> - (Optional, list of strings) The command to be run in the image. </li> <li>
<a href="#args"><code>args</code></a> - (Optional, list of strings) Arguments to the command. </li> <li>
<a href="#hostname"><code>hostname</code></a> - (Optional, string) The hostname to use for the container, as a valid RFC 1123 hostname. </li> <li>
<a href="#env"><code>env</code></a> - (Optional, map of string/string) A list of environment variables in the form VAR=value. </li> <li>
<a href="#dir"><code>dir</code></a> - (Optional, string) The working directory for commands to run in. </li> <li>
<a href="#user"><code>user</code></a> - (Optional, string) The user inside the container. </li> <li>
<a href="#groups"><code>groups</code></a> - (Optional, list of strings) A list of additional groups that the container process will run as. </li> <li>
<a href="#privileges"><code>privileges</code></a> (Optional, block) See <a href="#privileges">Privileges</a> below for details. </li> <li>
<a href="#read_only"><code>read_only</code></a> - (Optional, bool) Mount the container's root filesystem as read only. </li> <li>
<a href="#mounts"><code>mounts</code></a> - (Optional, set of blocks) See <a href="#mounts">Mounts</a> below for details. </li> <li>
<a href="#stop_signal"><code>stop_signal</code></a> - (Optional, string) Signal to stop the container. </li> <li>
<a href="#stop_grace_period"><code>stop_grace_period</code></a> - (Optional, string) Amount of time to wait for the container to terminate before forcefully removing it <code>(ms|s|m|h)</code>. </li> <li>
<a href="#healthcheck"><code>healthcheck</code></a> - (Optional, block) See <a href="#healthcheck">Healthcheck</a> below for details. </li> <li>
<a href="#host-1"><code>host</code></a> - (Optional, map of string/string) A list of hostname/IP mappings to add to the container's hosts file. <ul> <li>
<a href="#ip"><code>ip</code></a> - (Required string) The ip </li> <li>
<a href="#host"><code>host</code></a> - (Required string) The hostname </li> </ul> </li> <li>
<a href="#dns_config"><code>dns_config</code></a> - (Optional, block) See <a href="#dnsconfig">DNS Config</a> below for details. </li> <li>
<a href="#secrets"><code>secrets</code></a> - (Optional, set of blocks) See <a href="#secrets">Secrets</a> below for details. </li> <li>
<a href="#configs"><code>configs</code></a> - (Optional, set of blocks) See <a href="#configs">Configs</a> below for details. </li> <li>
<a href="#isolation"><code>isolation</code></a> - (Optional, string) Isolation technology of the containers running the service. (Windows only). Valid values are: <code>default|process|hyperv</code> </li> </ul>  <h4 id="privileges-1">  Privileges </h4> <p><code>privileges</code> is a block within the configuration that can be repeated only <strong>once</strong> to specify the mode configuration for the service. The <code>privileges</code> block holds the security options for the container and supports the following:</p> <ul> <li>
<a href="#credential_spec"><code>credential_spec</code></a> - (Optional, block) For managed service account (Windows only) <ul> <li>
<a href="#file"><code>file</code></a> - (Optional, string) Load credential spec from this file. </li> <li>
<a href="#registry"><code>registry</code></a> - (Optional, string) Load credential spec from this value in the Windows registry. </li> </ul> </li> <li>
<a href="#se_linux_context"><code>se_linux_context</code></a> - (Optional, block) SELinux labels of the container <ul> <li>
<a href="#disable"><code>disable</code></a> - (Optional, bool) Disable SELinux </li> <li>
<a href="#user-1"><code>user</code></a> - (Optional, string) SELinux user label </li> <li>
<a href="#role"><code>role</code></a> - (Optional, string) SELinux role label </li> <li>
<a href="#type"><code>type</code></a> - (Optional, string) SELinux type label </li> <li>
<a href="#level"><code>level</code></a> - (Optional, string) SELinux level label </li> </ul> </li> </ul>  <h4 id="mounts-1">  Mounts </h4> <p><code>mount</code> is a block within the configuration that can be repeated to specify the extra mount mappings for the container. Each <code>mount</code> block is the Specification for mounts to be added to containers created as part of the service and supports the following:</p> <ul> <li>
<a href="#target"><code>target</code></a> - (Required, string) The container path. </li> <li>
<a href="#source"><code>source</code></a> - (Required, string) The mount source (e.g., a volume name, a host path) </li> <li>
<a href="#type-1"><code>type</code></a> - (Required, string) The mount type: valid values are <code>bind|volume|tmpfs</code>. </li> <li>
<a href="#read_only-1"><code>read_only</code></a> - (Optional, string) Whether the mount should be read-only </li> <li>
<a href="#bind_options"><code>bind_options</code></a> - (Optional, map) Optional configuration for the <code>bind</code> type. <ul> <li>
<a href="#propagation"><code>propagation</code></a> - (Optional, string) A propagation mode with the value. </li> </ul> </li> <li>
<a href="#volume_options"><code>volume_options</code></a> - (Optional, map) Optional configuration for the <code>volume</code> type. <ul> <li>
<a href="#no_copy"><code>no_copy</code></a> - (Optional, string) Whether to populate volume with data from the target. </li> <li>
<a href="#labels-1"><code>labels</code></a> - (Optional, map of key/value pairs) Adding labels. </li> <li>
<a href="#driver_config"><code>driver_config</code></a> - (Optional, map) The name of the driver to create the volume. </li> <li>
<a href="#name-1"><code>name</code></a> - (Optional, string) The name of the driver to create the volume. </li> <li>
<a href="#options"><code>options</code></a> - (Optional, map of key/value pairs) Options for the driver. </li> </ul> </li> <li>
<a href="#tmpf_options"><code>tmpf_options</code></a> - (Optional, map) Optional configuration for the <code>tmpf</code> type. <ul> <li>
<a href="#size_bytes"><code>size_bytes</code></a> - (Optional, int) The size for the tmpfs mount in bytes. </li> <li>
<a href="#mode-1"><code>mode</code></a> - (Optional, int) The permission mode for the tmpfs mount in an integer. </li> </ul> </li> </ul>  <h4 id="healthcheck-1">  Healthcheck </h4> <p><code>healthcheck</code> is a block within the configuration that can be repeated only <strong>once</strong> to specify the extra healthcheck configuration for the containers of the service. The <code>healthcheck</code> block is a test to perform to check that the container is healthy and supports the following:</p> <ul> <li>
<a href="#test"><code>test</code></a> - (Required, list of strings) Command to run to check health. For example, to run <code>curl -f http://localhost/health</code> set the command to be <code>["CMD", "curl", "-f", "http://localhost/health"]</code>. </li> <li>
<a href="#interval"><code>interval</code></a> - (Optional, string) Time between running the check <code>(ms|s|m|h)</code>. Default: <code>0s</code>. </li> <li>
<a href="#timeout"><code>timeout</code></a> - (Optional, string) Maximum time to allow one check to run <code>(ms|s|m|h)</code>. Default: <code>0s</code>. </li> <li>
<a href="#start_period"><code>start_period</code></a> - (Optional, string) Start period for the container to initialize before counting retries towards unstable <code>(ms|s|m|h)</code>. Default: <code>0s</code>. </li> <li>
<a href="#start_period-1"><code>start_period</code></a> - Start period for the container to initialize before counting retries towards unstable <code>(ms|s|m|h)</code>. Default: <code>0s</code>. </li> <li>
<a href="#retries"><code>retries</code></a> - (Optional, int) Consecutive failures needed to report unhealthy. Default: <code>0</code>. </li> </ul>  <h3 id="dns-config">  DNS Config </h3> <p><code>dns_config</code> is a block within the configuration that can be repeated only <strong>once</strong> to specify the extra DNS configuration for the containers of the service. The <code>dns_config</code> block supports the following:</p> <ul> <li>
<a href="#nameservers"><code>nameservers</code></a> - (Required, list of strings) The IP addresses of the name servers, for example, <code>8.8.8.8</code> </li> <li>
<a href="#search"><code>search</code></a> - (Optional, list of strings)A search list for host-name lookup. </li> <li>
<a href="#options-1"><code>options</code></a> - (Optional, list of strings) A list of internal resolver variables to be modified, for example, <code>debug</code>, <code>ndots:3</code> </li> </ul>  <h3 id="secrets-1">  Secrets </h3> <p><code>secrets</code> is a block within the configuration that can be repeated to specify the extra mount mappings for the container. Each <code>secrets</code> block is a reference to a secret that will be exposed to the service and supports the following:</p> <ul> <li>
<a href="#secret_id"><code>secret_id</code></a> - (Required, string) ConfigID represents the ID of the specific secret. </li> <li>
<a href="#secret_name"><code>secret_name</code></a> - (Optional, string) The name of the secret that this references, but internally it is just provided for lookup/display purposes </li> <li>
<a href="#file_name"><code>file_name</code></a> - (Required, string) Represents the final filename in the filesystem. The specific target file that the secret data is written within the docker container, e.g. <code>/root/secret/secret.json</code> </li> </ul>  <h3 id="configs-1">  Configs </h3> <p><code>configs</code> is a block within the configuration that can be repeated to specify the extra mount mappings for the container. Each <code>configs</code> is a reference to a secret that is exposed to the service and supports the following:</p> <ul> <li>
<a href="#config_id"><code>config_id</code></a> - (Required, string) ConfigID represents the ID of the specific config. </li> <li>
<a href="#config_name"><code>config_name</code></a> - (Optional, string) The name of the config that this references, but internally it is just provided for lookup/display purposes </li> <li>
<a href="#file_name-1"><code>file_name</code></a> - (Required, string) Represents the final filename in the filesystem. The specific target file that the config data is written within the docker container, e.g. <code>/root/config/config.json</code> </li> </ul>    <h4 id="resources-1">  Resources </h4> <p><code>resources</code> is a block within the configuration that can be repeated only <strong>once</strong> to specify the mode configuration for the service. The <code>resources</code> block represents the requirements which apply to each container created as part of the service and supports the following:</p> <ul> <li>
<a href="#limits"><code>limits</code></a> - (Optional, list of strings) Describes the resources which can be advertised by a node and requested by a task. <ul> <li>
<a href="#nano_cpus"><code>nano_cpus</code></a> (Optional, int) CPU shares in units of 1/1e9 (or 10<sup>-9)</sup> of the CPU. Should be at least 1000000 </li> <li>
<a href="#memory_bytes"><code>memory_bytes</code></a> (Optional, int) The amount of memory in bytes the container allocates </li> <li>
<a href="#generic_resources"><code>generic_resources</code></a> (Optional, map) User-defined resources can be either Integer resources (e.g, SSD=3) or String resources (e.g, GPU=UUID1) </li> <li>
<a href="#named_resources_spec"><code>named_resources_spec</code></a> (Optional, set of string) The String resources, delimited by <code>=</code> </li> <li>
<a href="#discrete_resources_spec"><code>discrete_resources_spec</code></a> (Optional, set of string) The Integer resources, delimited by <code>=</code> </li> </ul> </li> <li>
<a href="#reservation"><code>reservation</code></a> - (Optional, list of strings) An object describing the resources which can be advertised by a node and requested by a task. <ul> <li>
<a href="#nano_cpus-1"><code>nano_cpus</code></a> (Optional, int) CPU shares in units of 1/1e9 (or 10<sup>-9)</sup> of the CPU. Should be at least 1000000 </li> <li>
<a href="#memory_bytes-1"><code>memory_bytes</code></a> (Optional, int) The amount of memory in bytes the container allocates </li> <li>
<a href="#generic_resources-1"><code>generic_resources</code></a> (Optional, map) User-defined resources can be either Integer resources (e.g, SSD=3) or String resources (e.g, GPU=UUID1) </li> <li>
<a href="#named_resources_spec-1"><code>named_resources_spec</code></a> (Optional, set of string) The String resources </li> <li>
<a href="#discrete_resources_spec-1"><code>discrete_resources_spec</code></a> (Optional, set of string) The Integer resources </li> </ul> </li> </ul>    <h4 id="restart-policy">  Restart Policy </h4> <p><code>restart_policy</code> is a block within the configuration that can be repeated only <strong>once</strong> to specify the mode configuration for the service. The <code>restart_policy</code> block specifies the restart policy which applies to containers created as part of this service and supports the following:</p> <ul> <li>
<a href="#condition"><code>condition</code></a> (Optional, string) Condition for restart: <code>(none|on-failure|any)</code> </li> <li>
<a href="#delay"><code>delay</code></a> (Optional, string) Delay between restart attempts <code>(ms|s|m|h)</code> </li> <li>
<a href="#max_attempts"><code>max_attempts</code></a> (Optional, string) Maximum attempts to restart a given container before giving up (default value is <code>0</code>, which is ignored) </li> <li>
<a href="#window"><code>window</code></a> (Optional, string) The time window used to evaluate the restart policy (default value is <code>0</code>, which is unbounded) <code>(ms|s|m|h)</code> </li> </ul>    <h4 id="placement-1">  Placement </h4> <p><code>placement</code> is a block within the configuration that can be repeated only <strong>once</strong> to specify the mode configuration for the service. The <code>placement</code> block specifies the placement preferences and supports the following:</p> <ul> <li>
<a href="#constraints"><code>constraints</code></a> (Optional, set of strings) An array of constraints. e.g.: <code>node.role==manager</code> </li> <li>
<a href="#prefs"><code>prefs</code></a> (Optional, set of string) Preferences provide a way to make the scheduler aware of factors such as topology. They are provided in order from highest to lowest precedence, e.g.: <code>spread=node.role.manager</code> </li> <li>
<a href="#platforms"><code>platforms</code></a> (Optional, set of) Platforms stores all the platforms that the service's image can run on <ul> <li>
<a href="#architecture"><code>architecture</code></a> (Required, string) The architecture, e.g., <code>amd64</code> </li> <li>
<a href="#os"><code>os</code></a> (Required, string) The operation system, e.g., <code>linux</code> </li> </ul> </li> </ul>    <h3 id="log-driver">  Log Driver </h3> <p><code>log_driver</code> is a block within the configuration that can be repeated only <strong>once</strong> to specify the extra log_driver configuration for the containers of the service. The <code>log_driver</code> specifies the log driver to use for tasks created from this spec. If not present, the default one for the swarm will be used, finally falling back to the engine default if not specified. The block supports the following:</p> <ul> <li>
<a href="#name-2"><code>name</code></a> - (Required, string) The logging driver to use. Either <code>(none|json-file|syslog|journald|gelf|fluentd|awslogs|splunk|etwlogs|gcplogs)</code>. </li> <li>
<a href="#options-2"><code>options</code></a> - (Optional, a map of strings and strings) The options for the logging driver, e.g. </li> </ul> <pre data-language="ruby">options {
  awslogs-region = "us-west-2"
  awslogs-group  = "dev/foo-service"
}
</pre>   <h3 id="mode-2">  Mode </h3> <p><code>mode</code> is a block within the configuration that can be repeated only <strong>once</strong> to specify the mode configuration for the service. The <code>mode</code> block supports the following:</p> <ul> <li>
<a href="#global"><code>global</code></a> - (Optional, bool) set it to <code>true</code> to run the service in the global mode </li> </ul> <pre data-language="ruby">resource "docker_service" "foo" {
  ...
  mode {
    global = true
  }
  ...
}
</pre> <ul> <li>
<a href="#replicated"><code>replicated</code></a> - (Optional, map), which contains atm only the amount of <code>replicas</code> </li> </ul> <pre data-language="ruby">resource "docker_service" "foo" {
  ...
  mode {
    replicated {
      replicas = 2
    }
  }
  ...
}
</pre>
<blockquote class="alert alert-warning" role="alert"> <p><strong>NOTE on <code>mode</code>:</strong> if neither <code>global</code> nor <code>replicated</code> is specified, the service is started in <code>replicated</code> mode with 1 replica. A change of service mode is not possible. The service has to be destroyed an recreated in the new mode.</p> </blockquote>  <h3 id="updateconfig-and-rollbackconfig">  UpdateConfig and RollbackConfig </h3> <p><code>update_config</code> or <code>rollback_config</code> is a block within the configuration that can be repeated only <strong>once</strong> to specify the extra update configuration for the containers of the service. The <code>update_config</code> <code>rollback_config</code> block supports the following:</p> <ul> <li>
<a href="#parallelism"><code>parallelism</code></a> - (Optional, int) The maximum number of tasks to be updated in one iteration simultaneously (0 to update all at once). </li> <li>
<a href="#delay-1"><code>delay</code></a> - (Optional, int) Delay between updates <code>(ns|us|ms|s|m|h)</code>, e.g. <code>5s</code>. </li> <li>
<a href="#failure_action"><code>failure_action</code></a> - (Optional, int) Action on update failure: <code>pause|continue|rollback</code>. </li> <li>
<a href="#monitor"><code>monitor</code></a> - (Optional, int) Duration after each task update to monitor for failure <code>(ns|us|ms|s|m|h)</code> </li> <li>
<a href="#max_failure_ratio"><code>max_failure_ratio</code></a> - (Optional, string) The failure rate to tolerate during an update as <code>float</code>. <strong>Important:</strong> the <code>float</code>need to be wrapped in a <code>string</code> to avoid internal casting and precision errors. </li> <li>
<a href="#order"><code>order</code></a> - (Optional, int) Update order either 'stop-first' or 'start-first'. </li> </ul>  <h3 id="endpointspec">  EndpointSpec </h3> <p><code>endpoint_spec</code> is a block within the configuration that can be repeated only <strong>once</strong> to specify properties that can be configured to access and load balance a service. The block supports the following:</p> <ul> <li>
<a href="#mode-3"><code>mode</code></a> - (Optional, string) The mode of resolution to use for internal load balancing between tasks. <code>(vip|dnsrr)</code>. Default: <code>vip</code>. </li> <li>
<a href="#ports"><code>ports</code></a> - (Optional, block) See <a href="#ports">Ports</a> below for details. </li> </ul>  <h4 id="ports-1">  Ports </h4> <p><code>ports</code> is a block within the configuration that can be repeated to specify the port mappings of the container. Each <code>ports</code> block supports the following:</p> <ul> <li>
<a href="#name-3"><code>name</code></a> - (Optional, string) A random name for the port. </li> <li>
<a href="#protocol"><code>protocol</code></a> - (Optional, string) Protocol that can be used over this port: <code>tcp|udp|sctp</code>. Default: <code>tcp</code>. </li> <li>
<a href="#target_port"><code>target_port</code></a> - (Required, int) Port inside the container. </li> <li>
<a href="#published_port"><code>published_port</code></a> - (Required, int) The port on the swarm hosts. If not set the value of <code>target_port</code> will be used. </li> <li>
<a href="#publish_mode"><code>publish_mode</code></a> - (Optional, string) Represents the mode in which the port is to be published: <code>ingress|host</code> </li> </ul>  <h3 id="converge-config">  Converge Config </h3> <p><code>converge_config</code> is a block within the configuration that can be repeated only <strong>once</strong> to specify the extra Converging configuration for the containers of the service. This is the same behavior as the <code>docker cli</code>. By adding this configuration, it is monitored with the given interval that, e.g., all tasks/replicas of a service are up and healthy</p> <p>The <code>converge_config</code> block supports the following:</p> <ul> <li>
<a href="#delay-2"><code>delay</code></a> - (Optional, string) Time between each the check to check docker endpoint <code>(ms|s|m|h)</code>. For example, to check if all tasks are up when a service is created, or to check if all tasks are successfully updated on an update. Default: <code>7s</code>. </li> <li>
<a href="#timeout-1"><code>timeout</code></a> - (Optional, string) The timeout of the service to reach the desired state <code>(s|m)</code>. Default: <code>3m</code>. </li> </ul> <h2 id="attributes-reference">  Attributes Reference </h2> <p>The following attributes are exported in addition to the above configuration:</p> <ul> <li>
<a href="#id"><code>id</code></a> (string) </li> </ul><div class="_attribution">
  <p class="_attribution-p">
    &copy; 2018 HashiCorp</br>Licensed under the MPL 2.0 License.<br>
    <a href="https://www.terraform.io/docs/providers/docker/r/service.html" class="_attribution-link">https://www.terraform.io/docs/providers/docker/r/service.html</a>
  </p>
</div>
