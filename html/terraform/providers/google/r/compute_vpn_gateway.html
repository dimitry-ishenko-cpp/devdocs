<h1 id="google_compute_vpn_gateway">  google_compute_vpn_gateway </h1> <p>Represents a VPN gateway running in GCP. This virtual device is managed by Google, but used only by you.</p> <p>To get more information about VpnGateway, see:</p> <ul> <li>
<a href="https://cloud.google.com/compute/docs/reference/rest/v1/targetVpnGateways">API documentation</a> </li> </ul> <h2 id="example-usage">  Example Usage </h2> <pre data-language="ruby">resource "google_compute_network" "network1" {
  name       = "network1"
  ipv4_range = "10.120.0.0/16"
}

resource "google_compute_vpn_gateway" "target_gateway" {
  name    = "vpn1"
  network = "${google_compute_network.network1.self_link}"
  region  = "${var.region}"
}

resource "google_compute_address" "vpn_static_ip" {
  name   = "vpn-static-ip"
  region = "${var.region}"
}

resource "google_compute_forwarding_rule" "fr_esp" {
  name        = "fr-esp"
  region      = "${var.region}"
  ip_protocol = "ESP"
  ip_address  = "${google_compute_address.vpn_static_ip.address}"
  target      = "${google_compute_vpn_gateway.target_gateway.self_link}"
}

resource "google_compute_forwarding_rule" "fr_udp500" {
  name        = "fr-udp500"
  region      = "${var.region}"
  ip_protocol = "UDP"
  port_range  = "500"
  ip_address  = "${google_compute_address.vpn_static_ip.address}"
  target      = "${google_compute_vpn_gateway.target_gateway.self_link}"
}

resource "google_compute_forwarding_rule" "fr_udp4500" {
  name        = "fr-udp4500"
  region      = "${var.region}"
  ip_protocol = "UDP"
  port_range  = "4500"
  ip_address  = "${google_compute_address.vpn_static_ip.address}"
  target      = "${google_compute_vpn_gateway.target_gateway.self_link}"
}

resource "google_compute_vpn_tunnel" "tunnel1" {
  name          = "tunnel1"
  region        = "${var.region}"
  peer_ip       = "15.0.0.120"
  shared_secret = "a secret message"

  target_vpn_gateway = "${google_compute_vpn_gateway.target_gateway.self_link}"

  depends_on = [
    "google_compute_forwarding_rule.fr_esp",
    "google_compute_forwarding_rule.fr_udp500",
    "google_compute_forwarding_rule.fr_udp4500",
  ]
}

resource "google_compute_route" "route1" {
  name       = "route1"
  network    = "${google_compute_network.network1.name}"
  dest_range = "15.0.0.0/24"
  priority   = 1000

  next_hop_vpn_tunnel = "${google_compute_vpn_tunnel.tunnel1.self_link}"
}
</pre>
<h2 id="argument-reference">  Argument Reference </h2> <p>The following arguments are supported:</p> <ul> <li>
<a href="#name"><code>name</code></a> - (Required) Name of the resource. Provided by the client when the resource is created. The name must be 1-63 characters long, and comply with RFC1035. Specifically, the name must be 1-63 characters long and match the regular expression <code>[a-z]([-a-z0-9]*[a-z0-9])?</code> which means the first character must be a lowercase letter, and all following characters must be a dash, lowercase letter, or digit, except the last character, which cannot be a dash. </li> <li>
<a href="#network"><code>network</code></a> - (Required) The network this VPN gateway is accepting traffic for. </li> </ul>  <ul> <li>
<a href="#description"><code>description</code></a> - (Optional) An optional description of this resource. </li> <li>
<a href="#region"><code>region</code></a> - (Optional) The region this gateway should sit in. </li> <li>
<a href="#project"><code>project</code></a> (Optional) The ID of the project in which the resource belongs. If it is not provided, the provider project is used. </li> </ul> <h2 id="attributes-reference">  Attributes Reference </h2> <p>In addition to the arguments listed above, the following computed attributes are exported:</p> <ul> <li>
<a href="#creation_timestamp"><code>creation_timestamp</code></a> - Creation timestamp in RFC3339 text format. </li> <li>
<a href="#self_link"><code>self_link</code></a> - The URI of the created resource. </li> </ul> <h2 id="timeouts">  Timeouts </h2> <p>This resource provides the following <a href="../../../configuration/resources.html#timeouts">Timeouts</a> configuration options:</p> <ul> <li>
<a href="#create"><code>create</code></a> - Default is 4 minutes. </li> <li>
<a href="#delete"><code>delete</code></a> - Default is 4 minutes. </li> </ul> <h2 id="import">  Import </h2> <p>VpnGateway can be imported using any of these accepted formats:</p> <pre>$ terraform import google_compute_vpn_gateway.default projects/{{project}}/regions/{{region}}/targetVpnGateways/{{name}}
$ terraform import google_compute_vpn_gateway.default {{project}}/{{region}}/{{name}}
$ terraform import google_compute_vpn_gateway.default {{name}}
</pre><div class="_attribution">
  <p class="_attribution-p">
    &copy; 2018 HashiCorp</br>Licensed under the MPL 2.0 License.<br>
    <a href="https://www.terraform.io/docs/providers/google/r/compute_vpn_gateway.html" class="_attribution-link">https://www.terraform.io/docs/providers/google/r/compute_vpn_gateway.html</a>
  </p>
</div>
