<h1 id="google_compute_router">  google_compute_router </h1> <p>Manages a Cloud Router resource. For more information see <a href="https://cloud.google.com/compute/docs/cloudrouter">the official documentation</a> and <a href="https://cloud.google.com/compute/docs/reference/latest/routers">API</a>.</p> <h2 id="example-usage">  Example Usage </h2> <pre data-language="ruby">resource "google_compute_network" "foobar" {
  name = "network-1"
}

resource "google_compute_subnetwork" "foobar" {
  name          = "subnet-1"
  network       = "${google_compute_network.foobar.self_link}"
  ip_cidr_range = "10.0.0.0/16"
  region        = "us-central1"
}

resource "google_compute_address" "foobar" {
  name   = "vpn-gateway-1-address"
  region = "${google_compute_subnetwork.foobar.region}"
}

resource "google_compute_vpn_gateway" "foobar" {
  name    = "vpn-gateway-1"
  network = "${google_compute_network.foobar.self_link}"
  region  = "${google_compute_subnetwork.foobar.region}"
}

resource "google_compute_forwarding_rule" "foobar_esp" {
  name        = "vpn-gw-1-esp"
  region      = "${google_compute_vpn_gateway.foobar.region}"
  ip_protocol = "ESP"
  ip_address  = "${google_compute_address.foobar.address}"
  target      = "${google_compute_vpn_gateway.foobar.self_link}"
}

resource "google_compute_forwarding_rule" "foobar_udp500" {
  name        = "vpn-gw-1-udp-500"
  region      = "${google_compute_forwarding_rule.foobar_esp.region}"
  ip_protocol = "UDP"
  port_range  = "500-500"
  ip_address  = "${google_compute_address.foobar.address}"
  target      = "${google_compute_vpn_gateway.foobar.self_link}"
}

resource "google_compute_forwarding_rule" "foobar_udp4500" {
  name        = "vpn-gw-1-udp-4500"
  region      = "${google_compute_forwarding_rule.foobar_udp500.region}"
  ip_protocol = "UDP"
  port_range  = "4500-4500"
  ip_address  = "${google_compute_address.foobar.address}"
  target      = "${google_compute_vpn_gateway.foobar.self_link}"
}

resource "google_compute_router" "foobar" {
  name    = "router-1"
  region  = "${google_compute_forwarding_rule.foobar_udp500.region}"
  network = "${google_compute_network.foobar.self_link}"

  bgp {
    asn = 64512
  }
}

resource "google_compute_vpn_tunnel" "foobar" {
  name               = "vpn-tunnel-1"
  region             = "${google_compute_forwarding_rule.foobar_udp4500.region}"
  target_vpn_gateway = "${google_compute_vpn_gateway.foobar.self_link}"
  shared_secret      = "unguessable"
  peer_ip            = "8.8.8.8"
  router             = "${google_compute_router.foobar.name}"
}

resource "google_compute_router_interface" "foobar" {
  name       = "interface-1"
  router     = "${google_compute_router.foobar.name}"
  region     = "${google_compute_router.foobar.region}"
  ip_range   = "169.254.1.1/30"
  vpn_tunnel = "${google_compute_vpn_tunnel.foobar.name}"
}

resource "google_compute_router_peer" "foobar" {
  name                      = "peer-1"
  router                    = "${google_compute_router.foobar.name}"
  region                    = "${google_compute_router.foobar.region}"
  peer_ip_address           = "169.254.1.2"
  peer_asn                  = 65513
  advertised_route_priority = 100
  interface                 = "${google_compute_router_interface.foobar.name}"
}
</pre>
<h2 id="argument-reference">  Argument Reference </h2> <p>The following arguments are supported:</p> <ul> <li>
<p><a href="#name"><code>name</code></a> - (Required) A unique name for the router, required by GCE. Changing this forces a new router to be created.</p> </li> <li>
<p><a href="#network"><code>network</code></a> - (Required) The name or resource link to the network this Cloud Router will use to learn and announce routes. Changing this forces a new router to be created.</p> </li> <li>
<p><a href="#bgp"><code>bgp</code></a> - (Required) BGP information specific to this router. Changing this forces a new router to be created. Structure is documented below.</p> </li> </ul>  <ul> <li>
<p><a href="#description"><code>description</code></a> - (Optional) A description of the resource. Changing this forces a new router to be created.</p> </li> <li>
<p><a href="#project"><code>project</code></a> - (Optional) The ID of the project in which the resource belongs. If it is not provided, the provider project is used. Changing this forces a new router to be created.</p> </li> <li>
<p><a href="#region"><code>region</code></a> - (Optional) The region this router should sit in. If not specified, the project region will be used. Changing this forces a new router to be created.</p> </li> </ul>  <p>The <code>bgp</code> block supports:</p> <ul> <li>
<a href="#asn"><code>asn</code></a> - (Required) Local BGP Autonomous System Number (ASN). Must be an RFC6996 private ASN. </li> </ul> <h2 id="attributes-reference">  Attributes Reference </h2> <p>In addition to the arguments listed above, the following computed attributes are exported:</p> <ul> <li>
<a href="#self_link"><code>self_link</code></a> - The URI of the created resource. </li> </ul> <h2 id="import">  Import </h2> <p>Routers can be imported using the <code>region</code> and <code>name</code>, e.g.</p> <pre>$ terraform import google_compute_router.foobar us-central1/router-1
</pre><div class="_attribution">
  <p class="_attribution-p">
    &copy; 2018 HashiCorp</br>Licensed under the MPL 2.0 License.<br>
    <a href="https://www.terraform.io/docs/providers/google/r/compute_router.html" class="_attribution-link">https://www.terraform.io/docs/providers/google/r/compute_router.html</a>
  </p>
</div>
