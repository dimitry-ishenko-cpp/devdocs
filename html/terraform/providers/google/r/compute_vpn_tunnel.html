<h1 id="google_compute_vpn_tunnel">  google_compute_vpn_tunnel </h1> <p>Manages a VPN Tunnel to the GCE network. For more info, read the <a href="https://cloud.google.com/compute/docs/vpn">documentation</a>.</p> <blockquote class="alert alert-warning" role="alert"> <p><strong>Note:</strong> All arguments including the <code>shared_secret</code> will be stored in the raw state as plain-text. <a href="../../../state/sensitive-data.html">Read more about sensitive data in state</a>.</p> </blockquote> <h2 id="example-usage">  Example Usage </h2> <pre data-language="ruby">resource "google_compute_network" "network1" {
  name = "network1"
}

resource "google_compute_subnetwork" "subnet1" {
  name          = "subnet1"
  network       = "${google_compute_network.network1.self_link}"
  ip_cidr_range = "10.120.0.0/16"
  region        = "us-central1"
}

resource "google_compute_vpn_gateway" "target_gateway" {
  name    = "vpn1"
  network = "${google_compute_network.network1.self_link}"
  region  = "${google_compute_subnetwork.subnet1.region}"
}

resource "google_compute_address" "vpn_static_ip" {
  name   = "vpn-static-ip"
  region = "${google_compute_subnetwork.subnet1.region}"
}

resource "google_compute_forwarding_rule" "fr_esp" {
  name        = "fr-esp"
  ip_protocol = "ESP"
  ip_address  = "${google_compute_address.vpn_static_ip.address}"
  target      = "${google_compute_vpn_gateway.target_gateway.self_link}"
}

resource "google_compute_forwarding_rule" "fr_udp500" {
  name        = "fr-udp500"
  ip_protocol = "UDP"
  port_range  = "500-500"
  ip_address  = "${google_compute_address.vpn_static_ip.address}"
  target      = "${google_compute_vpn_gateway.target_gateway.self_link}"
}

resource "google_compute_forwarding_rule" "fr_udp4500" {
  name        = "fr-udp4500"
  ip_protocol = "UDP"
  port_range  = "4500-4500"
  ip_address  = "${google_compute_address.vpn_static_ip.address}"
  target      = "${google_compute_vpn_gateway.target_gateway.self_link}"
}

resource "google_compute_vpn_tunnel" "tunnel1" {
  name          = "tunnel1"
  peer_ip       = "15.0.0.120"
  shared_secret = "a secret message"

  target_vpn_gateway = "${google_compute_vpn_gateway.target_gateway.self_link}"

  local_traffic_selector  = ["${google_compute_subnetwork.subnet1.ip_cidr_range}"]
  remote_traffic_selector = ["172.16.0.0/12"]

  depends_on = [
    "google_compute_forwarding_rule.fr_esp",
    "google_compute_forwarding_rule.fr_udp500",
    "google_compute_forwarding_rule.fr_udp4500",
  ]
}

resource "google_compute_route" "route1" {
  name       = "route1"
  network    = "${google_compute_network.network1.name}"
  dest_range = "15.0.0.0/24"
  priority   = 1000

  next_hop_vpn_tunnel = "${google_compute_vpn_tunnel.tunnel1.self_link}"
}
</pre>
<h2 id="argument-reference">  Argument Reference </h2> <p>The following arguments are supported:</p> <ul> <li>
<p><a href="#name"><code>name</code></a> - (Required) A unique name for the resource, required by GCE. Changing this forces a new resource to be created.</p> </li> <li>
<p><a href="#peer_ip"><code>peer_ip</code></a> - (Required) The VPN gateway sitting outside of GCE. Changing this forces a new resource to be created.</p> </li> <li>
<p><a href="#shared_secret"><code>shared_secret</code></a> - (Required) A passphrase shared between the two VPN gateways. Changing this forces a new resource to be created.</p> </li> <li>
<p><a href="#target_vpn_gateway"><code>target_vpn_gateway</code></a> - (Required) A link to the VPN gateway sitting inside GCE. Changing this forces a new resource to be created.</p> </li> </ul>  <ul> <li>
<p><a href="#description"><code>description</code></a> - (Optional) A description of the resource. Changing this forces a new resource to be created.</p> </li> <li>
<p><a href="#ike_version"><code>ike_version</code></a> - (Optional) Either version 1 or 2. Default is 2. Changing this forces a new resource to be created.</p> </li> <li>
<p><a href="#local_traffic_selector"><code>local_traffic_selector</code></a> - (Optional) Specifies which CIDR ranges are announced to the VPN peer. Mandatory if the VPN gateway is attached to a custom subnetted network. Refer to Google documentation for more information.</p> </li> <li>
<p><a href="#remote_traffic_selector"><code>remote_traffic_selector</code></a> - (Optional) Specifies which CIDR ranges the VPN tunnel can route to the remote side. Mandatory if the VPN gateway is attached to a custom subnetted network. Refer to Google documentation for more information.</p> </li> <li>
<p><a href="#router"><code>router</code></a> - (Optional) Name of a Cloud Router in the same region to be used for dynamic routing. Refer to Google documentation for more information.</p> </li> <li>
<p><a href="#project"><code>project</code></a> - (Optional) The ID of the project in which the resource belongs. If it is not provided, the provider project is used.</p> </li> <li>
<p><a href="#region"><code>region</code></a> - (Optional) The region this tunnel should sit in. If not specified, the project region will be used. Changing this forces a new resource to be created.</p> </li> </ul> <h2 id="attributes-reference">  Attributes Reference </h2> <p>In addition to the arguments listed above, the following computed attributes are exported:</p> <ul> <li>
<p><a href="#detailed_status"><code>detailed_status</code></a> - Information about the status of the VPN tunnel.</p> </li> <li>
<p><a href="#self_link"><code>self_link</code></a> - The URI of the created resource.</p> </li> </ul><div class="_attribution">
  <p class="_attribution-p">
    &copy; 2018 HashiCorp</br>Licensed under the MPL 2.0 License.<br>
    <a href="https://www.terraform.io/docs/providers/google/r/compute_vpn_tunnel.html" class="_attribution-link">https://www.terraform.io/docs/providers/google/r/compute_vpn_tunnel.html</a>
  </p>
</div>
