<h1 id="google_dns_record_set">  google_dns_record_set </h1> <p>Manages a set of DNS records within Google Cloud DNS. For more information see <a href="https://cloud.google.com/dns/records/">the official documentation</a> and <a href="https://cloud.google.com/dns/api/v1/resourceRecordSets">API</a>.</p> <blockquote class="alert alert-warning" role="alert"> <p><strong>Note:</strong> The Google Cloud DNS API requires NS records be present at all times. To accommodate this, when creating NS records, the default records Google automatically creates will be silently overwritten. Also, when destroying NS records, Terraform will not actually remove NS records, but will report that it did.</p> </blockquote> <h2 id="example-usage">  Example Usage </h2> <h3 id="binding-a-dns-name-to-the-ephemeral-ip-of-a-new-instance-">  Binding a DNS name to the ephemeral IP of a new instance: </h3> <pre data-language="ruby">resource "google_dns_record_set" "frontend" {
  name = "frontend.${google_dns_managed_zone.prod.dns_name}"
  type = "A"
  ttl  = 300

  managed_zone = "${google_dns_managed_zone.prod.name}"

  rrdatas = ["${google_compute_instance.frontend.network_interface.0.access_config.0.assigned_nat_ip}"]
}

resource "google_compute_instance" "frontend" {
  name         = "frontend"
  machine_type = "g1-small"
  zone         = "us-central1-b"

  boot_disk {
    initialize_params {
      image = "debian-cloud/debian-8"
    }
  }

  network_interface {
    network       = "default"
    access_config = {}
  }
}

resource "google_dns_managed_zone" "prod" {
  name     = "prod-zone"
  dns_name = "prod.mydomain.com."
}
</pre>
<h3 id="adding-an-a-record">  Adding an A record </h3> <pre data-language="ruby">resource "google_dns_record_set" "a" {
  name = "backend.${google_dns_managed_zone.prod.dns_name}"
  managed_zone = "${google_dns_managed_zone.prod.name}"
  type = "A"
  ttl  = 300

  rrdatas = ["8.8.8.8"]
}

resource "google_dns_managed_zone" "prod" {
  name     = "prod-zone"
  dns_name = "prod.mydomain.com."
}
</pre>
<h3 id="adding-an-mx-record">  Adding an MX record </h3> <pre data-language="ruby">resource "google_dns_record_set" "mx" {
  name = "${google_dns_managed_zone.prod.dns_name}"
  managed_zone = "${google_dns_managed_zone.prod.name}"
  type = "MX"
  ttl  = 3600

  rrdatas = [
    "1 aspmx.l.google.com.",
    "5 alt1.aspmx.l.google.com.",
    "5 alt2.aspmx.l.google.com.",
    "10 alt3.aspmx.l.google.com.",
    "10 alt4.aspmx.l.google.com."
  ]
}

resource "google_dns_managed_zone" "prod" {
  name     = "prod-zone"
  dns_name = "prod.mydomain.com."
}
</pre>
<h3 id="adding-an-spf-record">  Adding an SPF record </h3> <p>Quotes (<code>""</code>) must be added around your <code>rrdatas</code> for a SPF record. Otherwise <code>rrdatas</code> string gets split on spaces.</p> <pre data-language="ruby">resource "google_dns_record_set" "spf" {
  name = "frontend.${google_dns_managed_zone.prod.dns_name}"
  managed_zone = "${google_dns_managed_zone.prod.name}"
  type = "TXT"
  ttl  = 300

  rrdatas = ["\"v=spf1 ip4:111.111.111.111 include:backoff.email-example.com -all\""]
}

resource "google_dns_managed_zone" "prod" {
  name     = "prod-zone"
  dns_name = "prod.mydomain.com."
}
</pre>
<h2 id="argument-reference">  Argument Reference </h2> <p>The following arguments are supported:</p> <ul> <li>
<p><a href="#managed_zone"><code>managed_zone</code></a> - (Required) The name of the zone in which this record set will reside.</p> </li> <li>
<p><a href="#name"><code>name</code></a> - (Required) The DNS name this record set will apply to.</p> </li> <li>
<p><a href="#rrdatas"><code>rrdatas</code></a> - (Required) The string data for the records in this record set whose meaning depends on the DNS type. For TXT record, if the string data contains spaces, add surrounding <code>\"</code> if you don't want your string to get split on spaces.</p> </li> <li>
<p><a href="#ttl"><code>ttl</code></a> - (Required) The time-to-live of this record set (seconds).</p> </li> <li>
<p><a href="#type"><code>type</code></a> - (Required) The DNS record set type.</p> </li> </ul>  <ul> <li>
<a href="#project"><code>project</code></a> - (Optional) The ID of the project in which the resource belongs. If it is not provided, the provider project is used. </li> </ul> <h2 id="attributes-reference">  Attributes Reference </h2> <p>Only the arguments listed above are exposed as attributes.</p> <h2 id="import">  Import </h2> <p>DNS record set can be imported using the <code>zone name</code>, <code>record name</code> and record <code>type</code>, e.g.</p> <pre>$ terraform import google_dns_record_set.frontend prod-zone/frontend.prod.mydomain.com./A
</pre>
<p>Note: The record name must include the trailing dot at the end.</p><div class="_attribution">
  <p class="_attribution-p">
    &copy; 2018 HashiCorp</br>Licensed under the MPL 2.0 License.<br>
    <a href="https://www.terraform.io/docs/providers/google/r/dns_record_set.html" class="_attribution-link">https://www.terraform.io/docs/providers/google/r/dns_record_set.html</a>
  </p>
</div>
