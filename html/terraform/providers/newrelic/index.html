<h1 id="new-relic-provider">  New Relic Provider </h1> <p><a href="https://newrelic.com/">New Relic</a> offers a performance management solution enabling developers to diagnose and fix application performance problems in real time.</p> <p>Use the navigation to the left to read about the available resources.</p> <h2 id="example-usage">  Example Usage </h2> <pre data-language="ruby"># Configure the New Relic provider
provider "newrelic" {
  api_key = "${var.newrelic_api_key}"
}

# Create an alert policy
resource "newrelic_alert_policy" "alert" {
  name = "Alert"
}

# Add a condition
resource "newrelic_alert_condition" "foo" {
  policy_id = "${newrelic_alert_policy.alert.id}"

  name        = "foo"
  type        = "apm_app_metric"
  entities    = ["12345"]                             # You can look this up in New Relic
  metric      = "apdex"
  runbook_url = "https://docs.example.com/my-runbook"

  term {
    duration      = 5
    operator      = "below"
    priority      = "critical"
    threshold     = "0.75"
    time_function = "all"
  }
}

# Add a notification channel
resource "newrelic_alert_channel" "email" {
  name = "email"
  type = "email"

  configuration = {
    recipients              = "paul@example.com"
    include_json_attachment = "1"
  }
}

# Link the channel to the policy
resource "newrelic_alert_policy_channel" "alert_email" {
  policy_id  = "${newrelic_alert_policy.alert.id}"
  channel_id = "${newrelic_alert_channel.email.id}"
}
</pre>
<h2 id="argument-reference">  Argument Reference </h2> <p>The following arguments are supported:</p> <ul> <li>
<a href="#api_key"><code>api_key</code></a> - (Required) Your New Relic API key. Can also use <code>NEWRELIC_API_KEY</code> environment variable. </li> </ul><div class="_attribution">
  <p class="_attribution-p">
    &copy; 2018 HashiCorp</br>Licensed under the MPL 2.0 License.<br>
    <a href="https://www.terraform.io/docs/providers/newrelic/index.html" class="_attribution-link">https://www.terraform.io/docs/providers/newrelic/index.html</a>
  </p>
</div>
