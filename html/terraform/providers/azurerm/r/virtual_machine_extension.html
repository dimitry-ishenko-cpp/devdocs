<h1 id="azurerm_virtual_machine_extension">  azurerm_virtual_machine_extension </h1> <p>Manages a Virtual Machine Extension to provide post deployment configuration and run automated tasks.</p> <blockquote class="alert alert-warning" role="alert"> <p><strong>Please Note:</strong> The CustomScript extensions for Linux &amp; Windows require that the <code>commandToExecute</code> returns a <code>0</code> exit code to be classified as successfully deployed. You can achieve this by appending <code>exit 0</code> to the end of your <code>commandToExecute</code>.</p> </blockquote> <h2 id="example-usage">  Example Usage </h2> <pre data-language="ruby">resource "azurerm_resource_group" "test" {
  name     = "acctestRG"
  location = "West US"
}

resource "azurerm_virtual_network" "test" {
  name                = "acctvn"
  address_space       = ["10.0.0.0/16"]
  location            = "West US"
  resource_group_name = "${azurerm_resource_group.test.name}"
}

resource "azurerm_subnet" "test" {
  name                 = "acctsub"
  resource_group_name  = "${azurerm_resource_group.test.name}"
  virtual_network_name = "${azurerm_virtual_network.test.name}"
  address_prefix       = "10.0.2.0/24"
}

resource "azurerm_network_interface" "test" {
  name                = "acctni"
  location            = "West US"
  resource_group_name = "${azurerm_resource_group.test.name}"

  ip_configuration {
    name                          = "testconfiguration1"
    subnet_id                     = "${azurerm_subnet.test.id}"
    private_ip_address_allocation = "dynamic"
  }
}

resource "azurerm_storage_account" "test" {
  name                     = "accsa"
  resource_group_name      = "${azurerm_resource_group.test.name}"
  location                 = "westus"
  account_tier             = "Standard"
  account_replication_type = "LRS"

  tags {
    environment = "staging"
  }
}

resource "azurerm_storage_container" "test" {
  name                  = "vhds"
  resource_group_name   = "${azurerm_resource_group.test.name}"
  storage_account_name  = "${azurerm_storage_account.test.name}"
  container_access_type = "private"
}

resource "azurerm_virtual_machine" "test" {
  name                  = "acctvm"
  location              = "West US"
  resource_group_name   = "${azurerm_resource_group.test.name}"
  network_interface_ids = ["${azurerm_network_interface.test.id}"]
  vm_size               = "Standard_A0"

  storage_image_reference {
    publisher = "Canonical"
    offer     = "UbuntuServer"
    sku       = "16.04-LTS"
    version   = "latest"
  }

  storage_os_disk {
    name          = "myosdisk1"
    vhd_uri       = "${azurerm_storage_account.test.primary_blob_endpoint}${azurerm_storage_container.test.name}/myosdisk1.vhd"
    caching       = "ReadWrite"
    create_option = "FromImage"
  }

  os_profile {
    computer_name  = "hostname"
    admin_username = "testadmin"
    admin_password = "Password1234!"
  }

  os_profile_linux_config {
    disable_password_authentication = false
  }

  tags {
    environment = "staging"
  }
}

resource "azurerm_virtual_machine_extension" "test" {
  name                 = "hostname"
  location             = "West US"
  resource_group_name  = "${azurerm_resource_group.test.name}"
  virtual_machine_name = "${azurerm_virtual_machine.test.name}"
  publisher            = "Microsoft.Azure.Extensions"
  type                 = "CustomScript"
  type_handler_version = "2.0"

  settings = &lt;&lt;SETTINGS
    {
        "commandToExecute": "hostname &amp;&amp; uptime"
    }
SETTINGS

  tags {
    environment = "Production"
  }
}
</pre>
<h2 id="argument-reference">  Argument Reference </h2> <p>The following arguments are supported:</p> <ul> <li>
<p><a href="#name"><code>name</code></a> - (Required) The name of the virtual machine extension peering. Changing this forces a new resource to be created.</p> </li> <li>
<p><a href="#location"><code>location</code></a> - (Required) The location where the extension is created. Changing this forces a new resource to be created.</p> </li> <li>
<p><a href="#resource_group_name"><code>resource_group_name</code></a> - (Required) The name of the resource group in which to create the virtual network. Changing this forces a new resource to be created.</p> </li> <li>
<p><a href="#virtual_machine_name"><code>virtual_machine_name</code></a> - (Required) The name of the virtual machine. Changing this forces a new resource to be created.</p> </li> <li>
<p><a href="#publisher"><code>publisher</code></a> - (Required) The publisher of the extension, available publishers can be found by using the Azure CLI.</p> </li> <li>
<p><a href="#type"><code>type</code></a> - (Required) The type of extension, available types for a publisher can be found using the Azure CLI.</p> </li> </ul> <blockquote class="alert alert-warning" role="alert"> <p><strong>Note:</strong> The <code>Publisher</code> and <code>Type</code> of Virtual Machine Extensions can be found using the Azure CLI, via: <code>shell
$ az vm extension image list --location westus -o table
</code></p> </blockquote> <ul> <li>
<p><a href="#type_handler_version"><code>type_handler_version</code></a> - (Required) Specifies the version of the extension to use, available versions can be found using the Azure CLI.</p> </li> <li>
<p><a href="#auto_upgrade_minor_version"><code>auto_upgrade_minor_version</code></a> - (Optional) Specifies if the platform deploys the latest minor version update to the <code>type_handler_version</code> specified.</p> </li> <li>
<p><a href="#settings"><code>settings</code></a> - (Required) The settings passed to the extension, these are specified as a JSON object in a string.</p> </li> </ul> <blockquote class="alert alert-warning" role="alert"> <p><strong>Please Note:</strong> Certain VM Extensions require that the keys in the <code>settings</code> block are case sensitive. If you're seeing unhelpful errors, please ensure the keys are consistent with how Azure is expecting them (for instance, for the <code>JsonADDomainExtension</code> extension, the keys are expected to be in <code>TitleCase</code>.)</p> </blockquote> <ul> <li>
<a href="#protected_settings"><code>protected_settings</code></a> - (Optional) The protected_settings passed to the extension, like settings, these are specified as a JSON object in a string. </li> </ul> <blockquote class="alert alert-warning" role="alert"> <p><strong>Please Note:</strong> Certain VM Extensions require that the keys in the <code>protected_settings</code> block are case sensitive. If you're seeing unhelpful errors, please ensure the keys are consistent with how Azure is expecting them (for instance, for the <code>JsonADDomainExtension</code> extension, the keys are expected to be in <code>TitleCase</code>.)</p> </blockquote> <h2 id="attributes-reference">  Attributes Reference </h2> <p>The following attributes are exported:</p> <ul> <li>
<a href="#id"><code>id</code></a> - The Virtual Machine Extension ID. </li> </ul> <h2 id="import">  Import </h2> <p>Virtual Machine Extensions can be imported using the <code>resource id</code>, e.g.</p> <pre data-language="shell">terraform import azurerm_virtual_machine_extension.test /subscriptions/00000000-0000-0000-0000-000000000000/resourceGroups/mygroup1/providers/Microsoft.Compute/virtualMachines/myVM/extensions/hostname
</pre><div class="_attribution">
  <p class="_attribution-p">
    &copy; 2018 HashiCorp</br>Licensed under the MPL 2.0 License.<br>
    <a href="https://www.terraform.io/docs/providers/azurerm/r/virtual_machine_extension.html" class="_attribution-link">https://www.terraform.io/docs/providers/azurerm/r/virtual_machine_extension.html</a>
  </p>
</div>
