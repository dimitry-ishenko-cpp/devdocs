<h1 id="azurerm_virtual_machine_scale_set">  azurerm_virtual_machine_scale_set </h1> <p>Create a virtual machine scale set.</p> <blockquote class="alert alert-warning" role="alert"> <p><strong>Note:</strong> All arguments including the administrator login and password will be stored in the raw state as plain-text. <a href="../../../state/sensitive-data.html">Read more about sensitive data in state</a>.</p> </blockquote> <h2 id="example-usage-with-managed-disks-recommended-">  Example Usage with Managed Disks (Recommended) </h2> <pre data-language="ruby">resource "azurerm_resource_group" "test" {
  name     = "acctestRG"
  location = "West US 2"
}

resource "azurerm_virtual_network" "test" {
  name                = "acctvn"
  address_space       = ["10.0.0.0/16"]
  location            = "${azurerm_resource_group.test.location}"
  resource_group_name = "${azurerm_resource_group.test.name}"
}

resource "azurerm_subnet" "test" {
  name                 = "acctsub"
  resource_group_name  = "${azurerm_resource_group.test.name}"
  virtual_network_name = "${azurerm_virtual_network.test.name}"
  address_prefix       = "10.0.2.0/24"
}

resource "azurerm_public_ip" "test" {
  name                         = "test"
  location                     = "${azurerm_resource_group.test.location}"
  resource_group_name          = "${azurerm_resource_group.test.name}"
  public_ip_address_allocation = "static"
  domain_name_label            = "${azurerm_resource_group.test.name}"

  tags {
    environment = "staging"
  }
}

resource "azurerm_lb" "test" {
  name                = "test"
  location            = "${azurerm_resource_group.test.location}"
  resource_group_name = "${azurerm_resource_group.test.name}"

  frontend_ip_configuration {
    name                 = "PublicIPAddress"
    public_ip_address_id = "${azurerm_public_ip.test.id}"
  }
}

resource "azurerm_lb_backend_address_pool" "bpepool" {
  resource_group_name = "${azurerm_resource_group.test.name}"
  loadbalancer_id     = "${azurerm_lb.test.id}"
  name                = "BackEndAddressPool"
}

resource "azurerm_lb_nat_pool" "lbnatpool" {
  count                          = 3
  resource_group_name            = "${azurerm_resource_group.test.name}"
  name                           = "ssh"
  loadbalancer_id                = "${azurerm_lb.test.id}"
  protocol                       = "Tcp"
  frontend_port_start            = 50000
  frontend_port_end              = 50119
  backend_port                   = 22
  frontend_ip_configuration_name = "PublicIPAddress"
}

resource "azurerm_virtual_machine_scale_set" "test" {
  name                = "mytestscaleset-1"
  location            = "${azurerm_resource_group.test.location}"
  resource_group_name = "${azurerm_resource_group.test.name}"
  upgrade_policy_mode = "Manual"

  sku {
    name     = "Standard_A0"
    tier     = "Standard"
    capacity = 2
  }

  storage_profile_image_reference {
    publisher = "Canonical"
    offer     = "UbuntuServer"
    sku       = "16.04-LTS"
    version   = "latest"
  }

  storage_profile_os_disk {
    name              = ""
    caching           = "ReadWrite"
    create_option     = "FromImage"
    managed_disk_type = "Standard_LRS"
  }

  storage_profile_data_disk {
    lun            = 0
    caching        = "ReadWrite"
    create_option  = "Empty"
    disk_size_gb   = 10
  }

  os_profile {
    computer_name_prefix = "testvm"
    admin_username       = "myadmin"
    admin_password       = "Passwword1234"
  }

  os_profile_linux_config {
    disable_password_authentication = true

    ssh_keys {
      path     = "/home/myadmin/.ssh/authorized_keys"
      key_data = "${file("~/.ssh/demo_key.pub")}"
    }
  }

  network_profile {
    name    = "terraformnetworkprofile"
    primary = true

    ip_configuration {
      name                                   = "TestIPConfiguration"
      subnet_id                              = "${azurerm_subnet.test.id}"
      load_balancer_backend_address_pool_ids = ["${azurerm_lb_backend_address_pool.bpepool.id}"]
      load_balancer_inbound_nat_rules_ids    = ["${element(azurerm_lb_nat_pool.lbnatpool.*.id, count.index)}"]
    }
  }

  tags {
    environment = "staging"
  }
}
</pre>
<h2 id="example-usage-with-unmanaged-disks">  Example Usage with Unmanaged Disks </h2> <pre data-language="ruby">resource "azurerm_resource_group" "test" {
  name     = "acctestRG"
  location = "West US"
}

resource "azurerm_virtual_network" "test" {
  name                = "acctvn"
  address_space       = ["10.0.0.0/16"]
  location            = "West US"
  resource_group_name = "${azurerm_resource_group.test.name}"
}

resource "azurerm_subnet" "test" {
  name                 = "acctsub"
  resource_group_name  = "${azurerm_resource_group.test.name}"
  virtual_network_name = "${azurerm_virtual_network.test.name}"
  address_prefix       = "10.0.2.0/24"
}

resource "azurerm_storage_account" "test" {
  name                     = "accsa"
  resource_group_name      = "${azurerm_resource_group.test.name}"
  location                 = "westus"
  account_tier             = "Standard"
  account_replication_type = "LRS"

  tags {
    environment = "staging"
  }
}

resource "azurerm_storage_container" "test" {
  name                  = "vhds"
  resource_group_name   = "${azurerm_resource_group.test.name}"
  storage_account_name  = "${azurerm_storage_account.test.name}"
  container_access_type = "private"
}

resource "azurerm_virtual_machine_scale_set" "test" {
  name                = "mytestscaleset-1"
  location            = "West US"
  resource_group_name = "${azurerm_resource_group.test.name}"
  upgrade_policy_mode = "Manual"

  sku {
    name     = "Standard_A0"
    tier     = "Standard"
    capacity = 2
  }

  os_profile {
    computer_name_prefix = "testvm"
    admin_username       = "myadmin"
    admin_password       = "Passwword1234"
  }

  os_profile_linux_config {
    disable_password_authentication = true

    ssh_keys {
      path     = "/home/myadmin/.ssh/authorized_keys"
      key_data = "${file("~/.ssh/demo_key.pub")}"
    }
  }

  network_profile {
    name    = "TestNetworkProfile"
    primary = true

    ip_configuration {
      name      = "TestIPConfiguration"
      subnet_id = "${azurerm_subnet.test.id}"
    }
  }

  storage_profile_os_disk {
    name           = "osDiskProfile"
    caching        = "ReadWrite"
    create_option  = "FromImage"
    vhd_containers = ["${azurerm_storage_account.test.primary_blob_endpoint}${azurerm_storage_container.test.name}"]
  }

  storage_profile_image_reference {
    publisher = "Canonical"
    offer     = "UbuntuServer"
    sku       = "16.04-LTS"
    version   = "latest"
  }
}
</pre>
<h2 id="argument-reference">  Argument Reference </h2> <p>The following arguments are supported:</p> <ul> <li>
<a href="#name"><code>name</code></a> - (Required) Specifies the name of the virtual machine scale set resource. Changing this forces a new resource to be created. </li> <li>
<a href="#resource_group_name"><code>resource_group_name</code></a> - (Required) The name of the resource group in which to create the virtual machine scale set. Changing this forces a new resource to be created. </li> <li>
<a href="#location"><code>location</code></a> - (Required) Specifies the supported Azure location where the resource exists. Changing this forces a new resource to be created. </li> <li>
<a href="#sku"><code>sku</code></a> - (Required) A sku block as documented below. </li> <li>
<a href="#upgrade_policy_mode"><code>upgrade_policy_mode</code></a> - (Required) Specifies the mode of an upgrade to virtual machines in the scale set. Possible values, <code>Manual</code> or <code>Automatic</code>. </li> <li>
<a href="#overprovision"><code>overprovision</code></a> - (Optional) Specifies whether the virtual machine scale set should be overprovisioned. Defaults to <code>true</code>. </li> <li>
<a href="#single_placement_group"><code>single_placement_group</code></a> - (Optional) Specifies whether the scale set is limited to a single placement group with a maximum size of 100 virtual machines. If set to false, managed disks must be used. Defaults to <code>true</code>. Changing this forces a new resource to be created. See <a href="http://docs.microsoft.com/en-us/azure/virtual-machine-scale-sets/virtual-machine-scale-sets-placement-groups">documentation</a> for more information. </li> <li>
<a href="#license_type"><code>license_type</code></a> - (Optional, when a Windows machine) Specifies the Windows OS license type. If supplied, the only allowed values are <code>Windows_Client</code> and <code>Windows_Server</code>. </li> <li>
<a href="#os_profile"><code>os_profile</code></a> - (Required) A Virtual Machine OS Profile block as documented below. </li> <li>
<a href="#os_profile_secrets"><code>os_profile_secrets</code></a> - (Optional) A collection of Secret blocks as documented below. </li> <li>
<a href="#os_profile_windows_config"><code>os_profile_windows_config</code></a> - (Required, when a windows machine) A Windows config block as documented below. </li> <li>
<a href="#os_profile_linux_config"><code>os_profile_linux_config</code></a> - (Required, when a linux machine) A Linux config block as documented below. </li> <li>
<a href="#network_profile"><code>network_profile</code></a> - (Required) A collection of network profile block as documented below. </li> <li>
<a href="#storage_profile_os_disk"><code>storage_profile_os_disk</code></a> - (Required) A storage profile os disk block as documented below </li> <li>
<a href="#storage_profile_data_disk"><code>storage_profile_data_disk</code></a> - (Optional) A storage profile data disk block as documented below </li> <li>
<a href="#storage_profile_image_reference"><code>storage_profile_image_reference</code></a> - (Optional) A storage profile image reference block as documented below. </li> <li>
<a href="#extension"><code>extension</code></a> - (Optional) Can be specified multiple times to add extension profiles to the scale set. Each <code>extension</code> block supports the fields documented below. </li> <li>
<a href="#boot_diagnostics"><code>boot_diagnostics</code></a> - (Optional) A boot diagnostics profile block as referenced below. </li> <li>
<a href="#plan"><code>plan</code></a> - (Optional) A plan block as documented below. </li> <li>
<a href="#priority"><code>priority</code></a> - (Optional) Specifies the priority for the virtual machines in the scale set, defaults to <code>Regular</code>. Possible values are <code>Low</code> and <code>Regular</code>. </li> <li>
<a href="#tags"><code>tags</code></a> - (Optional) A mapping of tags to assign to the resource. </li> <li>
<a href="#zones"><code>zones</code></a> - (Optional) A collection of availability zones to spread the Virtual Machines over. </li> </ul> <blockquote class="alert alert-info" role="alert"> <p><strong>Please Note</strong>: Availability Zones are <a href="https://docs.microsoft.com/en-us/azure/availability-zones/az-overview">in Preview and only supported in several regions at this time</a> - as such you must be opted into the Preview to use this functionality. You can <a href="http://aka.ms/azenroll">opt into the Availability Zones Preview in the Azure Portal</a>.</p> </blockquote> <p><code>sku</code> supports the following:</p> <ul> <li>
<a href="#name-1"><code>name</code></a> - (Required) Specifies the size of virtual machines in a scale set. </li> <li>
<a href="#tier"><code>tier</code></a> - (Optional) Specifies the tier of virtual machines in a scale set. Possible values, <code>standard</code> or <code>basic</code>. </li> <li>
<a href="#capacity"><code>capacity</code></a> - (Required) Specifies the number of virtual machines in the scale set. </li> </ul> <p><code>identity</code> supports the following:</p> <ul> <li>
<a href="#type"><code>type</code></a> - (Required) Specifies the identity type to be assigned to the scale set. The only allowable value is <code>SystemAssigned</code>. To enable Managed Service Identity (MSI) on all machines in the scale set, an extension with the type "ManagedIdentityExtensionForWindows" or "ManagedIdentityExtensionForLinux" must also be added. The scale set's Service Principal ID (SPN) can be retrieved after the scale set has been created. </li> </ul> <pre data-language="ruby">resource "azurerm_virtual_machine_scale_set" "test" {
  name                = "vm-scaleset"
  resource_group_name = "${azurerm_resource_group.test.name}"
  location            = "${azurerm_resource_group.test.location}"

  sku {
    name     = "${var.vm_sku}"
    tier     = "Standard"
    capacity = "${var.instance_count}"
  }

  identity {
    type = "systemAssigned"
  }

  extension {
    name                 = "MSILinuxExtension"
    publisher            = "Microsoft.ManagedIdentity"
    type                 = "ManagedIdentityExtensionForLinux"
    type_handler_version = "1.0"
    settings             = "{\"port\": 50342}"
  }

  output "principal_id" {
    value = "${lookup(azurerm_virtual_machine.test.identity[0], "principal_id")}"
  }
</pre>
<p><code>os_profile</code> supports the following:</p> <ul> <li>
<a href="#computer_name_prefix"><code>computer_name_prefix</code></a> - (Required) Specifies the computer name prefix for all of the virtual machines in the scale set. Computer name prefixes must be 1 to 9 characters long for windows images and 1 - 58 for linux. Changing this forces a new resource to be created. </li> <li>
<a href="#admin_username"><code>admin_username</code></a> - (Required) Specifies the administrator account name to use for all the instances of virtual machines in the scale set. </li> <li>
<a href="#admin_password"><code>admin_password</code></a> - (Required) Specifies the administrator password to use for all the instances of virtual machines in a scale set. </li> <li>
<a href="#custom_data"><code>custom_data</code></a> - (Optional) Specifies custom data to supply to the machine. On linux-based systems, this can be used as a cloud-init script. On other systems, this will be copied as a file on disk. Internally, Terraform will base64 encode this value before sending it to the API. The maximum length of the binary array is 65535 bytes. </li> </ul> <p><code>os_profile_secrets</code> supports the following:</p> <ul> <li>
<a href="#source_vault_id"><code>source_vault_id</code></a> - (Required) Specifies the key vault to use. </li> <li>
<a href="#vault_certificates"><code>vault_certificates</code></a> - (Required, on windows machines) A collection of Vault Certificates as documented below </li> </ul> <p><code>vault_certificates</code> support the following:</p> <ul> <li>
<a href="#certificate_url"><code>certificate_url</code></a> - (Required) It is the Base64 encoding of a JSON Object that which is encoded in UTF-8 of which the contents need to be <code>data</code>, <code>dataType</code> and <code>password</code>. </li> <li>
<a href="#certificate_store"><code>certificate_store</code></a> - (Required, on windows machines) Specifies the certificate store on the Virtual Machine where the certificate should be added to. </li> </ul> <p><code>os_profile_windows_config</code> supports the following:</p> <ul> <li>
<a href="#provision_vm_agent"><code>provision_vm_agent</code></a> - (Optional) Indicates whether virtual machine agent should be provisioned on the virtual machines in the scale set. </li> <li>
<a href="#enable_automatic_upgrades"><code>enable_automatic_upgrades</code></a> - (Optional) Indicates whether virtual machines in the scale set are enabled for automatic updates. </li> <li>
<a href="#winrm"><code>winrm</code></a> - (Optional) A collection of WinRM configuration blocks as documented below. </li> <li>
<a href="#additional_unattend_config"><code>additional_unattend_config</code></a> - (Optional) An Additional Unattended Config block as documented below. </li> </ul> <p><code>winrm</code> supports the following:</p> <ul> <li>
<a href="#protocol"><code>protocol</code></a> - (Required) Specifies the protocol of listener </li> <li>
<a href="#certificate_url-1"><code>certificate_url</code></a> - (Optional) Specifies URL of the certificate with which new Virtual Machines is provisioned. </li> </ul> <p><code>additional_unattend_config</code> supports the following:</p> <ul> <li>
<a href="#pass"><code>pass</code></a> - (Required) Specifies the name of the pass that the content applies to. The only allowable value is <code>oobeSystem</code>. </li> <li>
<a href="#component"><code>component</code></a> - (Required) Specifies the name of the component to configure with the added content. The only allowable value is <code>Microsoft-Windows-Shell-Setup</code>. </li> <li>
<a href="#setting_name"><code>setting_name</code></a> - (Required) Specifies the name of the setting to which the content applies. Possible values are: <code>FirstLogonCommands</code> and <code>AutoLogon</code>. </li> <li>
<a href="#content"><code>content</code></a> - (Optional) Specifies the base-64 encoded XML formatted content that is added to the unattend.xml file for the specified path and component. </li> </ul> <p><code>os_profile_linux_config</code> supports the following:</p> <ul> <li>
<a href="#disable_password_authentication"><code>disable_password_authentication</code></a> - (Required) Specifies whether password authentication should be disabled. Changing this forces a new resource to be created. </li> <li>
<a href="#ssh_keys"><code>ssh_keys</code></a> - (Optional) Specifies a collection of <code>path</code> and <code>key_data</code> to be placed on the virtual machine. </li> </ul> <blockquote class="alert alert-warning" role="alert"> <p><em><strong>Note:</strong> Please note that the only allowed <code>path</code> is <code>/home/&lt;username&gt;/.ssh/authorized_keys</code> due to a limitation of Azure</em></p> </blockquote> <p><code>network_profile</code> supports the following:</p> <ul> <li>
<a href="#name-2"><code>name</code></a> - (Required) Specifies the name of the network interface configuration. </li> <li>
<a href="#primary"><code>primary</code></a> - (Required) Indicates whether network interfaces created from the network interface configuration will be the primary NIC of the VM. </li> <li>
<a href="#ip_configuration"><code>ip_configuration</code></a> - (Required) An ip_configuration block as documented below. </li> <li>
<a href="#accelerated_networking"><code>accelerated_networking</code></a> - (Optional) Specifies whether to enable accelerated networking or not. Defaults to <code>false</code>. </li> <li>
<a href="#dns_settings"><code>dns_settings</code></a> - (Optional) An dns_settings block as documented below. </li> <li>
<a href="#network_security_group_id"><code>network_security_group_id</code></a> - (Optional) Specifies the identifier for the network security group. </li> </ul> <p><code>dns_settings</code> supports the following:</p> <ul> <li>
<a href="#dns_servers"><code>dns_servers</code></a> - (Required) Specifies an array of dns servers. </li> </ul> <p><code>ip_configuration</code> supports the following:</p> <ul> <li>
<a href="#name-3"><code>name</code></a> - (Required) Specifies name of the IP configuration. </li> <li>
<a href="#subnet_id"><code>subnet_id</code></a> - (Required) Specifies the identifier of the subnet. </li> <li>
<a href="#application_gateway_backend_address_pool_ids"><code>application_gateway_backend_address_pool_ids</code></a> - (Optional) Specifies an array of references to backend address pools of application gateways. A scale set can reference backend address pools of one application gateway. Multiple scale sets cannot use the same application gateway. </li> <li>
<a href="#load_balancer_backend_address_pool_ids"><code>load_balancer_backend_address_pool_ids</code></a> - (Optional) Specifies an array of references to backend address pools of load balancers. A scale set can reference backend address pools of one public and one internal load balancer. Multiple scale sets cannot use the same load balancer. </li> <li>
<a href="#load_balancer_inbound_nat_rules_ids"><code>load_balancer_inbound_nat_rules_ids</code></a> - (Optional) Specifies an array of references to inbound NAT rules for load balancers. </li> <li>
<a href="#primary-1"><code>primary</code></a> - (Optional) Specifies if this ip_configuration is the primary one. </li> <li>
<a href="#public_ip_address_configuration"><code>public_ip_address_configuration</code></a> - (Optional) describes a virtual machines scale set IP Configuration's PublicIPAddress configuration. The public_ip_address_configuration is documented below. </li> </ul> <p><code>public_ip_address_configuration</code> supports the following:</p> <ul> <li>
<a href="#name-4"><code>name</code></a> - (Required) The name of the public ip address configuration </li> <li>
<a href="#idle_timeout"><code>idle_timeout</code></a> - (Required) The idle timeout in minutes. This value must be between 4 and 32. </li> <li>
<a href="#domain_name_label"><code>domain_name_label</code></a> - (Required) The domain name label for the dns settings. </li> </ul> <p><code>storage_profile_os_disk</code> supports the following:</p> <ul> <li>
<a href="#name-5"><code>name</code></a> - (Optional) Specifies the disk name. Must be specified when using unmanaged disk ('managed_disk_type' property not set). </li> <li>
<a href="#vhd_containers"><code>vhd_containers</code></a> - (Optional) Specifies the vhd uri. Cannot be used when <code>image</code> or <code>managed_disk_type</code> is specified. </li> <li>
<a href="#managed_disk_type"><code>managed_disk_type</code></a> - (Optional) Specifies the type of managed disk to create. Value you must be either <code>Standard_LRS</code> or <code>Premium_LRS</code>. Cannot be used when <code>vhd_containers</code> or <code>image</code> is specified. </li> <li>
<a href="#create_option"><code>create_option</code></a> - (Required) Specifies how the virtual machine should be created. The only possible option is <code>FromImage</code>. </li> <li>
<a href="#caching"><code>caching</code></a> - (Optional) Specifies the caching requirements. Possible values include: <code>None</code> (default), <code>ReadOnly</code>, <code>ReadWrite</code>. </li> <li>
<a href="#image"><code>image</code></a> - (Optional) Specifies the blob uri for user image. A virtual machine scale set creates an os disk in the same container as the user image. Updating the osDisk image causes the existing disk to be deleted and a new one created with the new image. If the VM scale set is in Manual upgrade mode then the virtual machines are not updated until they have manualUpgrade applied to them. When setting this field <code>os_type</code> needs to be specified. Cannot be used when <code>vhd_containers</code>, <code>managed_disk_type</code> or <code>storage_profile_image_reference</code> are specified. </li> <li>
<a href="#os_type"><code>os_type</code></a> - (Optional) Specifies the operating system Type, valid values are windows, linux. </li> </ul> <p><code>storage_profile_data_disk</code> supports the following:</p> <ul> <li>
<a href="#lun"><code>lun</code></a> - (Required) Specifies the Logical Unit Number of the disk in each virtual machine in the scale set. </li> <li>
<a href="#create_option-1"><code>create_option</code></a> - (Optional) Specifies how the data disk should be created. The only possible options are <code>FromImage</code> and <code>Empty</code>. </li> <li>
<a href="#caching-1"><code>caching</code></a> - (Optional) Specifies the caching requirements. Possible values include: <code>None</code> (default), <code>ReadOnly</code>, <code>ReadWrite</code>. </li> <li>
<a href="#disk_size_gb"><code>disk_size_gb</code></a> - (Optional) Specifies the size of the disk in GB. This element is required when creating an empty disk. </li> <li>
<a href="#managed_disk_type-1"><code>managed_disk_type</code></a> - (Optional) Specifies the type of managed disk to create. Value must be either <code>Standard_LRS</code> or <code>Premium_LRS</code>. </li> </ul> <p><code>storage_profile_image_reference</code> supports the following:</p> <ul> <li>
<a href="#id"><code>id</code></a> - (Optional) Specifies the ID of the (custom) image to use to create the virtual machine scale set, as in the <a href="#example-of-storage_profile_image_reference-with-id">example below</a>. </li> <li>
<a href="#publisher"><code>publisher</code></a> - (Optional) Specifies the publisher of the image used to create the virtual machines. </li> <li>
<a href="#offer"><code>offer</code></a> - (Optional) Specifies the offer of the image used to create the virtual machines. </li> <li>
<a href="#sku-1"><code>sku</code></a> - (Optional) Specifies the SKU of the image used to create the virtual machines. </li> <li>
<a href="#version"><code>version</code></a> - (Optional) Specifies the version of the image used to create the virtual machines. </li> </ul> <p><code>boot_diagnostics</code> supports the following:</p> <ul> <li>
<a href="#enabled"><code>enabled</code></a>: (Required) Whether to enable boot diagnostics for the virtual machine. </li> <li>
<a href="#storage_uri"><code>storage_uri</code></a>: (Required) Blob endpoint for the storage account to hold the virtual machine's diagnostic files. This must be the root of a storage account, and not a storage container. </li> </ul> <p><code>extension</code> supports the following:</p> <ul> <li>
<a href="#name-6"><code>name</code></a> - (Required) Specifies the name of the extension. </li> <li>
<a href="#publisher-1"><code>publisher</code></a> - (Required) The publisher of the extension, available publishers can be found by using the Azure CLI. </li> <li>
<a href="#type-1"><code>type</code></a> - (Required) The type of extension, available types for a publisher can be found using the Azure CLI. </li> <li>
<a href="#type_handler_version"><code>type_handler_version</code></a> - (Required) Specifies the version of the extension to use, available versions can be found using the Azure CLI. </li> <li>
<a href="#auto_upgrade_minor_version"><code>auto_upgrade_minor_version</code></a> - (Optional) Specifies whether or not to use the latest minor version available. </li> <li>
<a href="#settings"><code>settings</code></a> - (Required) The settings passed to the extension, these are specified as a JSON object in a string. </li> <li>
<a href="#protected_settings"><code>protected_settings</code></a> - (Optional) The protected_settings passed to the extension, like settings, these are specified as a JSON object in a string. </li> </ul> <p><code>plan</code> supports the following:</p> <ul> <li>
<a href="#name-7"><code>name</code></a> - (Required) Specifies the name of the image from the marketplace. </li> <li>
<a href="#publisher-2"><code>publisher</code></a> - (Required) Specifies the publisher of the image. </li> <li>
<a href="#product"><code>product</code></a> - (Required) Specifies the product of the image from the marketplace. </li> </ul> <h2 id="example-of-storage_profile_image_reference-with-id">  Example of storage_profile_image_reference with id </h2> <pre data-language="ruby">
resource "azurerm_image" "test" {
    name = "test"
  ...
}

resource "azurerm_virtual_machine_scale_set" "test" {
    name = "test"
  ...

    storage_profile_image_reference {
        id = "${azurerm_image.test.id}"
    }

...
</pre>
<h2 id="attributes-reference">  Attributes Reference </h2> <p>The following attributes are exported:</p> <ul> <li>
<a href="#id-1"><code>id</code></a> - The virtual machine scale set ID. </li> </ul> <h2 id="import">  Import </h2> <p>Virtual Machine Scale Sets can be imported using the <code>resource id</code>, e.g.</p> <pre data-language="shell">terraform import azurerm_virtual_machine_scale_set.scaleset1 /subscriptions/00000000-0000-0000-0000-000000000000/resourceGroups/mygroup1/providers/Microsoft.Compute/virtualMachineScaleSets/scaleset1
</pre><div class="_attribution">
  <p class="_attribution-p">
    &copy; 2018 HashiCorp</br>Licensed under the MPL 2.0 License.<br>
    <a href="https://www.terraform.io/docs/providers/azurerm/r/virtual_machine_scale_set.html" class="_attribution-link">https://www.terraform.io/docs/providers/azurerm/r/virtual_machine_scale_set.html</a>
  </p>
</div>
