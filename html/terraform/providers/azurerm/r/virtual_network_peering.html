<h1 id="azurerm_virtual_network_peering">  azurerm_virtual_network_peering </h1> <p>Manages a virtual network peering which allows resources to access other resources in the linked virtual network.</p> <h2 id="example-usage">  Example Usage </h2> <pre data-language="ruby">resource "azurerm_resource_group" "test" {
  name     = "peeredvnets-rg"
  location = "West US"
}

resource "azurerm_virtual_network" "test1" {
  name                = "peternetwork1"
  resource_group_name = "${azurerm_resource_group.test.name}"
  address_space       = ["10.0.1.0/24"]
  location            = "West US"
}

resource "azurerm_virtual_network" "test2" {
  name                = "peternetwork2"
  resource_group_name = "${azurerm_resource_group.test.name}"
  address_space       = ["10.0.2.0/24"]
  location            = "West US"
}

resource "azurerm_virtual_network_peering" "test1" {
  name                      = "peer1to2"
  resource_group_name       = "${azurerm_resource_group.test.name}"
  virtual_network_name      = "${azurerm_virtual_network.test1.name}"
  remote_virtual_network_id = "${azurerm_virtual_network.test2.id}"
}

resource "azurerm_virtual_network_peering" "test2" {
  name                      = "peer2to1"
  resource_group_name       = "${azurerm_resource_group.test.name}"
  virtual_network_name      = "${azurerm_virtual_network.test2.name}"
  remote_virtual_network_id = "${azurerm_virtual_network.test1.id}"
}
</pre>
<h2 id="example-usage-global-virtual-network-peering-">  Example Usage (Global virtual network peering) </h2> <pre data-language="ruby">variable "location" {
  default = [
    "uksouth",
    "southeastasia",
  ]
}

variable "vnet_address_space" {
  default = [
    "10.0.0.0/16",
    "10.1.0.0/16",
  ]
}

resource "azurerm_resource_group" "vnet" {
  count    = "${length(var.location)}"
  name     = "rg-global-vnet-peering-${count.index}"
  location = "${element(var.location, count.index)}"
}

resource "azurerm_virtual_network" "vnet" {
  count               = "${length(var.location)}"
  name                = "vnet-${count.index}"
  resource_group_name = "${element(azurerm_resource_group.vnet.*.name, count.index)}"
  address_space       = ["${element(var.vnet_address_space, count.index)}"]
  location            = "${element(azurerm_resource_group.vnet.*.location, count.index)}"
}

resource "azurerm_subnet" "nva" {
  count                = "${length(var.location)}"
  name                 = "nva"
  resource_group_name  = "${element(azurerm_resource_group.vnet.*.name, count.index)}"
  virtual_network_name = "${element(azurerm_virtual_network.vnet.*.name, count.index)}"
  address_prefix       = "${cidrsubnet("${element(azurerm_virtual_network.vnet.*.address_space[count.index], count.index)}", 13, 0)}" # /29
}

# enable global peering between the two virtual network 
resource "azurerm_virtual_network_peering" "peering" {
  count                        = "${length(var.location)}"
  name                         = "peering-to-${element(azurerm_virtual_network.vnet.*.name, 1 - count.index)}"
  resource_group_name          = "${element(azurerm_resource_group.vnet.*.name, count.index)}"
  virtual_network_name         = "${element(azurerm_virtual_network.vnet.*.name, count.index)}"
  remote_virtual_network_id    = "${element(azurerm_virtual_network.vnet.*.id, 1 - count.index)}"
  allow_virtual_network_access = true
  allow_forwarded_traffic      = true
  # `allow_gateway_transit` must be set to false for vnet Global Peering
  allow_gateway_transit        = false
}
</pre>
<h2 id="argument-reference">  Argument Reference </h2> <p>The following arguments are supported:</p> <ul> <li>
<p><a href="#name"><code>name</code></a> - (Required) The name of the virtual network peering. Changing this forces a new resource to be created.</p> </li> <li>
<p><a href="#virtual_network_name"><code>virtual_network_name</code></a> - (Required) The name of the virtual network. Changing this forces a new resource to be created.</p> </li> <li>
<p><a href="#remote_virtual_network_id"><code>remote_virtual_network_id</code></a> - (Required) The full Azure resource ID of the remote virtual network. Changing this forces a new resource to be created.</p> </li> <li>
<p><a href="#resource_group_name"><code>resource_group_name</code></a> - (Required) The name of the resource group in which to create the virtual network. Changing this forces a new resource to be created.</p> </li> <li>
<p><a href="#allow_virtual_network_access"><code>allow_virtual_network_access</code></a> - (Optional) Controls if the VMs in the remote virtual network can access VMs in the local virtual network. Defaults to false.</p> </li> <li>
<p><a href="#allow_forwarded_traffic"><code>allow_forwarded_traffic</code></a> - (Optional) Controls if forwarded traffic from VMs in the remote virtual network is allowed. Defaults to false.</p> </li> <li>
<p><a href="#allow_gateway_transit"><code>allow_gateway_transit</code></a> - (Optional) Controls gatewayLinks can be used in the remote virtual networkâ€™s link to the local virtual network.</p> </li> <li>
<p><a href="#use_remote_gateways"><code>use_remote_gateways</code></a> - (Optional) Controls if remote gateways can be used on the local virtual network. If the flag is set to <code>true</code>, and <code>allow_gateway_transit</code> on the remote peering is also <code>true</code>, virtual network will use gateways of remote virtual network for transit. Only one peering can have this flag set to <code>true</code>. This flag cannot be set if virtual network already has a gateway. Defaults to <code>false</code>.</p> </li> </ul> <blockquote class="alert alert-info" role="alert"> <p><strong>NOTE:</strong> <code>use_remote_gateways</code> must be set to <code>false</code> if using Global Virtual Network Peerings.</p> </blockquote> <h2 id="attributes-reference">  Attributes Reference </h2> <p>The following attributes are exported:</p> <ul> <li>
<a href="#id"><code>id</code></a> - The Virtual Network Peering resource ID. </li> </ul> <h2 id="note">  Note </h2> <p>Virtual Network peerings cannot be created, updated or deleted concurrently.</p> <h2 id="import">  Import </h2> <p>Virtual Network Peerings can be imported using the <code>resource id</code>, e.g.</p> <pre data-language="shell">terraform import azurerm_virtual_network_peering.testPeering /subscriptions/00000000-0000-0000-0000-000000000000/resourceGroups/mygroup1/providers/Microsoft.Network/virtualNetworks/myvnet1/virtualNetworkPeerings/myvnet1peering
</pre><div class="_attribution">
  <p class="_attribution-p">
    &copy; 2018 HashiCorp</br>Licensed under the MPL 2.0 License.<br>
    <a href="https://www.terraform.io/docs/providers/azurerm/r/virtual_network_peering.html" class="_attribution-link">https://www.terraform.io/docs/providers/azurerm/r/virtual_network_peering.html</a>
  </p>
</div>
