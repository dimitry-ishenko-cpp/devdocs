<h1 id="azurerm_virtual_network_gateway_connection">  azurerm_virtual_network_gateway_connection </h1> <p>Manages a connection in an existing Virtual Network Gateway.</p> <h2 id="example-usage">  Example Usage </h2> <h3 id="site-to-site-connection">  Site-to-Site connection </h3> <p>The following example shows a connection between an Azure virtual network and an on-premises VPN device and network.</p> <pre data-language="ruby">resource "azurerm_resource_group" "test" {
  name = "test"
  location = "West US"
}

resource "azurerm_virtual_network" "test" {
  name = "test"
  location = "${azurerm_resource_group.test.location}"
  resource_group_name = "${azurerm_resource_group.test.name}"
  address_space = ["10.0.0.0/16"]
}

resource "azurerm_subnet" "test" {
  name = "GatewaySubnet"
  resource_group_name = "${azurerm_resource_group.test.name}"
  virtual_network_name = "${azurerm_virtual_network.test.name}"
  address_prefix = "10.0.1.0/24"
}

resource "azurerm_local_network_gateway" "onpremise" {
  name = "onpremise"
  location = "${azurerm_resource_group.test.location}"
  resource_group_name = "${azurerm_resource_group.test.name}"
  gateway_address = "168.62.225.23"
  address_space = ["10.1.1.0/24"]
}

resource "azurerm_public_ip" "test" {
  name = "test"
  location = "${azurerm_resource_group.test.location}"
  resource_group_name = "${azurerm_resource_group.test.name}"
  public_ip_address_allocation = "Dynamic"
}

resource "azurerm_virtual_network_gateway" "test" {
  name = "test"
  location = "${azurerm_resource_group.test.location}"
  resource_group_name = "${azurerm_resource_group.test.name}"

  type = "Vpn"
  vpn_type = "RouteBased"

  active_active = false
  enable_bgp = false
    sku = "Basic"

  ip_configuration {
    public_ip_address_id = "${azurerm_public_ip.test.id}"
    private_ip_address_allocation = "Dynamic"
    subnet_id = "${azurerm_subnet.test.id}"
  }
}

resource "azurerm_virtual_network_gateway_connection" "onpremise" {
  name = "onpremise"
  location = "${azurerm_resource_group.test.location}"
  resource_group_name = "${azurerm_resource_group.test.name}"

  type = "IPsec"
  virtual_network_gateway_id = "${azurerm_virtual_network_gateway.test.id}"
  local_network_gateway_id = "${azurerm_local_network_gateway.onpremise.id}"

  shared_key = "4-v3ry-53cr37-1p53c-5h4r3d-k3y"
}
</pre>
<h3 id="vnet-to-vnet-connection">  VNet-to-VNet connection </h3> <p>The following example shows a connection between two Azure virtual network in different locations/regions.</p> <pre data-language="ruby">resource "azurerm_resource_group" "us" {
    name = "us"
    location = "East US"
}

resource "azurerm_virtual_network" "us" {
  name = "us"
  location = "${azurerm_resource_group.us.location}"
  resource_group_name = "${azurerm_resource_group.us.name}"
  address_space = ["10.0.0.0/16"]
}

resource "azurerm_subnet" "us_gateway" {
  name = "GatewaySubnet"
  resource_group_name = "${azurerm_resource_group.us.name}"
  virtual_network_name = "${azurerm_virtual_network.us.name}"
  address_prefix = "10.0.1.0/24"
}

resource "azurerm_public_ip" "us" {
  name = "us"
  location = "${azurerm_resource_group.us.location}"
  resource_group_name = "${azurerm_resource_group.us.name}"
  public_ip_address_allocation = "Dynamic"
}

resource "azurerm_virtual_network_gateway" "us" {
  name = "us-gateway"
  location = "${azurerm_resource_group.us.location}"
  resource_group_name = "${azurerm_resource_group.us.name}"

  type = "Vpn"
  vpn_type = "RouteBased"
    sku = "Basic"

  ip_configuration {
    public_ip_address_id = "${azurerm_public_ip.us.id}"
    private_ip_address_allocation = "Dynamic"
    subnet_id = "${azurerm_subnet.us_gateway.id}"
  }
}

resource "azurerm_resource_group" "europe" {
  name = "europe"
  location = "West Europe"
}

resource "azurerm_virtual_network" "europe" {
  name = "europe"
  location = "${azurerm_resource_group.europe.location}"
  resource_group_name = "${azurerm_resource_group.europe.name}"
  address_space = ["10.1.0.0/16"]
}

resource "azurerm_subnet" "europe_gateway" {
  name = "GatewaySubnet"
  resource_group_name = "${azurerm_resource_group.europe.name}"
  virtual_network_name = "${azurerm_virtual_network.europe.name}"
  address_prefix = "10.1.1.0/24"
}

resource "azurerm_public_ip" "europe" {
  name = "europe"
  location = "${azurerm_resource_group.europe.location}"
  resource_group_name = "${azurerm_resource_group.europe.name}"
  public_ip_address_allocation = "Dynamic"
}

resource "azurerm_virtual_network_gateway" "europe" {
  name = "europe-gateway"
  location = "${azurerm_resource_group.europe.location}"
  resource_group_name = "${azurerm_resource_group.europe.name}"

  type = "Vpn"
  vpn_type = "RouteBased"
    sku = "Basic"

  ip_configuration {
    public_ip_address_id = "${azurerm_public_ip.europe.id}"
    private_ip_address_allocation = "Dynamic"
    subnet_id = "${azurerm_subnet.europe_gateway.id}"
  }
}

resource "azurerm_virtual_network_gateway_connection" "us_to_europe" {
  name = "us-to-europe"
  location = "${azurerm_resource_group.us.location}"
  resource_group_name = "${azurerm_resource_group.us.name}"

  type = "Vnet2Vnet"
  virtual_network_gateway_id = "${azurerm_virtual_network_gateway.us.id}"
  peer_virtual_network_gateway_id = "${azurerm_virtual_network_gateway.europe.id}"

  shared_key = "4-v3ry-53cr37-1p53c-5h4r3d-k3y"
}

resource "azurerm_virtual_network_gateway_connection" "europe_to_us" {
  name = "europe-to-us"
  location = "${azurerm_resource_group.europe.location}"
  resource_group_name = "${azurerm_resource_group.europe.name}"

  type = "Vnet2Vnet"
  virtual_network_gateway_id = "${azurerm_virtual_network_gateway.europe.id}"
  peer_virtual_network_gateway_id = "${azurerm_virtual_network_gateway.us.id}"

  shared_key = "4-v3ry-53cr37-1p53c-5h4r3d-k3y"
}
</pre>
<h2 id="argument-reference">  Argument Reference </h2> <p>The following arguments are supported:</p> <ul> <li>
<p><a href="#name"><code>name</code></a> - (Required) The name of the connection. Changing the name forces a new resource to be created.</p> </li> <li>
<p><a href="#resource_group_name"><code>resource_group_name</code></a> - (Required) The name of the resource group in which to create the connection Changing the name forces a new resource to be created.</p> </li> <li>
<p><a href="#location"><code>location</code></a> - (Required) The location/region where the connection is located. Changing this forces a new resource to be created.</p> </li> <li>
<p><a href="#type"><code>type</code></a> - (Required) The type of connection. Valid options are <code>IPsec</code> (Site-to-Site), <code>ExpressRoute</code> (ExpressRoute), and <code>Vnet2Vnet</code> (VNet-to-VNet). Each connection type requires different mandatory arguments (refer to the examples above). Changing the connection type will force a new connection to be created.</p> </li> <li>
<p><a href="#virtual_network_gateway_id"><code>virtual_network_gateway_id</code></a> - (Required) The ID of the Virtual Network Gateway in which the connection will be created. Changing the gateway forces a new resource to be created.</p> </li> <li>
<p><a href="#authorization_key"><code>authorization_key</code></a> - (Optional) The authorization key associated with the Express Route Circuit. This field is required only if the type is an ExpressRoute connection.</p> </li> <li>
<p><a href="#express_route_circuit_id"><code>express_route_circuit_id</code></a> - (Optional) The ID of the Express Route Circuit when creating an ExpressRoute connection (i.e. when <code>type</code> is <code>ExpressRoute</code>). The Express Route Circuit can be in the same or in a different subscription.</p> </li> <li>
<p><a href="#peer_virtual_network_gateway_id"><code>peer_virtual_network_gateway_id</code></a> - (Optional) The ID of the peer virtual network gateway when creating a VNet-to-VNet connection (i.e. when <code>type</code> is <code>Vnet2Vnet</code>). The peer Virtual Network Gateway can be in the same or in a different subscription.</p> </li> <li>
<p><a href="#local_network_gateway_id"><code>local_network_gateway_id</code></a> - (Optional) The ID of the local network gateway when creating Site-to-Site connection (i.e. when <code>type</code> is <code>IPsec</code>).</p> </li> <li>
<p><a href="#routing_weight"><code>routing_weight</code></a> - (Optional) The routing weight. Defaults to <code>10</code>.</p> </li> <li>
<p><a href="#shared_key"><code>shared_key</code></a> - (Optional) The shared IPSec key. A key must be provided if a Site-to-Site or VNet-to-VNet connection is created whereas ExpressRoute connections do not need a shared key.</p> </li> <li>
<p><a href="#enable_bgp"><code>enable_bgp</code></a> - (Optional) If <code>true</code>, BGP (Border Gateway Protocol) is enabled for this connection. Defaults to <code>false</code>.</p> </li> <li>
<p><a href="#use_policy_based_traffic_selectors"><code>use_policy_based_traffic_selectors</code></a> - (Optional) If <code>true</code>, policy-based traffic selectors are enabled for this connection. Enabling policy-based traffic selectors requires an <code>ipsec_policy</code> block. Defaults to <code>false</code>.</p> </li> <li>
<p><a href="#ipsec_policy"><code>ipsec_policy</code></a> (Optional) A <code>ipsec_policy</code> block which is documented below. Only a single policy can be defined for a connection. For details on custom policies refer to <a href="https://docs.microsoft.com/en-us/azure/vpn-gateway/vpn-gateway-ipsecikepolicy-rm-powershell">the relevant section in the Azure documentation</a>.</p> </li> <li>
<p><a href="#tags"><code>tags</code></a> - (Optional) A mapping of tags to assign to the resource.</p> </li> </ul> <p>The <code>ipsec_policy</code> block supports:</p> <ul> <li>
<p><a href="#dh_group"><code>dh_group</code></a> - (Required) The DH group used in IKE phase 1 for initial SA. Valid options are <code>DHGroup1</code>, <code>DHGroup14</code>, <code>DHGroup2</code>, <code>DHGroup2048</code>, <code>DHGroup24</code>, <code>ECP256</code>, <code>ECP384</code>, or <code>None</code>.</p> </li> <li>
<p><a href="#ike_encryption"><code>ike_encryption</code></a> - (Required) The IKE encryption algorithm. Valid options are <code>AES128</code>, <code>AES192</code>, <code>AES256</code>, <code>DES</code>, or <code>DES3</code>.</p> </li> <li>
<p><a href="#ike_integrity"><code>ike_integrity</code></a> - (Required) The IKE integrity algorithm. Valid options are <code>MD5</code>, <code>SHA1</code>, <code>SHA256</code>, or <code>SHA384</code>.</p> </li> <li>
<p><a href="#ipsec_encryption"><code>ipsec_encryption</code></a> - (Required) The IPSec encryption algorithm. Valid options are <code>AES128</code>, <code>AES192</code>, <code>AES256</code>, <code>DES</code>, <code>DES3</code>, <code>GCMAES128</code>, <code>GCMAES192</code>, <code>GCMAES256</code>, or <code>None</code>.</p> </li> <li>
<p><a href="#ipsec_integrity"><code>ipsec_integrity</code></a> - (Required) The IPSec integrity algorithm. Valid options are <code>GCMAES128</code>, <code>GCMAES192</code>, <code>GCMAES256</code>, <code>MD5</code>, <code>SHA1</code>, or <code>SHA256</code>.</p> </li> <li>
<p><a href="#pfs_group"><code>pfs_group</code></a> - (Required) The DH group used in IKE phase 2 for new child SA. Valid options are <code>ECP256</code>, <code>ECP384</code>, <code>PFS1</code>, <code>PFS2</code>, <code>PFS2048</code>, <code>PFS24</code>, or <code>None</code>.</p> </li> <li>
<p><a href="#sa_datasize"><code>sa_datasize</code></a> - (Optional) The IPSec SA payload size in KB. Must be at least <code>1024</code> KB. Defaults to <code>102400000</code> KB.</p> </li> <li>
<p><a href="#sa_lifetime"><code>sa_lifetime</code></a> - (Optional) The IPSec SA lifetime in seconds. Must be at least <code>300</code> seconds. Defaults to <code>27000</code> seconds.</p> </li> </ul> <h2 id="attributes-reference">  Attributes Reference </h2> <p>The following attributes are exported:</p> <ul> <li>
<a href="#id"><code>id</code></a> - The connection ID. </li> </ul> <h2 id="import">  Import </h2> <p>Virtual Network Gateway Connections can be imported using their <code>resource id</code>, e.g.</p> <pre>terraform import azurerm_virtual_network_gateway_connection.testConnection /subscriptions/00000000-0000-0000-0000-000000000000/resourceGroups/myGroup1/providers/Microsoft.Network/connections/myConnection1
</pre><div class="_attribution">
  <p class="_attribution-p">
    &copy; 2018 HashiCorp</br>Licensed under the MPL 2.0 License.<br>
    <a href="https://www.terraform.io/docs/providers/azurerm/r/virtual_network_gateway_connection.html" class="_attribution-link">https://www.terraform.io/docs/providers/azurerm/r/virtual_network_gateway_connection.html</a>
  </p>
</div>
