<h1 id="data-source-azurerm_managed_disk">  Data Source: azurerm_managed_disk </h1> <p>Use this data source to access the properties of an existing Azure Managed Disk.</p> <h2 id="example-usage">  Example Usage </h2> <pre data-language="ruby">data "azurerm_managed_disk" "datasourcemd" {
    name = "testManagedDisk"
    resource_group_name = "acctestRG"
}

resource "azurerm_virtual_network" "test" {
  name                = "acctvn"
  address_space       = ["10.0.0.0/16"]
  location            = "West US 2"
  resource_group_name = "acctestRG"
}

resource "azurerm_subnet" "test" {
  name                 = "acctsub"
  resource_group_name  = "acctestRG"
  virtual_network_name = "${azurerm_virtual_network.test.name}"
  address_prefix       = "10.0.2.0/24"
}

resource "azurerm_network_interface" "test" {
  name                = "acctni"
  location            = "West US 2"
  resource_group_name = "acctestRG"

  ip_configuration {
    name                          = "testconfiguration1"
    subnet_id                     = "${azurerm_subnet.test.id}"
    private_ip_address_allocation = "dynamic"
  }
}

resource "azurerm_virtual_machine" "test" {
  name                  = "acctvm"
  location              = "West US 2"
  resource_group_name   = "acctestRG"
  network_interface_ids = ["${azurerm_network_interface.test.id}"]
  vm_size               = "Standard_DS1_v2"

  storage_image_reference {
    publisher = "Canonical"
    offer     = "UbuntuServer"
    sku       = "16.04-LTS"
    version   = "latest"
  }

  storage_os_disk {
    name              = "myosdisk1"
    caching           = "ReadWrite"
    create_option     = "FromImage"
    managed_disk_type = "Standard_LRS"
  }

  storage_data_disk {
    name              = "datadisk_new"
    managed_disk_type = "Standard_LRS"
    create_option     = "Empty"
    lun               = 0
    disk_size_gb      = "1023"
  }

  storage_data_disk {
    name            = "${data.azurerm_managed_disk.datasourcemd.name}"
    managed_disk_id = "${data.azurerm_managed_disk.datasourcemd.id}"
    create_option   = "Attach"
    lun             = 1
    disk_size_gb    = "${data.azurerm_managed_disk.datasourcemd.disk_size_gb}"
  }

  os_profile {
    computer_name  = "hostname"
    admin_username = "testadmin"
    admin_password = "Password1234!"
  }

  os_profile_linux_config {
    disable_password_authentication = false
  }

  tags {
    environment = "staging"
  }
}
</pre>
<h2 id="argument-reference">  Argument Reference </h2> <ul> <li>
<a href="#name"><code>name</code></a> - (Required) Specifies the name of the Managed Disk. </li> <li>
<a href="#resource_group_name"><code>resource_group_name</code></a> - (Required) Specifies the name of the resource group. </li> </ul> <h2 id="attributes-reference">  Attributes Reference </h2> <ul> <li>
<a href="#storage_account_type"><code>storage_account_type</code></a> - The storage account type for the managed disk. </li> <li>
<a href="#source_uri"><code>source_uri</code></a> - The source URI for the managed disk </li> <li>
<a href="#source_resource_id"><code>source_resource_id</code></a> - ID of an existing managed disk that the current resource was created from. </li> <li>
<a href="#os_type"><code>os_type</code></a> - The operating system for managed disk. Valid values are <code>Linux</code> or <code>Windows</code> </li> <li>
<a href="#disk_size_gb"><code>disk_size_gb</code></a> - The size of the managed disk in gigabytes. </li> <li>
<a href="#tags"><code>tags</code></a> - A mapping of tags assigned to the resource. </li> <li>
<a href="#zones"><code>zones</code></a> - (Optional) A collection containing the availability zone the managed disk is allocated in. </li> </ul> <blockquote class="alert alert-info" role="alert"> <p><strong>Please Note</strong>: Availability Zones are <a href="https://docs.microsoft.com/en-us/azure/availability-zones/az-overview">in Preview and only supported in several regions at this time</a> - as such you must be opted into the Preview to use this functionality. You can <a href="http://aka.ms/azenroll">opt into the Availability Zones Preview in the Azure Portal</a>.</p> </blockquote><div class="_attribution">
  <p class="_attribution-p">
    &copy; 2018 HashiCorp</br>Licensed under the MPL 2.0 License.<br>
    <a href="https://www.terraform.io/docs/providers/azurerm/d/managed_disk.html" class="_attribution-link">https://www.terraform.io/docs/providers/azurerm/d/managed_disk.html</a>
  </p>
</div>
