<h1 id="vsphere_compute_cluster_vm_dependency_rule">  vsphere_compute_cluster_vm_dependency_rule </h1> <p>The <code>vsphere_compute_cluster_vm_dependency_rule</code> resource can be used to manage VM dependency rules in a cluster, either created by the <a href="compute_cluster.html"><code>vsphere_compute_cluster</code></a> resource or looked up by the <a href="../d/compute_cluster.html"><code>vsphere_compute_cluster</code></a> data source.</p> <p>A virtual machine dependency rule applies to vSphere HA, and allows user-defined startup orders for virtual machines in the case of host failure. Virtual machines are supplied via groups, which can be managed via the <a href="compute_cluster_vm_group.html"><code>vsphere_compute_cluster_vm_group</code></a> resource.</p> <blockquote class="alert alert-warning" role="alert"> <p><strong>NOTE:</strong> This resource requires vCenter and is not available on direct ESXi connections.</p> </blockquote> <h2 id="example-usage">  Example Usage </h2> <p>The example below creates two virtual machine in a cluster using the <a href="virtual_machine.html"><code>vsphere_virtual_machine</code></a> resource in a cluster looked up by the <a href="../d/compute_cluster.html"><code>vsphere_compute_cluster</code></a> data source. It then creates a group with this virtual machine. Two groups are created, each with one of the created VMs. Finally, a rule is created to ensure that <code>vm1</code> starts before <code>vm2</code>.</p> <blockquote class="alert alert-info" role="alert"> <p>Note how <a href="#dependency_vm_group_name"><code>dependency_vm_group_name</code></a> and <a href="#vm_group_name"><code>vm_group_name</code></a> are sourced off of the <code>name</code> attributes from the <a href="compute_cluster_vm_group.html"><code>vsphere_compute_cluster_vm_group</code></a> resource. This is to ensure that the rule is not created before the groups exist, which may not possibly happen in the event that the names came from a "static" source such as a variable.</p> </blockquote> <pre data-language="ruby">data "vsphere_datacenter" "dc" {
  name = "dc1"
}

data "vsphere_datastore" "datastore" {
  name          = "datastore1"
  datacenter_id = "${data.vsphere_datacenter.dc.id}"
}

data "vsphere_compute_cluster" "cluster" {
  name          = "cluster1"
  datacenter_id = "${data.vsphere_datacenter.dc.id}"
}

data "vsphere_network" "network" {
  name          = "network1"
  datacenter_id = "${data.vsphere_datacenter.dc.id}"
}

resource "vsphere_virtual_machine" "vm1" {
  name             = "terraform-test1"
  resource_pool_id = "${data.vsphere_compute_cluster.cluster.resource_pool_id}"
  datastore_id     = "${data.vsphere_datastore.datastore.id}"

  num_cpus = 2
  memory   = 2048
  guest_id = "other3xLinux64Guest"

  network_interface {
    network_id = "${data.vsphere_network.network.id}"
  }

  disk {
    label = "disk0"
    size  = 20
  }
}

resource "vsphere_virtual_machine" "vm2" {
  name             = "terraform-test2"
  resource_pool_id = "${data.vsphere_compute_cluster.cluster.resource_pool_id}"
  datastore_id     = "${data.vsphere_datastore.datastore.id}"

  num_cpus = 2
  memory   = 2048
  guest_id = "other3xLinux64Guest"

  network_interface {
    network_id = "${data.vsphere_network.network.id}"
  }

  disk {
    label = "disk0"
    size  = 20
  }
}

resource "vsphere_compute_cluster_vm_group" "cluster_vm_group1" {
  name                = "terraform-test-cluster-vm-group1"
  compute_cluster_id  = "${data.vsphere_compute_cluster.cluster.id}"
  virtual_machine_ids = ["${vsphere_virtual_machine.vm1.id}"]
}

resource "vsphere_compute_cluster_vm_group" "cluster_vm_group2" {
  name                = "terraform-test-cluster-vm-group2"
  compute_cluster_id  = "${data.vsphere_compute_cluster.cluster.id}"
  virtual_machine_ids = ["${vsphere_virtual_machine.vm2.id}"]
}

resource "vsphere_compute_cluster_vm_dependency_rule" "cluster_vm_dependency_rule" {
  compute_cluster_id       = "${data.vsphere_compute_cluster.cluster.id}"
  name                     = "terraform-test-cluster-vm-dependency-rule"
  dependency_vm_group_name = "${vsphere_compute_cluster_vm_group.cluster_vm_group1.name}"
  vm_group_name            = "${vsphere_compute_cluster_vm_group.cluster_vm_group2.name}"
}
</pre>
<h2 id="argument-reference">  Argument Reference </h2> <p>The following arguments are supported:</p> <ul> <li>
<p><a href="#compute_cluster_id"><code>compute_cluster_id</code></a> - (Required) The <a href="../index.html#use-of-managed-object-references-by-the-vsphere-provider">managed object reference ID</a> of the cluster to put the group in. Forces a new resource if changed.</p> </li> <li>
<p><a href="#name"><code>name</code></a> - (Required) The name of the rule. This must be unique in the cluster.</p> </li> <li>
<p><a href="#dependency_vm_group_name"><code>dependency_vm_group_name</code></a> - (Required) The name of the VM group that this rule depends on. The VMs defined in the group specified by <a href="#vm_group_name"><code>vm_group_name</code></a> will not be started until the VMs in this group are started.</p> </li> <li>
<p><a href="#vm_group_name"><code>vm_group_name</code></a> - (Required) The name of the VM group that is the subject of this rule. The VMs defined in this group will not be started until the VMs in the group specified by <a href="#dependency_vm_group_name"><code>dependency_vm_group_name</code></a> are started.</p> </li> <li>
<p><a href="#enabled"><code>enabled</code></a> - (Optional) Enable this rule in the cluster. Default: <code>true</code>.</p> </li> <li>
<p><a href="#mandatory"><code>mandatory</code></a> - (Optional) When this value is <code>true</code>, prevents any virtual machine operations that may violate this rule. Default: <code>false</code>.</p> </li> </ul> <blockquote class="alert alert-warning" role="alert"> <p><strong>NOTE:</strong> The namespace for rule names on this resource (defined by the <a href="#name"><code>name</code></a> argument) is shared with all rules in the cluster - consider this when naming your rules.</p> </blockquote> <h2 id="attribute-reference">  Attribute Reference </h2> <p>The only attribute this resource exports is the <code>id</code> of the resource, which is a combination of the <a href="../index.html#use-of-managed-object-references-by-the-vsphere-provider">managed object reference ID</a> of the cluster, and the rule's key within the cluster configuration.</p> <h2 id="importing">  Importing </h2> <p>An existing rule can be <a href="../../../import/index.html">imported</a> into this resource by supplying both the path to the cluster, and the name the rule. If the name or cluster is not found, or if the rule is of a different type, an error will be returned. An example is below:</p> <pre>terraform import vsphere_compute_cluster_vm_dependency_rule.cluster_vm_dependency_rule \
  '{"compute_cluster_path": "/dc1/host/cluster1", \
  "name": "terraform-test-cluster-vm-dependency-rule"}'
</pre><div class="_attribution">
  <p class="_attribution-p">
    &copy; 2018 HashiCorp</br>Licensed under the MPL 2.0 License.<br>
    <a href="https://www.terraform.io/docs/providers/vsphere/r/compute_cluster_vm_dependency_rule.html" class="_attribution-link">https://www.terraform.io/docs/providers/vsphere/r/compute_cluster_vm_dependency_rule.html</a>
  </p>
</div>
