<h1 id="aws_route53_query_log">  aws_route53_query_log </h1> <p>Provides a Route53 query logging configuration resource.</p> <blockquote class="alert alert-warning" role="alert"> <p><strong>NOTE:</strong> There are restrictions on the configuration of query logging. Notably, the CloudWatch log group must be in the <code>us-east-1</code> region, a permissive CloudWatch log resource policy must be in place, and the Route53 hosted zone must be public. See <a href="https://docs.aws.amazon.com/Route53/latest/DeveloperGuide/query-logs.html?console_help=true#query-logs-configuring">Configuring Logging for DNS Queries</a> for additional details.</p> </blockquote> <h2 id="example-usage">  Example Usage </h2> <pre data-language="ruby"># Example CloudWatch log group in us-east-1

provider "aws" {
  alias  = "us-east-1"
  region = "us-east-1"
}

resource "aws_cloudwatch_log_group" "aws_route53_example_com" {
  provider = "aws.us-east-1"

  name              = "/aws/route53/${aws_route53_zone.example_com.name}"
  retention_in_days = 30
}

# Example CloudWatch log resource policy to allow Route53 to write logs
# to any log group under /aws/route53/*

data "aws_iam_policy_document" "route53-query-logging-policy" {
  statement {
    actions = [
      "logs:CreateLogStream",
      "logs:PutLogEvents",
    ]

    resources = ["arn:aws:logs:*:*:log-group:/aws/route53/*"]

    principals {
      identifiers = ["route53.amazonaws.com"]
      type        = "Service"
    }
  }
}

resource "aws_cloudwatch_log_resource_policy" "route53-query-logging-policy" {
  provider = "aws.us-east-1"

  policy_document = "${data.aws_iam_policy_document.route53-query-logging-policy.json}"
  policy_name     = "route53-query-logging-policy"
}

# Example Route53 zone with query logging

resource "aws_route53_zone" "example_com" {
  name = "example.com"
}

resource "aws_route53_query_log" "example_com" {
  depends_on = ["aws_cloudwatch_log_resource_policy.route53-query-logging-policy"]

  cloudwatch_log_group_arn = "${aws_cloudwatch_log_group.aws_route53_example_com.arn}"
  zone_id                  = "${aws_route53_zone.example_com.zone_id}"
}
</pre>
<h2 id="argument-reference">  Argument Reference </h2> <p>The following arguments are supported:</p> <ul> <li>
<a href="#cloudwatch_log_group_arn"><code>cloudwatch_log_group_arn</code></a> - (Required) CloudWatch log group ARN to send query logs. </li> <li>
<a href="#zone_id"><code>zone_id</code></a> - (Required) Route53 hosted zone ID to enable query logs. </li> </ul> <h2 id="attributes-reference">  Attributes Reference </h2> <p>In addition to all arguments above, the following attributes are exported:</p> <ul> <li>
<a href="#id"><code>id</code></a> - The query logging configuration ID </li> </ul> <h2 id="import">  Import </h2> <p>Route53 query logging configurations can be imported using their ID, e.g.</p> <pre>$ terraform import aws_route53_query_log.example_com xxxxxxxx-xxxx-xxxx-xxxx-xxxxxxxxxxxx
</pre><div class="_attribution">
  <p class="_attribution-p">
    &copy; 2018 HashiCorp</br>Licensed under the MPL 2.0 License.<br>
    <a href="https://www.terraform.io/docs/providers/aws/r/route53_query_log.html" class="_attribution-link">https://www.terraform.io/docs/providers/aws/r/route53_query_log.html</a>
  </p>
</div>
