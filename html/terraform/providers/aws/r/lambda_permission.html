<h1 id="aws_lambda_permission">  aws_lambda_permission </h1> <p>Creates a Lambda permission to allow external sources invoking the Lambda function (e.g. CloudWatch Event Rule, SNS or S3).</p> <h2 id="example-usage">  Example Usage </h2> <pre data-language="ruby">resource "aws_lambda_permission" "allow_cloudwatch" {
  statement_id   = "AllowExecutionFromCloudWatch"
  action         = "lambda:InvokeFunction"
  function_name  = "${aws_lambda_function.test_lambda.function_name}"
  principal      = "events.amazonaws.com"
  source_arn     = "arn:aws:events:eu-west-1:111122223333:rule/RunDaily"
  qualifier      = "${aws_lambda_alias.test_alias.name}"
}

resource "aws_lambda_alias" "test_alias" {
  name             = "testalias"
  description      = "a sample description"
  function_name    = "${aws_lambda_function.test_lambda.function_name}"
  function_version = "$LATEST"
}

resource "aws_lambda_function" "test_lambda" {
  filename      = "lambdatest.zip"
  function_name = "lambda_function_name"
  role          = "${aws_iam_role.iam_for_lambda.arn}"
  handler       = "exports.handler"
  runtime       = "nodejs6.10"
}

resource "aws_iam_role" "iam_for_lambda" {
  name = "iam_for_lambda"

  assume_role_policy = &lt;&lt;EOF
{
  "Version": "2012-10-17",
  "Statement": [
    {
      "Action": "sts:AssumeRole",
      "Principal": {
        "Service": "lambda.amazonaws.com"
      },
      "Effect": "Allow",
      "Sid": ""
    }
  ]
}
EOF
}
</pre>
<h2 id="usage-with-sns">  Usage with SNS </h2> <pre data-language="ruby">resource "aws_lambda_permission" "with_sns" {
  statement_id  = "AllowExecutionFromSNS"
  action        = "lambda:InvokeFunction"
  function_name = "${aws_lambda_function.func.function_name}"
  principal     = "sns.amazonaws.com"
  source_arn    = "${aws_sns_topic.default.arn}"
}

resource "aws_sns_topic" "default" {
  name = "call-lambda-maybe"
}

resource "aws_sns_topic_subscription" "lambda" {
  topic_arn = "${aws_sns_topic.default.arn}"
  protocol  = "lambda"
  endpoint  = "${aws_lambda_function.func.arn}"
}

resource "aws_lambda_function" "func" {
  filename      = "lambdatest.zip"
  function_name = "lambda_called_from_sns"
  role          = "${aws_iam_role.default.arn}"
  handler       = "exports.handler"
  runtime       = "python2.7"
}

resource "aws_iam_role" "default" {
  name = "iam_for_lambda_with_sns"

  assume_role_policy = &lt;&lt;EOF
{
  "Version": "2012-10-17",
  "Statement": [
    {
      "Action": "sts:AssumeRole",
      "Principal": {
        "Service": "lambda.amazonaws.com"
      },
      "Effect": "Allow",
      "Sid": ""
    }
  ]
}
EOF
}
</pre>
<h2 id="specify-lambda-permissions-for-api-gateway-rest-api">  Specify Lambda permissions for API Gateway REST API </h2> <pre data-language="ruby">resource "aws_api_gateway_rest_api" "MyDemoAPI" {
  name        = "MyDemoAPI"
  description = "This is my API for demonstration purposes"
}

resource "aws_lambda_permission" "lambda_permission" {
  statement_id  = "AllowMyDemoAPIInvoke"
  action        = "lambda:InvokeFunction"
  function_name = "MyDemoFunction"
  principal     = "apigateway.amazonaws.com"

  # The /*/*/* part allows invocation from any stage, method and resource path
  # within API Gateway REST API.
  source_arn = "${aws_api_gateway_rest_api.MyDemoAPI.execution_arn}/*/*/*"
}
</pre>
<h2 id="argument-reference">  Argument Reference </h2> <ul> <li>
<a href="#action"><code>action</code></a> - (Required) The AWS Lambda action you want to allow in this statement. (e.g. <code>lambda:InvokeFunction</code>) </li> <li>
<a href="#function_name"><code>function_name</code></a> - (Required) Name of the Lambda function whose resource policy you are updating </li> <li>
<a href="#principal"><code>principal</code></a> - (Required) The principal who is getting this permission. e.g. <code>s3.amazonaws.com</code>, an AWS account ID, or any valid AWS service principal such as <code>events.amazonaws.com</code> or <code>sns.amazonaws.com</code>. </li> <li>
<a href="#qualifier"><code>qualifier</code></a> - (Optional) Query parameter to specify function version or alias name. The permission will then apply to the specific qualified ARN. e.g. <code>arn:aws:lambda:aws-region:acct-id:function:function-name:2</code> </li> <li>
<a href="#source_account"><code>source_account</code></a> - (Optional) This parameter is used for S3 and SES. The AWS account ID (without a hyphen) of the source owner. </li> <li>
<a href="#source_arn"><code>source_arn</code></a> - (Optional) When granting Amazon S3 or CloudWatch Events permission to invoke your function, you should specify this field with the Amazon Resource Name (ARN) for the S3 Bucket or CloudWatch Events Rule as its value. This ensures that only events generated from the specified bucket or rule can invoke the function. API Gateway ARNs have a unique structure described <a href="http://docs.aws.amazon.com/apigateway/latest/developerguide/api-gateway-control-access-using-iam-policies-to-invoke-api.html">here</a>. </li> <li>
<a href="#statement_id"><code>statement_id</code></a> - (Optional) A unique statement identifier. By default generated by Terraform. </li> <li>
<a href="#statement_id_prefix"><code>statement_id_prefix</code></a> - (Optional) A statement identifier prefix. Terraform will generate a unique suffix. Conflicts with <code>statement_id</code>. </li> </ul><div class="_attribution">
  <p class="_attribution-p">
    &copy; 2018 HashiCorp</br>Licensed under the MPL 2.0 License.<br>
    <a href="https://www.terraform.io/docs/providers/aws/r/lambda_permission.html" class="_attribution-link">https://www.terraform.io/docs/providers/aws/r/lambda_permission.html</a>
  </p>
</div>
