<h1 id="aws_s3_bucket_notification">  aws_s3_bucket_notification </h1> <p>Provides a S3 bucket notification resource.</p> <h2 id="example-usage">  Example Usage </h2> <h3 id="add-notification-configuration-to-sns-topic">  Add notification configuration to SNS Topic </h3> <pre data-language="ruby">resource "aws_sns_topic" "topic" {
  name = "s3-event-notification-topic"

  policy = &lt;&lt;POLICY
{
    "Version":"2012-10-17",
    "Statement":[{
        "Effect": "Allow",
        "Principal": {"AWS":"*"},
        "Action": "SNS:Publish",
        "Resource": "arn:aws:sns:*:*:s3-event-notification-topic",
        "Condition":{
            "ArnLike":{"aws:SourceArn":"${aws_s3_bucket.bucket.arn}"}
        }
    }]
}
POLICY
}

resource "aws_s3_bucket" "bucket" {
  bucket = "your_bucket_name"
}

resource "aws_s3_bucket_notification" "bucket_notification" {
  bucket = "${aws_s3_bucket.bucket.id}"

  topic {
    topic_arn     = "${aws_sns_topic.topic.arn}"
    events        = ["s3:ObjectCreated:*"]
    filter_suffix = ".log"
  }
}
</pre>
<h3 id="add-notification-configuration-to-sqs-queue">  Add notification configuration to SQS Queue </h3> <pre data-language="ruby">resource "aws_sqs_queue" "queue" {
  name = "s3-event-notification-queue"

  policy = &lt;&lt;POLICY
{
  "Version": "2012-10-17",
  "Statement": [
    {
      "Effect": "Allow",
      "Principal": "*",
      "Action": "sqs:SendMessage",
      "Resource": "arn:aws:sqs:*:*:s3-event-notification-queue",
      "Condition": {
        "ArnEquals": { "aws:SourceArn": "${aws_s3_bucket.bucket.arn}" }
      }
    }
  ]
}
POLICY
}

resource "aws_s3_bucket" "bucket" {
  bucket = "your_bucket_name"
}

resource "aws_s3_bucket_notification" "bucket_notification" {
  bucket = "${aws_s3_bucket.bucket.id}"

  queue {
    queue_arn     = "${aws_sqs_queue.queue.arn}"
    events        = ["s3:ObjectCreated:*"]
    filter_suffix = ".log"
  }
}
</pre>
<h3 id="add-notification-configuration-to-lambda-function">  Add notification configuration to Lambda Function </h3> <pre data-language="ruby">resource "aws_iam_role" "iam_for_lambda" {
  name = "iam_for_lambda"

  assume_role_policy = &lt;&lt;EOF
{
  "Version": "2012-10-17",
  "Statement": [
    {
      "Action": "sts:AssumeRole",
      "Principal": {
        "Service": "lambda.amazonaws.com"
      },
      "Effect": "Allow"
    }
  ]
}
EOF
}

resource "aws_lambda_permission" "allow_bucket" {
  statement_id  = "AllowExecutionFromS3Bucket"
  action        = "lambda:InvokeFunction"
  function_name = "${aws_lambda_function.func.arn}"
  principal     = "s3.amazonaws.com"
  source_arn    = "${aws_s3_bucket.bucket.arn}"
}

resource "aws_lambda_function" "func" {
  filename      = "your-function.zip"
  function_name = "example_lambda_name"
  role          = "${aws_iam_role.iam_for_lambda.arn}"
  handler       = "exports.example"
}

resource "aws_s3_bucket" "bucket" {
  bucket = "your_bucket_name"
}

resource "aws_s3_bucket_notification" "bucket_notification" {
  bucket = "${aws_s3_bucket.bucket.id}"

  lambda_function {
    lambda_function_arn = "${aws_lambda_function.func.arn}"
    events              = ["s3:ObjectCreated:*"]
    filter_prefix       = "AWSLogs/"
    filter_suffix       = ".log"
  }
}
</pre>
<h3 id="trigger-multiple-lambda-functions">  Trigger multiple Lambda functions </h3> <pre data-language="ruby">resource "aws_iam_role" "iam_for_lambda" {
  name = "iam_for_lambda"

  assume_role_policy = &lt;&lt;EOF
{
  "Version": "2012-10-17",
  "Statement": [
    {
      "Action": "sts:AssumeRole",
      "Principal": {
        "Service": "lambda.amazonaws.com"
      },
      "Effect": "Allow"
    }
  ]
}
EOF
}

resource "aws_lambda_permission" "allow_bucket1" {
  statement_id  = "AllowExecutionFromS3Bucket1"
  action        = "lambda:InvokeFunction"
  function_name = "${aws_lambda_function.func1.arn}"
  principal     = "s3.amazonaws.com"
  source_arn    = "${aws_s3_bucket.bucket.arn}"
}

resource "aws_lambda_function" "func1" {
  filename      = "your-function1.zip"
  function_name = "example_lambda_name1"
  role          = "${aws_iam_role.iam_for_lambda.arn}"
  handler       = "exports.example"
}

resource "aws_lambda_permission" "allow_bucket2" {
  statement_id  = "AllowExecutionFromS3Bucket2"
  action        = "lambda:InvokeFunction"
  function_name = "${aws_lambda_function.func2.arn}"
  principal     = "s3.amazonaws.com"
  source_arn    = "${aws_s3_bucket.bucket.arn}"
}

resource "aws_lambda_function" "func2" {
  filename      = "your-function2.zip"
  function_name = "example_lambda_name2"
  role          = "${aws_iam_role.iam_for_lambda.arn}"
  handler       = "exports.example"
}

resource "aws_s3_bucket" "bucket" {
  bucket = "your_bucket_name"
}

resource "aws_s3_bucket_notification" "bucket_notification" {
  bucket = "${aws_s3_bucket.bucket.id}"

  lambda_function {
    lambda_function_arn = "${aws_lambda_function.func1.arn}"
    events              = ["s3:ObjectCreated:*"]
    filter_prefix       = "AWSLogs/"
    filter_suffix       = ".log"
  }

  lambda_function {
    lambda_function_arn = "${aws_lambda_function.func2.arn}"
    events              = ["s3:ObjectCreated:*"]
    filter_prefix       = "OtherLogs/"
    filter_suffix       = ".log"
  }
}
</pre>
<h3 id="add-multiple-notification-configurations-to-sqs-queue">  Add multiple notification configurations to SQS Queue </h3> <pre data-language="ruby">resource "aws_sqs_queue" "queue" {
  name = "s3-event-notification-queue"

  policy = &lt;&lt;POLICY
{
  "Version": "2012-10-17",
  "Statement": [
    {
      "Effect": "Allow",
      "Principal": "*",
      "Action": "sqs:SendMessage",
      "Resource": "arn:aws:sqs:*:*:s3-event-notification-queue",
      "Condition": {
        "ArnEquals": { "aws:SourceArn": "${aws_s3_bucket.bucket.arn}" }
      }
    }
  ]
}
POLICY
}

resource "aws_s3_bucket" "bucket" {
  bucket = "your_bucket_name"
}

resource "aws_s3_bucket_notification" "bucket_notification" {
  bucket = "${aws_s3_bucket.bucket.id}"

  queue {
    id            = "image-upload-event"
    queue_arn     = "${aws_sqs_queue.queue.arn}"
    events        = ["s3:ObjectCreated:*"]
    filter_prefix = "images/"
  }

  queue {
    id            = "video-upload-event"
    queue_arn     = "${aws_sqs_queue.queue.arn}"
    events        = ["s3:ObjectCreated:*"]
    filter_prefix = "videos/"
  }
}
</pre>
<p>For Terraform's <a href="../../../configuration/syntax.html">JSON syntax</a>, use an array instead of defining the <code>queue</code> key twice.</p> <pre data-language="json">{
    "bucket": "${aws_s3_bucket.bucket.id}",
    "queue": [
        {
            "id": "image-upload-event",
            "queue_arn": "${aws_sqs_queue.queue.arn}",
            "events": ["s3:ObjectCreated:*"],
            "filter_prefix": "images/"
        },
        {
            "id": "video-upload-event",
            "queue_arn": "${aws_sqs_queue.queue.arn}",
            "events": ["s3:ObjectCreated:*"],
            "filter_prefix": "videos/"
        }
    ]
}
</pre>
<h2 id="argument-reference">  Argument Reference </h2> <p>The following arguments are supported:</p> <ul> <li>
<a href="#bucket"><code>bucket</code></a> - (Required) The name of the bucket to put notification configuration. </li> <li>
<a href="#topic"><code>topic</code></a> - (Optional) The notification configuration to SNS Topic (documented below). </li> <li>
<a href="#queue"><code>queue</code></a> - (Optional) The notification configuration to SQS Queue (documented below). </li> <li>
<a href="#lambda_function"><code>lambda_function</code></a> - (Optional, Multiple) Used to configure notifications to a Lambda Function (documented below). </li> </ul> <p>The <code>topic</code> notification configuration supports the following:</p> <ul> <li>
<a href="#id"><code>id</code></a> - (Optional) Specifies unique identifier for each of the notification configurations. </li> <li>
<a href="#topic_arn"><code>topic_arn</code></a> - (Required) Specifies Amazon SNS topic ARN. </li> <li>
<a href="#events"><code>events</code></a> - (Required) Specifies <a href="http://docs.aws.amazon.com/AmazonS3/latest/dev/NotificationHowTo.html#notification-how-to-event-types-and-destinations">event</a> for which to send notifications. </li> <li>
<a href="#filter_prefix"><code>filter_prefix</code></a> - (Optional) Specifies object key name prefix. </li> <li>
<a href="#filter_suffix"><code>filter_suffix</code></a> - (Optional) Specifies object key name suffix. </li> </ul> <p>The <code>queue</code> notification configuration supports the following:</p> <ul> <li>
<a href="#id-1"><code>id</code></a> - (Optional) Specifies unique identifier for each of the notification configurations. </li> <li>
<a href="#queue_arn"><code>queue_arn</code></a> - (Required) Specifies Amazon SQS queue ARN. </li> <li>
<a href="#events-1"><code>events</code></a> - (Required) Specifies <a href="http://docs.aws.amazon.com/AmazonS3/latest/dev/NotificationHowTo.html#notification-how-to-event-types-and-destinations">event</a> for which to send notifications. </li> <li>
<a href="#filter_prefix-1"><code>filter_prefix</code></a> - (Optional) Specifies object key name prefix. </li> <li>
<a href="#filter_suffix-1"><code>filter_suffix</code></a> - (Optional) Specifies object key name suffix. </li> </ul> <p>The <code>lambda_function</code> notification configuration supports the following:</p> <ul> <li>
<a href="#id-2"><code>id</code></a> - (Optional) Specifies unique identifier for each of the notification configurations. </li> <li>
<a href="#lambda_function_arn"><code>lambda_function_arn</code></a> - (Required) Specifies Amazon Lambda function ARN. </li> <li>
<a href="#events-2"><code>events</code></a> - (Required) Specifies <a href="http://docs.aws.amazon.com/AmazonS3/latest/dev/NotificationHowTo.html#notification-how-to-event-types-and-destinations">event</a> for which to send notifications. </li> <li>
<a href="#filter_prefix-2"><code>filter_prefix</code></a> - (Optional) Specifies object key name prefix. </li> <li>
<a href="#filter_suffix-2"><code>filter_suffix</code></a> - (Optional) Specifies object key name suffix. </li> </ul> <h2 id="import">  Import </h2> <p>S3 bucket notification can be imported using the <code>bucket</code>, e.g.</p> <pre>$ terraform import aws_s3_bucket_notification.bucket_notification bucket-name
</pre><div class="_attribution">
  <p class="_attribution-p">
    &copy; 2018 HashiCorp</br>Licensed under the MPL 2.0 License.<br>
    <a href="https://www.terraform.io/docs/providers/aws/r/s3_bucket_notification.html" class="_attribution-link">https://www.terraform.io/docs/providers/aws/r/s3_bucket_notification.html</a>
  </p>
</div>
