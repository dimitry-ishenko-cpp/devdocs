<h1 id="aws_dynamodb_global_table">  aws_dynamodb_global_table </h1> <p>Provides a resource to manage a DynamoDB Global Table. These are layered on top of existing DynamoDB Tables.</p> <blockquote class="alert alert-warning" role="alert"> <p>Note: There are many restrictions before you can properly create DynamoDB Global Tables in multiple regions. See the <a href="http://docs.aws.amazon.com/amazondynamodb/latest/developerguide/globaltables_reqs_bestpractices.html">AWS DynamoDB Global Table Requirements</a> for more information.</p> </blockquote> <h2 id="example-usage">  Example Usage </h2> <pre data-language="ruby">provider "aws" {
  alias  = "us-east-1"
  region = "us-east-1"
}

provider "aws" {
  alias  = "us-west-2"
  region = "us-west-2"
}

resource "aws_dynamodb_table" "us-east-1" {
  provider = "aws.us-east-1"

  hash_key         = "myAttribute"
  name             = "myTable"
  stream_enabled   = true
  stream_view_type = "NEW_AND_OLD_IMAGES"
  read_capacity    = 1
  write_capacity   = 1

  attribute {
    name = "myAttribute"
    type = "S"
  }
}

resource "aws_dynamodb_table" "us-west-2" {
  provider = "aws.us-west-2"

  hash_key         = "myAttribute"
  name             = "myTable"
  stream_enabled   = true
  stream_view_type = "NEW_AND_OLD_IMAGES"
  read_capacity    = 1
  write_capacity   = 1

  attribute {
    name = "myAttribute"
    type = "S"
  }
}

resource "aws_dynamodb_global_table" "myTable" {
  depends_on = ["aws_dynamodb_table.us-east-1", "aws_dynamodb_table.us-west-2"]
  provider   = "aws.us-east-1"

  name = "myTable"

  replica {
    region_name = "us-east-1"
  }

  replica {
    region_name = "us-west-2"
  }
}
</pre>
<h2 id="argument-reference">  Argument Reference </h2> <p>The following arguments are supported:</p> <ul> <li>
<a href="#name"><code>name</code></a> - (Required) The name of the global table. Must match underlying DynamoDB Table names in all regions. </li> <li>
<a href="#replica"><code>replica</code></a> - (Required) Underlying DynamoDB Table. At least 1 replica must be defined. See below. </li> </ul> <h3 id="nested-fields">  Nested Fields </h3> <h4 id="replica-1">  <code>replica</code> </h4> <ul> <li>
<a href="#region_name"><code>region_name</code></a> - (Required) AWS region name of replica DynamoDB Table. e.g. <code>us-east-1</code> </li> </ul> <h2 id="attributes-reference">  Attributes Reference </h2> <p>In addition to all arguments above, the following attributes are exported:</p> <ul> <li>
<a href="#id"><code>id</code></a> - The name of the DynamoDB Global Table </li> <li>
<a href="#arn"><code>arn</code></a> - The ARN of the DynamoDB Global Table </li> </ul> <h2 id="import">  Import </h2> <p>DynamoDB Global Tables can be imported using the global table name, e.g.</p> <pre>$ terraform import aws_dynamodb_global_table.MyTable MyTable
</pre><div class="_attribution">
  <p class="_attribution-p">
    &copy; 2018 HashiCorp</br>Licensed under the MPL 2.0 License.<br>
    <a href="https://www.terraform.io/docs/providers/aws/r/dynamodb_global_table.html" class="_attribution-link">https://www.terraform.io/docs/providers/aws/r/dynamodb_global_table.html</a>
  </p>
</div>
