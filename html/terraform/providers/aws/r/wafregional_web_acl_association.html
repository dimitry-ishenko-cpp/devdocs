<h1 id="aws_wafregional_web_acl_association">  aws_wafregional_web_acl_association </h1> <p>Provides a resource to create an association between a WAF Regional WebACL and Application Load Balancer.</p> <blockquote class="alert alert-info" role="alert"> <p><strong>Note:</strong> An Application Load Balancer can only be associated with one WAF Regional WebACL.</p> </blockquote> <h2 id="example-usage">  Example Usage </h2> <pre data-language="ruby">resource "aws_wafregional_ipset" "ipset" {
  name = "tfIPSet"

  ip_set_descriptor {
    type  = "IPV4"
    value = "192.0.7.0/24"
  }
}

resource "aws_wafregional_rule" "foo" {
  name        = "tfWAFRule"
  metric_name = "tfWAFRule"

  predicate {
    data_id = "${aws_wafregional_ipset.ipset.id}"
    negated = false
    type    = "IPMatch"
  }
}

resource "aws_wafregional_web_acl" "foo" {
  name = "foo"
  metric_name = "foo"
  default_action {
    type = "ALLOW"
  }
  rule {
    action {
      type = "BLOCK"
    }
    priority = 1
    rule_id = "${aws_wafregional_rule.foo.id}"
  }
}

resource "aws_vpc" "foo" {
  cidr_block = "10.1.0.0/16"
}

data "aws_availability_zones" "available" {}

resource "aws_subnet" "foo" {
  vpc_id = "${aws_vpc.foo.id}"
  cidr_block = "10.1.1.0/24"
  availability_zone = "${data.aws_availability_zones.available.names[0]}"
}

resource "aws_subnet" "bar" {
  vpc_id = "${aws_vpc.foo.id}"
  cidr_block = "10.1.2.0/24"
  availability_zone = "${data.aws_availability_zones.available.names[1]}"
}

resource "aws_alb" "foo" {
  internal = true
  subnets = ["${aws_subnet.foo.id}", "${aws_subnet.bar.id}"]
}

resource "aws_wafregional_web_acl_association" "foo" {
  resource_arn = "${aws_alb.foo.arn}"
  web_acl_id = "${aws_wafregional_web_acl.foo.id}"
}
</pre>
<h2 id="argument-reference">  Argument Reference </h2> <p>The following arguments are supported:</p> <ul> <li>
<a href="#web_acl_id"><code>web_acl_id</code></a> - (Required) The ID of the WAF Regional WebACL to create an association. </li> <li>
<a href="#resource_arn"><code>resource_arn</code></a> - (Required) Application Load Balancer ARN to associate with. </li> </ul> <h2 id="attributes-reference">  Attributes Reference </h2> <p>In addition to all arguments above, the following attributes are exported:</p> <ul> <li>
<a href="#id"><code>id</code></a> - The ID of the association </li> </ul><div class="_attribution">
  <p class="_attribution-p">
    &copy; 2018 HashiCorp</br>Licensed under the MPL 2.0 License.<br>
    <a href="https://www.terraform.io/docs/providers/aws/r/wafregional_web_acl_association.html" class="_attribution-link">https://www.terraform.io/docs/providers/aws/r/wafregional_web_acl_association.html</a>
  </p>
</div>
