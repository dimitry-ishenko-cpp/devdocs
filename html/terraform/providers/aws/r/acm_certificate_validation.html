<h1 id="aws_acm_certificate_validation">  aws_acm_certificate_validation </h1> <p>This resource represents a successful validation of an ACM certificate in concert with other resources.</p> <p>Most commonly, this resource is used together with <a href="route53_record.html"><code>aws_route53_record</code></a> and <a href="acm_certificate.html"><code>aws_acm_certificate</code></a> to request a DNS validated certificate, deploy the required validation records and wait for validation to complete.</p> <blockquote class="alert alert-warning" role="alert"> <p><strong>WARNING:</strong> This resource implements a part of the validation workflow. It does not represent a real-world entity in AWS, therefore changing or deleting this resource on its own has no immediate effect.</p> </blockquote> <h2 id="example-usage">  Example Usage </h2> <h3 id="dns-validation-with-route-53">  DNS Validation with Route 53 </h3> <pre data-language="ruby">resource "aws_acm_certificate" "cert" {
  domain_name = "example.com"
  validation_method = "DNS"
}

data "aws_route53_zone" "zone" {
  name = "example.com."
  private_zone = false
}

resource "aws_route53_record" "cert_validation" {
  name = "${aws_acm_certificate.cert.domain_validation_options.0.resource_record_name}"
  type = "${aws_acm_certificate.cert.domain_validation_options.0.resource_record_type}"
  zone_id = "${data.aws_route53_zone.zone.id}"
  records = ["${aws_acm_certificate.cert.domain_validation_options.0.resource_record_value}"]
  ttl = 60
}

resource "aws_acm_certificate_validation" "cert" {
  certificate_arn = "${aws_acm_certificate.cert.arn}"
  validation_record_fqdns = ["${aws_route53_record.cert_validation.fqdn}"]
}

resource "aws_lb_listener" "front_end" {
  # [...]
  certificate_arn   = "${aws_acm_certificate_validation.cert.certificate_arn}"
}
</pre>
<h3 id="alternative-domains-dns-validation-with-route-53">  Alternative Domains DNS Validation with Route 53 </h3> <pre data-language="ruby">resource "aws_acm_certificate" "cert" {
  domain_name = "example.com"
  subject_alternative_names = ["www.example.com","example.org"]
  validation_method = "DNS"
}

data "aws_route53_zone" "zone" {
  name = "example.com."
  private_zone = false
}

data "aws_route53_zone" "zone_alt" {
  name = "example.org."
  private_zone = false
}

resource "aws_route53_record" "cert_validation" {
  name = "${aws_acm_certificate.cert.domain_validation_options.0.resource_record_name}"
  type = "${aws_acm_certificate.cert.domain_validation_options.0.resource_record_type}"
  zone_id = "${data.aws_route53_zone.zone.id}"
  records = ["${aws_acm_certificate.cert.domain_validation_options.0.resource_record_value}"]
  ttl = 60
}

resource "aws_route53_record" "cert_validation_alt1" {
  name = "${aws_acm_certificate.cert.domain_validation_options.1.resource_record_name}"
  type = "${aws_acm_certificate.cert.domain_validation_options.1.resource_record_type}"
  zone_id = "${data.aws_route53_zone.zone.id}"
  records = ["${aws_acm_certificate.cert.domain_validation_options.1.resource_record_value}"]
  ttl = 60
}

resource "aws_route53_record" "cert_validation_alt2" {
  name = "${aws_acm_certificate.cert.domain_validation_options.2.resource_record_name}"
  type = "${aws_acm_certificate.cert.domain_validation_options.2.resource_record_type}"
  zone_id = "${data.aws_route53_zone.zone_alt.id}"
  records = ["${aws_acm_certificate.cert.domain_validation_options.2.resource_record_value}"]
  ttl = 60
}

resource "aws_acm_certificate_validation" "cert" {
  certificate_arn = "${aws_acm_certificate.cert.arn}"
  validation_record_fqdns = [
    "${aws_route53_record.cert_validation.fqdn}",
    "${aws_route53_record.cert_validation_alt1.fqdn}",
    "${aws_route53_record.cert_validation_alt2.fqdn}"
  ]
}

resource "aws_lb_listener" "front_end" {
  # [...]
  certificate_arn   = "${aws_acm_certificate_validation.cert.certificate_arn}"
}
</pre>
<h3 id="email-validation">  Email Validation </h3> <p>In this situation, the resource is simply a waiter for manual email approval of ACM certificates.</p> <pre data-language="ruby">resource "aws_acm_certificate" "cert" {
  domain_name       = "example.com"
  validation_method = "EMAIL"
}

resource "aws_acm_certificate_validation" "cert" {
  certificate_arn = "${aws_acm_certificate.cert.arn}"
}
</pre>
<h2 id="argument-reference">  Argument Reference </h2> <p>The following arguments are supported:</p> <ul> <li>
<a href="#certificate_arn"><code>certificate_arn</code></a> - (Required) The ARN of the certificate that is being validated. </li> <li>
<a href="#validation_record_fqdns"><code>validation_record_fqdns</code></a> - (Optional) List of FQDNs that implement the validation. Only valid for DNS validation method ACM certificates. If this is set, the resource can implement additional sanity checks and has an explicit dependency on the resource that is implementing the validation </li> </ul> <h2 id="timeouts">  Timeouts </h2> <p><code>acm_certificate_validation</code> provides the following <a href="../../../configuration/resources.html#timeouts">Timeouts</a> configuration options:</p> <ul> <li>
<a href="#create"><code>create</code></a> - (Default <code>45m</code>) How long to wait for a certificate to be issued. </li> </ul><div class="_attribution">
  <p class="_attribution-p">
    &copy; 2018 HashiCorp</br>Licensed under the MPL 2.0 License.<br>
    <a href="https://www.terraform.io/docs/providers/aws/r/acm_certificate_validation.html" class="_attribution-link">https://www.terraform.io/docs/providers/aws/r/acm_certificate_validation.html</a>
  </p>
</div>
