<h1 id="aws_api_gateway_integration">  aws_api_gateway_integration </h1> <p>Provides an HTTP Method Integration for an API Gateway Integration.</p> <h2 id="example-usage">  Example Usage </h2> <pre data-language="ruby">resource "aws_api_gateway_rest_api" "MyDemoAPI" {
  name        = "MyDemoAPI"
  description = "This is my API for demonstration purposes"
}

resource "aws_api_gateway_resource" "MyDemoResource" {
  rest_api_id = "${aws_api_gateway_rest_api.MyDemoAPI.id}"
  parent_id   = "${aws_api_gateway_rest_api.MyDemoAPI.root_resource_id}"
  path_part   = "mydemoresource"
}

resource "aws_api_gateway_method" "MyDemoMethod" {
  rest_api_id   = "${aws_api_gateway_rest_api.MyDemoAPI.id}"
  resource_id   = "${aws_api_gateway_resource.MyDemoResource.id}"
  http_method   = "GET"
  authorization = "NONE"
}

resource "aws_api_gateway_integration" "MyDemoIntegration" {
  rest_api_id          = "${aws_api_gateway_rest_api.MyDemoAPI.id}"
  resource_id          = "${aws_api_gateway_resource.MyDemoResource.id}"
  http_method          = "${aws_api_gateway_method.MyDemoMethod.http_method}"
  type                 = "MOCK"
  cache_key_parameters = ["method.request.path.param"]
  cache_namespace      = "foobar"

  request_parameters = {
    "integration.request.header.X-Authorization" = "'static'"
  }

  # Transforms the incoming XML request to JSON
  request_templates {
    "application/xml" = &lt;&lt;EOF
{
   "body" : $input.json('$')
}
EOF
  }
}
</pre>
<h2 id="lambda-integration">  Lambda integration </h2> <pre data-language="ruby"># Variables
variable "myregion" {}
variable "accountId" {}

# API Gateway
resource "aws_api_gateway_rest_api" "api" {
  name = "myapi"
}

resource "aws_api_gateway_resource" "resource" {
  path_part = "resource"
  parent_id = "${aws_api_gateway_rest_api.api.root_resource_id}"
  rest_api_id = "${aws_api_gateway_rest_api.api.id}"
}

resource "aws_api_gateway_method" "method" {
  rest_api_id   = "${aws_api_gateway_rest_api.api.id}"
  resource_id   = "${aws_api_gateway_resource.resource.id}"
  http_method   = "GET"
  authorization = "NONE"
}

resource "aws_api_gateway_integration" "integration" {
  rest_api_id             = "${aws_api_gateway_rest_api.api.id}"
  resource_id             = "${aws_api_gateway_resource.resource.id}"
  http_method             = "${aws_api_gateway_method.method.http_method}"
  integration_http_method = "POST"
  type                    = "AWS_PROXY"
  uri                     = "arn:aws:apigateway:${var.myregion}:lambda:path/2015-03-31/functions/${aws_lambda_function.lambda.arn}/invocations"
}

# Lambda
resource "aws_lambda_permission" "apigw_lambda" {
  statement_id  = "AllowExecutionFromAPIGateway"
  action        = "lambda:InvokeFunction"
  function_name = "${aws_lambda_function.lambda.arn}"
  principal     = "apigateway.amazonaws.com"

  # More: http://docs.aws.amazon.com/apigateway/latest/developerguide/api-gateway-control-access-using-iam-policies-to-invoke-api.html
  source_arn = "arn:aws:execute-api:${var.myregion}:${var.accountId}:${aws_api_gateway_rest_api.api.id}/*/${aws_api_gateway_method.method.http_method}${aws_api_gateway_resource.resource.path}"
}

resource "aws_lambda_function" "lambda" {
  filename         = "lambda.zip"
  function_name    = "mylambda"
  role             = "${aws_iam_role.role.arn}"
  handler          = "lambda.lambda_handler"
  runtime          = "python2.7"
  source_code_hash = "${base64sha256(file("lambda.zip"))}"
}

# IAM
resource "aws_iam_role" "role" {
  name = "myrole"

  assume_role_policy = &lt;&lt;POLICY
{
  "Version": "2012-10-17",
  "Statement": [
    {
      "Action": "sts:AssumeRole",
      "Principal": {
        "Service": "lambda.amazonaws.com"
      },
      "Effect": "Allow",
      "Sid": ""
    }
  ]
}
POLICY
}
</pre>
<h2 id="vpc-link">  VPC Link </h2> <pre data-language="ruby">variable "name" {}
variable "subnet_id" {}

resource "aws_lb" "test" {
  name               = "${var.name}"
  internal           = true
  load_balancer_type = "network"
  subnets            = ["${var.subnet_id}"]
}

resource "aws_api_gateway_vpc_link" "test" {
  name        = "${var.name}"
  target_arns = ["${aws_lb.test.arn}"]
}

resource "aws_api_gateway_rest_api" "test" {
  name = "${var.name}"
}

resource "aws_api_gateway_resource" "test" {
  rest_api_id = "${aws_api_gateway_rest_api.test.id}"
  parent_id   = "${aws_api_gateway_rest_api.test.root_resource_id}"
  path_part   = "test"
}

resource "aws_api_gateway_method" "test" {
  rest_api_id   = "${aws_api_gateway_rest_api.test.id}"
  resource_id   = "${aws_api_gateway_resource.test.id}"
  http_method   = "GET"
  authorization = "NONE"

  request_models = {
    "application/json" = "Error"
  }
}

resource "aws_api_gateway_integration" "test" {
  rest_api_id = "${aws_api_gateway_rest_api.test.id}"
  resource_id = "${aws_api_gateway_resource.test.id}"
  http_method = "${aws_api_gateway_method.test.http_method}"

  request_templates = {
    "application/json" = ""
    "application/xml"  = "#set($inputRoot = $input.path('$'))\n{ }"
  }

  request_parameters = {
    "integration.request.header.X-Authorization" = "'static'"
    "integration.request.header.X-Foo"           = "'Bar'"
  }

  type                    = "HTTP"
  uri                     = "https://www.google.de"
  integration_http_method = "GET"
  passthrough_behavior    = "WHEN_NO_MATCH"
  content_handling        = "CONVERT_TO_TEXT"

  connection_type = "VPC_LINK"
  connection_id   = "${aws_api_gateway_vpc_link.test.id}"
}
</pre>
<h2 id="argument-reference">  Argument Reference </h2> <p>The following arguments are supported:</p> <ul> <li>
<a href="#rest_api_id"><code>rest_api_id</code></a> - (Required) The ID of the associated REST API. </li> <li>
<a href="#resource_id"><code>resource_id</code></a> - (Required) The API resource ID. </li> <li>
<a href="#http_method"><code>http_method</code></a> - (Required) The HTTP method (<code>GET</code>, <code>POST</code>, <code>PUT</code>, <code>DELETE</code>, <code>HEAD</code>, <code>OPTION</code>, <code>ANY</code>) when calling the associated resource. </li> <li>
<a href="#integration_http_method"><code>integration_http_method</code></a> - (Optional) The integration HTTP method (<code>GET</code>, <code>POST</code>, <code>PUT</code>, <code>DELETE</code>, <code>HEAD</code>, <code>OPTION</code>) specifying how API Gateway will interact with the back end. <strong>Required</strong> if <code>type</code> is <code>AWS</code>, <code>AWS_PROXY</code>, <code>HTTP</code> or <code>HTTP_PROXY</code>. Not all methods are compatible with all <code>AWS</code> integrations. e.g. Lambda function <a href="https://github.com/awslabs/aws-apigateway-importer/issues/9#issuecomment-129651005">can only be invoked</a> via <code>POST</code>. </li> <li>
<a href="#type"><code>type</code></a> - (Required) The integration input's <a href="https://docs.aws.amazon.com/apigateway/api-reference/resource/integration/">type</a>. Valid values are <code>HTTP</code> (for HTTP backends), <code>MOCK</code> (not calling any real backend), <code>AWS</code> (for AWS services), <code>AWS_PROXY</code> (for Lambda proxy integration) and <code>HTTP_PROXY</code> (for HTTP proxy integration). An <code>HTTP</code> or <code>HTTP_PROXY</code> integration with a <code>connection_type</code> of <code>VPC_LINK</code> is referred to as a private integration and uses a VpcLink to connect API Gateway to a network load balancer of a VPC. </li> <li>
<a href="#connection_type"><code>connection_type</code></a> - (Optional) The integration input's <a href="https://docs.aws.amazon.com/apigateway/api-reference/resource/integration/#connectionType">connectionType</a>. Valid values are <code>INTERNET</code> (default for connections through the public routable internet), and <code>VPC_LINK</code> (for private connections between API Gateway and a network load balancer in a VPC). </li> <li>
<a href="#connection_id"><code>connection_id</code></a> - (Optional) The id of the VpcLink used for the integration. <strong>Required</strong> if <code>connection_type</code> is <code>VPC_LINK</code> </li> <li>
<a href="#uri"><code>uri</code></a> - (Optional) The input's URI (HTTP, AWS). <strong>Required</strong> if <code>type</code> is <code>HTTP</code> or <code>AWS</code>. For HTTP integrations, the URI must be a fully formed, encoded HTTP(S) URL according to the RFC-3986 specification . For AWS integrations, the URI should be of the form <code>arn:aws:apigateway:{region}:{subdomain.service|service}:{path|action}/{service_api}</code>. <code>region</code>, <code>subdomain</code> and <code>service</code> are used to determine the right endpoint. e.g. <code>arn:aws:apigateway:eu-west-1:lambda:path/2015-03-31/functions/arn:aws:lambda:eu-west-1:012345678901:function:my-func/invocations</code> </li> <li>
<a href="#credentials"><code>credentials</code></a> - (Optional) The credentials required for the integration. For <code>AWS</code> integrations, 2 options are available. To specify an IAM Role for Amazon API Gateway to assume, use the role's ARN. To require that the caller's identity be passed through from the request, specify the string <code>arn:aws:iam::\*:user/\*</code>. </li> <li>
<a href="#request_templates"><code>request_templates</code></a> - (Optional) A map of the integration's request templates. </li> <li>
<a href="#request_parameters"><code>request_parameters</code></a> - (Optional) A map of request query string parameters and headers that should be passed to the backend responder. For example: <code>request_parameters = { "integration.request.header.X-Some-Other-Header" = "method.request.header.X-Some-Header" }</code> </li> <li>
<a href="#passthrough_behavior"><code>passthrough_behavior</code></a> - (Optional) The integration passthrough behavior (<code>WHEN_NO_MATCH</code>, <code>WHEN_NO_TEMPLATES</code>, <code>NEVER</code>). <strong>Required</strong> if <code>request_templates</code> is used. </li> <li>
<a href="#cache_key_parameters"><code>cache_key_parameters</code></a> - (Optional) A list of cache key parameters for the integration. </li> <li>
<a href="#cache_namespace"><code>cache_namespace</code></a> - (Optional) The integration's cache namespace. </li> <li>
<a href="#request_parameters_in_json"><code>request_parameters_in_json</code></a> - <strong>Deprecated</strong>, use <code>request_parameters</code> instead. </li> <li>
<a href="#content_handling"><code>content_handling</code></a> - (Optional) Specifies how to handle request payload content type conversions. Supported values are <code>CONVERT_TO_BINARY</code> and <code>CONVERT_TO_TEXT</code>. If this property is not defined, the request payload will be passed through from the method request to integration request without modification, provided that the passthroughBehaviors is configured to support payload pass-through. </li> </ul><div class="_attribution">
  <p class="_attribution-p">
    &copy; 2018 HashiCorp</br>Licensed under the MPL 2.0 License.<br>
    <a href="https://www.terraform.io/docs/providers/aws/r/api_gateway_integration.html" class="_attribution-link">https://www.terraform.io/docs/providers/aws/r/api_gateway_integration.html</a>
  </p>
</div>
