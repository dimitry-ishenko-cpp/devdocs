<h1 id="aws_elb_load_balancer_policy">  aws_elb_load_balancer_policy </h1> <p>Provides a load balancer policy, which can be attached to an ELB listener or backend server.</p> <h2 id="example-usage">  Example Usage </h2> <pre data-language="ruby">resource "aws_elb" "wu-tang" {
  name               = "wu-tang"
  availability_zones = ["us-east-1a"]

  listener {
    instance_port      = 443
    instance_protocol  = "http"
    lb_port            = 443
    lb_protocol        = "https"
    ssl_certificate_id = "arn:aws:iam::000000000000:server-certificate/wu-tang.net"
  }

  tags {
    Name = "wu-tang"
  }
}

resource "aws_load_balancer_policy" "wu-tang-ca-pubkey-policy" {
  load_balancer_name = "${aws_elb.wu-tang.name}"
  policy_name        = "wu-tang-ca-pubkey-policy"
  policy_type_name   = "PublicKeyPolicyType"

  policy_attribute = {
    name  = "PublicKey"
    value = "${file("wu-tang-pubkey")}"
  }
}

resource "aws_load_balancer_policy" "wu-tang-root-ca-backend-auth-policy" {
  load_balancer_name = "${aws_elb.wu-tang.name}"
  policy_name        = "wu-tang-root-ca-backend-auth-policy"
  policy_type_name   = "BackendServerAuthenticationPolicyType"

  policy_attribute = {
    name  = "PublicKeyPolicyName"
    value = "${aws_load_balancer_policy.wu-tang-root-ca-pubkey-policy.policy_name}"
  }
}

resource "aws_load_balancer_policy" "wu-tang-ssl" {
  load_balancer_name = "${aws_elb.wu-tang.name}"
  policy_name        = "wu-tang-ssl"
  policy_type_name   = "SSLNegotiationPolicyType"

  policy_attribute = {
    name  = "ECDHE-ECDSA-AES128-GCM-SHA256"
    value = "true"
  }

  policy_attribute = {
    name  = "Protocol-TLSv1.2"
    value = "true"
  }
}

resource "aws_load_balancer_backend_server_policy" "wu-tang-backend-auth-policies-443" {
  load_balancer_name = "${aws_elb.wu-tang.name}"
  instance_port      = 443

  policy_names = [
    "${aws_load_balancer_policy.wu-tang-root-ca-backend-auth-policy.policy_name}",
  ]
}

resource "aws_load_balancer_listener_policy" "wu-tang-listener-policies-443" {
  load_balancer_name = "${aws_elb.wu-tang.name}"
  load_balancer_port = 443

  policy_names = [
    "${aws_load_balancer_policy.wu-tang-ssl.policy_name}",
  ]
}
</pre>
<p>Where the file <code>pubkey</code> in the current directory contains only the <em>public key</em> of the certificate.</p> <pre data-language="shell">cat wu-tang-ca.pem | openssl x509 -pubkey -noout | grep -v '\-\-\-\-' | tr -d '\n' &gt; wu-tang-pubkey
</pre>
<p>This example shows how to enable backend authentication for an ELB as well as customize the TLS settings.</p> <h2 id="argument-reference">  Argument Reference </h2> <p>The following arguments are supported:</p> <ul> <li>
<a href="#load_balancer_name"><code>load_balancer_name</code></a> - (Required) The load balancer on which the policy is defined. </li> <li>
<a href="#policy_name"><code>policy_name</code></a> - (Required) The name of the load balancer policy. </li> <li>
<a href="#policy_type_name"><code>policy_type_name</code></a> - (Required) The policy type. </li> <li>
<a href="#policy_attribute"><code>policy_attribute</code></a> - (Optional) Policy attribute to apply to the policy. </li> </ul> <h2 id="attributes-reference">  Attributes Reference </h2> <p>In addition to all arguments above, the following attributes are exported:</p> <ul> <li>
<a href="#id"><code>id</code></a> - The ID of the policy. </li> <li>
<a href="#policy_name-1"><code>policy_name</code></a> - The name of the stickiness policy. </li> <li>
<a href="#policy_type_name-1"><code>policy_type_name</code></a> - The policy type of the policy. </li> <li>
<a href="#load_balancer_name-1"><code>load_balancer_name</code></a> - The load balancer on which the policy is defined. </li> </ul><div class="_attribution">
  <p class="_attribution-p">
    &copy; 2018 HashiCorp</br>Licensed under the MPL 2.0 License.<br>
    <a href="https://www.terraform.io/docs/providers/aws/r/load_balancer_policy.html" class="_attribution-link">https://www.terraform.io/docs/providers/aws/r/load_balancer_policy.html</a>
  </p>
</div>
