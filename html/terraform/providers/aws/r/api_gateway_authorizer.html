<h1 id="aws_api_gateway_authorizer">  aws_api_gateway_authorizer </h1> <p>Provides an API Gateway Authorizer.</p> <h2 id="example-usage">  Example Usage </h2> <pre data-language="ruby">resource "aws_api_gateway_authorizer" "demo" {
  name                   = "demo"
  rest_api_id            = "${aws_api_gateway_rest_api.demo.id}"
  authorizer_uri         = "${aws_lambda_function.authorizer.invoke_arn}"
  authorizer_credentials = "${aws_iam_role.invocation_role.arn}"
}

resource "aws_api_gateway_rest_api" "demo" {
  name = "auth-demo"
}

resource "aws_iam_role" "invocation_role" {
  name = "api_gateway_auth_invocation"
  path = "/"

  assume_role_policy = &lt;&lt;EOF
{
  "Version": "2012-10-17",
  "Statement": [
    {
      "Action": "sts:AssumeRole",
      "Principal": {
        "Service": "apigateway.amazonaws.com"
      },
      "Effect": "Allow",
      "Sid": ""
    }
  ]
}
EOF
}

resource "aws_iam_role_policy" "invocation_policy" {
  name = "default"
  role = "${aws_iam_role.invocation_role.id}"

  policy = &lt;&lt;EOF
{
  "Version": "2012-10-17",
  "Statement": [
    {
      "Action": "lambda:InvokeFunction",
      "Effect": "Allow",
      "Resource": "${aws_lambda_function.authorizer.arn}"
    }
  ]
}
EOF
}

resource "aws_iam_role" "lambda" {
  name = "demo-lambda"

  assume_role_policy = &lt;&lt;EOF
{
  "Version": "2012-10-17",
  "Statement": [
    {
      "Action": "sts:AssumeRole",
      "Principal": {
        "Service": "lambda.amazonaws.com"
      },
      "Effect": "Allow",
      "Sid": ""
    }
  ]
}
EOF
}

resource "aws_lambda_function" "authorizer" {
  filename         = "lambda-function.zip"
  source_code_hash = "${base64sha256(file("lambda-function.zip"))}"
  function_name    = "api_gateway_authorizer"
  role             = "${aws_iam_role.lambda.arn}"
  handler          = "exports.example"
}
</pre>
<h2 id="argument-reference">  Argument Reference </h2> <p>The following arguments are supported:</p> <ul> <li>
<a href="#authorizer_uri"><code>authorizer_uri</code></a> - (Optional, required for type <code>TOKEN</code>/<code>REQUEST</code>) The authorizer's Uniform Resource Identifier (URI). This must be a well-formed Lambda function URI in the form of <code>arn:aws:apigateway:{region}:lambda:path/{service_api}</code>, e.g. <code>arn:aws:apigateway:us-west-2:lambda:path/2015-03-31/functions/arn:aws:lambda:us-west-2:012345678912:function:my-function/invocations</code> </li> <li>
<a href="#name"><code>name</code></a> - (Required) The name of the authorizer </li> <li>
<a href="#rest_api_id"><code>rest_api_id</code></a> - (Required) The ID of the associated REST API </li> <li>
<a href="#identity_source"><code>identity_source</code></a> - (Optional) The source of the identity in an incoming request. Defaults to <code>method.request.header.Authorization</code>. For <code>REQUEST</code> type, this may be a comma-separated list of values, including headers, query string parameters and stage variables - e.g. <code>"method.request.header.SomeHeaderName,method.request.querystring.SomeQueryStringName,stageVariables.SomeStageVariableName"</code> </li> <li>
<a href="#type"><code>type</code></a> - (Optional) The type of the authorizer. Possible values are <code>TOKEN</code> for a Lambda function using a single authorization token submitted in a custom header, <code>REQUEST</code> for a Lambda function using incoming request parameters, or <code>COGNITO_USER_POOLS</code> for using an Amazon Cognito user pool. Defaults to <code>TOKEN</code>. </li> <li>
<a href="#authorizer_credentials"><code>authorizer_credentials</code></a> - (Optional) The credentials required for the authorizer. To specify an IAM Role for API Gateway to assume, use the IAM Role ARN. </li> <li>
<a href="#authorizer_result_ttl_in_seconds"><code>authorizer_result_ttl_in_seconds</code></a> - (Optional) The TTL of cached authorizer results in seconds. Defaults to <code>300</code>. </li> <li>
<a href="#identity_validation_expression"><code>identity_validation_expression</code></a> - (Optional) A validation expression for the incoming identity. For <code>TOKEN</code> type, this value should be a regular expression. The incoming token from the client is matched against this expression, and will proceed if the token matches. If the token doesn't match, the client receives a 401 Unauthorized response. </li> <li>
<a href="#provider_arns"><code>provider_arns</code></a> - (Optional, required for type <code>COGNITO_USER_POOLS</code>) A list of the Amazon Cognito user pool ARNs. Each element is of this format: <code>arn:aws:cognito-idp:{region}:{account_id}:userpool/{user_pool_id}</code>. </li> </ul><div class="_attribution">
  <p class="_attribution-p">
    &copy; 2018 HashiCorp</br>Licensed under the MPL 2.0 License.<br>
    <a href="https://www.terraform.io/docs/providers/aws/r/api_gateway_authorizer.html" class="_attribution-link">https://www.terraform.io/docs/providers/aws/r/api_gateway_authorizer.html</a>
  </p>
</div>
