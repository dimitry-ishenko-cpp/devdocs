<div class="innertube"> <h1 id="Dependencies between Test Cases and Suites">13 Dependencies between Test Cases and Suites</h1> <h2 id="general" class="title-link"> <div class="title-name">13.1 General</div>  </h2> <p>When creating test suites, it is strongly recommended to not create dependencies between test cases, that is, letting test cases depend on the result of previous test cases. There are various reasons for this, such as, the following:</p> <ul> <li>It makes it impossible to run test cases individually.</li> <li>It makes it impossible to run test cases in a different order.</li> <li>It makes debugging difficult (as a fault can be the result of a problem in a different test case than the one failing).</li> <li>There are no good and explicit ways to declare dependencies, so it can be difficult to see and understand these in test suite code and in test logs.</li> <li>Extending, restructuring, and maintaining test suites with test case dependencies is difficult.</li> </ul> <p>There are often sufficient means to work around the need for test case dependencies. Generally, the problem is related to the state of the System Under Test (SUT). The action of one test case can change the system state. For some other test case to run properly, this new state must be known.</p> <p>Instead of passing data between test cases, it is recommended that the test cases read the state from the SUT and perform assertions (that is, let the test case run if the state is as expected, otherwise reset or fail). It is also recommended to use the state to set variables necessary for the test case to execute properly. Common actions can often be implemented as library functions for test cases to call to set the SUT in a required state. (Such common actions can also be separately tested, if necessary, to ensure that they work as expected). It is sometimes also possible, but not always desirable, to group tests together in one test case, that is, let a test case perform a "scenario" test (a test consisting of subtests).</p> <p>Consider, for example, a server application under test. The following functionality is to be tested:</p> <ul> <li>Starting the server</li> <li>Configuring the server</li> <li>Connecting a client to the server</li> <li>Disconnecting a client from the server</li> <li>Stopping the server</li> </ul> <p>There are obvious dependencies between the listed functions. The server cannot be configured if it has not first been started, a client cannot be connected until the server is properly configured, and so on. If we want to have one test case for each function, we might be tempted to try to always run the test cases in the stated order and carry possible data (identities, handles, and so on) between the cases and therefore introduce dependencies between them.</p> <p>To avoid this, we can consider starting and stopping the server for every test. We can thus implement the start and stop action as common functions to be called from <code><a href="ct_suite.html#Module:init_per_testcase-2">init_per_testcase</a></code> and <code><a href="ct_suite.html#Module:end_per_testcase-2">end_per_testcase</a></code>. (Remember to test the start and stop functionality separately.) The configuration can also be implemented as a common function, maybe grouped with the start function. Finally, the testing of connecting and disconnecting a client can be grouped into one test case. The resulting suite can look as follows:</p> <pre data-language="erlang">     
-module(my_server_SUITE).
-compile(export_all).
-include_lib("ct.hrl").

%%% init and end functions...

suite() -&gt; [{require,my_server_cfg}].

init_per_testcase(start_and_stop, Config) -&gt;
    Config;

init_per_testcase(config, Config) -&gt;
    [{server_pid,start_server()} | Config];

init_per_testcase(_, Config) -&gt;
    ServerPid = start_server(),
    configure_server(),
    [{server_pid,ServerPid} | Config].

end_per_testcase(start_and_stop, _) -&gt;
    ok;

end_per_testcase(_, _) -&gt;
    ServerPid = ?config(server_pid),
    stop_server(ServerPid).

%%% test cases...

all() -&gt; [start_and_stop, config, connect_and_disconnect].

%% test that starting and stopping works
start_and_stop(_) -&gt;
    ServerPid = start_server(),
    stop_server(ServerPid).

%% configuration test
config(Config) -&gt;
    ServerPid = ?config(server_pid, Config),
    configure_server(ServerPid).

%% test connecting and disconnecting client
connect_and_disconnect(Config) -&gt;
    ServerPid = ?config(server_pid, Config),
    {ok,SessionId} = my_server:connect(ServerPid),
    ok = my_server:disconnect(ServerPid, SessionId).

%%% common functions...

start_server() -&gt;
    {ok,ServerPid} = my_server:start(),
    ServerPid.

stop_server(ServerPid) -&gt;
    ok = my_server:stop(),
    ok.

configure_server(ServerPid) -&gt;
    ServerCfgData = ct:get_config(my_server_cfg),
    ok = my_server:configure(ServerPid, ServerCfgData),
    ok.</pre> <h2 id="saving-configuration-data" class="title-link"> <div class="title-name">13.2 Saving Configuration Data</div>  </h2> <p>Sometimes it is impossible, or infeasible, to implement independent test cases. Maybe it is not possible to read the SUT state. Maybe resetting the SUT is impossible and it takes too long time to restart the system. In situations where test case dependency is necessary, CT offers a structured way to carry data from one test case to the next. The same mechanism can also be used to carry data from one test suite to the next.</p> <p>The mechanism for passing data is called <code>save_config</code>. The idea is that one test case (or suite) can save the current value of <code>Config</code>, or any list of key-value tuples, so that the next executing test case (or test suite) can read it. The configuration data is not saved permanently but can only be passed from one case (or suite) to the next.</p> <p>To save <code>Config</code> data, return tuple <code>{save_config,ConfigList}</code> from <code>end_per_testcase</code> or from the main test case function.</p> <p>To read data saved by a previous test case, use macro <code>config</code> with a <code>saved_config</code> key as follows:</p> <p><code>{Saver,ConfigList} = ?config(saved_config, Config)</code></p> <p><code>Saver</code> (<code>atom()</code>) is the name of the previous test case (where the data was saved). The <code>config</code> macro can be used to extract particular data also from the recalled <code>ConfigList</code>. It is strongly recommended that <code>Saver</code> is always matched to the expected name of the saving test case. This way, problems because of restructuring of the test suite can be avoided. Also, it makes the dependency more explicit and the test suite easier to read and maintain.</p> <p>To pass data from one test suite to another, the same mechanism is used. The data is to be saved by finction <code><a href="ct_suite.html#Module:end_per_suite-1">end_per_suite</a></code> and read by function <code><a href="ct_suite.html#Module:init_per_suite-1">init_per_suite</a></code> in the suite that follows. When passing data between suites, <code>Saver</code> carries the name of the test suite.</p> <p><strong>Example:</strong></p> <pre data-language="erlang">
-module(server_b_SUITE).
-compile(export_all).
-include_lib("ct.hrl").

%%% init and end functions...

init_per_suite(Config) -&gt;
    %% read config saved by previous test suite
    {server_a_SUITE,OldConfig} = ?config(saved_config, Config),
    %% extract server identity (comes from server_a_SUITE)
    ServerId = ?config(server_id, OldConfig),
    SessionId = connect_to_server(ServerId),
    [{ids,{ServerId,SessionId}} | Config].

end_per_suite(Config) -&gt;
    %% save config for server_c_SUITE (session_id and server_id)
    {save_config,Config}

%%% test cases...

all() -&gt; [allocate, deallocate].

allocate(Config) -&gt;
    {ServerId,SessionId} = ?config(ids, Config),
    {ok,Handle} = allocate_resource(ServerId, SessionId),
    %% save handle for deallocation test
    NewConfig = [{handle,Handle}],
    {save_config,NewConfig}.

deallocate(Config) -&gt;
    {ServerId,SessionId} = ?config(ids, Config),
    {allocate,OldConfig} = ?config(saved_config, Config),
    Handle = ?config(handle, OldConfig),
    ok = deallocate_resource(ServerId, SessionId, Handle).</pre> <p>To save <code>Config</code> data from a test case that is to be skipped, return tuple <code>{skip_and_save,Reason,ConfigList}</code>.</p> <p>The result is that the test case is skipped with <code>Reason</code> printed to the log file (as described earlier) and <code>ConfigList</code> is saved for the next test case. <code>ConfigList</code> can be read using <code>?config(saved_config, Config)</code>, as described earlier. <code>skip_and_save</code> can also be returned from <code>init_per_suite</code>. In this case, the saved data can be read by <code>init_per_suite</code> in the suite that follows.</p> <h2 id="sequences" class="title-link"> <div class="title-name">13.3 Sequences</div>  </h2> <p>Sometimes test cases depend on each other so that if one case fails, the following tests are not to be executed. Typically, if the <code>save_config</code> facility is used and a test case that is expected to save data crashes, the following case cannot run. <code>Common Test</code> offers a way to declare such dependencies, called sequences.</p> <p>A sequence of test cases is defined as a test case group with a <code>sequence</code> property. Test case groups are defined through function <code>groups/0</code> in the test suite (for details, see section <code><a href="write_test_chapter.html#test_case_groups">Test Case Groups</a></code>.</p> <p>For example, to ensure that if <code>allocate</code> in <code>server_b_SUITE</code> crashes, <code>deallocate</code> is skipped, the following sequence can be defined:</p> <pre data-language="erlang">
groups() -&gt; [{alloc_and_dealloc, [sequence], [alloc,dealloc]}].</pre> <p>Assume that the suite contains the test case <code>get_resource_status</code> that is independent of the other two cases, then function <code>all</code> can look as follows:</p> <pre data-language="erlang">
all() -&gt; [{group,alloc_and_dealloc}, get_resource_status].</pre> <p>If <code>alloc</code> succeeds, <code>dealloc</code> is also executed. If <code>alloc</code> fails however, <code>dealloc</code> is not executed but marked as <code>SKIPPED</code> in the HTML log. <code>get_resource_status</code> runs no matter what happens to the <code>alloc_and_dealloc</code> cases.</p> <p>Test cases in a sequence are executed in order until all succeed or one fails. If one fails, all following cases in the sequence are skipped. The cases in the sequence that have succeeded up to that point are reported as successful in the log. Any number of sequences can be specified.</p> <p><strong>Example:</strong></p> <pre data-language="erlang">
 groups() -&gt; [{scenarioA, [sequence], [testA1, testA2]}, 
              {scenarioB, [sequence], [testB1, testB2, testB3]}].

 all() -&gt; [test1, 
           test2, 
           {group,scenarioA}, 
test3, 
           {group,scenarioB}, 
           test4].</pre> <p>A sequence group can have subgroups. Such subgroups can have any property, that is, they are not required to also be sequences. If you want the status of the subgroup to affect the sequence on the level above, return <code>{return_group_result,Status}</code> from <code><a href="ct_suite.html#Module:end_per_group-2">end_per_group/2</a></code>, as described in section <code><a href="write_test_chapter.html#repeated_groups">Repeated Groups</a></code> in Writing Test Suites. A failed subgroup (<code>Status == failed</code>) causes the execution of a sequence to fail in the same way a test case does.</p> </div><div class="_attribution">
  <p class="_attribution-p">
    &copy; 2010&ndash;2023 Ericsson AB<br>Licensed under the Apache License, Version 2.0.<br>
    
  </p>
</div>
