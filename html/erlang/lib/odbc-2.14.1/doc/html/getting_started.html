<div class="innertube"> <h1 id="Getting started">2 Getting started</h1> <h2 id="setting-things-up-" class="title-link"> <div class="title-name">2.1 Setting things up </div>  </h2> <p>As the Erlang ODBC application is dependent on third party products there are a few administrative things that needs to be done before you can get things up and running.</p>  <ul> <li>The first thing you need to do, is to make sure you have an ODBC driver installed for the database that you want to access. Both the client machine where you plan to run your erlang node and the server machine running the database needs the the ODBC driver. (In some cases the client and the server may be the same machine).</li> <li>Secondly you might need to set environment variables and paths to appropriate values. This may differ a lot between different os's, databases and ODBC drivers. This is a configuration problem related to the third party product and hence we cannot give you a standard solution in this guide.</li> <li>The Erlang ODBC application consists of both <code>Erlang</code> and <code>C</code> code. The <code>C</code> code is delivered as a precompiled executable for windows, solaris and linux (SLES10) in the commercial build. In the open source distribution it is built the same way as all other application using configure and make. You may want to provide the the path to your ODBC libraries using --with-odbc=PATH. </li> </ul> <div class="note"> <div class="label">Note</div> <div class="content">

<p>The Erlang ODBC application should run on all Unix dialects including Linux, Windows 2000, Windows XP and NT. But currently it is only tested for Solaris, Windows 2000, Windows XP and NT.</p> </div> </div> <h2 id="using-the-erlang-api" class="title-link"> <div class="title-name">2.2 Using the Erlang API</div>  </h2> <p>The following dialog within the Erlang shell illustrates the functionality of the Erlang ODBC interface. The table used in the example does not have any relevance to anything that exist in reality, it is just a simple example. The example was created using <code>sqlserver 7.0 with servicepack 1</code> as database and the ODBC driver for <code>sqlserver</code> with version <code>2000.80.194.00</code>.</p> <pre data-language="erlang">1 &gt; odbc:start().
     ok    </pre> <p>Connect to the database </p> <pre data-language="erlang">2 &gt; {ok, Ref} = odbc:connect("DSN=sql-server;UID=aladdin;PWD=sesame", []).
     {ok,&lt;0.342.0&gt;}    </pre> <p>Create a table </p> <pre data-language="erlang">3 &gt; odbc:sql_query(Ref, "CREATE TABLE EMPLOYEE (NR integer,
     FIRSTNAME  char varying(20), LASTNAME  char varying(20), GENDER char(1),
     PRIMARY KEY(NR))").
     {updated,undefined}    </pre> <p>Insert some data </p> <pre data-language="erlang">4 &gt; odbc:sql_query(Ref, "INSERT INTO EMPLOYEE VALUES(1, 'Jane', 'Doe', 'F')").
     {updated,1}    </pre> <p>Check what data types the database assigned for the columns. Hopefully this is not a surprise, some times it can be! These are the data types that you should use if you want to do a parameterized query.</p> <pre data-language="erlang">5 &gt; odbc:describe_table(Ref, "EMPLOYEE").
     {ok, [{"NR", sql_integer},
           {"FIRSTNAME", {sql_varchar, 20}},
           {"LASTNAME", {sql_varchar, 20}}
           {"GENDER", {sql_char, 1}}]}</pre> <h3 id="param_query">  Use a parameterized query to insert many rows in one go. </h3> <pre data-language="erlang">6 &gt; odbc:param_query(Ref,"INSERT INTO EMPLOYEE (NR, FIRSTNAME, "
                 "LASTNAME, GENDER) VALUES(?, ?, ?, ?)",
                  [{sql_integer,[2,3,4,5,6,7,8]},
                   {{sql_varchar, 20},
                            ["John", "Monica", "Ross", "Rachel",
                            "Piper", "Prue", "Louise"]},
                  {{sql_varchar, 20},
                            ["Doe","Geller","Geller", "Green",
                             "Halliwell", "Halliwell", "Lane"]},
                  {{sql_char, 1}, ["M","F","M","F","F","F","F"]}]).
     {updated, 7}</pre> <p>Fetch all data in the table employee </p> <pre data-language="erlang">7&gt; odbc:sql_query(Ref, "SELECT * FROM EMPLOYEE").
   {selected,["NR","FIRSTNAME","LASTNAME","GENDER"],
         [{1,"Jane","Doe","F"},
          {2,"John","Doe","M"},
          {3,"Monica","Geller","F"},
          {4,"Ross","Geller","M"},
          {5,"Rachel","Green","F"},
          {6,"Piper","Halliwell","F"},
          {7,"Prue","Halliwell","F"},
          {8,"Louise","Lane","F"}]]}     </pre> <p>Associate a result set containing the whole table <code>EMPLOYEE</code> to the connection. The number of rows in the result set is returned.</p> <pre data-language="erlang">8 &gt; odbc:select_count(Ref, "SELECT * FROM EMPLOYEE").
     {ok,8}     </pre> <p>You can always traverse the result set sequential by using next</p> <pre data-language="erlang">9 &gt; odbc:next(Ref).
     {selected,["NR","FIRSTNAME","LASTNAME","GENDER"],[{1,"Jane","Doe","F"}]}</pre> <pre data-language="erlang">10 &gt; odbc:next(Ref).
     {selected,["NR","FIRSTNAME","LASTNAME","GENDER"],[{2,"John","Doe","M"}]}</pre> <p>If your driver supports scrollable cursors you have a little more freedom, and can do things like this. </p> <pre data-language="erlang">11 &gt; odbc:last(Ref).
     {selected,["NR","FIRSTNAME","LASTNAME","GENDER"],[{8,"Louise","Lane","F"}]}    </pre> <pre data-language="erlang">12 &gt; odbc:prev(Ref).
     {selected,["NR","FIRSTNAME","LASTNAME","GENDER"],[{7,"Prue","Halliwell","F"}]}    </pre> <pre data-language="erlang">13 &gt; odbc:first(Ref).
     {selected,["NR","FIRSTNAME","LASTNAME","GENDER"],[{1,"Jane","Doe","F"}]}        </pre> <pre data-language="erlang">14 &gt; odbc:next(Ref).
     {selected,["NR","FIRSTNAME","LASTNAME","GENDER"],[{2,"John","Doe","M"}]}</pre> <p>Fetch the fields <code>FIRSTNAME</code> and <code>NR</code> for all female employees</p> <pre data-language="erlang">15 &gt; odbc:sql_query(Ref, "SELECT FIRSTNAME, NR FROM EMPLOYEE WHERE GENDER = 'F'").
    {selected,["FIRSTNAME","NR"],
         [{"Jane",1},
          {"Monica",3},
          {"Rachel",5},
          {"Piper",6},
          {"Prue",7},
          {"Louise",8}]}     </pre> <p>Fetch the fields <code>FIRSTNAME</code> and <code>NR</code> for all female employees and sort them on the field <code>FIRSTNAME</code>. </p> <pre data-language="erlang">16 &gt; odbc:sql_query(Ref, "SELECT FIRSTNAME, NR FROM EMPLOYEE WHERE GENDER = 'F'
     ORDER BY FIRSTNAME").
   {selected,["FIRSTNAME","NR"],
         [{"Jane",1},
          {"Louise",8},
          {"Monica",3},
          {"Piper",6},
          {"Prue",7},
          {"Rachel",5}]}</pre> <p>Associate a result set that contains the fields <code>FIRSTNAME</code> and <code>NR</code> for all female employees to the connection. The number of rows in the result set is returned.</p> <pre data-language="erlang">17 &gt; odbc:select_count(Ref, "SELECT FIRSTNAME, NR FROM EMPLOYEE WHERE GENDER = 'F'").
     {ok,6}    </pre> <p>A few more ways of retrieving parts of the result set when the driver supports scrollable cursors. Note that next will work even without support for scrollable cursors. </p> <pre data-language="erlang">18 &gt; odbc:select(Ref, {relative, 2}, 3).
   {selected,["FIRSTNAME","NR"],[{"Monica",3},{"Rachel",5},{"Piper",6}]}</pre> <pre data-language="erlang">19 &gt; odbc:select(Ref, next, 2).
     {selected,["FIRSTNAME","NR"],[{"Prue",7},{"Louise",8}]}</pre> <pre data-language="erlang">20 &gt; odbc:select(Ref, {absolute, 1}, 2).
     {selected,["FIRSTNAME","NR"],[{"Jane",1},{"Monica",3}]}</pre> <pre data-language="erlang">21 &gt; odbc:select(Ref, next, 2).
   {selected,["FIRSTNAME","NR"],[{"Rachel",5},{"Piper",6}]}</pre> <pre data-language="erlang">22 &gt; odbc:select(Ref, {absolute, 1}, 4). 
     {selected,["FIRSTNAME","NR"],
               [{"Jane",1},{"Monica",3},{"Rachel",5},{"Piper",6}]}</pre> <p>Select, using a parameterized query. </p> <pre data-language="erlang">23 &gt; odbc:param_query(Ref, "SELECT * FROM EMPLOYEE WHERE GENDER=?",
     [{{sql_char, 1}, ["M"]}]).
     {selected,["NR","FIRSTNAME","LASTNAME","GENDER"],
               [{2,"John", "Doe", "M"},{4,"Ross","Geller","M"}]} </pre> <p>Delete the table <code>EMPLOYEE</code>.</p> <pre data-language="erlang">24 &gt; odbc:sql_query(Ref, "DROP TABLE EMPLOYEE").
     {updated,undefined}</pre> <p>Shut down the connection. </p> <pre data-language="erlang">25 &gt; odbc:disconnect(Ref).
     ok</pre> <p>Shut down the application. </p> <pre data-language="erlang">26 &gt; odbc:stop().
   =INFO REPORT==== 7-Jan-2004::17:00:59 ===
   application: odbc
   exited: stopped
   type: temporary

   ok</pre> </div><div class="_attribution">
  <p class="_attribution-p">
    &copy; 2010&ndash;2023 Ericsson AB<br>Licensed under the Apache License, Version 2.0.<br>
    
  </p>
</div>
