<div class="innertube"> <h1 id="Records and Macros">5 Records and Macros</h1> <p>Larger programs are usually written as a collection of files with a well-defined interface between the various parts.</p> <h2 id="the-larger-example-divided-into-several-files" class="title-link"> <div class="title-name">5.1 The Larger Example Divided into Several Files</div>  </h2> <p>To illustrate this, the messenger example from the previous section is divided into the following five files:</p> <ul> <li> <p><code>mess_config.hrl</code></p> <p>Header file for configuration data</p> </li> <li> <p><code>mess_interface.hrl</code></p> <p>Interface definitions between the client and the messenger</p> </li> <li> <p><code>user_interface.erl</code></p> <p>Functions for the user interface</p> </li> <li> <p><code>mess_client.erl</code></p> <p>Functions for the client side of the messenger</p> </li> <li> <p><code>mess_server.erl</code></p> <p>Functions for the server side of the messenger</p> </li> </ul> <p>While doing this, the message passing interface between the shell, the client, and the server is cleaned up and is defined using <strong>records</strong>. Also, <strong>macros</strong> are introduced:</p> <pre data-language="erlang">%%%----FILE mess_config.hrl----

%%% Configure the location of the server node,
-define(server_node, messenger@super).

%%%----END FILE----</pre> <pre data-language="erlang">%%%----FILE mess_interface.hrl----

%%% Message interface between client and server and client shell for
%%% messenger program 

%%%Messages from Client to server received in server/1 function.
-record(logon,{client_pid, username}).
-record(message,{client_pid, to_name, message}).
%%% {'EXIT', ClientPid, Reason}  (client terminated or unreachable.

%%% Messages from Server to Client, received in await_result/0 function 
-record(abort_client,{message}).
%%% Messages are: user_exists_at_other_node, 
%%%               you_are_not_logged_on
-record(server_reply,{message}).
%%% Messages are: logged_on
%%%               receiver_not_found
%%%               sent  (Message has been sent (no guarantee)
%%% Messages from Server to Client received in client/1 function
-record(message_from,{from_name, message}).

%%% Messages from shell to Client received in client/1 function
%%% spawn(mess_client, client, [server_node(), Name])
-record(message_to,{to_name, message}).
%%% logoff

%%%----END FILE----</pre> <pre data-language="erlang">%%%----FILE user_interface.erl----

%%% User interface to the messenger program
%%% login(Name)
%%%     One user at a time can log in from each Erlang node in the
%%%     system messenger: and choose a suitable Name. If the Name
%%%     is already logged in at another node or if someone else is
%%%     already logged in at the same node, login will be rejected
%%%     with a suitable error message.

%%% logoff()
%%%     Logs off anybody at that node

%%% message(ToName, Message)
%%%     sends Message to ToName. Error messages if the user of this 
%%%     function is not logged on or if ToName is not logged on at
%%%     any node.

-module(user_interface).
-export([logon/1, logoff/0, message/2]).
-include("mess_interface.hrl").
-include("mess_config.hrl").

logon(Name) -&gt;
    case whereis(mess_client) of 
        undefined -&gt;
            register(mess_client, 
                     spawn(mess_client, client, [?server_node, Name]));
        _ -&gt; already_logged_on
    end.

logoff() -&gt;
    mess_client ! logoff.

message(ToName, Message) -&gt;
    case whereis(mess_client) of % Test if the client is running
        undefined -&gt;
            not_logged_on;
        _ -&gt; mess_client ! #message_to{to_name=ToName, message=Message},
             ok
end.

%%%----END FILE----</pre> <pre data-language="erlang">%%%----FILE mess_client.erl----

%%% The client process which runs on each user node

-module(mess_client).
-export([client/2]).
-include("mess_interface.hrl").

client(Server_Node, Name) -&gt;
    {messenger, Server_Node} ! #logon{client_pid=self(), username=Name},
    await_result(),
    client(Server_Node).

client(Server_Node) -&gt;
    receive
        logoff -&gt;
            exit(normal);
        #message_to{to_name=ToName, message=Message} -&gt;
            {messenger, Server_Node} ! 
                #message{client_pid=self(), to_name=ToName, message=Message},
            await_result();
        {message_from, FromName, Message} -&gt;
            io:format("Message from ~p: ~p~n", [FromName, Message])
    end,
    client(Server_Node).

%%% wait for a response from the server
await_result() -&gt;
    receive
        #abort_client{message=Why} -&gt;
            io:format("~p~n", [Why]),
            exit(normal);
        #server_reply{message=What} -&gt;
            io:format("~p~n", [What])
    after 5000 -&gt;
            io:format("No response from server~n", []),
            exit(timeout)
    end.

%%%----END FILE---</pre> <pre data-language="erlang">%%%----FILE mess_server.erl----

%%% This is the server process of the messenger service

-module(mess_server).
-export([start_server/0, server/0]).
-include("mess_interface.hrl").

server() -&gt;
    process_flag(trap_exit, true),
    server([]).

%%% the user list has the format [{ClientPid1, Name1},{ClientPid22, Name2},...]
server(User_List) -&gt;
    io:format("User list = ~p~n", [User_List]),
    receive
        #logon{client_pid=From, username=Name} -&gt;
            New_User_List = server_logon(From, Name, User_List),
            server(New_User_List);
        {'EXIT', From, _} -&gt;
            New_User_List = server_logoff(From, User_List),
            server(New_User_List);
        #message{client_pid=From, to_name=To, message=Message} -&gt;
            server_transfer(From, To, Message, User_List),
            server(User_List)
    end.

%%% Start the server
start_server() -&gt;
    register(messenger, spawn(?MODULE, server, [])).

%%% Server adds a new user to the user list
server_logon(From, Name, User_List) -&gt;
    %% check if logged on anywhere else
    case lists:keymember(Name, 2, User_List) of
        true -&gt;
            From ! #abort_client{message=user_exists_at_other_node},
            User_List;
        false -&gt;
            From ! #server_reply{message=logged_on},
            link(From),
            [{From, Name} | User_List]        %add user to the list
    end.

%%% Server deletes a user from the user list
server_logoff(From, User_List) -&gt;
    lists:keydelete(From, 1, User_List).

%%% Server transfers a message between user
server_transfer(From, To, Message, User_List) -&gt;
    %% check that the user is logged on and who he is
    case lists:keysearch(From, 1, User_List) of
        false -&gt;
            From ! #abort_client{message=you_are_not_logged_on};
        {value, {_, Name}} -&gt;
            server_transfer(From, Name, To, Message, User_List)
    end.
%%% If the user exists, send the message
server_transfer(From, Name, To, Message, User_List) -&gt;
    %% Find the receiver and send the message
    case lists:keysearch(To, 2, User_List) of
        false -&gt;
            From ! #server_reply{message=receiver_not_found};
        {value, {ToPid, To}} -&gt;
            ToPid ! #message_from{from_name=Name, message=Message}, 
            From !  #server_reply{message=sent} 
    end.

%%%----END FILE---</pre> <h2 id="header-files" class="title-link"> <div class="title-name">5.2 Header Files</div>  </h2> <p>As shown above, some files have extension <code>.hrl</code>. These are header files that are included in the <code>.erl</code> files by:</p> <pre data-language="erlang">-include("File_Name").</pre> <p>for example:</p> <pre data-language="erlang">-include("mess_interface.hrl").</pre> <p>In the case above the file is fetched from the same directory as all the other files in the messenger example. (*manual*).</p> <p>.hrl files can contain any valid Erlang code but are most often used for record and macro definitions.</p> <h2 id="records" class="title-link"> <div class="title-name">5.3 Records</div>  </h2> <p>A record is defined as:</p> <pre data-language="erlang">-record(name_of_record,{field_name1, field_name2, field_name3, ......}).</pre> <p>For example:</p> <pre data-language="erlang">-record(message_to,{to_name, message}).</pre> <p>This is equivalent to:</p> <pre data-language="erlang">{message_to, To_Name, Message}</pre> <p>Creating a record is best illustrated by an example:</p> <pre data-language="erlang">#message_to{message="hello", to_name=fred)</pre> <p>This creates:</p> <pre data-language="erlang">{message_to, fred, "hello"}</pre> <p>Notice that you do not have to worry about the order you assign values to the various parts of the records when you create it. The advantage of using records is that by placing their definitions in header files you can conveniently define interfaces that are easy to change. For example, if you want to add a new field to the record, you only have to change the code where the new field is used and not at every place the record is referred to. If you leave out a field when creating a record, it gets the value of the atom <code>undefined</code>. (*manual*)</p> <p>Pattern matching with records is very similar to creating records. For example, inside a <code>case</code> or <code>receive</code>:</p> <pre data-language="erlang">#message_to{to_name=ToName, message=Message} -&gt;</pre> <p>This is the same as:</p> <pre data-language="erlang">{message_to, ToName, Message}</pre> <h2 id="macros" class="title-link"> <div class="title-name">5.4 Macros</div>  </h2> <p>Another thing that has been added to the messenger is a macro. The file <code>mess_config.hrl</code> contains the definition:</p> <pre data-language="erlang">%%% Configure the location of the server node,
-define(server_node, messenger@super).</pre> <p>This file is included in <code>mess_server.erl</code>:</p> <pre data-language="erlang">-include("mess_config.hrl").</pre> <p>Every occurrence of <code>?server_node</code> in <code>mess_server.erl</code> is now replaced by <code>messenger@super</code>.</p> <p>A macro is also used when spawning the server process:</p> <pre data-language="erlang">spawn(?MODULE, server, [])</pre> <p>This is a standard macro (that is, defined by the system, not by the user). <code>?MODULE</code> is always replaced by the name of the current module (that is, the <code>-module</code> definition near the start of the file). There are more advanced ways of using macros with, for example, parameters (*manual*).</p> <p>The three Erlang (<code>.erl</code>) files in the messenger example are individually compiled into object code file (<code>.beam</code>). The Erlang system loads and links these files into the system when they are referred to during execution of the code. In this case, they are simply put in our current working directory (that is, the place you have done "cd" to). There are ways of putting the <code>.beam</code> files in other directories.</p> <p>In the messenger example, no assumptions have been made about what the message being sent is. It can be any valid Erlang term.</p> </div><div class="_attribution">
  <p class="_attribution-p">
    &copy; 2010&ndash;2023 Ericsson AB<br>Licensed under the Apache License, Version 2.0.<br>
    
  </p>
</div>
