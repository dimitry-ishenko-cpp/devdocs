<div class="innertube"> <h1 id="Tables and Databases">8 Tables and Databases</h1> <h2 id="ets,-dets,-and-mnesia" class="title-link"> <div class="title-name">8.1 Ets, Dets, and Mnesia</div>  </h2> <p>Every example using Ets has a corresponding example in Mnesia. In general, all Ets examples also apply to Dets tables.</p> <h3 id="select-match-operations" class="title-link"> <div class="title-name">Select/Match Operations</div>  </h3> <p>Select/match operations on Ets and Mnesia tables can become very expensive operations. They usually need to scan the complete table. Try to structure the data to minimize the need for select/match operations. However, if you require a select/match operation, it is still more efficient than using <code>tab2list</code>. Examples of this and of how to avoid select/match are provided in the following sections. The functions <code>ets:select/2</code> and <code>mnesia:select/3</code> are to be preferred over <code>ets:match/2</code>, <code>ets:match_object/2</code>, and <code>mnesia:match_object/3</code>.</p> <p>In some circumstances, the select/match operations do not need to scan the complete table. For example, if part of the key is bound when searching an <code>ordered_set</code> table, or if it is a Mnesia table and there is a secondary index on the field that is selected/matched. If the key is fully bound, there is no point in doing a select/match, unless you have a bag table and are only interested in a subset of the elements with the specific key.</p> <p>When creating a record to be used in a select/match operation, you want most of the fields to have the value "_". The easiest and fastest way to do that is as follows:</p> <pre data-language="erlang">
#person{age = 42, _ = '_'}. </pre> <h3 id="deleting-an-element" class="title-link"> <div class="title-name">Deleting an Element</div>  </h3> <p>The <code>delete</code> operation is considered successful if the element was not present in the table. Hence all attempts to check that the element is present in the Ets/Mnesia table before deletion are unnecessary. Here follows an example for Ets tables:</p> <p><strong>DO</strong></p> <pre data-language="erlang">
...
ets:delete(Tab, Key),
...</pre> <p><strong>DO NOT</strong></p> <pre data-language="erlang">
...
case ets:lookup(Tab, Key) of
    [] -&gt;
        ok;
    [_|_] -&gt;
        ets:delete(Tab, Key)
end,
...</pre> <h3 id="fetching-data" class="title-link"> <div class="title-name">Fetching Data</div>  </h3> <p>Do not fetch data that you already have.</p> <p>Consider that you have a module that handles the abstract data type <code>Person</code>. You export the interface function <code>print_person/1</code>, which uses the internal functions <code>print_name/1</code>, <code>print_age/1</code>, and <code>print_occupation/1</code>.</p> <div class="note"> <div class="label">Note</div> <div class="content">

<p>If the function <code>print_name/1</code>, and so on, had been interface functions, the situation would have been different, as you do not want the user of the interface to know about the internal data representation. </p> </div> </div> <p><strong>DO</strong></p> <pre data-language="erlang">%%% Interface function
print_person(PersonId) -&gt;
    %% Look up the person in the named table person,
    case ets:lookup(person, PersonId) of
        [Person] -&gt;
            print_name(Person),
            print_age(Person),
            print_occupation(Person);
        [] -&gt;
            io:format("No person with ID = ~p~n", [PersonID])
    end.

%%% Internal functions
print_name(Person) -&gt; 
    io:format("No person ~p~n", [Person#person.name]).
                      
print_age(Person) -&gt; 
    io:format("No person ~p~n", [Person#person.age]).

print_occupation(Person) -&gt; 
    io:format("No person ~p~n", [Person#person.occupation]).</pre> <p><strong>DO NOT</strong></p> <pre data-language="erlang">%%% Interface function
print_person(PersonId) -&gt;
    %% Look up the person in the named table person,
    case ets:lookup(person, PersonId) of
        [Person] -&gt;
            print_name(PersonID),
            print_age(PersonID),
            print_occupation(PersonID);
        [] -&gt;
            io:format("No person with ID = ~p~n", [PersonID])
    end.

%%% Internal functions
print_name(PersonID) -&gt; 
    [Person] = ets:lookup(person, PersonId),
    io:format("No person ~p~n", [Person#person.name]).

print_age(PersonID) -&gt; 
    [Person] = ets:lookup(person, PersonId),
    io:format("No person ~p~n", [Person#person.age]).

print_occupation(PersonID) -&gt; 
    [Person] = ets:lookup(person, PersonId),
    io:format("No person ~p~n", [Person#person.occupation]).</pre> <h3 id="non-persistent-database-storage" class="title-link"> <div class="title-name">Non-Persistent Database Storage</div>  </h3> <p>For non-persistent database storage, prefer Ets tables over Mnesia <code>local_content</code> tables. Even the Mnesia <code>dirty_write</code> operations carry a fixed overhead compared to Ets writes. Mnesia must check if the table is replicated or has indices, this involves at least one Ets lookup for each <code>dirty_write</code>. Thus, Ets writes is always faster than Mnesia writes.</p> <h3 id="tab2list" class="title-link"> <div class="title-name">tab2list</div>  </h3> <p>Assuming an Ets table that uses <code>idno</code> as key and contains the following:</p> <pre data-language="erlang">
[#person{idno = 1, name = "Adam",  age = 31, occupation = "mailman"},
 #person{idno = 2, name = "Bryan", age = 31, occupation = "cashier"},
 #person{idno = 3, name = "Bryan", age = 35, occupation = "banker"},
 #person{idno = 4, name = "Carl",  age = 25, occupation = "mailman"}]</pre> <p>If you <strong>must</strong> return all data stored in the Ets table, you can use <code>ets:tab2list/1</code>. However, usually you are only interested in a subset of the information in which case <code>ets:tab2list/1</code> is expensive. If you only want to extract one field from each record, for example, the age of every person, then:</p> <p><strong>DO</strong></p> <pre data-language="erlang">
...
ets:select(Tab,[{ #person{idno='_', 
                          name='_', 
                          age='$1', 
                          occupation = '_'},
                [],
                ['$1']}]),
...</pre> <p><strong>DO NOT</strong></p> <pre data-language="erlang">
...
TabList = ets:tab2list(Tab),
lists:map(fun(X) -&gt; X#person.age end, TabList),
...</pre> <p>If you are only interested in the age of all persons named "Bryan", then:</p> <p><strong>DO</strong></p> <pre data-language="erlang">
...
ets:select(Tab,[{ #person{idno='_', 
                          name="Bryan", 
                          age='$1', 
                          occupation = '_'},
                [],
                ['$1']}]),
...</pre> <p><strong>DO NOT</strong></p> <pre data-language="erlang">
...
TabList = ets:tab2list(Tab),
lists:foldl(fun(X, Acc) -&gt; case X#person.name of
                                "Bryan" -&gt;
                                    [X#person.age|Acc];
                                 _ -&gt;
                                     Acc
                           end
             end, [], TabList),
...</pre> <p><strong>REALLY DO NOT</strong></p> <pre data-language="erlang">
...
TabList = ets:tab2list(Tab),
BryanList = lists:filter(fun(X) -&gt; X#person.name == "Bryan" end,
                         TabList),
lists:map(fun(X) -&gt; X#person.age end, BryanList),
...</pre> <p>If you need all information stored in the Ets table about persons named "Bryan", then:</p> <p><strong>DO</strong></p> <pre data-language="erlang">
...
ets:select(Tab, [{#person{idno='_', 
                          name="Bryan", 
                          age='_', 
                          occupation = '_'}, [], ['$_']}]),
...</pre> <p><strong>DO NOT</strong></p> <pre data-language="erlang">
...
TabList = ets:tab2list(Tab),
lists:filter(fun(X) -&gt; X#person.name == "Bryan" end, TabList),
...</pre> <h3 id="ordered_set-tables" class="title-link"> <div class="title-name">Ordered_set Tables</div>  </h3> <p>If the data in the table is to be accessed so that the order of the keys in the table is significant, the table type <code>ordered_set</code> can be used instead of the more usual <code>set</code> table type. An <code>ordered_set</code> is always traversed in Erlang term order regarding the key field so that the return values from functions such as <code>select</code>, <code>match_object</code>, and <code>foldl</code> are ordered by the key values. Traversing an <code>ordered_set</code> with the <code>first</code> and <code>next</code> operations also returns the keys ordered.</p> <div class="note"> <div class="label">Note</div> <div class="content">

<p>An <code>ordered_set</code> only guarantees that objects are processed in <strong>key</strong> order. Results from functions such as <code>ets:select/2</code> appear in <strong>key</strong> order even if the key is not included in the result.</p> </div> </div> <h2 id="ets-specific" class="title-link"> <div class="title-name">8.2 Ets-Specific</div>  </h2> <h3 id="using-keys-of-ets-table" class="title-link"> <div class="title-name">Using Keys of Ets Table</div>  </h3> <p>An Ets table is a single-key table (either a hash table or a tree ordered by the key) and is to be used as one. In other words, use the key to look up things whenever possible. A lookup by a known key in a <code>set</code> Ets table is constant and for an <code>ordered_set</code> Ets table it is O(logN). A key lookup is always preferable to a call where the whole table has to be scanned. In the previous examples, the field <code>idno</code> is the key of the table and all lookups where only the name is known result in a complete scan of the (possibly large) table for a matching result.</p> <p>A simple solution would be to use the <code>name</code> field as the key instead of the <code>idno</code> field, but that would cause problems if the names were not unique. A more general solution would be to create a second table with <code>name</code> as key and <code>idno</code> as data, that is, to index (invert) the table regarding the <code>name</code> field. Clearly, the second table would have to be kept consistent with the master table. Mnesia can do this for you, but a home brew index table can be very efficient compared to the overhead involved in using Mnesia.</p> <p>An index table for the table in the previous examples would have to be a bag (as keys would appear more than once) and can have the following contents:</p> <pre data-language="erlang">
[#index_entry{name="Adam", idno=1},
 #index_entry{name="Bryan", idno=2},
 #index_entry{name="Bryan", idno=3},
 #index_entry{name="Carl", idno=4}]</pre> <p>Given this index table, a lookup of the <code>age</code> fields for all persons named "Bryan" can be done as follows:</p> <pre data-language="erlang">
...
MatchingIDs = ets:lookup(IndexTable,"Bryan"),
lists:map(fun(#index_entry{idno = ID}) -&gt;
                 [#person{age = Age}] = ets:lookup(PersonTable, ID),
                 Age
          end,
          MatchingIDs),
...</pre> <p>Notice that this code never uses <code>ets:match/2</code> but instead uses the <code>ets:lookup/2</code> call. The <code>lists:map/2</code> call is only used to traverse the <code>idno</code>s matching the name "Bryan" in the table; thus the number of lookups in the master table is minimized.</p> <p>Keeping an index table introduces some overhead when inserting records in the table. The number of operations gained from the table must therefore be compared against the number of operations inserting objects in the table. However, notice that the gain is significant when the key can be used to lookup elements.</p> <h2 id="mnesia-specific" class="title-link"> <div class="title-name">8.3 Mnesia-Specific</div>  </h2> <h3 id="secondary-index" class="title-link"> <div class="title-name">Secondary Index</div>  </h3> <p>If you frequently do a lookup on a field that is not the key of the table, you lose performance using "mnesia:select/match_object" as this function traverses the whole table. You can create a secondary index instead and use "mnesia:index_read" to get faster access, however this requires more memory.</p> <p><strong>Example</strong></p> <pre data-language="erlang">
-record(person, {idno, name, age, occupation}).
        ...
{atomic, ok} = 
mnesia:create_table(person, [{index,[#person.age]},
                              {attributes,
                                    record_info(fields, person)}]),
{atomic, ok} = mnesia:add_table_index(person, age), 
...

PersonsAge42 =
     mnesia:dirty_index_read(person, 42, #person.age),
...</pre> <h3 id="transactions-" class="title-link"> <div class="title-name">Transactions </div>  </h3> <p>Using transactions is a way to guarantee that the distributed Mnesia database remains consistent, even when many different processes update it in parallel. However, if you have real-time requirements it is recommended to use <code>dirty</code> operations instead of transactions. When using <code>dirty</code> operations, you lose the consistency guarantee; this is usually solved by only letting one process update the table. Other processes must send update requests to that process.</p> <p><strong>Example</strong></p> <pre data-language="erlang">
...
% Using transaction

Fun = fun() -&gt;
          [mnesia:read({Table, Key}),
           mnesia:read({Table2, Key2})]
      end, 

{atomic, [Result1, Result2]}  = mnesia:transaction(Fun),
...

% Same thing using dirty operations
...

Result1 = mnesia:dirty_read({Table, Key}),
Result2 = mnesia:dirty_read({Table2, Key2}),
...</pre> </div><div class="_attribution">
  <p class="_attribution-p">
    &copy; 2010&ndash;2023 Ericsson AB<br>Licensed under the Apache License, Version 2.0.<br>
    
  </p>
</div>
