<div class="innertube"> <h1 id="Ports">4 Ports</h1> <p>This section outlines an example of how to solve the example problem in the <code><a href="example.html">previous section</a></code> by using a port.</p> <p>The scenario is illustrated in the following figure:</p> <div class="doc-image-wrapper">  <p class="doc-image-caption">Figure 4.1: Port Communication</p> </div> <h2 id="erlang-program" class="title-link"> <div class="title-name">4.1 Erlang Program</div>  </h2> <p>All communication between Erlang and C must be established by creating the port. The Erlang process that creates a port is said to be <strong>the connected process</strong> of the port. All communication to and from the port must go through the connected process. If the connected process terminates, the port also terminates (and the external program, if it is written properly).</p> <p>The port is created using the BIF <code>open_port/2</code> with <code>{spawn,ExtPrg}</code> as the first argument. The string <code>ExtPrg</code> is the name of the external program, including any command line arguments. The second argument is a list of options, in this case only <code>{packet,2}</code>. This option says that a 2 byte length indicator is to be used to simplify the communication between C and Erlang. The Erlang port automatically adds the length indicator, but this must be done explicitly in the external C program.</p> <p>The process is also set to trap exits, which enables detection of failure of the external program:</p> <pre data-language="erlang">
-module(complex1).
-export([start/1, init/1]).

start(ExtPrg) -&gt;
  spawn(?MODULE, init, [ExtPrg]).

init(ExtPrg) -&gt;
  register(complex, self()),
  process_flag(trap_exit, true),
  Port = open_port({spawn, ExtPrg}, [{packet, 2}]),
  loop(Port).</pre> <p>Now <code>complex1:foo/1</code> and <code>complex1:bar/1</code> can be implemented. Both send a message to the <code>complex</code> process and receive the following replies:</p> <pre data-language="erlang">
foo(X) -&gt;
  call_port({foo, X}).
bar(Y) -&gt;
  call_port({bar, Y}).

call_port(Msg) -&gt;
  complex ! {call, self(), Msg},
  receive
    {complex, Result} -&gt;
      Result
  end.</pre> <p>The <code>complex</code> process does the following:</p> <ul> <li>Encodes the message into a sequence of bytes.</li> <li>Sends it to the port.</li> <li>Waits for a reply.</li> <li>Decodes the reply.</li> <li>Sends it back to the caller:</li> </ul> <pre data-language="erlang">
loop(Port) -&gt;
  receive
    {call, Caller, Msg} -&gt;
      Port ! {self(), {command, encode(Msg)}},
      receive
        {Port, {data, Data}} -&gt;
          Caller ! {complex, decode(Data)}
      end,
      loop(Port)
  end.</pre> <p>Assuming that both the arguments and the results from the C functions are less than 256, a simple encoding/decoding scheme is employed. In this scheme, <code>foo</code> is represented by byte 1, <code>bar</code> is represented by 2, and the argument/result is represented by a single byte as well:</p> <pre data-language="erlang">
encode({foo, X}) -&gt; [1, X];
encode({bar, Y}) -&gt; [2, Y].

decode([Int]) -&gt; Int.</pre> <p>The resulting Erlang program, including functionality for stopping the port and detecting port failures, is as follows: </p> <pre data-language="erlang">-module(complex1).
-export([start/1, stop/0, init/1]).
-export([foo/1, bar/1]).

start(ExtPrg) -&gt;
    spawn(?MODULE, init, [ExtPrg]).
stop() -&gt;
    complex ! stop.

foo(X) -&gt;
    call_port({foo, X}).
bar(Y) -&gt;
    call_port({bar, Y}).

call_port(Msg) -&gt;
    complex ! {call, self(), Msg},
    receive
	{complex, Result} -&gt;
	    Result
    end.

init(ExtPrg) -&gt;
    register(complex, self()),
    process_flag(trap_exit, true),
    Port = open_port({spawn, ExtPrg}, [{packet, 2}]),
    loop(Port).

loop(Port) -&gt;
    receive
	{call, Caller, Msg} -&gt;
	    Port ! {self(), {command, encode(Msg)}},
	    receive
		{Port, {data, Data}} -&gt;
		    Caller ! {complex, decode(Data)}
	    end,
	    loop(Port);
	stop -&gt;
	    Port ! {self(), close},
	    receive
		{Port, closed} -&gt;
		    exit(normal)
	    end;
	{'EXIT', Port, Reason} -&gt;
	    exit(port_terminated)
    end.

encode({foo, X}) -&gt; [1, X];
encode({bar, Y}) -&gt; [2, Y].

decode([Int]) -&gt; Int.</pre> <h2 id="c-program" class="title-link"> <div class="title-name">4.2 C Program</div>  </h2> <p>On the C side, it is necessary to write functions for receiving and sending data with 2 byte length indicators from/to Erlang. By default, the C program is to read from standard input (file descriptor 0) and write to standard output (file descriptor 1). Examples of such functions, <code>read_cmd/1</code> and <code>write_cmd/2</code>, follows:</p> <pre data-language="erlang">/* erl_comm.c */

#include &lt;stdio.h&gt;
#include &lt;unistd.h&gt;

typedef unsigned char byte;

int read_exact(byte *buf, int len)
{
  int i, got=0;

  do {
      if ((i = read(0, buf+got, len-got)) &lt;= 0){
          return(i);
      }
    got += i;
  } while (got&lt;len);

  return(len);
}

int write_exact(byte *buf, int len)
{
  int i, wrote = 0;

  do {
    if ((i = write(1, buf+wrote, len-wrote)) &lt;= 0)
      return (i);
    wrote += i;
  } while (wrote&lt;len);

  return (len);
}

int read_cmd(byte *buf)
{
  int len;

  if (read_exact(buf, 2) != 2)
    return(-1);
  len = (buf[0] &lt;&lt; 8) | buf[1];
  return read_exact(buf, len);
}

int write_cmd(byte *buf, int len)
{
  byte li;

  li = (len &gt;&gt; 8) &amp; 0xff;
  write_exact(&amp;li, 1);
  
  li = len &amp; 0xff;
  write_exact(&amp;li, 1);

  return write_exact(buf, len);
}</pre> <p>Notice that <code>stdin</code> and <code>stdout</code> are for buffered input/output and must <strong>not</strong> be used for the communication with Erlang.</p> <p>In the <code>main</code> function, the C program is to listen for a message from Erlang and, according to the selected encoding/decoding scheme, use the first byte to determine which function to call and the second byte as argument to the function. The result of calling the function is then to be sent back to Erlang:</p> <pre data-language="erlang">/* port.c */

typedef unsigned char byte;

int main() {
  int fn, arg, res;
  byte buf[100];

  while (read_cmd(buf) &gt; 0) {
    fn = buf[0];
    arg = buf[1];
    
    if (fn == 1) {
      res = foo(arg);
    } else if (fn == 2) {
      res = bar(arg);
    }

    buf[0] = res;
    write_cmd(buf, 1);
  }
}</pre> <p>Notice that the C program is in a <code>while</code>-loop, checking for the return value of <code>read_cmd/1</code>. This is because the C program must detect when the port closes and terminates.</p> <h2 id="running-the-example" class="title-link"> <div class="title-name">4.3 Running the Example</div>  </h2> <p><strong>Step 1.</strong> Compile the C code:</p> <pre data-language="erlang">
unix&gt; <code>gcc -o extprg complex.c erl_comm.c port.c</code></pre> <p><strong>Step 2.</strong> Start Erlang and compile the Erlang code:</p> <pre data-language="erlang">
unix&gt; <code>erl</code>
Erlang (BEAM) emulator version 4.9.1.2

Eshell V4.9.1.2 (abort with ^G)
1&gt; <code>c(complex1).</code>
{ok,complex1}</pre> <p><strong>Step 3.</strong> Run the example:</p> <pre data-language="erlang">
2&gt; <code>complex1:start("./extprg").</code>
&lt;0.34.0&gt;
3&gt; <code>complex1:foo(3).</code>
4
4&gt; <code>complex1:bar(5).</code>
10
5&gt; <code>complex1:stop().</code>
stop</pre> </div><div class="_attribution">
  <p class="_attribution-p">
    &copy; 2010&ndash;2023 Ericsson AB<br>Licensed under the Apache License, Version 2.0.<br>
    
  </p>
</div>
