<h1>   <span>Replicas and dynamic repositories</span> </h1> <p>When applications reach a certain scale, a single database may not be enough to sustain the required throughput. In such scenarios, it is very common to introduce read replicas: all write operations are sent to the primary database and most of the read operations are performed against the replicas. The credentials of the primary and replica databases are typically known upfront by the time the code is compiled.</p>
<p>In other cases, you may need a single Ecto repository to interact with different database instances which are not known upfront. For instance, you may need to communicate with hundreds of databases very sporadically, so instead of opening up a connection to each of those hundreds of databases when your application starts, you want to quickly start a connection, perform some queries, and then shut down, while still leveraging Ecto's APIs as a whole.</p>
<p>This guide will cover how to tackle both approaches.</p>
<h2 id="primary-and-replicas" class="section-heading">  Primary and Replicas </h2> <p>Since the credentials of the primary and replicas databases are known upfront, adding support for primary and replica databases in your Ecto application is relatively straightforward. Imagine you have a <code class="inline">MyApp.Repo</code> and you want to add four read replicas. This could be done in three steps.</p>
<p>First, define the primary and replicas repositories in <code class="inline">lib/my_app/repo.ex</code>:</p>
<pre data-language="elixir">defmodule MyApp.Repo do
  use Ecto.Repo,
    otp_app: :my_app,
    adapter: Ecto.Adapters.Postgres

  @replicas [
    MyApp.Repo.Replica1,
    MyApp.Repo.Replica2,
    MyApp.Repo.Replica3,
    MyApp.Repo.Replica4
  ]

  def replica do
    Enum.random(@replicas)
  end

  for repo &lt;- @replicas do
    defmodule repo do
      use Ecto.Repo,
        otp_app: :my_app,
        adapter: Ecto.Adapters.Postgres,
        read_only: true
    end
  end
end</pre>
<p>The code above defines a regular <code class="inline">MyApp.Repo</code> and four replicas, called <code class="inline">MyApp.Repo.Replica1</code> up to <code class="inline">MyApp.Repo.Replica4</code>. We pass the <code class="inline">:read_only</code> option to the replica repositories, so operations such as <code class="inline">insert</code>, <code class="inline">update</code> and friends are not made accessible. We also define a function called <code class="inline">replica</code> with the purpose of returning a random replica.</p>
<p>Next we need to make sure both primary and replicas are configured properly in your <code class="inline">config/config.exs</code> files. In development and test, you can likely use the same database credentials for all repositories, all pointing to the same database address:</p>
<pre data-language="elixir">replicas = [
  MyApp.Repo,
  MyApp.Repo.Replica1,
  MyApp.Repo.Replica2,
  MyApp.Repo.Replica3,
  MyApp.Repo.Replica4
]

for repo &lt;- replicas do
  config :my_app, repo,
    username: "postgres",
    password: "postgres",
    database: "my_app_prod",
    hostname: "localhost",
    pool_size: 10
end</pre>
<p>In production, you want each database to connect to a different hostname:</p>
<pre data-language="elixir">repos = %{
  MyApp.Repo =&gt; "prod-primary",
  MyApp.Repo.Replica1 =&gt; "prod-replica-1",
  MyApp.Repo.Replica2 =&gt; "prod-replica-2",
  MyApp.Repo.Replica3 =&gt; "prod-replica-3",
  MyApp.Repo.Replica4 =&gt; "prod-replica-4"
}

for {repo, hostname} &lt;- repos do
  config :my_app, repo,
    username: "postgres",
    password: "postgres",
    database: "my_app_prod",
    hostname: hostname,
    pool_size: 10
end</pre>
<p>Finally, make sure to start all repositories in your supervision tree:</p>
<pre data-language="elixir">children = [
  MyApp.Repo,
  MyApp.Repo.Replica1,
  MyApp.Repo.Replica2,
  MyApp.Repo.Replica3,
  MyApp.Repo.Replica4
]</pre>
<p>Now that all repositories are configured, we can safely use them in your application code. Every time you are performing a read operation, you can call the <code class="inline">replica/0</code> function that we have added to return a random replica we will send the query to:</p>
<pre data-language="elixir">MyApp.Repo.replica().all(query)</pre>
<p>And now you are ready to work with primary and replicas, no hacks or complex dependencies required!</p>
<h2 id="testing-replicas" class="section-heading">  Testing replicas </h2> <p>While all of the work we have done so far should fully work in development and production, it may not be enough for tests. Most developers testing Ecto applications are using a sandbox, such as the <a href="https://hexdocs.pm/ecto_sql/Ecto.Adapters.SQL.Sandbox.html">Ecto SQL Sandbox</a>.</p>
<p>When using a sandbox, each of your tests run in an isolated and independent transaction. Once the test is done, the transaction is rolled back. Which means we can trivially revert all of the changes done in a test in a very performant way.</p>
<p>Unfortunately, even if you configure your primary and replicas to have the same credentials and point to the same hostname, each Ecto repository will open up their own pool of database connections. This means that, once you move to a primary + replicas setup, a simple test like this one won't pass:</p>
<pre data-language="elixir">user = Repo.insert!(%User{name: "jane doe"})
assert Repo.replica().get!(User, user.id)</pre>
<p>That's because <code class="inline">Repo.insert!</code> will write to one database connection and the repository returned by <code class="inline">Repo.replica()</code> will perform the read in another connection. Since the write is done in a transaction, its contents won't be available to other connections until the transaction commits, which will never happen for test connections.</p>
<p>There are two options to tackle this problem: one is to change replicas and the other is to use dynamic repos.</p>
<h3 id="a-custom-replica-definition" class="section-heading">  A custom <code class="inline">replica</code> definition </h3> <p>One simple solution to the problem above is to use a custom <code class="inline">replica</code> implementation during tests that always return the primary repository, like this:</p>
<pre data-language="elixir">if Mix.env() == :test do
  def replica, do: __MODULE__
else
  def replica, do: Enum.random(@replicas)
end</pre>
<p>Now during tests, the replica will always return the repository primary repository itself. While this approach works fine, it has the downside that, if you accidentally invoke a write function in a replica, the test will pass, since the <code class="inline">replica</code> function is returning the primary repo, while the code will fail in production.</p>
<h3 id="using-default_dynamic_repo" class="section-heading">  Using <code class="inline">:default_dynamic_repo</code> </h3> <p>Another approach to testing is to set the <code class="inline">:default_dynamic_repo</code> option when defining the repository. Let's see what we mean by that.</p>
<p>When you list a repository in your supervision tree, such as <code class="inline">MyApp.Repo</code>, behind the scenes it will start a supervision tree with a process named <code class="inline">MyApp.Repo</code>. By default, the process has the same name as the repository module itself. Now every time you invoke a function in <code class="inline">MyApp.Repo</code>, such as <code class="inline">MyApp.Repo.insert/2</code>, Ecto will use the connection pool from the process named <code class="inline">MyApp.Repo</code>.</p>
<p>From v3.0, Ecto has the ability to start multiple processes from the same repository. The only requirement is that they must have different process names, like this:</p>
<pre data-language="elixir">children = [
  MyApp.Repo,
  {MyApp.Repo, name: :another_instance_of_repo}
]</pre>
<p>While the particular example doesn't make much sense (we will cover an actual use case for this feature next), the idea is that now you have two repositories running: one is named <code class="inline">MyApp.Repo</code> and the other one is named <code class="inline">:another_instance_of_repo</code>. Each of those processes have their own connection pool. You can tell Ecto which process you want to use in your repo operations by calling:</p>
<pre data-language="elixir">MyApp.Repo.put_dynamic_repo(MyApp.Repo)
MyApp.Repo.put_dynamic_repo(:another_instance_of_repo)</pre>
<p>Once you call <code class="inline">MyApp.Repo.put_dynamic_repo(name)</code>, all invocations made on <code class="inline">MyApp.Repo</code> will use the connection pool denoted by <code class="inline">name</code>.</p>
<p>How can this help with our replica tests? If we look back to the supervision tree we defined earlier in this guide, you will find this:</p>
<pre data-language="elixir">children = [
  MyApp.Repo,
  MyApp.Repo.Replica1,
  MyApp.Repo.Replica2,
  MyApp.Repo.Replica3,
  MyApp.Repo.Replica4
]</pre>
<p>We are starting five different repositories and five different connection pools. Since we want the replica repositories to use the <code class="inline">MyApp.Repo</code>, we can achieve this by doing the following on the setup of each test:</p>
<pre data-language="elixir">@replicas [
  MyApp.Repo.Replica1,
  MyApp.Repo.Replica2,
  MyApp.Repo.Replica3,
  MyApp.Repo.Replica4
]

setup do
  for replica &lt;- @replicas do
    replica.put_dynamic_repo(MyApp.Repo)
  end

  :ok
end</pre>
<p>Note <code class="inline">put_dynamic_repo</code> is per process. So every time you spawn a new process, the <code class="inline">dynamic_repo</code> value will reset to its default until you call <code class="inline">put_dynamic_repo</code> again.</p>
<p>Luckily, there is even a better way! We can pass a <code class="inline">:default_dynamic_repo</code> option when we define the repository. In this case, we want to set the <code class="inline">:default_dynamic_repo</code> to <code class="inline">MyApp.Repo</code> only during the test environment. In your <code class="inline">lib/my_app/repo.ex</code>, do this:</p>
<pre data-language="elixir">  for repo &lt;- @replicas do
    default_dynamic_repo =
      if Mix.env() == :test do
        MyApp.Repo
      else
        repo
      end

    defmodule repo do
      use Ecto.Repo,
        otp_app: :my_app,
        adapter: Ecto.Adapters.Postgres,
        read_only: true,
        default_dynamic_repo: default_dynamic_repo
    end
  end</pre>
<p>And now your tests should work as before, while still being able to detect if you accidentally perform a write operation in a replica.</p>
<h2 id="dynamic-repositories" class="section-heading">  Dynamic repositories </h2> <p>At this point, we have learned that Ecto allows you to start multiple connections based on the same repository. This is typically useful when you have to connect multiple databases or perform short-lived database connections.</p>
<p>For example, you can start a repository with a given set of credentials dynamically, like this:</p>
<pre data-language="elixir">MyApp.Repo.start_link(
  name: :some_client,
  hostname: "client.example.com",
  username: "...",
  password: "...",
  pool_size: 1
)</pre>
<p>In other words, <code class="inline">start_link</code> accepts the same options as the database configuration. Now let's do a query on the dynamically started repository. If you attempt to simply perform <code class="inline">MyApp.Repo.all(Post)</code>, it may fail, as by default it will try to use a process named <code class="inline">MyApp.Repo</code>, which may or may not be running. So don't forget to call <code class="inline">put_dynamic_repo/1</code> before:</p>
<pre data-language="elixir">MyApp.Repo.put_dynamic_repo(:some_client)
MyApp.Repo.all(Post)</pre>
<p>Ecto also allows you to start a repository with no name (just like that famous horse). In such cases, you need to explicitly pass <code class="inline">name: nil</code> and match on the result of <code class="inline">MyApp.Repo.start_link/1</code> to retrieve the PID, which should be given to <code class="inline">put_dynamic_repo</code>. Let's also use this opportunity and perform proper database clean-up, by shutting up the new repository and reverting the value of <code class="inline">put_dynamic_repo</code>:</p>
<pre data-language="elixir">default_dynamic_repo = MyApp.Repo.get_dynamic_repo()

{:ok, repo} =
  MyApp.Repo.start_link(
    name: nil,
    hostname: "client.example.com",
    username: "...",
    password: "...",
    pool_size: 1
  )

try do
  MyApp.Repo.put_dynamic_repo(repo)
  MyApp.Repo.all(Post)
after
  MyApp.Repo.put_dynamic_repo(default_dynamic_repo)
  Supervisor.stop(repo)
end</pre>
<p>We can encapsulate all of this in a function too, which you could define in your repository:</p>
<pre data-language="elixir">defmodule MyApp.Repo do
  use Ecto.Repo, ...

  def with_dynamic_repo(credentials, callback) do
    default_dynamic_repo = get_dynamic_repo()
    start_opts = [name: nil, pool_size: 1] ++ credentials
    {:ok, repo} = MyApp.Repo.start_link(start_opts)

    try do
      MyApp.Repo.put_dynamic_repo(repo)
      callback.()
    after
      MyApp.Repo.put_dynamic_repo(default_dynamic_repo)
      Supervisor.stop(repo)
    end
  end
end</pre>
<p>And now use it as:</p>
<pre data-language="elixir">credentials = [
  hostname: "client.example.com",
  username: "...",
  password: "..."
]

MyApp.Repo.with_dynamic_repo(credentials, fn -&gt;
  MyApp.Repo.all(Post)
end)</pre>
<p>And that's it! Now you can have dynamic connections, all properly encapsulated in a single function and built on top of the dynamic repo API.</p> <div class="bottom-actions"> <div class="bottom-actions-item"> <a href="polymorphic-associations-with-many-to-many.html" class="bottom-actions-button" rel="prev"> <span class="subheader"> ← Previous Page </span> <span class="title"> Polymorphic associations with many to many </span> </a> </div> <div class="bottom-actions-item"> <a href="schemaless-queries.html" class="bottom-actions-button" rel="next"> <span class="subheader"> Next Page → </span> <span class="title"> Schemaless queries </span> </a> </div> </div><div class="_attribution">
  <p class="_attribution-p">
    &copy; 2013 Plataformatec<br>&copy; 2020 Dashbit<br>Licensed under the Apache License, Version 2.0.<br>
    <a href="https://hexdocs.pm/ecto/replicas-and-dynamic-repositories.html" class="_attribution-link">https://hexdocs.pm/ecto/replicas-and-dynamic-repositories.html</a>
  </p>
</div>
