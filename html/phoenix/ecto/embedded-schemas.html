<h1>   <span>Embedded Schemas</span> </h1> <p>Embedded schemas allow you to define and validate structured data. This data can live in memory, or can be stored in the database. Some use cases for embedded schemas include:</p>
<ul>
<li><p>You are maintaining intermediate-state data, like when UI form fields map onto multiple tables in a database.</p></li>
<li>
<p>You are working within a persisted parent schema and you want to embed data that is...</p>
<ul>
<li>simple, like a map of user preferences inside a User schema.</li>
<li>changes often, like a list of product images with associated structured data inside a Product schema.</li>
<li>requires complex tracking and validation, like an Address schema inside a User schema.</li>
</ul>
</li>
<li><p>You are using a document storage database and you want to interact with and manipulate embedded documents.</p></li>
</ul>
<h2 id="user-profile-example" class="section-heading">  User Profile Example </h2> <p>Let's explore an example where we have a User and want to store "profile" information about them. The data we want to store is UI-dependent information which is likely to change over time alongside changes in the UI. Also, this data is not necessarily important enough to warrant new User <code class="inline">field</code>s in the User schema, as it is not data that is fundamental to the User. An embedded schema is a good solution for this kind of data.</p>
<pre data-language="elixir">defmodule User do
  use Ecto.Schema

  schema "users" do
    field :full_name, :string
    field :email, :string
    field :avatar_url, :string
    field :confirmed_at, :naive_datetime

    embeds_one :profile do
      field :online, :boolean
      field :dark_mode, :boolean
      field :visibility, Ecto.Enum, values: [:public, :private, :friends_only]
    end

    timestamps()
  end
end</pre>
<h3 id="embeds" class="section-heading">  Embeds </h3> <p>There are two ways to represent embedded data within a schema, <code class="inline">embeds_many</code>, which creates a list of embeds, and <code class="inline">embeds_one</code>, which creates only a single instance of the embed. Your choice here affects the behavior of embed-specific functions like <a href="ecto.changeset.html#put_embed/4"><code class="inline">Ecto.Changeset.put_embed/4</code></a> and <a href="ecto.changeset.html#cast_embed/3"><code class="inline">Ecto.Changeset.cast_embed/3</code></a>, so choose whichever is most appropriate to your use case. In our example we are going to use <code class="inline">embeds_one</code> since users will only ever have one profile associated with them.</p>
<pre data-language="elixir">defmodule User do
  use Ecto.Schema

  schema "users" do
    field :full_name, :string
    field :email, :string
    field :avatar_url, :string
    field :confirmed_at, :naive_datetime

    embeds_one :profile do
      field :online, :boolean
      field :dark_mode, :boolean
      field :visibility, Ecto.Enum, values: [:public, :private, :friends_only]
    end

    timestamps()
  end
end</pre>
<h3 id="extracting-the-embeds" class="section-heading">  Extracting the embeds </h3> <p>While the above User schema is simple and sufficient, we might want to work independently with the embedded profile struct. For example, if there was a lot of functionality devoted solely to manipulating the profile data, we'd want to consider extracting the embedded schema into its own module.</p>
<pre data-language="elixir"># user/user.ex
defmodule User do
  use Ecto.Schema

  schema "users" do
    field :full_name, :string
    field :email, :string
    field :avatar_url, :string
    field :confirmed_at, :naive_datetime

    embeds_one :profile, UserProfile

    timestamps()
  end
end

# user/user_profile.ex
defmodule UserProfile do
  use Ecto.Schema

  embedded_schema do
    field :online, :boolean
    field :dark_mode, :boolean
    field :visibility, Ecto.Enum, values: [:public, :private, :friends_only]
  end
end</pre>
<p>It is important to remember that <code class="inline">embedded_schema</code> has many use cases independent of <code class="inline">embeds_one</code> and <code class="inline">embeds_many</code>. You can think of embedded schemas as persistence agnostic <code class="inline">schema</code>s. This makes embedded schemas ideal for scenarios where you want to manage structured data without necessarily persisting it. For example, if you want to build a contact form, you still want to parse and validate the data, but the data is likely not persisted anywhere. Instead, it is used to send an email. Embedded schemas would be a good fit for such a use case.</p>
<h3 id="migrations" class="section-heading">  Migrations </h3> <p>If you wish to save your embedded schema to the database, you need to write a migration to include the embedded data.</p>
<pre data-language="elixir">alter table("users") do
  add :profile, :map
end</pre>
<p>Whether you use <code class="inline">embeds_one</code> or <code class="inline">embeds_many</code>, it is recommended to use the <code class="inline">:map</code> data type (although <code class="inline">{:array, :map}</code> will work with <code class="inline">embeds_many</code> as well). The reason is that typical relational databases are likely to represent a <code class="inline">:map</code> as JSON (or JSONB in Postgres), allowing Ecto adapter libraries more flexibility over how to efficiently store the data.</p>
<h3 id="changesets" class="section-heading">  Changesets </h3> <p>Changeset functionality for embeds will allow you to enforce arbitrary validations on the data. You can define a changeset function for each module. For example, the UserProfile module could require the <code class="inline">online</code> and <code class="inline">visibility</code> fields to be present when generating a changeset.</p>
<pre data-language="elixir">defmodule UserProfile do
  # ...

  def changeset(%UserProfile{} = profile, attrs \\ %{}) do
    profile
    |&gt; cast(attrs, [:online, :dark_mode, :visibility])
    |&gt; validate_required([:online, :visibility])
  end
end

profile = %UserProfile{}
UserProfile.changeset(profile, %{online: true, visibility: :public})</pre>
<p>Meanwhile, the User changeset function can require its own validations without worrying about the details of the UserProfile changes because it can pass that responsibility to UserProfile via <code class="inline">cast_embed/3</code>. A validation failure in an embed will cause the parent changeset to be invalid, even if the parent changeset itself had no errors.</p>
<pre data-language="elixir">defmodule User do
  # ...

  def changeset(user, attrs \\ %{}) do
    user
    |&gt; cast(attrs, [:full_name, :email, :avatar_url])
    |&gt; cast_embed(:profile, required: true)
  end
end

changeset = User.changeset(%User{}, %{profile: %{online: true}})
changeset.valid? # =&gt; false; "visibility can't be blank"
changeset = User.changeset(%User{}, %{profile: %{online: true, visibility: :public}})
changeset.valid? # =&gt; true</pre>
<p>In situations where you have kept the embedded schema within the parent module, e.g., you have not extracted a UserProfile, you can still have custom changeset functions for the embedded data within the parent schema.</p>
<pre data-language="elixir">defmodule User do
  use Ecto.Schema

  schema "users" do
    field :full_name, :string
    field :email, :string
    field :avatar_url, :string
    field :confirmed_at, :naive_datetime

    embeds_one :profile, Profile do
      field :online, :boolean
      field :dark_mode, :boolean
      field :visibility, Ecto.Enum, values: [:public, :private, :friends_only]
    end

    timestamps()
  end

  def changeset(%User{} = user, attrs \\ %{}) do
    user
    |&gt; cast(attrs, [:full_name, :email])
    |&gt; cast_embed(:profile, required: true, with: &amp;profile_changeset/2)
  end

  def profile_changeset(profile, attrs \\ %{}) do
    profile
    |&gt; cast(attrs, [:online, :dark_mode, :visibility])
    |&gt; validate_required([:online, :visibility])
  end
end

changeset = User.changeset(%User{}, %{profile: %{online: true, visibility: :public}})
changeset.valid? # =&gt; true</pre>
<h3 id="querying-embedded-data" class="section-heading">  Querying embedded data </h3> <p>Once you have written embedded data to the database, you can use it in queries on the parent schema.</p>
<pre data-language="elixir">user_changeset = User.changeset(%User{}, %{profile: %{online: true, visibility: :public}})
{:ok, _user} = Repo.insert(user_changeset)

(Ecto.Query.from u in User, select: {u.profile["online"], u.profile["visibility"]}) |&gt; Repo.one
# =&gt; {true, "public"}

(Ecto.Query.from u in User, select: u.profile, where: u.profile["visibility"] == ^:public) |&gt; Repo.all
# =&gt; [
#  %UserProfile{
#    id: "...",
#    online: true,
#    dark_mode: nil,
#    visibility: :public
#  }
#]</pre>
<p>In databases where <code class="inline">:map</code>s are stored as JSONB (like Postgres), Ecto constructs the appropriate jsonpath queries for you. More examples of embedded schema queries are documented in <a href="ecto.query.api.html#json_extract_path/2"><code class="inline">json_extract_path/2</code></a>.</p> <div class="bottom-actions"> <div class="bottom-actions-item"> <a href="getting-started.html" class="bottom-actions-button" rel="prev"> <span class="subheader"> ← Previous Page </span> <span class="title"> Getting Started </span> </a> </div> <div class="bottom-actions-item"> <a href="testing-with-ecto.html" class="bottom-actions-button" rel="next"> <span class="subheader"> Next Page → </span> <span class="title"> Testing with Ecto </span> </a> </div> </div><div class="_attribution">
  <p class="_attribution-p">
    &copy; 2013 Plataformatec<br>&copy; 2020 Dashbit<br>Licensed under the Apache License, Version 2.0.<br>
    <a href="https://hexdocs.pm/ecto/embedded-schemas.html" class="_attribution-link">https://hexdocs.pm/ecto/embedded-schemas.html</a>
  </p>
</div>
