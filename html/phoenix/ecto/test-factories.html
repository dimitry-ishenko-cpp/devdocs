<h1>   <span>Test factories</span> </h1> <p>Many projects depend on external libraries to build their test data. Some of those libraries are called factories because they provide convenience functions for producing different groups of data. However, given Ecto is able to manage complex data trees, we can implement such functionality without relying on third-party projects.</p>
<p>To get started, let's create a file at "test/support/factory.ex" with the following contents:</p>
<pre data-language="elixir">defmodule MyApp.Factory do
  alias MyApp.Repo

  # Factories

  def build(:post) do
    %MyApp.Post{title: "hello world"}
  end

  def build(:comment) do
    %MyApp.Comment{body: "good post"}
  end

  def build(:post_with_comments) do
    %MyApp.Post{
      title: "hello with comments",
      comments: [
        build(:comment, body: "first"),
        build(:comment, body: "second")
      ]
    }
  end

  def build(:user) do
    %MyApp.User{
      email: "hello#{System.unique_integer()}",
      username: "hello#{System.unique_integer()}"
    }
  end

  # Convenience API

  def build(factory_name, attributes) do
    factory_name |&gt; build() |&gt; struct!(attributes)
  end

  def insert!(factory_name, attributes \\ []) do
    factory_name |&gt; build(attributes) |&gt; Repo.insert!()
  end
end</pre>
<p>Our factory module defines four "factories" as different clauses to the build function: <code class="inline">:post</code>, <code class="inline">:comment</code>, <code class="inline">:post_with_comments</code> and <code class="inline">:user</code>. Each clause defines structs with the fields that are required by the database. In certain cases, the generated struct also needs to generate unique fields, such as the user's email and username. We did so by calling Elixir's <code class="inline">System.unique_integer()</code> - you could call <code class="inline">System.unique_integer([:positive])</code> if you need a strictly positive number.</p>
<p>At the end, we defined two functions, <code class="inline">build/2</code> and <code class="inline">insert!/2</code>, which are conveniences for building structs with specific attributes and for inserting data directly in the repository respectively.</p>
<p>That's literally all that is necessary for building our factories. We are now ready to use them in our tests. First, open up your "mix.exs" and make sure the "test/support/factory.ex" file is compiled:</p>
<pre data-language="elixir">def project do
  [...,
   elixirc_paths: elixirc_paths(Mix.env),
   ...]
end

defp elixirc_paths(:test), do: ["lib", "test/support"]
defp elixirc_paths(_), do: ["lib"]</pre>
<p>Now in any of the tests that need to generate data, we can import the <code class="inline">MyApp.Factory</code> module and use its functions:</p>
<pre data-language="elixir">import MyApp.Factory

build(:post)
#=&gt; %MyApp.Post{id: nil, title: "hello world", ...}

build(:post, title: "custom title")
#=&gt; %MyApp.Post{id: nil, title: "custom title", ...}

insert!(:post, title: "custom title")
#=&gt; %MyApp.Post{id: ..., title: "custom title"}</pre>
<p>By building the functionality we need on top of Ecto capabilities, we are able to extend and improve our factories on whatever way we desire, without being constrained to third-party limitations.</p> <div class="bottom-actions"> <div class="bottom-actions-item"> <a href="schemaless-queries.html" class="bottom-actions-button" rel="prev"> <span class="subheader"> ‚Üê Previous Page </span> <span class="title"> Schemaless queries </span> </a> </div>  </div><div class="_attribution">
  <p class="_attribution-p">
    &copy; 2013 Plataformatec<br>&copy; 2020 Dashbit<br>Licensed under the Apache License, Version 2.0.<br>
    <a href="https://hexdocs.pm/ecto/test-factories.html" class="_attribution-link">https://hexdocs.pm/ecto/test-factories.html</a>
  </p>
</div>
