<h1>   <span>Aggregates and subqueries</span> </h1> <p>Now it's time to discuss aggregates and subqueries. As we will learn, one builds directly on the other.</p>
<h2 id="aggregates" class="section-heading">  Aggregates </h2> <p>Ecto includes a convenience function in repositories to calculate aggregates.</p>
<p>For example, if we assume every post has an integer column named visits, we can find the average number of visits across all posts with:</p>
<pre data-language="elixir">MyApp.Repo.aggregate(MyApp.Post, :avg, :visits)
#=&gt; #Decimal&lt;1743&gt;</pre>
<p>Behind the scenes, the query above translates to:</p>
<pre data-language="elixir">MyApp.Repo.one(from p in MyApp.Post, select: avg(p.visits))</pre>
<p>The <a href="ecto.repo.html#c:aggregate/4"><code class="inline">Ecto.Repo.aggregate/4</code></a> function supports any of the aggregate operations listed in the <a href="ecto.query.api.html"><code class="inline">Ecto.Query.API</code></a> module.</p>
<p>At first, it looks like the implementation of <code class="inline">aggregate/4</code> is quite straight-forward. You could even start to wonder why it was added to Ecto in the first place. However, complexities start to arise on queries that rely on <code class="inline">limit</code>, <code class="inline">offset</code> or <code class="inline">distinct</code> clauses.</p>
<p>Imagine that instead of calculating the average of all posts, you want the average of only the top 10. Your first try may be:</p>
<pre data-language="elixir">MyApp.Repo.one(
  from p in MyApp.Post,
    order_by: [desc: :visits],
    limit: 10,
    select: avg(p.visits)
)
#=&gt; #Decimal&lt;1743&gt;</pre>
<p>Oops. The query above returned the same value as the queries before. The option <code class="inline">limit: 10</code> has no effect here since it is limiting the aggregated result and queries with aggregates return only a single row anyway. In order to retrieve the correct result, we would need to first find the top 10 posts and only then aggregate. That's exactly what <code class="inline">aggregate/4</code> does:</p>
<pre data-language="elixir">query =
  from MyApp.Post,
    order_by: [desc: :visits],
    limit: 10

MyApp.Repo.aggregate(query, :avg, :visits)
#=&gt; #Decimal&lt;4682&gt;</pre>
<p>When <code class="inline">limit</code>, <code class="inline">offset</code> or <code class="inline">distinct</code> is specified in the query, <code class="inline">aggregate/4</code> automatically wraps the given query in a subquery. Therefore the query executed by <code class="inline">aggregate/4</code> above is rather equivalent to:</p>
<pre data-language="elixir">inner_query =
  from MyApp.Post,
    order_by: [desc: :visits],
    limit: 10

query =
  from q in subquery(inner_query),
  select: avg(q.visits)

MyApp.Repo.one(query)</pre>
<p>Let's take a closer look at subqueries.</p>
<h2 id="subqueries" class="section-heading">  Subqueries </h2> <p>In the previous section we have already learned some queries that would be hard to express without support for subqueries. That's one of many examples that caused subqueries to be added to Ecto.</p>
<p>Subqueries in Ecto are created by calling <a href="ecto.query.html#subquery/1"><code class="inline">Ecto.Query.subquery/1</code></a>. This function receives any data structure that can be converted to a query, via the <a href="ecto.queryable.html"><code class="inline">Ecto.Queryable</code></a> protocol, and returns a subquery construct (which is also queryable).</p>
<p>In Ecto, it is allowed for a subquery to select a whole table (<code class="inline">p</code>) or a field (<code class="inline">p.field</code>). All fields selected in a subquery can be accessed from the parent query. Let's revisit the aggregate query we saw in the previous section:</p>
<pre data-language="elixir">inner_query =
  from MyApp.Post,
    order_by: [desc: :visits],
    limit: 10

query =
  from q in subquery(inner_query),
    select: avg(q.visits)

MyApp.Repo.one(query)</pre>
<p>Because the query does not specify a <code class="inline">:select</code> clause, it will return <code class="inline">select: p</code> where <code class="inline">p</code> is controlled by <code class="inline">MyApp.Post</code> schema. Since the query will return all fields in <code class="inline">MyApp.Post</code>, when we convert it to a subquery, all of the fields from <code class="inline">MyApp.Post</code> will be available on the parent query, such as <code class="inline">q.visits</code>. In fact, Ecto will keep the schema properties across queries. For example, if you write <code class="inline">q.field_that_does_not_exist</code>, your Ecto query won't compile.</p>
<p>Ecto also allows an Elixir map to be returned from a subquery, making the map keys directly available to the parent query.</p>
<p>Let's see one last example. Imagine you manage a library (as in an actual library in the real world) and there is a table that logs every time the library lends a book. The "lendings" table uses an auto-incrementing primary key and can be backed by the following schema:</p>
<pre data-language="elixir">defmodule Library.Lending do
  use Ecto.Schema

  schema "lendings" do
    belongs_to :book, MyApp.Book       # defines book_id
    belongs_to :visitor, MyApp.Visitor # defines visitor_id
  end
end</pre>
<p>Now consider we want to retrieve the name of every book alongside the name of the last person the library has lent it to. To do so, we need to find the last lending ID of every book, and then join on the book and visitor tables. With subqueries, that's straight-forward:</p>
<pre data-language="elixir">last_lendings =
  from l in MyApp.Lending,
    group_by: l.book_id,
    select: %{
      book_id: l.book_id,
      last_lending_id: max(l.id)
    }

from l in Lending,
  join: last in subquery(last_lendings),
  on: last.last_lending_id == l.id,
  join: b in assoc(l, :book),
  join: v in assoc(l, :visitor),
  select: {b.name, v.name}</pre> <div class="bottom-actions"> <div class="bottom-actions-item"> <a href="testing-with-ecto.html" class="bottom-actions-button" rel="prev"> <span class="subheader"> ← Previous Page </span> <span class="title"> Testing with Ecto </span> </a> </div> <div class="bottom-actions-item"> <a href="composable-transactions-with-multi.html" class="bottom-actions-button" rel="next"> <span class="subheader"> Next Page → </span> <span class="title"> Composable transactions with Multi </span> </a> </div> </div><div class="_attribution">
  <p class="_attribution-p">
    &copy; 2013 Plataformatec<br>&copy; 2020 Dashbit<br>Licensed under the Apache License, Version 2.0.<br>
    <a href="https://hexdocs.pm/ecto/aggregates-and-subqueries.html" class="_attribution-link">https://hexdocs.pm/ecto/aggregates-and-subqueries.html</a>
  </p>
</div>
