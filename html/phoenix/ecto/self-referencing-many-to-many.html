<h1>   <span>Self-referencing many to many</span> </h1> <p><a href="ecto.schema.html#many_to_many/3"><code class="inline">Ecto.Schema.many_to_many/3</code></a> is used to establish the association between two schemas with a join table (or a join schema) tracking the relationship between them. But, what if we want the same table to reference itself? This is commonly used for symmetric relationships and is often referred to as a self-referencing <code class="inline">many_to_many</code> association.</p>
<h2 id="people-relationships" class="section-heading">  People relationships </h2> <p>Let's imagine we are building a system that supports a model for relationships between people.</p>
<pre data-language="elixir">defmodule MyApp.Accounts.Person do
  use Ecto.Schema
  
  alias MyApp.Accounts.Person
  alias MyApp.Relationships.Relationship

  schema "people" do
    field :name, :string

    many_to_many :relationships,
                 Person,
                 join_through: Relationship,
                 join_keys: [person_id: :id, relation_id: :id]

    many_to_many :reverse_relationships,
                 Person,
                 join_through: Relationship,
                 join_keys: [relation_id: :id, person_id: :id]

    timestamps()
  end
end

defmodule MyApp.Relationships.Relationship do
  use Ecto.Schema

  schema "relationships" do
    field :person_id, :id
    field :relation_id, :id
    timestamps()
  end
end</pre>
<p>In our example, we implement an intermediate schema, <code class="inline">MyApp.Relationships.Relationship</code>, on our <code class="inline">:join_through</code> option and pass in a pair of ids that we will be creating a unique index on in our database migration. By implementing an intermediate schema, we make it easy to add additional attributes and functionality to relationships in the future.</p>
<p>We had to create an additional <code class="inline">many_to_many</code> <code class="inline">:reverse_relationships</code> call with an inverse of the <code class="inline">:join_keys</code> in order to finish the other half of the association. This ensures that both sides of the relationship will get added in the database when either side completes a successful relationship request. </p>
<p>The person who is the inverse of the relationship will have the relationship struct stored in a list under the "reverse_relationships" key. We can then construct queries for both <code class="inline">:relationships</code> and <code class="inline">:reverse_relationships</code> with the proper <code class="inline">:preload</code>:</p>
<pre data-language="elixir">iex&gt; preloads = [:relationships, :reverse_relationships]
iex&gt; people = Repo.all from p in Person, preload: preloads
[
  MyApp.Accounts.Person&lt;
    ...
    relationships: [
      MyApp.Accounts.Person&lt;
        id: ...,
        ...
      &gt;
    ]
  &gt;,
  MyApp.Accounts.Person&lt;
    ...
    reverse_relationships: [
      MyApp.Accounts.Person&lt;
        id: ...,
        ...
      &gt;
    ]
  &gt;
]</pre>
<p>In the example query above, we are assuming that we have two "people" that have entered into a relationship. Our query illustrates how one person is added on the <code class="inline">:relationships</code> side and the other on the <code class="inline">:reverse_relationships</code> side.</p>
<p>It is also worth noticing that we are implementing separate parent modules for both our <code class="inline">Person</code> and <code class="inline">Relationship</code> modules. This separation of concerns helps improve code organization and maintainability by allowing us to isolate core functions for relationships in the <code class="inline">MyApp.Relationships</code> context and vice-versa.</p>
<p>Let's take a look at our Ecto migration:</p>
<pre data-language="elixir">def change do
  create table(:relationships) do
    add :person_id, references(:people)
    add :relation_id, references(:people)
    timestamps()
  end

  create index(:relationships, [:person_id])
  create index(:relationships, [:relation_id])

  create unique_index(
    :relationships,
    [:person_id, :relation_id],
    name: :relationships_person_id_relation_id_index
  )

  create unique_index(
    :relationships,
    [:relation_id, :person_id],
    name: :relationships_relation_id_person_id_index
  )
end</pre>
<p>We create indexes on both the <code class="inline">:person_id</code> and <code class="inline">:relation_id</code> for quicker access in the future. Then, we create one unique index on the <code class="inline">:relationships</code> and another unique index on the inverse of <code class="inline">:relationships</code> to ensure that people cannot have duplicate relationships. Lastly, we pass a name to the <code class="inline">:name</code> option to help clarify the unique constraint when working with our changeset.</p>
<pre data-language="elixir"># In MyApp.Relationships.Relationship
@attrs [:person_id, :relation_id]

def changeset(struct, params \\ %{}) do
  struct
  |&gt; Ecto.Changeset.cast(params, @attrs)
  |&gt; Ecto.Changeset.unique_constraint(
    [:person_id, :relation_id],
    name: :relationships_person_id_relation_id_index
  )
  |&gt; Ecto.Changeset.unique_constraint(
    [:relation_id, :person_id],
    name: :relationships_relation_id_person_id_index
  )
end</pre>
<p>Due to the self-referential nature, we will only need to cast the <code class="inline">:join_keys</code> in order for Ecto to correctly associate the two records in the database. When considering production applications, we will most likely want to add additional attributes and validations. This is where our isolation of modules will help us maintain and organize the increasing complexity.</p>
<h2 id="summary" class="section-heading">  Summary </h2> <p>In this guide we used <code class="inline">many_to_many</code> associations to implement a self-referencing symmetric relationship. </p>
<p>Our goal was to allow "people" to associate to different "people". Further, we wanted to lay a strong foundation for code organization and maintainability into the future. We have done this by creating intermediate tables, two separate functional core modules, a clear naming strategy, an inverse association, and by using <code class="inline">many_to_many</code> <code class="inline">:join_keys</code> to automatically manage those join tables.</p>
<p>Overall, our code contains a small structural modification, when compared with a typical <code class="inline">many_to_many</code>, in order to implement an inverse join between our self-referenced table and schema.</p>
<p>Where we go from here will depend greatly on the specific needs of our application. If we remember to adhere to our clear naming strategy with a strong separation of concerns, we will go a long way in keeping our self-referencing <code class="inline">many_to_many</code> association organized and easier to maintain.</p> <div class="bottom-actions"> <div class="bottom-actions-item"> <a href="multi-tenancy-with-foreign-keys.html" class="bottom-actions-button" rel="prev"> <span class="subheader"> ← Previous Page </span> <span class="title"> Multi tenancy with foreign keys </span> </a> </div> <div class="bottom-actions-item"> <a href="polymorphic-associations-with-many-to-many.html" class="bottom-actions-button" rel="next"> <span class="subheader"> Next Page → </span> <span class="title"> Polymorphic associations with many to many </span> </a> </div> </div><div class="_attribution">
  <p class="_attribution-p">
    &copy; 2013 Plataformatec<br>&copy; 2020 Dashbit<br>Licensed under the Apache License, Version 2.0.<br>
    <a href="https://hexdocs.pm/ecto/self-referencing-many-to-many.html" class="_attribution-link">https://hexdocs.pm/ecto/self-referencing-many-to-many.html</a>
  </p>
</div>
