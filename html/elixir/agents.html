<div id="top-content"> <h1> <a href="https://github.com/elixir-lang/elixir/blob/v1.18.1/lib/elixir/pages/mix-and-otp/agents.md#L1" title="View Source" class="source" rel="help">Source</a> <span>Simple state management with agents</span> </h1> <p>In this chapter, we will learn how to keep and share state between multiple entities. If you have previous programming experience, you may think of globally shared variables, but the model we will learn here is quite different. The next chapters will generalize the concepts introduced here.</p>
<p>If you have skipped the <em>Getting Started</em> guide or read it long ago, be sure to re-read the <a href="processes.html">Processes</a> chapter. We will use it as a starting point.</p>
<h2 id="the-trouble-with-mutable-state" class="section-heading">  <span class="text">The trouble with (mutable) state</span> </h2> <p>Elixir is an immutable language where nothing is shared by default. If we want to share information, which can be read and modified from multiple places, we have two main options in Elixir:</p>
<ul>
<li>Using processes and message passing</li>
<li><a href="http://www.erlang.org/doc/man/ets.html">ETS (Erlang Term Storage)</a></li>
</ul>
<p>We covered processes in the <em>Getting Started</em> guide. ETS (Erlang Term Storage) is a new topic that we will explore in later chapters. When it comes to processes though, we rarely hand-roll our own, instead we use the abstractions available in Elixir and OTP:</p>
<ul>
<li>
<a href="agent.html"><code class="inline">Agent</code></a> — Simple wrappers around state.</li>
<li>
<a href="genserver.html"><code class="inline">GenServer</code></a> — "Generic servers" (processes) that encapsulate state, provide sync and async calls, support code reloading, and more.</li>
<li>
<a href="task.html"><code class="inline">Task</code></a> — Asynchronous units of computation that allow spawning a process and potentially retrieving its result at a later time.</li>
</ul>
<p>We will explore these abstractions as we move forward. Keep in mind that they are all implemented on top of processes using the basic features provided by the VM, like <a href="kernel.html#send/2"><code class="inline">send/2</code></a>, <a href="kernel.specialforms.html#receive/1"><code class="inline">receive/1</code></a>, <a href="kernel.html#spawn/1"><code class="inline">spawn/1</code></a> and <a href="process.html#link/1"><code class="inline">Process.link/1</code></a>.</p>
<p>Here, we will use agents, and create a module named <code class="inline">KV.Bucket</code>, responsible for storing our key-value entries in a way that allows them to be read and modified by other processes.</p>
<h2 id="agents-101" class="section-heading">  <span class="text">Agents 101</span> </h2> <p><a href="agent.html"><code class="inline">Agent</code></a>s are simple wrappers around state. If all you want from a process is to keep state, agents are a great fit. Let's start a <code class="inline">iex</code> session inside the project with:</p>
<pre data-language="elixir">$ iex -S mix
</pre>
<p>And play a bit with agents:</p>
<pre data-language="elixir">iex&gt; {:ok, agent} = Agent.start_link(fn -&gt; [] end)
{:ok, #PID&lt;0.57.0&gt;}
iex&gt; Agent.update(agent, fn list -&gt; ["eggs" | list] end)
:ok
iex&gt; Agent.get(agent, fn list -&gt; list end)
["eggs"]
iex&gt; Agent.stop(agent)
:ok</pre>
<p>We started an agent with an initial state of an empty list. We updated the agent's state, adding our new item to the head of the list. The second argument of <a href="agent.html#update/3"><code class="inline">Agent.update/3</code></a> is a function that takes the agent's current state as input and returns its desired new state. Finally, we retrieved the whole list. The second argument of <a href="agent.html#get/3"><code class="inline">Agent.get/3</code></a> is a function that takes the state as input and returns the value that <a href="agent.html#get/3"><code class="inline">Agent.get/3</code></a> itself will return. Once we are done with the agent, we can call <a href="agent.html#stop/3"><code class="inline">Agent.stop/3</code></a> to terminate the agent process.</p>
<p>The <a href="agent.html#update/3"><code class="inline">Agent.update/3</code></a> function accepts as a second argument any function that receives one argument and returns a value:</p>
<pre data-language="elixir">iex&gt; {:ok, agent} = Agent.start_link(fn -&gt; [] end)
{:ok, #PID&lt;0.338.0&gt;}
iex&gt; Agent.update(agent, fn _list -&gt; 123 end)
:ok
iex&gt; Agent.update(agent, fn content -&gt; %{a: content} end)
:ok
iex&gt; Agent.update(agent, fn content -&gt; [12 | [content]] end)
:ok
iex&gt; Agent.update(agent, fn list -&gt; [:nop | list] end)
:ok
iex&gt; Agent.get(agent, fn content -&gt; content end)
[:nop, 12, %{a: 123}]</pre>
<p>As you can see, we can modify the agent state in any way we want. Therefore, we most likely don't want to access the Agent API throughout many different places in our code. Instead, we want to encapsulate all Agent-related functionality in a single module, which we will call <code class="inline">KV.Bucket</code>. Before we implement it, let's write some tests which will outline the API exposed by our module.</p>
<p>Create a file at <code class="inline">test/kv/bucket_test.exs</code> (remember the <code class="inline">.exs</code> extension) with the following:</p>
<pre data-language="elixir">defmodule KV.BucketTest do
  use ExUnit.Case, async: true

  test "stores values by key" do
    {:ok, bucket} = KV.Bucket.start_link([])
    assert KV.Bucket.get(bucket, "milk") == nil

    KV.Bucket.put(bucket, "milk", 3)
    assert KV.Bucket.get(bucket, "milk") == 3
  end
end</pre>
<p><code class="inline">use ExUnit.Case</code> is responsible for setting up our module for testing and imports many test-related functionality, such as the <code class="inline">test/2</code> macro.</p>
<p>Our first test starts a new <code class="inline">KV.Bucket</code> by calling the <code class="inline">start_link/1</code> and passing an empty list of options. Then we perform some <code class="inline">get/2</code> and <code class="inline">put/3</code> operations on it, asserting the result.</p>
<p>Also note the <code class="inline">async: true</code> option passed to <a href="https://hexdocs.pm/ex_unit/1.18.1/ExUnit.Case.html"><code class="inline">ExUnit.Case</code></a>. This option makes the test case run in parallel with other <code class="inline">:async</code> test cases by using multiple cores in our machine. This is extremely useful to speed up our test suite. However, <code class="inline">:async</code> must <em>only</em> be set if the test case does not rely on or change any global values. For example, if the test requires writing to the file system or access a database, keep it synchronous (omit the <code class="inline">:async</code> option) to avoid race conditions between tests.</p>
<p>Async or not, our new test should obviously fail, as none of the functionality is implemented in the module being tested:</p>
<pre data-language="elixir">** (UndefinedFunctionError) function KV.Bucket.start_link/1 is undefined (module KV.Bucket is not available)</pre>
<p>In order to fix the failing test, let's create a file at <code class="inline">lib/kv/bucket.ex</code> with the contents below. Feel free to give a try at implementing the <code class="inline">KV.Bucket</code> module yourself using agents before peeking at the implementation below.</p>
<pre data-language="elixir">defmodule KV.Bucket do
  use Agent

  @doc """
  Starts a new bucket.
  """
  def start_link(_opts) do
    Agent.start_link(fn -&gt; %{} end)
  end

  @doc """
  Gets a value from the `bucket` by `key`.
  """
  def get(bucket, key) do
    Agent.get(bucket, &amp;Map.get(&amp;1, key))
  end

  @doc """
  Puts the `value` for the given `key` in the `bucket`.
  """
  def put(bucket, key, value) do
    Agent.update(bucket, &amp;Map.put(&amp;1, key, value))
  end
end</pre>
<p>The first step in our implementation is to call <code class="inline">use Agent</code>. Most of the functionality we will learn, such as <a href="genserver.html"><code class="inline">GenServer</code></a> and <a href="supervisor.html"><code class="inline">Supervisor</code></a>, follow this pattern. For all of them, calling <code class="inline">use</code> generates a <code class="inline">child_spec/1</code> function with default configuration, which will be handy when we start supervising processes in chapter 4.</p>
<p>Then we define a <code class="inline">start_link/1</code> function, which will effectively start the agent. It is a convention to define a <code class="inline">start_link/1</code> function that always accepts a list of options. We don't plan on using any options right now, but we might later on. We then proceed to call <a href="agent.html#start_link/1"><code class="inline">Agent.start_link/1</code></a>, which receives an anonymous function that returns the Agent's initial state.</p>
<p>We are keeping a map inside the agent to store our keys and values. Getting and putting values on the map is done with the Agent API and the capture operator <code class="inline">&amp;</code>, introduced in <a href="anonymous-functions.html#the-capture-operator">the Getting Started guide</a>. The agent passes its state to the anonymous function via the <code class="inline">&amp;1</code> argument when <a href="agent.html#get/2"><code class="inline">Agent.get/2</code></a> and <a href="agent.html#update/2"><code class="inline">Agent.update/2</code></a> are called.</p>
<p>Now that the <code class="inline">KV.Bucket</code> module has been defined, our test should pass! You can try it yourself by running: <a href="https://hexdocs.pm/mix/1.18.1/Mix.Tasks.Test.html"><code class="inline">mix test</code></a>.</p>
<h2 id="test-setup-with-exunit-callbacks" class="section-heading">  <span class="text">Test setup with ExUnit callbacks</span> </h2> <p>Before moving on and adding more features to <code class="inline">KV.Bucket</code>, let's talk about ExUnit callbacks. As you may expect, all <code class="inline">KV.Bucket</code> tests will require a bucket agent to be up and running. Luckily, ExUnit supports callbacks that allow us to skip such repetitive tasks.</p>
<p>Let's rewrite the test case to use callbacks:</p>
<pre data-language="elixir">defmodule KV.BucketTest do
  use ExUnit.Case, async: true

  setup do
    {:ok, bucket} = KV.Bucket.start_link([])
    %{bucket: bucket}
  end

  test "stores values by key", %{bucket: bucket} do
    assert KV.Bucket.get(bucket, "milk") == nil

    KV.Bucket.put(bucket, "milk", 3)
    assert KV.Bucket.get(bucket, "milk") == 3
  end
end</pre>
<p>We have first defined a setup callback with the help of the <code class="inline">setup/1</code> macro. The <code class="inline">setup/1</code> macro defines a callback that is run before every test, in the same process as the test itself.</p>
<p>Note that we need a mechanism to pass the <code class="inline">bucket</code> PID from the callback to the test. We do so by using the <em>test context</em>. When we return <code class="inline">%{bucket: bucket}</code> from the callback, ExUnit will merge this map into the test context. Since the test context is a map itself, we can pattern match the bucket out of it, providing access to the bucket inside the test:</p>
<pre data-language="elixir">test "stores values by key", %{bucket: bucket} do
  # `bucket` is now the bucket from the setup block
end</pre>
<p>You can read more about ExUnit cases in the <a href="https://hexdocs.pm/ex_unit/1.18.1/ExUnit.Case.html"><code class="inline">ExUnit.Case</code> module documentation</a> and more about callbacks in <a href="https://hexdocs.pm/ex_unit/1.18.1/ExUnit.Callbacks.html"><code class="inline">ExUnit.Callbacks</code></a>.</p>
<h2 id="other-agent-actions" class="section-heading">  <span class="text">Other agent actions</span> </h2> <p>Besides getting a value and updating the agent state, agents allow us to get a value and update the agent state in one function call via <a href="agent.html#get_and_update/2"><code class="inline">Agent.get_and_update/2</code></a>. Let's implement a <code class="inline">KV.Bucket.delete/2</code> function that deletes a key from the bucket, returning its current value:</p>
<pre data-language="elixir">@doc """
Deletes `key` from `bucket`.

Returns the current value of `key`, if `key` exists.
"""
def delete(bucket, key) do
  Agent.get_and_update(bucket, &amp;Map.pop(&amp;1, key))
end</pre>
<p>Now it is your turn to write a test for the functionality above! Also, be sure to explore <a href="agent.html">the documentation for the <code class="inline">Agent</code> module</a> to learn more about them.</p>
<h2 id="client-server-in-agents" class="section-heading">  <span class="text">Client/server in agents</span> </h2> <p>Before we move on to the next chapter, let's discuss the client/server dichotomy in agents. Let's expand the <code class="inline">delete/2</code> function we have just implemented:</p>
<pre data-language="elixir">def delete(bucket, key) do
  Agent.get_and_update(bucket, fn map -&gt;
    Map.pop(map, key)
  end)
end</pre>
<p>Everything that is inside the function we passed to the agent happens in the agent process. In this case, since the agent process is the one receiving and responding to our messages, we say the agent process is the server. Everything outside the function is happening in the client.</p>
<p>This distinction is important. If there are expensive actions to be done, you must consider if it will be better to perform these actions on the client or on the server. For example:</p>
<pre data-language="elixir">def delete(bucket, key) do
  Process.sleep(1000) # puts client to sleep
  Agent.get_and_update(bucket, fn map -&gt;
    Process.sleep(1000) # puts server to sleep
    Map.pop(map, key)
  end)
end</pre>
<p>When a long action is performed on the server, all other requests to that particular server will wait until the action is done, which may cause some clients to timeout.</p>
<p>In the next chapter, we will explore GenServers, where the segregation between clients and servers is made more apparent.</p> </div> <div class="bottom-actions" id="bottom-actions"> <div class="bottom-actions-item"> <a href="introduction-to-mix.html" class="bottom-actions-button" rel="prev"> <span class="subheader"> ← Previous Page </span> <span class="title"> Introduction to Mix </span> </a> </div> <div class="bottom-actions-item"> <a href="genservers.html" class="bottom-actions-button" rel="next"> <span class="subheader"> Next Page → </span> <span class="title"> Client-server communication with GenServer </span> </a> </div> </div> <footer class="footer"> <p> <span class="line"> <button class="a-main footer-button display-quick-switch" title="Search HexDocs packages"> Search HexDocs </button> <a href="elixir.epub.html" title="ePub version"> Download ePub version </a> </span> </p> <p class="built-using"> Built using <a href="https://github.com/elixir-lang/ex_doc" title="ExDoc" target="_blank" rel="help noopener" translate="no">ExDoc</a> (v0.36.1) for the <a href="https://elixir-lang.org" title="Elixir" target="_blank" translate="no">Elixir programming language</a> </p> </footer><div class="_attribution">
  <p class="_attribution-p">
    &copy; 2012-2024 The Elixir Team<br>Licensed under the Apache License, Version 2.0.<br>
    <a href="https://hexdocs.pm/elixir/1.18.1/agents.html" class="_attribution-link">https://hexdocs.pm/elixir/1.18.1/agents.html</a>
  </p>
</div>
