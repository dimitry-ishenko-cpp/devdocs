<div id="top-content"> <h1> <a href="https://github.com/elixir-lang/elixir/blob/v1.17.2/lib/elixir/pages/getting-started/processes.md#L1" title="View Source" class="source" rel="help">Source</a> <span>Processes</span> </h1> <p>In Elixir, all code runs inside processes. Processes are isolated from each other, run concurrent to one another and communicate via message passing. Processes are not only the basis for concurrency in Elixir, but they also provide the means for building distributed and fault-tolerant programs.</p>
<p>Elixir's processes should not be confused with operating system processes. Processes in Elixir are extremely lightweight in terms of memory and CPU (even compared to threads as used in many other programming languages). Because of this, it is not uncommon to have tens or even hundreds of thousands of processes running simultaneously.</p>
<p>In this chapter, we will learn about the basic constructs for spawning new processes, as well as sending and receiving messages between processes.</p>
<h2 id="spawning-processes" class="section-heading">  <span class="text">Spawning processes</span> </h2> <p>The basic mechanism for spawning new processes is the auto-imported <a href="kernel.html#spawn/1"><code class="inline">spawn/1</code></a> function:</p>
<pre data-language="elixir">iex&gt; spawn(fn -&gt; 1 + 2 end)
#PID&lt;0.43.0&gt;</pre>
<p><a href="kernel.html#spawn/1"><code class="inline">spawn/1</code></a> takes a function which it will execute in another process.</p>
<p>Notice <a href="kernel.html#spawn/1"><code class="inline">spawn/1</code></a> returns a PID (process identifier). At this point, the process you spawned is very likely dead. The spawned process will execute the given function and exit after the function is done:</p>
<pre data-language="elixir">iex&gt; pid = spawn(fn -&gt; 1 + 2 end)
#PID&lt;0.44.0&gt;
iex&gt; Process.alive?(pid)
false</pre>
<blockquote><p>Note: you will likely get different process identifiers than the ones we are showing in our snippets.</p></blockquote>
<p>We can retrieve the PID of the current process by calling <a href="kernel.html#self/0"><code class="inline">self/0</code></a>:</p>
<pre data-language="elixir">iex&gt; self()
#PID&lt;0.41.0&gt;
iex&gt; Process.alive?(self())
true</pre>
<p>Processes get much more interesting when we are able to send and receive messages.</p>
<h2 id="sending-and-receiving-messages" class="section-heading">  <span class="text">Sending and receiving messages</span> </h2> <p>We can send messages to a process with <a href="kernel.html#send/2"><code class="inline">send/2</code></a> and receive them with <a href="kernel.specialforms.html#receive/1"><code class="inline">receive/1</code></a>:</p>
<pre data-language="elixir">iex&gt; send(self(), {:hello, "world"})
{:hello, "world"}
iex&gt; receive do
...&gt;   {:hello, msg} -&gt; msg
...&gt;   {:world, _msg} -&gt; "won't match"
...&gt; end
"world"</pre>
<p>When a message is sent to a process, the message is stored in the process mailbox. The <a href="kernel.specialforms.html#receive/1"><code class="inline">receive/1</code></a> block goes through the current process mailbox searching for a message that matches any of the given patterns. <a href="kernel.specialforms.html#receive/1"><code class="inline">receive/1</code></a> supports guards and many clauses, such as <a href="kernel.specialforms.html#case/2"><code class="inline">case/2</code></a>.</p>
<p>The process that sends the message does not block on <a href="kernel.html#send/2"><code class="inline">send/2</code></a>, it puts the message in the recipient's mailbox and continues. In particular, a process can send messages to itself.</p>
<p>If there is no message in the mailbox matching any of the patterns, the current process will wait until a matching message arrives. A timeout can also be specified:</p>
<pre data-language="elixir">iex&gt; receive do
...&gt;   {:hello, msg}  -&gt; msg
...&gt; after
...&gt;   1_000 -&gt; "nothing after 1s"
...&gt; end
"nothing after 1s"</pre>
<p>A timeout of 0 can be given when you already expect the message to be in the mailbox.</p>
<p>Let's put it all together and send messages between processes:</p>
<pre data-language="elixir">iex&gt; parent = self()
#PID&lt;0.41.0&gt;
iex&gt; spawn(fn -&gt; send(parent, {:hello, self()}) end)
#PID&lt;0.48.0&gt;
iex&gt; receive do
...&gt;   {:hello, pid} -&gt; "Got hello from #{inspect pid}"
...&gt; end
"Got hello from #PID&lt;0.48.0&gt;"</pre>
<p>The <a href="kernel.html#inspect/1"><code class="inline">inspect/1</code></a> function is used to convert a data structure's internal representation into a string, typically for printing. Notice that when the <code class="inline">receive</code> block gets executed the sender process we have spawned may already be dead, as its only instruction was to send a message.</p>
<p>While in the shell, you may find the helper <code class="inline">flush/0</code> quite useful. It flushes and prints all the messages in the mailbox.</p>
<pre data-language="elixir">iex&gt; send(self(), :hello)
:hello
iex&gt; flush()
:hello
:ok</pre>
<h2 id="links" class="section-heading">  <span class="text">Links</span> </h2> <p>The majority of times we spawn processes in Elixir, we spawn them as linked processes. Before we show an example with <a href="kernel.html#spawn_link/1"><code class="inline">spawn_link/1</code></a>, let's see what happens when a process started with <a href="kernel.html#spawn/1"><code class="inline">spawn/1</code></a> fails:</p>
<pre data-language="elixir">iex&gt; spawn(fn -&gt; raise "oops" end)
#PID&lt;0.58.0&gt;

[error] Process #PID&lt;0.58.00&gt; raised an exception
** (RuntimeError) oops
    (stdlib) erl_eval.erl:668: :erl_eval.do_apply/6</pre>
<p>It merely logged an error but the parent process is still running. That's because processes are isolated. If we want the failure in one process to propagate to another one, we should link them. This can be done with <a href="kernel.html#spawn_link/1"><code class="inline">spawn_link/1</code></a>:</p>
<pre data-language="elixir">iex&gt; self()
#PID&lt;0.41.0&gt;
iex&gt; spawn_link(fn -&gt; raise "oops" end)

** (EXIT from #PID&lt;0.41.0&gt;) evaluator process exited with reason: an exception was raised:
    ** (RuntimeError) oops
        (stdlib) erl_eval.erl:668: :erl_eval.do_apply/6

[error] Process #PID&lt;0.289.0&gt; raised an exception
** (RuntimeError) oops
    (stdlib) erl_eval.erl:668: :erl_eval.do_apply/6</pre>
<p>Because processes are linked, we now see a message saying the parent process, which is the shell process, has received an EXIT signal from another process causing the shell to terminate. IEx detects this situation and starts a new shell session.</p>
<p>Linking can also be done manually by calling <a href="process.html#link/1"><code class="inline">Process.link/1</code></a>. We recommend that you take a look at the <a href="process.html"><code class="inline">Process</code></a> module for other functionality provided by processes.</p>
<p>Processes and links play an important role when building fault-tolerant systems. Elixir processes are isolated and don't share anything by default. Therefore, a failure in a process will never crash or corrupt the state of another process. Links, however, allow processes to establish a relationship in case of failure. We often link our processes to supervisors which will detect when a process dies and start a new process in its place.</p>
<p>While other languages would require us to catch/handle exceptions, in Elixir we are actually fine with letting processes fail because we expect supervisors to properly restart our systems. "Failing fast" (sometimes referred as "let it crash") is a common philosophy when writing Elixir software!</p>
<p><a href="kernel.html#spawn/1"><code class="inline">spawn/1</code></a> and <a href="kernel.html#spawn_link/1"><code class="inline">spawn_link/1</code></a> are the basic primitives for creating processes in Elixir. Although we have used them exclusively so far, most of the time we are going to use abstractions that build on top of them. Let's see the most common one, called tasks.</p>
<h2 id="tasks" class="section-heading">  <span class="text">Tasks</span> </h2> <p>Tasks build on top of the spawn functions to provide better error reports and introspection:</p>
<pre data-language="elixir">iex&gt; Task.start(fn -&gt; raise "oops" end)
{:ok, #PID&lt;0.55.0&gt;}

15:22:33.046 [error] Task #PID&lt;0.55.0&gt; started from #PID&lt;0.53.0&gt; terminating
** (RuntimeError) oops
    (stdlib) erl_eval.erl:668: :erl_eval.do_apply/6
    (elixir) lib/task/supervised.ex:85: Task.Supervised.do_apply/2
    (stdlib) proc_lib.erl:247: :proc_lib.init_p_do_apply/3
Function: #Function&lt;20.99386804/0 in :erl_eval.expr/5&gt;
    Args: []</pre>
<p>Instead of <a href="kernel.html#spawn/1"><code class="inline">spawn/1</code></a> and <a href="kernel.html#spawn_link/1"><code class="inline">spawn_link/1</code></a>, we use <a href="task.html#start/1"><code class="inline">Task.start/1</code></a> and <a href="task.html#start_link/1"><code class="inline">Task.start_link/1</code></a> which return <code class="inline">{:ok, pid}</code> rather than just the PID. This is what enables tasks to be used in supervision trees. Furthermore, <a href="task.html"><code class="inline">Task</code></a> provides convenience functions, like <a href="task.html#async/1"><code class="inline">Task.async/1</code></a> and <a href="task.html#await/1"><code class="inline">Task.await/1</code></a>, and functionality to ease distribution.</p>
<p>We will explore tasks and other abstractions around processes in the <a href="introduction-to-mix.html">"Mix and OTP guide"</a>.</p>
<h2 id="state" class="section-heading">  <span class="text">State</span> </h2> <p>We haven't talked about state so far. If you are building an application that requires state, for example, to keep your application configuration, or you need to parse a file and keep it in memory, where would you store it?</p>
<p>Processes are the most common answer to this question. We can write processes that loop infinitely, maintain state, and send and receive messages. As an example, let's write a module that starts new processes that work as a key-value store in a file named <code class="inline">kv.exs</code>:</p>
<pre data-language="elixir">defmodule KV do
  def start_link do
    Task.start_link(fn -&gt; loop(%{}) end)
  end

  defp loop(map) do
    receive do
      {:get, key, caller} -&gt;
        send(caller, Map.get(map, key))
        loop(map)
      {:put, key, value} -&gt;
        loop(Map.put(map, key, value))
    end
  end
end</pre>
<p>Note that the <code class="inline">start_link</code> function starts a new process that runs the <code class="inline">loop/1</code> function, starting with an empty map. The <code class="inline">loop/1</code> (private) function then waits for messages and performs the appropriate action for each message. We made <code class="inline">loop/1</code> private by using <code class="inline">defp</code> instead of <code class="inline">def</code>. In the case of a <code class="inline">:get</code> message, it sends a message back to the caller and calls <code class="inline">loop/1</code> again, to wait for a new message. While the <code class="inline">:put</code> message actually invokes <code class="inline">loop/1</code> with a new version of the map, with the given <code class="inline">key</code> and <code class="inline">value</code> stored.</p>
<p>Let's give it a try by running <code class="inline">iex kv.exs</code>:</p>
<pre data-language="elixir">iex&gt; {:ok, pid} = KV.start_link()
{:ok, #PID&lt;0.62.0&gt;}
iex&gt; send(pid, {:get, :hello, self()})
{:get, :hello, #PID&lt;0.41.0&gt;}
iex&gt; flush()
nil
:ok</pre>
<p>At first, the process map has no keys, so sending a <code class="inline">:get</code> message and then flushing the current process inbox returns <code class="inline">nil</code>. Let's send a <code class="inline">:put</code> message and try it again:</p>
<pre data-language="elixir">iex&gt; send(pid, {:put, :hello, :world})
{:put, :hello, :world}
iex&gt; send(pid, {:get, :hello, self()})
{:get, :hello, #PID&lt;0.41.0&gt;}
iex&gt; flush()
:world
:ok</pre>
<p>Notice how the process is keeping a state and we can get and update this state by sending the process messages. In fact, any process that knows the <code class="inline">pid</code> above will be able to send it messages and manipulate the state.</p>
<p>It is also possible to register the <code class="inline">pid</code>, giving it a name, and allowing everyone that knows the name to send it messages:</p>
<pre data-language="elixir">iex&gt; Process.register(pid, :kv)
true
iex&gt; send(:kv, {:get, :hello, self()})
{:get, :hello, #PID&lt;0.41.0&gt;}
iex&gt; flush()
:world
:ok</pre>
<p>Using processes to maintain state and name registration are very common patterns in Elixir applications. However, most of the time, we won't implement those patterns manually as above, but by using one of the many abstractions that ship with Elixir. For example, Elixir provides <a href="agent.html"><code class="inline">Agent</code></a>s, which are simple abstractions around state. Our code above could be directly written as:</p>
<pre data-language="elixir">iex&gt; {:ok, pid} = Agent.start_link(fn -&gt; %{} end)
{:ok, #PID&lt;0.72.0&gt;}
iex&gt; Agent.update(pid, fn map -&gt; Map.put(map, :hello, :world) end)
:ok
iex&gt; Agent.get(pid, fn map -&gt; Map.get(map, :hello) end)
:world</pre>
<p>A <code class="inline">:name</code> option could also be given to <a href="agent.html#start_link/2"><code class="inline">Agent.start_link/2</code></a> and it would be automatically registered. Besides agents, Elixir provides an API for building generic servers (called <a href="genserver.html"><code class="inline">GenServer</code></a>), registries, and more, all powered by processes underneath. Those, along with supervision trees, will be explored with more detail in the <a href="introduction-to-mix.html">"Mix and OTP guide"</a>, which will build a complete Elixir application from start to finish.</p>
<p>For now, let's move on and explore the world of I/O in Elixir.</p> </div> <div class="bottom-actions" id="bottom-actions"> <div class="bottom-actions-item"> <a href="enumerable-and-streams.html" class="bottom-actions-button" rel="prev"> <span class="subheader"> ← Previous Page </span> <span class="title"> Enumerables and Streams </span> </a> </div> <div class="bottom-actions-item"> <a href="io-and-the-file-system.html" class="bottom-actions-button" rel="next"> <span class="subheader"> Next Page → </span> <span class="title"> IO and the file system </span> </a> </div> </div> <footer class="footer"> <p> <span class="line"> <button class="a-main footer-button display-quick-switch" title="Search HexDocs packages"> Search HexDocs </button> <a href="elixir.epub.html" title="ePub version"> Download ePub version </a> </span> </p> <p class="built-using"> Built using <a href="https://github.com/elixir-lang/ex_doc" title="ExDoc" target="_blank" rel="help noopener" translate="no">ExDoc</a> (v0.34.1) for the <a href="https://elixir-lang.org" title="Elixir" target="_blank" translate="no">Elixir programming language</a> </p> </footer><div class="_attribution">
  <p class="_attribution-p">
    &copy; 2012-2024 The Elixir Team<br>Licensed under the Apache License, Version 2.0.<br>
    <a href="https://hexdocs.pm/elixir/1.17.2/processes.html" class="_attribution-link">https://hexdocs.pm/elixir/1.17.2/processes.html</a>
  </p>
</div>
