<div class="getting-started-title"> <small>Mix and OTP</small> <h1>Doctests, patterns and with</h1> </div>  <blockquote> <p>This chapter is part of the <i>Mix and OTP guide</i> and it depends on previous chapters in this guide. For more information, <a href="introduction-to-mix.html">read the introduction guide</a> or check out the chapter index in the sidebar.</p> </blockquote> <p>In this chapter, we will implement the code that parses the commands we described in the first chapter:</p> <div class="language-plaintext highlighter-rouge"><pre class="highlight" data-language="">CREATE shopping
OK

PUT shopping milk 1
OK

PUT shopping eggs 3
OK

GET shopping milk
1
OK

DELETE shopping eggs
OK
</pre></div> <p>After the parsing is done, we will update our server to dispatch the parsed commands to the <code class="language-plaintext highlighter-rouge">:kv</code> application we built previously.</p> <h2 id="doctests">Doctests</h2> <p>On the language homepage, we mention that Elixir makes documentation a first-class citizen in the language. We have explored this concept many times throughout this guide, be it via <code class="language-plaintext highlighter-rouge">mix help</code> or by typing <code class="language-plaintext highlighter-rouge">h Enum</code> or another module in an IEx console.</p> <p>In this section, we will implement the parsing functionality, document it and make sure our documentation is up to date with doctests. This helps us provide documentation with accurate code samples.</p> <p>We need two steps to achieve this: first we write the documentation and then we instruct ExUnit to extract and run the examples in it by calling <a href="https://hexdocs.pm/ex_unit/ExUnit.DocTest.html#doctest/1"><code class="language-plaintext highlighter-rouge">ExUnit.DocTest.doctest/1</code></a>.</p> <p>Let’s start by creating an empty definition of the parsing function with documentation at <code class="language-plaintext highlighter-rouge">lib/kv_server/command.ex</code>:</p> <div class="language-elixir highlighter-rouge"><pre class="highlight" data-language="">defmodule KVServer.Command do
  @doc ~S"""
  Parses the given `line` into a command.

  ## Examples

      iex&gt; KVServer.Command.parse("CREATE shopping\r\n")
      {:ok, {:create, "shopping"}}

  """
  def parse(_line) do
    :not_implemented
  end
end
</pre></div> <p>Doctests are specified by an indentation of four spaces followed by the <code class="language-plaintext highlighter-rouge">iex&gt;</code> prompt in a documentation string. If a command spans multiple lines, you can use <code class="language-plaintext highlighter-rouge">...&gt;</code>, as in IEx. The expected result should start at the next line after <code class="language-plaintext highlighter-rouge">iex&gt;</code> or <code class="language-plaintext highlighter-rouge">...&gt;</code> line(s) and is terminated either by a newline or a new <code class="language-plaintext highlighter-rouge">iex&gt;</code> prefix.</p> <p>Also, note that we started the documentation string using <code class="language-plaintext highlighter-rouge">@doc ~S"""</code>. The <code class="language-plaintext highlighter-rouge">~S</code> prevents the <code class="language-plaintext highlighter-rouge">\r\n</code> characters from being converted to a carriage return and line feed until they are evaluated in the test.</p> <p>To run our doctests, we’ll create a file at <code class="language-plaintext highlighter-rouge">test/kv_server/command_test.exs</code> and call <code class="language-plaintext highlighter-rouge">doctest KVServer.Command</code> in the test case:</p> <div class="language-elixir highlighter-rouge"><pre class="highlight" data-language="">defmodule KVServer.CommandTest do
  use ExUnit.Case, async: true
  doctest KVServer.Command
end
</pre></div> <p>Run the test suite and the doctest should fail:</p> <div class="language-plaintext highlighter-rouge"><pre class="highlight" data-language="">  1) doctest KVServer.Command.parse/1 (1) (KVServer.CommandTest)
     test/kv_server/command_test.exs:3
     Doctest failed
     doctest:
       iex&gt; KVServer.Command.parse("CREATE shopping\r\n")
       {:ok, {:create, "shopping"}}
     code: KVServer.Command.parse "CREATE shopping\r\n" === {:ok, {:create, "shopping"}}
     left:  :not_implemented
     right: {:ok, {:create, "shopping"}}
     stacktrace:
       lib/kv_server/command.ex:7: KVServer.Command (module)
</pre></div> <p>Excellent!</p> <p>Now let’s make the doctest pass. Let’s implement the <code class="language-plaintext highlighter-rouge">parse/1</code> function:</p> <div class="language-elixir highlighter-rouge"><pre class="highlight" data-language="">def parse(line) do
  case String.split(line) do
    ["CREATE", bucket] -&gt; {:ok, {:create, bucket}}
  end
end
</pre></div> <p>Our implementation splits the line on whitespace and then matches the command against a list. Using <code class="language-plaintext highlighter-rouge">String.split/1</code> means our commands will be whitespace-insensitive. Leading and trailing whitespace won’t matter, nor will consecutive spaces between words. Let’s add some new doctests to test this behaviour along with the other commands:</p> <div class="language-elixir highlighter-rouge"><pre class="highlight" data-language="">@doc ~S"""
Parses the given `line` into a command.

## Examples

    iex&gt; KVServer.Command.parse "CREATE shopping\r\n"
    {:ok, {:create, "shopping"}}

    iex&gt; KVServer.Command.parse "CREATE  shopping  \r\n"
    {:ok, {:create, "shopping"}}

    iex&gt; KVServer.Command.parse "PUT shopping milk 1\r\n"
    {:ok, {:put, "shopping", "milk", "1"}}

    iex&gt; KVServer.Command.parse "GET shopping milk\r\n"
    {:ok, {:get, "shopping", "milk"}}

    iex&gt; KVServer.Command.parse "DELETE shopping eggs\r\n"
    {:ok, {:delete, "shopping", "eggs"}}

Unknown commands or commands with the wrong number of
arguments return an error:

    iex&gt; KVServer.Command.parse "UNKNOWN shopping eggs\r\n"
    {:error, :unknown_command}

    iex&gt; KVServer.Command.parse "GET shopping\r\n"
    {:error, :unknown_command}

"""
</pre></div> <p>With doctests at hand, it is your turn to make tests pass! Once you’re ready, you can compare your work with our solution below:</p> <div class="language-elixir highlighter-rouge"><pre class="highlight" data-language="">def parse(line) do
  case String.split(line) do
    ["CREATE", bucket] -&gt; {:ok, {:create, bucket}}
    ["GET", bucket, key] -&gt; {:ok, {:get, bucket, key}}
    ["PUT", bucket, key, value] -&gt; {:ok, {:put, bucket, key, value}}
    ["DELETE", bucket, key] -&gt; {:ok, {:delete, bucket, key}}
    _ -&gt; {:error, :unknown_command}
  end
end
</pre></div> <p>Notice how we were able to elegantly parse the commands without adding a bunch of <code class="language-plaintext highlighter-rouge">if/else</code> clauses that check the command name and number of arguments!</p> <p>Finally, you may have observed that each doctest corresponds to a different test in our suite, which now reports a total of 7 doctests. That is because ExUnit considers the following to define two different doctests:</p> <div class="language-elixir highlighter-rouge"><pre class="highlight" data-language="">iex&gt; KVServer.Command.parse("UNKNOWN shopping eggs\r\n")
{:error, :unknown_command}

iex&gt; KVServer.Command.parse("GET shopping\r\n")
{:error, :unknown_command}
</pre></div> <p>Without new lines, as seen below, ExUnit compiles it into a single doctest:</p> <div class="language-elixir highlighter-rouge"><pre class="highlight" data-language="">iex&gt; KVServer.Command.parse("UNKNOWN shopping eggs\r\n")
{:error, :unknown_command}
iex&gt; KVServer.Command.parse("GET shopping\r\n")
{:error, :unknown_command}
</pre></div> <p>As the name says, doctest is documentation first and a test later. Their goal is not to replace tests but to provide up to date documentation. You can read more about doctests in <a href="https://hexdocs.pm/ex_unit/ExUnit.DocTest.html">the <code class="language-plaintext highlighter-rouge">ExUnit.DocTest</code> docs</a>.</p> <h2 id="with">with</h2> <p>As we are now able to parse commands, we can finally start implementing the logic that runs the commands. Let’s add a stub definition for this function for now:</p> <div class="language-elixir highlighter-rouge"><pre class="highlight" data-language="">defmodule KVServer.Command do
  @doc """
  Runs the given command.
  """
  def run(command) do
    {:ok, "OK\r\n"}
  end
end
</pre></div> <p>Before we implement this function, let’s change our server to start using our new <code class="language-plaintext highlighter-rouge">parse/1</code> and <code class="language-plaintext highlighter-rouge">run/1</code> functions. Remember, our <code class="language-plaintext highlighter-rouge">read_line/1</code> function was also crashing when the client closed the socket, so let’s take the opportunity to fix it, too. Open up <code class="language-plaintext highlighter-rouge">lib/kv_server.ex</code> and replace the existing server definition:</p> <div class="language-elixir highlighter-rouge"><pre class="highlight" data-language="">defp serve(socket) do
  socket
  |&gt; read_line()
  |&gt; write_line(socket)

  serve(socket)
end

defp read_line(socket) do
  {:ok, data} = :gen_tcp.recv(socket, 0)
  data
end

defp write_line(line, socket) do
  :gen_tcp.send(socket, line)
end
</pre></div> <p>by the following:</p> <div class="language-elixir highlighter-rouge"><pre class="highlight" data-language="">defp serve(socket) do
  msg =
    case read_line(socket) do
      {:ok, data} -&gt;
        case KVServer.Command.parse(data) do
          {:ok, command} -&gt;
            KVServer.Command.run(command)
          {:error, _} = err -&gt;
            err
        end
      {:error, _} = err -&gt;
        err
    end

  write_line(socket, msg)
  serve(socket)
end

defp read_line(socket) do
  :gen_tcp.recv(socket, 0)
end

defp write_line(socket, {:ok, text}) do
  :gen_tcp.send(socket, text)
end

defp write_line(socket, {:error, :unknown_command}) do
  # Known error; write to the client
  :gen_tcp.send(socket, "UNKNOWN COMMAND\r\n")
end

defp write_line(_socket, {:error, :closed}) do
  # The connection was closed, exit politely
  exit(:shutdown)
end

defp write_line(socket, {:error, error}) do
  # Unknown error; write to the client and exit
  :gen_tcp.send(socket, "ERROR\r\n")
  exit(error)
end
</pre></div> <p>If we start our server, we can now send commands to it. For now, we will get two different responses: “OK” when the command is known and “UNKNOWN COMMAND” otherwise:</p> <div class="language-console highlighter-rouge"><pre class="highlight" data-language="">$ telnet 127.0.0.1 4040
Trying 127.0.0.1...
Connected to localhost.
Escape character is '^]'.
CREATE shopping
OK
HELLO
UNKNOWN COMMAND
</pre></div> <p>This means our implementation is going in the correct direction, but it doesn’t look very elegant, does it?</p> <p>The previous implementation used pipelines which made the logic straightforward to follow. However, now that we need to handle different error codes along the way, our server logic is nested inside many <code class="language-plaintext highlighter-rouge">case</code> calls.</p> <p>Thankfully, Elixir v1.2 introduced the <code class="language-plaintext highlighter-rouge">with</code> construct, which allows you to simplify code like the above, replacing nested <code class="language-plaintext highlighter-rouge">case</code> calls with a chain of matching clauses. Let’s rewrite the <code class="language-plaintext highlighter-rouge">serve/1</code> function to use <code class="language-plaintext highlighter-rouge">with</code>:</p> <div class="language-elixir highlighter-rouge"><pre class="highlight" data-language="">defp serve(socket) do
  msg =
    with {:ok, data} &lt;- read_line(socket),
         {:ok, command} &lt;- KVServer.Command.parse(data),
         do: KVServer.Command.run(command)

  write_line(socket, msg)
  serve(socket)
end
</pre></div> <p>Much better! <code class="language-plaintext highlighter-rouge">with</code> will retrieve the value returned by the right-side of <code class="language-plaintext highlighter-rouge">&lt;-</code> and match it against the pattern on the left side. If the value matches the pattern, <code class="language-plaintext highlighter-rouge">with</code> moves on to the next expression. In case there is no match, the non-matching value is returned.</p> <p>In other words, we converted each expression given to <code class="language-plaintext highlighter-rouge">case/2</code> as a step in <code class="language-plaintext highlighter-rouge">with</code>. As soon as any of the steps return something that does not match <code class="language-plaintext highlighter-rouge">{:ok, x}</code>, <code class="language-plaintext highlighter-rouge">with</code> aborts, and returns the non-matching value.</p> <p>You can read more about <a href="https://hexdocs.pm/elixir/Kernel.SpecialForms.html#with/1"><code class="language-plaintext highlighter-rouge">with</code> in our documentation</a>.</p> <h2 id="running-commands">Running commands</h2> <p>The last step is to implement <code class="language-plaintext highlighter-rouge">KVServer.Command.run/1</code>, to run the parsed commands against the <code class="language-plaintext highlighter-rouge">:kv</code> application. Its implementation is shown below:</p> <div class="language-elixir highlighter-rouge"><pre class="highlight" data-language="">@doc """
Runs the given command.
"""
def run(command)

def run({:create, bucket}) do
  KV.Registry.create(KV.Registry, bucket)
  {:ok, "OK\r\n"}
end

def run({:get, bucket, key}) do
  lookup(bucket, fn pid -&gt;
    value = KV.Bucket.get(pid, key)
    {:ok, "#{value}\r\nOK\r\n"}
  end)
end

def run({:put, bucket, key, value}) do
  lookup(bucket, fn pid -&gt;
    KV.Bucket.put(pid, key, value)
    {:ok, "OK\r\n"}
  end)
end

def run({:delete, bucket, key}) do
  lookup(bucket, fn pid -&gt;
    KV.Bucket.delete(pid, key)
    {:ok, "OK\r\n"}
  end)
end

defp lookup(bucket, callback) do
  case KV.Registry.lookup(KV.Registry, bucket) do
    {:ok, pid} -&gt; callback.(pid)
    :error -&gt; {:error, :not_found}
  end
end
</pre></div> <p>Every function clause dispatches the appropriate command to the <code class="language-plaintext highlighter-rouge">KV.Registry</code> server that we registered during the <code class="language-plaintext highlighter-rouge">:kv</code> application startup. Since our <code class="language-plaintext highlighter-rouge">:kv_server</code> depends on the <code class="language-plaintext highlighter-rouge">:kv</code> application, it is completely fine to depend on the services it provides.</p> <p>You might have noticed we have a function head, <code class="language-plaintext highlighter-rouge">def run(command)</code>, without a body. In the <a href="../modules-and-functions.html#default-arguments">Modules and Functions</a> chapter, we learned that a bodiless function can be used to declare default arguments for a multi-clause function. Here is another use case where we use a function without a body to document what the arguments are.</p> <p>Note that we have also defined a private function named <code class="language-plaintext highlighter-rouge">lookup/2</code> to help with the common functionality of looking up a bucket and returning its <code class="language-plaintext highlighter-rouge">pid</code> if it exists, <code class="language-plaintext highlighter-rouge">{:error, :not_found}</code> otherwise.</p> <p>By the way, since we are now returning <code class="language-plaintext highlighter-rouge">{:error, :not_found}</code>, we should amend the <code class="language-plaintext highlighter-rouge">write_line/2</code> function in <code class="language-plaintext highlighter-rouge">KVServer</code> to print such error as well:</p> <div class="language-elixir highlighter-rouge"><pre class="highlight" data-language="">defp write_line(socket, {:error, :not_found}) do
  :gen_tcp.send(socket, "NOT FOUND\r\n")
end
</pre></div> <p>Our server functionality is almost complete. Only tests are missing. This time, we have left tests for last because there are some important considerations to be made.</p> <p><code class="language-plaintext highlighter-rouge">KVServer.Command.run/1</code>’s implementation is sending commands directly to the server named <code class="language-plaintext highlighter-rouge">KV.Registry</code>, which is registered by the <code class="language-plaintext highlighter-rouge">:kv</code> application. This means this server is global and if we have two tests sending messages to it at the same time, our tests will conflict with each other (and likely fail). We need to decide between having unit tests that are isolated and can run asynchronously, or writing integration tests that work on top of the global state, but exercise our application’s full stack as it is meant to be exercised in production.</p> <p>So far we have only written unit tests, typically testing a single module directly. However, in order to make <code class="language-plaintext highlighter-rouge">KVServer.Command.run/1</code> testable as a unit we would need to change its implementation to not send commands directly to the <code class="language-plaintext highlighter-rouge">KV.Registry</code> process but instead pass a server as an argument. For example, we would need to change <code class="language-plaintext highlighter-rouge">run</code>’s signature to <code class="language-plaintext highlighter-rouge">def run(command, pid)</code> and then change all clauses accordingly:</p> <div class="language-elixir highlighter-rouge"><pre class="highlight" data-language="">def run({:create, bucket}, pid) do
  KV.Registry.create(pid, bucket)
  {:ok, "OK\r\n"}
end

# ... other run clauses ...
</pre></div> <p>Feel free to go ahead and do the changes above and write some unit tests. The idea is that your tests will start an instance of the <code class="language-plaintext highlighter-rouge">KV.Registry</code> and pass it as an argument to <code class="language-plaintext highlighter-rouge">run/2</code> instead of relying on the global <code class="language-plaintext highlighter-rouge">KV.Registry</code>. This has the advantage of keeping our tests asynchronous as there is no shared state.</p> <p>But let’s also try something different. Let’s write integration tests that rely on the global server names to exercise the whole stack from the TCP server to the bucket. Our integration tests will rely on global state and must be synchronous. With integration tests, we get coverage on how the components in our application work together at the cost of test performance. They are typically used to test the main flows in your application. For example, we should avoid using integration tests to test an edge case in our command parsing implementation.</p> <p>Our integration test will use a TCP client that sends commands to our server and assert we are getting the desired responses.</p> <p>Let’s implement the integration test in <code class="language-plaintext highlighter-rouge">test/kv_server_test.exs</code> as shown below:</p> <div class="language-elixir highlighter-rouge"><pre class="highlight" data-language="">defmodule KVServerTest do
  use ExUnit.Case

  setup do
    Application.stop(:kv)
    :ok = Application.start(:kv)
  end

  setup do
    opts = [:binary, packet: :line, active: false]
    {:ok, socket} = :gen_tcp.connect('localhost', 4040, opts)
    %{socket: socket}
  end

  test "server interaction", %{socket: socket} do
    assert send_and_recv(socket, "UNKNOWN shopping\r\n") ==
           "UNKNOWN COMMAND\r\n"

    assert send_and_recv(socket, "GET shopping eggs\r\n") ==
           "NOT FOUND\r\n"

    assert send_and_recv(socket, "CREATE shopping\r\n") ==
           "OK\r\n"

    assert send_and_recv(socket, "PUT shopping eggs 3\r\n") ==
           "OK\r\n"

    # GET returns two lines
    assert send_and_recv(socket, "GET shopping eggs\r\n") == "3\r\n"
    assert send_and_recv(socket, "") == "OK\r\n"

    assert send_and_recv(socket, "DELETE shopping eggs\r\n") ==
           "OK\r\n"

    # GET returns two lines
    assert send_and_recv(socket, "GET shopping eggs\r\n") == "\r\n"
    assert send_and_recv(socket, "") == "OK\r\n"
  end

  defp send_and_recv(socket, command) do
    :ok = :gen_tcp.send(socket, command)
    {:ok, data} = :gen_tcp.recv(socket, 0, 1000)
    data
  end
end
</pre></div> <p>Our integration test checks all server interaction, including unknown commands and not found errors. It is worth noting that, as with <abbr title="Erlang Term Storage">ETS</abbr> tables and linked processes, there is no need to close the socket. Once the test process exits, the socket is automatically closed.</p> <p>This time, since our test relies on global data, we have not given <code class="language-plaintext highlighter-rouge">async: true</code> to <code class="language-plaintext highlighter-rouge">use ExUnit.Case</code>. Furthermore, in order to guarantee our test is always in a clean state, we stop and start the <code class="language-plaintext highlighter-rouge">:kv</code> application before each test. In fact, stopping the <code class="language-plaintext highlighter-rouge">:kv</code> application even prints a warning on the terminal:</p> <div class="language-plaintext highlighter-rouge"><pre class="highlight" data-language="">18:12:10.698 [info] Application kv exited: :stopped
</pre></div> <p>To avoid printing log messages during tests, ExUnit provides a neat feature called <code class="language-plaintext highlighter-rouge">:capture_log</code>. By setting <code class="language-plaintext highlighter-rouge">@tag :capture_log</code> before each test or <code class="language-plaintext highlighter-rouge">@moduletag :capture_log</code> for the whole test module, ExUnit will automatically capture anything that is logged while the test runs. In case our test fails, the captured logs will be printed alongside the ExUnit report.</p> <p>Between <code class="language-plaintext highlighter-rouge">use ExUnit.Case</code> and setup, add the following call:</p> <div class="language-elixir highlighter-rouge"><pre class="highlight" data-language="">@moduletag :capture_log
</pre></div> <p>In case the test crashes, you will see a report as follows:</p> <div class="language-plaintext highlighter-rouge"><pre class="highlight" data-language="">  1) test server interaction (KVServerTest)
     test/kv_server_test.exs:17
     ** (RuntimeError) oops
     stacktrace:
       test/kv_server_test.exs:29

     The following output was logged:

     13:44:10.035 [notice] Application kv exited: :stopped
</pre></div> <p>With this simple integration test, we start to see why integration tests may be slow. Not only can this test not run asynchronously, but it also requires the expensive setup of stopping and starting the <code class="language-plaintext highlighter-rouge">:kv</code> application. In fact, your test suite may even fail and run into timeouts. If that’s the case, you can tweak the <code class="language-plaintext highlighter-rouge">:gen_tcp.recv(socket, 0)</code> call to pass a third argument, which is the timeout in milliseconds. In the next chapter we will learn about application configuration, which we could use to make the timeout configurable, if desired.</p> <p>At the end of the day, it is up to you and your team to figure out the best testing strategy for your applications. You need to balance code quality, confidence, and test suite runtime. For example, we may start with testing the server only with integration tests, but if the server continues to grow in future releases, or it becomes a part of the application with frequent bugs, it is important to consider breaking it apart and writing more intensive unit tests that don’t have the weight of an integration test.</p> <p>Let’s move to the next chapter. We will finally make our system distributed by adding a bucket routing mechanism. We will use this opportunity to also improve our testing chops.</p><div class="_attribution">
  <p class="_attribution-p">
    &copy; 2012 Plataformatec<br>Licensed under the Apache License, Version 2.0.<br>
    <a href="https://elixir-lang.org/getting-started/mix-otp/docs-tests-and-with.html" class="_attribution-link">https://elixir-lang.org/getting-started/mix-otp/docs-tests-and-with.html</a>
  </p>
</div>
