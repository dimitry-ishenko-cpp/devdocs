<div class="getting-started-title"> <small>Mix and OTP</small> <h1>ETS</h1> </div>  <blockquote> <p>This chapter is part of the <i>Mix and OTP guide</i> and it depends on previous chapters in this guide. For more information, <a href="introduction-to-mix.html">read the introduction guide</a> or check out the chapter index in the sidebar.</p> </blockquote> <p>Every time we need to look up a bucket, we need to send a message to the registry. In case our registry is being accessed concurrently by multiple processes, the registry may become a bottleneck!</p> <p>In this chapter, we will learn about ETS (Erlang Term Storage) and how to use it as a cache mechanism.</p> <blockquote> <p>Warning! Don’t use ETS as a cache prematurely! Log and analyze your application performance and identify which parts are bottlenecks, so you know <em>whether</em> you should cache, and <em>what</em> you should cache. This chapter is merely an example of how ETS can be used, once you’ve determined the need.</p> </blockquote> <h2 id="ets-as-a-cache">ETS as a cache</h2> <p>ETS allows us to store any Elixir term in an in-memory table. Working with ETS tables is done via <a href="http://www.erlang.org/doc/man/ets.html">Erlang’s <code class="language-plaintext highlighter-rouge">:ets</code> module</a>:</p> <div class="language-elixir highlighter-rouge"><pre class="highlight" data-language="">iex&gt; table = :ets.new(:buckets_registry, [:set, :protected])
#Reference&lt;0.1885502827.460455937.234656&gt;
iex&gt; :ets.insert(table, {"foo", self()})
true
iex&gt; :ets.lookup(table, "foo")
[{"foo", #PID&lt;0.41.0&gt;}]
</pre></div> <p>When creating an ETS table, two arguments are required: the table name and a set of options. From the available options, we passed the table type and its access rules. We have chosen the <code class="language-plaintext highlighter-rouge">:set</code> type, which means that keys cannot be duplicated. We’ve also set the table’s access to <code class="language-plaintext highlighter-rouge">:protected</code>, meaning only the process that created the table can write to it, but all processes can read from it. The possible access controls:</p> <p><code class="language-plaintext highlighter-rouge">:public</code> — Read/Write available to all processes.</p> <p><code class="language-plaintext highlighter-rouge">:protected</code> — Read available to all processes. Only writable by owner process. This is the default.</p> <p><code class="language-plaintext highlighter-rouge">:private</code> — Read/Write limited to owner process.</p> <p>Be aware that if your Read/Write call violates the access control, the operation will raise <code class="language-plaintext highlighter-rouge">ArgumentError</code>. Finally, since <code class="language-plaintext highlighter-rouge">:set</code> and <code class="language-plaintext highlighter-rouge">:protected</code> are the default values, we will skip them from now on.</p> <p>ETS tables can also be named, allowing us to access them by a given name:</p> <div class="language-elixir highlighter-rouge"><pre class="highlight" data-language="">iex&gt; :ets.new(:buckets_registry, [:named_table])
:buckets_registry
iex&gt; :ets.insert(:buckets_registry, {"foo", self()})
true
iex&gt; :ets.lookup(:buckets_registry, "foo")
[{"foo", #PID&lt;0.41.0&gt;}]
</pre></div> <p>Let’s change the <code class="language-plaintext highlighter-rouge">KV.Registry</code> to use ETS tables. The first change is to modify our registry to require a name argument, we will use it to name the ETS table and the registry process itself. ETS names and process names are stored in different locations, so there is no chance of conflicts.</p> <p>Open up <code class="language-plaintext highlighter-rouge">lib/kv/registry.ex</code>, and let’s change its implementation. We’ve added comments to the source code to highlight the changes we’ve made:</p> <div class="language-elixir highlighter-rouge"><pre class="highlight" data-language="">defmodule KV.Registry do
  use GenServer

  ## Client API

  @doc """
  Starts the registry with the given options.

  `:name` is always required.
  """
  def start_link(opts) do
    # 1. Pass the name to GenServer's init
    server = Keyword.fetch!(opts, :name)
    GenServer.start_link(__MODULE__, server, opts)
  end

  @doc """
  Looks up the bucket pid for `name` stored in `server`.

  Returns `{:ok, pid}` if the bucket exists, `:error` otherwise.
  """
  def lookup(server, name) do
    # 2. Lookup is now done directly in ETS, without accessing the server
    case :ets.lookup(server, name) do
      [{^name, pid}] -&gt; {:ok, pid}
      [] -&gt; :error
    end
  end

  @doc """
  Ensures there is a bucket associated with the given `name` in `server`.
  """
  def create(server, name) do
    GenServer.cast(server, {:create, name})
  end

  ## Server callbacks

  @impl true
  def init(table) do
    # 3. We have replaced the names map by the ETS table
    names = :ets.new(table, [:named_table, read_concurrency: true])
    refs  = %{}
    {:ok, {names, refs}}
  end

  # 4. The previous handle_call callback for lookup was removed

  @impl true
  def handle_cast({:create, name}, {names, refs}) do
    # 5. Read and write to the ETS table instead of the map
    case lookup(names, name) do
      {:ok, _pid} -&gt;
        {:noreply, {names, refs}}
      :error -&gt;
        {:ok, pid} = DynamicSupervisor.start_child(KV.BucketSupervisor, KV.Bucket)
        ref = Process.monitor(pid)
        refs = Map.put(refs, ref, name)
        :ets.insert(names, {name, pid})
        {:noreply, {names, refs}}
    end
  end

  @impl true
  def handle_info({:DOWN, ref, :process, _pid, _reason}, {names, refs}) do
    # 6. Delete from the ETS table instead of the map
    {name, refs} = Map.pop(refs, ref)
    :ets.delete(names, name)
    {:noreply, {names, refs}}
  end

  @impl true
  def handle_info(_msg, state) do
    {:noreply, state}
  end
end
</pre></div> <p>Notice that before our changes <code class="language-plaintext highlighter-rouge">KV.Registry.lookup/2</code> sent requests to the server, but now it reads directly from the ETS table, which is shared across all processes. That’s the main idea behind the cache mechanism we are implementing.</p> <p>In order for the cache mechanism to work, the created ETS table needs to have access <code class="language-plaintext highlighter-rouge">:protected</code> (the default), so all clients can read from it, while only the <code class="language-plaintext highlighter-rouge">KV.Registry</code> process writes to it. We have also set <code class="language-plaintext highlighter-rouge">read_concurrency: true</code> when starting the table, optimizing the table for the common scenario of concurrent read operations.</p> <p>The changes we have performed above have broken our tests because the registry requires the <code class="language-plaintext highlighter-rouge">:name</code> option when starting up. Furthermore, some registry operations such as <code class="language-plaintext highlighter-rouge">lookup/2</code> require the name to be given as an argument, instead of a PID, so we can do the ETS table lookup. Let’s change the setup function in <code class="language-plaintext highlighter-rouge">test/kv/registry_test.exs</code> to fix both issues:</p> <div class="language-elixir highlighter-rouge"><pre class="highlight" data-language="">  setup context do
    _ = start_supervised!({KV.Registry, name: context.test})
    %{registry: context.test}
  end
</pre></div> <p>Since each test has a unique name, we use the test name to name our registries. This way, we no longer need to pass the registry PID around, instead we identify it by the test name. Also note we assigned the result of <code class="language-plaintext highlighter-rouge">start_supervised!</code> to underscore (<code class="language-plaintext highlighter-rouge">_</code>). This idiom is often used to signal that we are not interested in the result of <code class="language-plaintext highlighter-rouge">start_supervised!</code>.</p> <p>Once we change <code class="language-plaintext highlighter-rouge">setup</code>, some tests will continue to fail. You may even notice tests pass and fail inconsistently between runs. For example, the “spawns buckets” test:</p> <div class="language-elixir highlighter-rouge"><pre class="highlight" data-language="">test "spawns buckets", %{registry: registry} do
  assert KV.Registry.lookup(registry, "shopping") == :error

  KV.Registry.create(registry, "shopping")
  assert {:ok, bucket} = KV.Registry.lookup(registry, "shopping")

  KV.Bucket.put(bucket, "milk", 1)
  assert KV.Bucket.get(bucket, "milk") == 1
end
</pre></div> <p>may be failing on this line:</p> <div class="language-elixir highlighter-rouge"><pre class="highlight" data-language="">{:ok, bucket} = KV.Registry.lookup(registry, "shopping")
</pre></div> <p>How can this line fail if we just created the bucket in the previous line?</p> <p>The reason those failures are happening is because, for didactic purposes, we have made two mistakes:</p> <ol> <li>We are prematurely optimizing (by adding this cache layer)</li> <li>We are using <code class="language-plaintext highlighter-rouge">cast/2</code> (while we should be using <code class="language-plaintext highlighter-rouge">call/2</code>)</li> </ol> <h2 id="race-conditions">Race conditions?</h2> <p>Developing in Elixir does not make your code free of race conditions. However, Elixir’s abstractions where nothing is shared by default make it easier to spot a race condition’s root cause.</p> <p>What is happening in our tests is that there is a delay in between an operation and the time we can observe this change in the ETS table. Here is what we were expecting to happen:</p> <ol> <li>We invoke <code class="language-plaintext highlighter-rouge">KV.Registry.create(registry, "shopping")</code>
</li> <li>The registry creates the bucket and updates the cache table</li> <li>We access the information from the table with <code class="language-plaintext highlighter-rouge">KV.Registry.lookup(registry, "shopping")</code>
</li> <li>The command above returns <code class="language-plaintext highlighter-rouge">{:ok, bucket}</code>
</li> </ol> <p>However, since <code class="language-plaintext highlighter-rouge">KV.Registry.create/2</code> is a cast operation, the command will return before we actually write to the table! In other words, this is happening:</p> <ol> <li>We invoke <code class="language-plaintext highlighter-rouge">KV.Registry.create(registry, "shopping")</code>
</li> <li>We access the information from the table with <code class="language-plaintext highlighter-rouge">KV.Registry.lookup(registry, "shopping")</code>
</li> <li>The command above returns <code class="language-plaintext highlighter-rouge">:error</code>
</li> <li>The registry creates the bucket and updates the cache table</li> </ol> <p>To fix the failure we need to make <code class="language-plaintext highlighter-rouge">KV.Registry.create/2</code> synchronous by using <code class="language-plaintext highlighter-rouge">call/2</code> rather than <code class="language-plaintext highlighter-rouge">cast/2</code>. This will guarantee that the client will only continue after changes have been made to the table. Let’s go back to <code class="language-plaintext highlighter-rouge">lib/kv/registry.ex</code> and change the function and its callback as follows:</p> <div class="language-elixir highlighter-rouge"><pre class="highlight" data-language="">def create(server, name) do
  GenServer.call(server, {:create, name})
end
</pre></div> <div class="language-elixir highlighter-rouge"><pre class="highlight" data-language="">@impl true
def handle_call({:create, name}, _from, {names, refs}) do
  case lookup(names, name) do
    {:ok, pid} -&gt;
      {:reply, pid, {names, refs}}
    :error -&gt;
      {:ok, pid} = DynamicSupervisor.start_child(KV.BucketSupervisor, KV.Bucket)
      ref = Process.monitor(pid)
      refs = Map.put(refs, ref, name)
      :ets.insert(names, {name, pid})
      {:reply, pid, {names, refs}}
  end
end
</pre></div> <p>We changed the callback from <code class="language-plaintext highlighter-rouge">handle_cast/2</code> to <code class="language-plaintext highlighter-rouge">handle_call/3</code> and changed it to reply with the pid of the created bucket. Generally speaking, Elixir developers prefer to use <code class="language-plaintext highlighter-rouge">call/2</code> instead of <code class="language-plaintext highlighter-rouge">cast/2</code> as it also provides back-pressure - you block until you get a reply. Using <code class="language-plaintext highlighter-rouge">cast/2</code> when not necessary can also be considered a premature optimization.</p> <p>Let’s run the tests once again. This time though, we will pass the <code class="language-plaintext highlighter-rouge">--trace</code> option:</p> <div class="language-console highlighter-rouge"><pre class="highlight" data-language="">$ mix test --trace
</pre></div> <p>The <code class="language-plaintext highlighter-rouge">--trace</code> option is useful when your tests are deadlocking or there are race conditions, as it runs all tests synchronously (<code class="language-plaintext highlighter-rouge">async: true</code> has no effect) and shows detailed information about each test. If you run the tests multiple times you may see this intermittent failure:</p> <div class="language-plaintext highlighter-rouge"><pre class="highlight" data-language="">  1) test removes buckets on exit (KV.RegistryTest)
     test/kv/registry_test.exs:19
     Assertion with == failed
     code:  assert KV.Registry.lookup(registry, "shopping") == :error
     left:  {:ok, #PID&lt;0.109.0&gt;}
     right: :error
     stacktrace:
       test/kv/registry_test.exs:23
</pre></div> <p>According to the failure message, we are expecting that the bucket no longer exists on the table, but it still does! This problem is the opposite of the one we have just solved: while previously there was a delay between the command to create a bucket and updating the table, now there is a delay between the bucket process dying and its entry being removed from the table. Since this is a race condition, you may not be able to reproduce it on your machine, but it is there.</p> <p>Last time we fixed the race condition by replacing the asynchronous operation, a <code class="language-plaintext highlighter-rouge">cast</code>, by a <code class="language-plaintext highlighter-rouge">call</code>, which is synchronous. Unfortunately, the <code class="language-plaintext highlighter-rouge">handle_info/2</code> callback we are using to receive the <code class="language-plaintext highlighter-rouge">:DOWN</code> message and delete the entry from the ETS table does not have a synchronous equivalent. This time, we need to find a way to guarantee the registry has processed the <code class="language-plaintext highlighter-rouge">:DOWN</code> notification sent when the bucket process terminated.</p> <p>An easy way to do so is by sending a synchronous request to the registry before we do the bucket lookup. The <code class="language-plaintext highlighter-rouge">Agent.stop/2</code> operation is synchronous and only returns after the bucket process terminates and all <code class="language-plaintext highlighter-rouge">:DOWN</code> messages are delivered. Therefore, once <code class="language-plaintext highlighter-rouge">Agent.stop/2</code> returns, the registry has already received the <code class="language-plaintext highlighter-rouge">:DOWN</code> message but it may not have processed it yet. In order to guarantee the processing of the <code class="language-plaintext highlighter-rouge">:DOWN</code> message, we can do a synchronous request. Since messages are processed in order, once the registry replies to the synchronous request, then the <code class="language-plaintext highlighter-rouge">:DOWN</code> message will definitely have been processed.</p> <p>Let’s do so by creating a “bogus” bucket, which is a synchronous request, after <code class="language-plaintext highlighter-rouge">Agent.stop/2</code> in both “remove” tests at <code class="language-plaintext highlighter-rouge">test/kv/registry_test.exs</code>:</p> <div class="language-elixir highlighter-rouge"><pre class="highlight" data-language="">  test "removes buckets on exit", %{registry: registry} do
    KV.Registry.create(registry, "shopping")
    {:ok, bucket} = KV.Registry.lookup(registry, "shopping")
    Agent.stop(bucket)

    # Do a call to ensure the registry processed the DOWN message
    _ = KV.Registry.create(registry, "bogus")
    assert KV.Registry.lookup(registry, "shopping") == :error
  end

  test "removes bucket on crash", %{registry: registry} do
    KV.Registry.create(registry, "shopping")
    {:ok, bucket} = KV.Registry.lookup(registry, "shopping")

    # Stop the bucket with non-normal reason
    Agent.stop(bucket, :shutdown)

    # Do a call to ensure the registry processed the DOWN message
    _ = KV.Registry.create(registry, "bogus")
    assert KV.Registry.lookup(registry, "shopping") == :error
  end
</pre></div> <p>Our tests should now (always) pass!</p> <p>Note that the purpose of the test is to check whether the registry processes the bucket’s shutdown message correctly. The fact that the <code class="language-plaintext highlighter-rouge">KV.Registry.lookup/2</code> sends us a valid bucket does not mean that the bucket is still alive by the time you call it. For example, it might have crashed for some reason. The following test depicts this situation:</p> <div class="language-elixir highlighter-rouge"><pre class="highlight" data-language="">  test "bucket can crash at any time", %{registry: registry} do
    KV.Registry.create(registry, "shopping")
    {:ok, bucket} = KV.Registry.lookup(registry, "shopping")

    # Simulate a bucket crash by explicitly and synchronously shutting it down
    Agent.stop(bucket, :shutdown)

    # Now trying to call the dead process causes a :noproc exit
    catch_exit KV.Bucket.put(bucket, "milk", 3)
  end
</pre></div> <p>This concludes our optimization chapter. We have used ETS as a cache mechanism where reads can happen from any processes but writes are still serialized through a single process. More importantly, we have also learned that once data can be read asynchronously, we need to be aware of the race conditions it might introduce.</p> <p>In practice, if you find yourself in a position where you need a process registry for dynamic processes, you should use <a href="https://hexdocs.pm/elixir/Registry.html">the <code class="language-plaintext highlighter-rouge">Registry</code> module</a> provided as part of Elixir. It provides functionality similar to the one we have built using a GenServer + <code class="language-plaintext highlighter-rouge">:ets</code> while also being able to perform both writes and reads concurrently. <a href="https://elixir-lang.org/blog/2017/01/05/elixir-v1-4-0-released/">It has been benchmarked to scale across all cores even on machines with 40 cores</a>.</p> <p>Next, let’s discuss external and internal dependencies and how Mix helps us manage large codebases.</p><div class="_attribution">
  <p class="_attribution-p">
    &copy; 2012 Plataformatec<br>Licensed under the Apache License, Version 2.0.<br>
    <a href="https://elixir-lang.org/getting-started/mix-otp/ets.html" class="_attribution-link">https://elixir-lang.org/getting-started/mix-otp/ets.html</a>
  </p>
</div>
