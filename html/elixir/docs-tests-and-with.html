<div id="top-content"> <h1> <a href="https://github.com/elixir-lang/elixir/blob/v1.18.1/lib/elixir/pages/mix-and-otp/docs-tests-and-with.md#L1" title="View Source" class="source" rel="help">Source</a> <span>Doctests, patterns, and with</span> </h1> <p>In this chapter, we will implement the code that parses the commands we described in the first chapter:</p>
<pre data-language="elixir">CREATE shopping
OK

PUT shopping milk 1
OK

PUT shopping eggs 3
OK

GET shopping milk
1
OK

DELETE shopping eggs
OK</pre>
<p>After the parsing is done, we will update our server to dispatch the parsed commands to the <code class="inline">:kv</code> application we built previously.</p>
<h2 id="doctests" class="section-heading">  <span class="text">Doctests</span> </h2> <p>On the language homepage, we mention that Elixir makes documentation a first-class citizen in the language. We have explored this concept many times throughout this guide, be it via <a href="https://hexdocs.pm/mix/1.18.1/Mix.Tasks.Help.html"><code class="inline">mix help</code></a> or by typing <code class="inline">h Enum</code> or another module in an IEx console.</p>
<p>In this section, we will implement the parsing functionality, document it and make sure our documentation is up to date with doctests. This helps us provide documentation with accurate code samples.</p>
<p>Let's create our command parser at <code class="inline">lib/kv_server/command.ex</code> and start with the doctest:</p>
<pre data-language="elixir">defmodule KVServer.Command do
  @doc ~S"""
  Parses the given `line` into a command.

  ## Examples

      iex&gt; KVServer.Command.parse("CREATE shopping\r\n")
      {:ok, {:create, "shopping"}}

  """
  def parse(_line) do
    :not_implemented
  end
end</pre>
<p>Doctests are specified by an indentation of four spaces followed by the <code class="inline">iex&gt;</code> prompt in a documentation string. If a command spans multiple lines, you can use <code class="inline">...&gt;</code>, as in IEx. The expected result should start at the next line after <code class="inline">iex&gt;</code> or <code class="inline">...&gt;</code> line(s) and is terminated either by a newline or a new <code class="inline">iex&gt;</code> prefix.</p>
<p>Also, note that we started the documentation string using <code class="inline">@doc ~S"""</code>. The <code class="inline">~S</code> prevents the <code class="inline">\r\n</code> characters from being converted to a carriage return and line feed until they are evaluated in the test.</p>
<p>To run our doctests, we'll create a file at <code class="inline">test/kv_server/command_test.exs</code> and call <code class="inline">doctest KVServer.Command</code> in the test case:</p>
<pre data-language="elixir">defmodule KVServer.CommandTest do
  use ExUnit.Case, async: true
  doctest KVServer.Command
end</pre>
<p>Run the test suite and the doctest should fail:</p>
<pre data-language="elixir">  1) doctest KVServer.Command.parse/1 (1) (KVServer.CommandTest)
     test/kv_server/command_test.exs:3
     Doctest failed
     doctest:
       iex&gt; KVServer.Command.parse("CREATE shopping\r\n")
       {:ok, {:create, "shopping"}}
     code: KVServer.Command.parse "CREATE shopping\r\n" === {:ok, {:create, "shopping"}}
     left:  :not_implemented
     right: {:ok, {:create, "shopping"}}
     stacktrace:
       lib/kv_server/command.ex:7: KVServer.Command (module)</pre>
<p>Excellent!</p>
<p>Now let's make the doctest pass. Let's implement the <code class="inline">parse/1</code> function:</p>
<pre data-language="elixir">def parse(line) do
  case String.split(line) do
    ["CREATE", bucket] -&gt; {:ok, {:create, bucket}}
  end
end</pre>
<p>Our implementation splits the line on whitespace and then matches the command against a list. Using <a href="string.html#split/1"><code class="inline">String.split/1</code></a> means our commands will be whitespace-insensitive. Leading and trailing whitespace won't matter, nor will consecutive spaces between words. Let's add some new doctests to test this behavior along with the other commands:</p>
<pre data-language="elixir">@doc ~S"""
Parses the given `line` into a command.

## Examples

    iex&gt; KVServer.Command.parse "CREATE shopping\r\n"
    {:ok, {:create, "shopping"}}

    iex&gt; KVServer.Command.parse "CREATE  shopping  \r\n"
    {:ok, {:create, "shopping"}}

    iex&gt; KVServer.Command.parse "PUT shopping milk 1\r\n"
    {:ok, {:put, "shopping", "milk", "1"}}

    iex&gt; KVServer.Command.parse "GET shopping milk\r\n"
    {:ok, {:get, "shopping", "milk"}}

    iex&gt; KVServer.Command.parse "DELETE shopping eggs\r\n"
    {:ok, {:delete, "shopping", "eggs"}}

Unknown commands or commands with the wrong number of
arguments return an error:

    iex&gt; KVServer.Command.parse "UNKNOWN shopping eggs\r\n"
    {:error, :unknown_command}

    iex&gt; KVServer.Command.parse "GET shopping\r\n"
    {:error, :unknown_command}

"""</pre>
<p>With doctests at hand, it is your turn to make tests pass! Once you're ready, you can compare your work with our solution below:</p>
<pre data-language="elixir">def parse(line) do
  case String.split(line) do
    ["CREATE", bucket] -&gt; {:ok, {:create, bucket}}
    ["GET", bucket, key] -&gt; {:ok, {:get, bucket, key}}
    ["PUT", bucket, key, value] -&gt; {:ok, {:put, bucket, key, value}}
    ["DELETE", bucket, key] -&gt; {:ok, {:delete, bucket, key}}
    _ -&gt; {:error, :unknown_command}
  end
end</pre>
<p>Notice how we were able to elegantly parse the commands without adding a bunch of <code class="inline">if/else</code> clauses that check the command name and number of arguments!</p>
<p>Finally, you may have observed that each doctest corresponds to a different test in our suite, which now reports a total of 7 doctests. That is because ExUnit considers the following to define two different doctests:</p>
<pre data-language="elixir">iex&gt; KVServer.Command.parse("UNKNOWN shopping eggs\r\n")
{:error, :unknown_command}

iex&gt; KVServer.Command.parse("GET shopping\r\n")
{:error, :unknown_command}</pre>
<p>Without new lines, as seen below, ExUnit compiles it into a single doctest:</p>
<pre data-language="elixir">iex&gt; KVServer.Command.parse("UNKNOWN shopping eggs\r\n")
{:error, :unknown_command}
iex&gt; KVServer.Command.parse("GET shopping\r\n")
{:error, :unknown_command}</pre>
<p>As the name says, doctest is documentation first and a test later. Their goal is not to replace tests but to provide up-to-date documentation. You can read more about doctests in the <a href="https://hexdocs.pm/ex_unit/1.18.1/ExUnit.DocTest.html"><code class="inline">ExUnit.DocTest</code></a> documentation.</p>
<h2 id="with" class="section-heading">  <span class="text"><code class="inline">with</code></span> </h2> <p>As we are now able to parse commands, we can finally start implementing the logic that runs the commands. Let's add a stub definition for this function for now:</p>
<pre data-language="elixir">defmodule KVServer.Command do
  @doc """
  Runs the given command.
  """
  def run(command) do
    {:ok, "OK\r\n"}
  end
end</pre>
<p>Before we implement this function, let's change our server to start using our new <code class="inline">parse/1</code> and <code class="inline">run/1</code> functions. Remember, our <code class="inline">read_line/1</code> function was also crashing when the client closed the socket, so let's take the opportunity to fix it, too. Open up <code class="inline">lib/kv_server.ex</code> and replace the existing server definition:</p>
<pre data-language="elixir">defp serve(socket) do
  socket
  |&gt; read_line()
  |&gt; write_line(socket)

  serve(socket)
end

defp read_line(socket) do
  {:ok, data} = :gen_tcp.recv(socket, 0)
  data
end

defp write_line(line, socket) do
  :gen_tcp.send(socket, line)
end</pre>
<p>by the following:</p>
<pre data-language="elixir">defp serve(socket) do
  msg =
    case read_line(socket) do
      {:ok, data} -&gt;
        case KVServer.Command.parse(data) do
          {:ok, command} -&gt;
            KVServer.Command.run(command)
          {:error, _} = err -&gt;
            err
        end
      {:error, _} = err -&gt;
        err
    end

  write_line(socket, msg)
  serve(socket)
end

defp read_line(socket) do
  :gen_tcp.recv(socket, 0)
end

defp write_line(socket, {:ok, text}) do
  :gen_tcp.send(socket, text)
end

defp write_line(socket, {:error, :unknown_command}) do
  # Known error; write to the client
  :gen_tcp.send(socket, "UNKNOWN COMMAND\r\n")
end

defp write_line(_socket, {:error, :closed}) do
  # The connection was closed, exit politely
  exit(:shutdown)
end

defp write_line(socket, {:error, error}) do
  # Unknown error; write to the client and exit
  :gen_tcp.send(socket, "ERROR\r\n")
  exit(error)
end</pre>
<p>If we start our server, we can now send commands to it. For now, we will get two different responses: "OK" when the command is known and "UNKNOWN COMMAND" otherwise:</p>
<pre data-language="elixir">$ telnet 127.0.0.1 4040
Trying 127.0.0.1...
Connected to localhost.
Escape character is '^]'.
CREATE shopping
OK
HELLO
UNKNOWN COMMAND
</pre>
<p>This means our implementation is going in the correct direction, but it doesn't look very elegant, does it?</p>
<p>The previous implementation used pipelines which made the logic straightforward to follow. However, now that we need to handle different error codes along the way, our server logic is nested inside many <code class="inline">case</code> calls.</p>
<p>Thankfully, Elixir v1.2 introduced the <code class="inline">with</code> construct, which allows you to simplify code like the above, replacing nested <code class="inline">case</code> calls with a chain of matching clauses. Let's rewrite the <code class="inline">serve/1</code> function to use <code class="inline">with</code>:</p>
<pre data-language="elixir">defp serve(socket) do
  msg =
    with {:ok, data} &lt;- read_line(socket),
         {:ok, command} &lt;- KVServer.Command.parse(data),
         do: KVServer.Command.run(command)

  write_line(socket, msg)
  serve(socket)
end</pre>
<p>Much better! <code class="inline">with</code> will retrieve the value returned by the right-side of <code class="inline">&lt;-</code> and match it against the pattern on the left side. If the value matches the pattern, <code class="inline">with</code> moves on to the next expression. In case there is no match, the non-matching value is returned.</p>
<p>In other words, we converted each expression given to <a href="kernel.specialforms.html#case/2"><code class="inline">case/2</code></a> as a step in <code class="inline">with</code>. As soon as any of the steps return something that does not match <code class="inline">{:ok, x}</code>, <code class="inline">with</code> aborts, and returns the non-matching value.</p>
<p>You can read more about <a href="kernel.specialforms.html#with/1"><code class="inline">with/1</code></a> in our documentation.</p>
<h2 id="running-commands" class="section-heading">  <span class="text">Running commands</span> </h2> <p>The last step is to implement <code class="inline">KVServer.Command.run/1</code>, to run the parsed commands against the <code class="inline">:kv</code> application. Its implementation is shown below:</p>
<pre data-language="elixir">@doc """
Runs the given command.
"""
def run(command)

def run({:create, bucket}) do
  KV.Registry.create(KV.Registry, bucket)
  {:ok, "OK\r\n"}
end

def run({:get, bucket, key}) do
  lookup(bucket, fn pid -&gt;
    value = KV.Bucket.get(pid, key)
    {:ok, "#{value}\r\nOK\r\n"}
  end)
end

def run({:put, bucket, key, value}) do
  lookup(bucket, fn pid -&gt;
    KV.Bucket.put(pid, key, value)
    {:ok, "OK\r\n"}
  end)
end

def run({:delete, bucket, key}) do
  lookup(bucket, fn pid -&gt;
    KV.Bucket.delete(pid, key)
    {:ok, "OK\r\n"}
  end)
end

defp lookup(bucket, callback) do
  case KV.Registry.lookup(KV.Registry, bucket) do
    {:ok, pid} -&gt; callback.(pid)
    :error -&gt; {:error, :not_found}
  end
end</pre>
<p>Every function clause dispatches the appropriate command to the <code class="inline">KV.Registry</code> server that we registered during the <code class="inline">:kv</code> application startup. Since our <code class="inline">:kv_server</code> depends on the <code class="inline">:kv</code> application, it is completely fine to depend on the services it provides.</p>
<p>You might have noticed we have a function head, <code class="inline">def run(command)</code>, without a body. In the <a href="modules-and-functions.html#default-arguments">Modules and Functions</a> chapter, we learned that a bodiless function can be used to declare default arguments for a multi-clause function. Here is another use case where we use a function without a body to document what the arguments are.</p>
<p>Note that we have also defined a private function named <code class="inline">lookup/2</code> to help with the common functionality of looking up a bucket and returning its <code class="inline">pid</code> if it exists, <code class="inline">{:error, :not_found}</code> otherwise.</p>
<p>By the way, since we are now returning <code class="inline">{:error, :not_found}</code>, we should amend the <code class="inline">write_line/2</code> function in <code class="inline">KVServer</code> to print such error as well:</p>
<pre data-language="elixir">defp write_line(socket, {:error, :not_found}) do
  :gen_tcp.send(socket, "NOT FOUND\r\n")
end</pre>
<p>Our server functionality is almost complete. Only tests are missing. This time, we have left tests for last because there are some important considerations to be made.</p>
<p><code class="inline">KVServer.Command.run/1</code>'s implementation is sending commands directly to the server named <code class="inline">KV.Registry</code>, which is registered by the <code class="inline">:kv</code> application. This means this server is global and if we have two tests sending messages to it at the same time, our tests will conflict with each other (and likely fail). We need to decide between having unit tests that are isolated and can run asynchronously, or writing integration tests that work on top of the global state, but exercise our application's full stack as it is meant to be exercised in production.</p>
<p>So far we have only written unit tests, typically testing a single module directly. However, in order to make <code class="inline">KVServer.Command.run/1</code> testable as a unit we would need to change its implementation to not send commands directly to the <code class="inline">KV.Registry</code> process but instead pass a server as an argument. For example, we would need to change <code class="inline">run</code>'s signature to <code class="inline">def run(command, pid)</code> and then change all clauses accordingly:</p>
<pre data-language="elixir">def run({:create, bucket}, pid) do
  KV.Registry.create(pid, bucket)
  {:ok, "OK\r\n"}
end

# ... other run clauses ...</pre>
<p>Feel free to go ahead and do the changes above and write some unit tests. The idea is that your tests will start an instance of the <code class="inline">KV.Registry</code> and pass it as an argument to <code class="inline">run/2</code> instead of relying on the global <code class="inline">KV.Registry</code>. This has the advantage of keeping our tests asynchronous as there is no shared state.</p>
<p>But let's also try something different. Let's write integration tests that rely on the global server names to exercise the whole stack from the TCP server to the bucket. Our integration tests will rely on global state and must be synchronous. With integration tests, we get coverage on how the components in our application work together at the cost of test performance. They are typically used to test the main flows in your application. For example, we should avoid using integration tests to test an edge case in our command parsing implementation.</p>
<p>Our integration test will use a TCP client that sends commands to our server and assert we are getting the desired responses.</p>
<p>Let's implement the integration test in <code class="inline">test/kv_server_test.exs</code> as shown below:</p>
<pre data-language="elixir">defmodule KVServerTest do
  use ExUnit.Case

  setup do
    Application.stop(:kv)
    :ok = Application.start(:kv)
  end

  setup do
    opts = [:binary, packet: :line, active: false]
    {:ok, socket} = :gen_tcp.connect(~c"localhost", 4040, opts)
    %{socket: socket}
  end

  test "server interaction", %{socket: socket} do
    assert send_and_recv(socket, "UNKNOWN shopping\r\n") ==
           "UNKNOWN COMMAND\r\n"

    assert send_and_recv(socket, "GET shopping eggs\r\n") ==
           "NOT FOUND\r\n"

    assert send_and_recv(socket, "CREATE shopping\r\n") ==
           "OK\r\n"

    assert send_and_recv(socket, "PUT shopping eggs 3\r\n") ==
           "OK\r\n"

    # GET returns two lines
    assert send_and_recv(socket, "GET shopping eggs\r\n") == "3\r\n"
    assert send_and_recv(socket, "") == "OK\r\n"

    assert send_and_recv(socket, "DELETE shopping eggs\r\n") ==
           "OK\r\n"

    # GET returns two lines
    assert send_and_recv(socket, "GET shopping eggs\r\n") == "\r\n"
    assert send_and_recv(socket, "") == "OK\r\n"
  end

  defp send_and_recv(socket, command) do
    :ok = :gen_tcp.send(socket, command)
    {:ok, data} = :gen_tcp.recv(socket, 0, 1000)
    data
  end
end</pre>
<p>Our integration test checks all server interaction, including unknown commands and not found errors. It is worth noting that, as with ETS tables and linked processes, there is no need to close the socket. Once the test process exits, the socket is automatically closed.</p>
<p>This time, since our test relies on global data, we have not given <code class="inline">async: true</code> to <code class="inline">use ExUnit.Case</code>. Furthermore, in order to guarantee our test is always in a clean state, we stop and start the <code class="inline">:kv</code> application before each test. In fact, stopping the <code class="inline">:kv</code> application even prints a warning on the terminal:</p>
<pre data-language="elixir">18:12:10.698 [info] Application kv exited: :stopped</pre>
<p>To avoid printing log messages during tests, ExUnit provides a neat feature called <code class="inline">:capture_log</code>. By setting <code class="inline">@tag :capture_log</code> before each test or <code class="inline">@moduletag :capture_log</code> for the whole test module, ExUnit will automatically capture anything that is logged while the test runs. In case our test fails, the captured logs will be printed alongside the ExUnit report.</p>
<p>Between <code class="inline">use ExUnit.Case</code> and <code class="inline">setup</code>, add the following call:</p>
<pre data-language="elixir">@moduletag :capture_log</pre>
<p>In case the test crashes, you will see a report as follows:</p>
<pre data-language="elixir">  1) test server interaction (KVServerTest)
     test/kv_server_test.exs:17
     ** (RuntimeError) oops
     stacktrace:
       test/kv_server_test.exs:29

     The following output was logged:

     13:44:10.035 [notice] Application kv exited: :stopped</pre>
<p>With this simple integration test, we start to see why integration tests may be slow. Not only can this test not run asynchronously, but it also requires the expensive setup of stopping and starting the <code class="inline">:kv</code> application.</p>
<p>At the end of the day, it is up to you and your team to figure out the best testing strategy for your applications. You need to balance code quality, confidence, and test suite runtime. For example, we may start with testing the server only with integration tests, but if the server continues to grow in future releases, or it becomes a part of the application with frequent bugs, it is important to consider breaking it apart and writing more intensive unit tests that don't have the weight of an integration test.</p>
<p>Let's move to the next chapter. We will finally make our system distributed by adding a bucket routing mechanism. We will use this opportunity to also improve our testing chops.</p> </div> <div class="bottom-actions" id="bottom-actions"> <div class="bottom-actions-item"> <a href="task-and-gen-tcp.html" class="bottom-actions-button" rel="prev"> <span class="subheader"> ← Previous Page </span> <span class="title"> Task and gen_tcp </span> </a> </div> <div class="bottom-actions-item"> <a href="distributed-tasks.html" class="bottom-actions-button" rel="next"> <span class="subheader"> Next Page → </span> <span class="title"> Distributed tasks and tags </span> </a> </div> </div> <footer class="footer"> <p> <span class="line"> <button class="a-main footer-button display-quick-switch" title="Search HexDocs packages"> Search HexDocs </button> <a href="elixir.epub.html" title="ePub version"> Download ePub version </a> </span> </p> <p class="built-using"> Built using <a href="https://github.com/elixir-lang/ex_doc" title="ExDoc" target="_blank" rel="help noopener" translate="no">ExDoc</a> (v0.36.1) for the <a href="https://elixir-lang.org" title="Elixir" target="_blank" translate="no">Elixir programming language</a> </p> </footer><div class="_attribution">
  <p class="_attribution-p">
    &copy; 2012-2024 The Elixir Team<br>Licensed under the Apache License, Version 2.0.<br>
    <a href="https://hexdocs.pm/elixir/1.18.1/docs-tests-and-with.html" class="_attribution-link">https://hexdocs.pm/elixir/1.18.1/docs-tests-and-with.html</a>
  </p>
</div>
